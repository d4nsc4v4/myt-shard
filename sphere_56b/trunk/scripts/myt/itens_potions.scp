    ////////////////////////////////////////////////////////////////
   ////////////////////////////////////////////////////////////////
  /////                          POTIONS                     /////
 ////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////

[TYPEDEF t_potion]

on=@ClientToolTip
IF (<attr>&attr_identified)
 LOCAL.name <DEF.<BASEID>_IDNAME> <DEF.BFONT_LRED>(<f_potionStr>)
 LOCAL.tip <DEF.<BASEID>_IDTIP>
ELIF (!<src.isgm>)
  LOCAL.name <NAME>
 LOCAL.tip (Não identificado)
ELSE
 LOCAL.name <NAME>
 LOCAL.tip (Não identificado) <DEF.BR><DEF.BFONT_YELLOW>[<DEF.<BASEID>_IDNAME> <DEF.BFONT_RED>(<f_potionStr>)<DEF.BFONT_YELLOW>]<DEF.BR><DEF.BFONT_LRED>[<DEF.<BASEID>_IDTIP>]
ENDIF
IF (<amount> > 1)
 LOCAL.tip = Qtd.: <eval <amount>><DEF.BR><LOCAL.tip>
endif
src.addcliloc 1042971,<LOCAL.name>
src.addcliloc 1070722,<LOCAL.tip>
RETURN 1

[FUNCTION f_potionStr]
if (<more2> < 955)
 return fraca
elif (<more2> < 1250)
 return normal
elif (<more2> < 1750)
 return forte
else
 return concentrada
endif

//########################################################
//##                            ##
//##            Poções ordinárias       ##
//##                            ##
//########################################################

// --- HEALING ---
[ITEMDEF 0f0c]
//Potion (Yellow)
DEFNAME=i_bottle_yellow
NAME=pocao amarela
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_bottle_empty
WEIGHT=1
DESCRIPTION=Poção amarela(Heal)
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

on=@create
more2={0 2000}

on=@dclick
if (0<src.findlayer.layer_flag_potionused.uid>)
 return 0
endif
src.effect 3 i_fx_sparkle 0 16 0
if (<more2> <= 890)
 LOCAL.effect={8 15}
elif (<more2> <= 1000)
 LOCAL.effect={22 27}
elif (<more2> <= 1500)
 LOCAL.effect={33 55}
else
 src.sfx 0202 
 LOCAL.effect={65 90}
endif
LOCAL.effect=<eval <LOCAL.effect>>
src.hits=<eval <src.hits>+<LOCAL.effect>>
if (<src.hits> > <src.maxhits>)
 src.hits=<src.maxhits>
endif


// --- NIGHTSIGHT ---

[ITEMDEF 0f06]
//Potion (Black)
DEFNAME=i_bottle_black
NAME=pocao negra
TYPE=t_potion
TDATA1=i_bottle_empty
RESOURCES=i_bottle_empty
WEIGHT=1
DESCRIPTION=Poção negra(Nightsight)
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@Create
    MORE1=S_Night_Sight
    MORE2={ 30.0 90.0 }



// --- CURE ---

[ITEMDEF 0f07]
//Potion (Orange)
DEFNAME=i_bottle_orange
NAME=pocao alaranjada
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_bottle_empty
WEIGHT=1
DESCRIPTION=Poção alaranjada(Cura)
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@Create
    MORE1=s_cure
    MORE2={ 300 900 }

// --- AGILITY ---

[ITEMDEF 0f08]
//Potion (Blue)
DEFNAME=i_bottle_blue
NAME=pocao azulada
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_bottle_empty
WEIGHT=1
DESCRIPTION=Poção azulada(Agility)
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@Create
    MORE1=s_agility
    MORE2={ 300 900 }

// --- STRENGTH ---

[ITEMDEF 0f09]
//Potion (White)
DEFNAME=i_bottle_white
NAME=pocao alva
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_bottle_empty
WEIGHT=1
DESCRIPTION=Poção alva(Strength)
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@Create
    MORE1=s_Strength
    MORE2={ 300 900 }

// --- POISON ---

[ITEMDEF 0f0a]
//Potion (Green)
DEFNAME=i_bottle_green
NAME=pocao verde
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_bottle_empty
WEIGHT=1
DESCRIPTION=Poção verde(Poison)
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@Create
    MORE1=s_Poison
    MORE2={ 300 900 }

on=@dclick
if (<attr>&attr_identified)
 src.f_usePoison <uid>
else
 src.veneno <more2>
 src.sysmessagered Voce tomou veneno!
 src.cry
endif
return 1

// --- REFRESH ---

[ITEMDEF 0f0b]
//Potion (Red)
DEFNAME=i_bottle_red
NAME=pocao escarlate
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_bottle_empty
WEIGHT=1
DESCRIPTION=Poção escarlate(Stam)
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@Create
    MORE1=s_refresh
    MORE2={ 300 900 }

// --- EXPLOSION ---

[ITEMDEF 0f0d]
//Potion (Purple)
DEFNAME=i_bottle_purple
NAME=pocao rosea
TYPE=T_POTION
TDATA1=0
RESOURCES=i_bottle_empty
WEIGHT=1
DESCRIPTION=Poção rosea(Explosion)
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@Create
    MORE1=s_explosion
    MORE2={ 30.0 80.0 }

on=@dclick
if (0<src.findlayer.layer_flag_potionused.uid>)
 return 0
endif
//Reduzir 80% do efeito.
more2=<eval ((<more2>*2)/10)>

// --- MANA ---

[ITEMDEF i_potion_Mana]
ID=i_bottle_WHITE
NAME=pocao alva
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_reag_eye_of_newt 3, i_bottle_EMPTY
SKILLMAKE=ALCHEMY 65.1
DESCRIPTION=Poção prateada(Mana)
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@Create
    MORE2={0 200}

on=@dclick
if (0<src.findlayer.layer_flag_potionused.uid>)
 return 0
endif
src.effect 3 i_fx_sparkle 0 16 0
if (<more2> <= 890)
 LOCAL.effect={5 15}
elif (<more2> <= 1000)
 LOCAL.effect={17 27}
elif (<more2> <= 1500)
 LOCAL.effect={30 55}
else
 src.sfx 01e7
 LOCAL.effect={60 80}
endif
LOCAL.effect=<eval <LOCAL.effect>>
src.mana=<eval <src.mana>+<LOCAL.effect>>
if (<src.mana> > <src.maxmana>)
 src.mana=<src.maxmana>
endif


// --- GARRAFA VAZIA ---

[ITEMDEF 0f0e]
//Empty Potion Bottle
DEFNAME=i_bottle_empty
NAME=garrafa alquimica
TYPE=T_POTION_EMPTY
SKILLMAKE=TINKERING 21.3,t_tinker_tools
RESOURCES=2 i_ingot_iron
WEIGHT=1
CATEGORY=Provisions - Alchemy & Magic
SUBSECTION=Magical Items
DESCRIPTION=Empty Alchemist Bottle


//########################################################
//##                            ##
//##            Poções avançadas        ##
//##                            ##
//########################################################


// --- Magic Reflection ---

[itemdef i_potion_magic_reflection]
ID=i_bottle_black
NAME=pocao negra
TYPE=t_potion
TDATA1=i_bottle_empty
DESCRIPTION=Poção da reflexão
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

on=@create
more2=50


on=@dclick
src.spelleffect s_magic_reflection,<more2>


// --- Encolher ---

[itemdef i_potion_encolher]
ID=i_bottle_blue
NAME=pocao azulada
TYPE=T_POTION
TDATA1=i_bottle_empty
DESCRIPTION=Poção de encolher
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

on=@create
more2=50

on=@dclick
if (<topobj.uid> != <src.uid>)
 src.sysmessage Voce deve estar com esse tipo de pocao nas maos...
 return 1
endif
 target Selecione o animal para encolher...
return 1

on=@targon_char
if (<src.targ.npc> != 1)
 src.sysmessage Este alvo nao pode ser encolhido por meio de uma pocao...
 return 1
endif
if (<src.targ.ismypet> == 0)
 src.sysmessage Voce deve ser o dono do animal...
 return 1
endif
if (<src.targ.stam> > <eval (<more2>/10) + 10>)
 src.targ.stam=<src.targ.stam> + (- (<more2>/(10))) - 10
 src.newitem i_bottle_empty
 src.act.bounce
 src.sysmessage A dose aplicada nao surtiu efeito, tente com mais pocoes...
 remove
 return 1
endif
if (<src.targ.stam> <= <eval (<more2>/10) + 10>)
 src.targ.stam=<src.targ.str>
 src.targ.shrink
 src.say *Encolheu um animal*
 src.newitem i_bottle_empty
 src.act.bounce
 remove
 return 1
endif
return 1



// --- Explosao Terrestre ---

[itemdef i_potion_explosao_terrestre]
ID=i_bottle_green
NAME=pocao verde
TYPE=T_POTION
TDATA1=i_bottle_empty
DESCRIPTION=Poção de explosão terrestre
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic


on=@dclick
if (0<tag.armed>==0)
 decrement
 serv.newitem <baseid>
 new.p=<src.p>
 new.update
 new.attr=010
 new.emotered preparando
 new.tag.detecting_hidden=<eval <src.hiding>/2>
 new.tag.armed=1
 new.morex=3
 new.timer=1
elif ((0<tag.armed>==2) && <distance> >= 2) 
 src.sysmessageyellow Muito longe...
else
 say *Desarmando*          // 100% de chance enquanto nao tiver sistema de trap/remove trap em ativo
 timer=4
endif
return 1

on=@timer
 if ((0<tag.armed>==2)
  remove          // 100% de chance enquanto nao tiver sistema de trap/remove trap em ativo
  endif
 if (<tag.armed>==1) && (<morex> > 0)
  emoteyellow <eval <morex>>
  morex=<morex> + (-1)
  timer=1
  return 1
 endif
 if (<morex> <= 0) && (<tag.armed> == 1)
  emotered Armada
  tag.armed=2
  attr=090
  color=0b89
  name=<name> **ARMADA**
  update
  return 1
 endif
return 1

on=@step 
 if (<src.gm> || <src.flags>&statf_dead)
  return 1
 elif (0<tag.armed> == 2)
  src.damage <R25,40>,dam_fire|dam_physical
  src.effect 3,i_fx_explode,0,13,0
  sfx 011b
  src.emotered atingido por uma explosao terrestre
  remove
  return 1
 endif
return 1



// --- Sopro de Fogo ---

[itemdef i_potion_sopro_de_fogo]
ID=i_bottle_orange
NAME=pocao alaranjada
TYPE=T_POTION
TDATA1=i_bottle_empty
DESCRIPTION=Poção sopro de fogo
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

on=@dclick
if (<topobj.uid> != <src.uid>)
 src.sysmessage Voce deve estar com esse tipo de pocao nas maos...
 return 1
endif
 target Selecione o alvo...
return 1

on=@targon_char
src.anim 11
src.targ.effect 0,i_fx_fireball_small,6,1,1
src.targ.damage (<src.int>/5) 0010 <src.uid>
src.newitem i_bottle_empty
src.act.bounce
src.consume 1 <baseid>
return 1

on=@targon_item
src.sysmessage Alvo invalido.
return 1

on=@targon_ground
src.sysmessage Alvo invalido.
return 1



// --- Morvor (stamina) ---

[itemdef i_potion_morvor]
ID=i_bottle_purple
NAME=pocao rosea
TYPE=T_POTION
TDATA1=i_bottle_empty
DESCRIPTION=Morvor (Stamina)
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

on=@dclick
if (<topobj.uid> != <src.uid>)
 src.sysmessage Voce deve estar com esse tipo de pocao nas maos...
 return 1
endif
src.newitem i_mry_morvor
src.act.equip
src.act.more1=<src.stamina>
src.act.more2={10 20}
src.act.timer=1
src.sysmessage Voce se sente estranhamnte vigoroso.
src.consume 1 <baseid>
return 1

[itemdef i_mry_morvor]
ID=i_memory
NAME=pocao do maximo vigor
TYPE=T_eq_script
layer=30

on=@timer
if (<more2> > 1)
 cont.stamina=<cont.dex>
 more2=(<more2>+(-1))
 timer=1
 return 1
else
 cont.stamina=<more1>
 cont.sysmessage O efeito da pocao passou.
 remove
 return 1
endif



// --- Invisibilidade ---

[ITEMDEF i_potion_invis]
ID=i_bottle_red
NAME=pocao escarlate
TYPE=t_potion
TDATA1=i_bottle_empty
DESCRIPTION=Poção da invisibilidade
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@DCLICK
if (<topobj> != <src>)
    src.sysmessageyellow Voce deve estar com esse tipo de pocao nas maos...
    return 1
endif
src.invis_enter

// --- Furor de Batalha ---

[itemdef i_potion_furor]
ID=i_bottle_yellow
NAME=pocao amarela
TYPE=T_POTION
TDATA1=i_bottle_empty
DESCRIPTION=Poção do furor de batalha
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

on=@dclick
if (<topobj.uid> != <src.uid>)
 src.sysmessage Voce deve estar com esse tipo de pocao nas maos...
 return 1
endif
src.newitem i_mry_furor
src.act.equip
src.act.timer=(<src.str>/3)
src.sysmessageblue Sentese estranhamnte disposto a lutar.
src.tactics=(<src.tactics>+200)
src.fencing=(<src.fencing>+200)
src.wrestling=(<src.wrestling>+200)
src.swordsmanship=(<src.swordsmanship>+200)
src.macefighting=(<src.macefighting>+200)
src.parrying=(<src.parrying>+200)
src.consume 1 <baseid>
src.skill_init
src.say=@026 Graurrrr!
return 1

[itemdef i_mry_furor]
ID=i_memory
NAME=pocao do furor de batalha
TYPE=T_eq_script
layer=30

on=@timer
cont.tactics=(<cont.tactics>+(-200))
cont.fencing=(<cont.fencing>+(-200))
cont.wrestling=(<cont.wrestling>+(-200))
cont.swordsmanship=(<cont.swordsmanship>+(-200))
cont.macefighting=(<cont.macefighting>+(-200))
cont.parrying=(<cont.parrying>+(-200))
cont.sysmessage Perdeu o furor de batalha.
cont.say ouf!
remove
return 1



// --- Formula 21 (ferro para ouro) ---

[itemdef i_potion_formula_21]
ID=i_bottle_black
NAME=pocao negra
TYPE=T_POTION
TDATA1=i_bottle_empty
DESCRIPTION=Formula 21 (ouro)
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

on=@dclick
if (<topobj.uid> != <src.uid>)
 src.sysmessage Voce deve estar com esse tipo de pocao nas maos...
 return 1
endif
 target Selecione um lingote de ferro.
return 1

on=@targon_char
src.sysmessage Alvo invalido.
return 1

on=@targon_item
if (<src.targ.dispid>!=i_ingot_iron)
 src.sysmessageyellow Isso nao e um lingote de ferro.
 return 1
elif (<src.targ.cont.uid> == <src.findlayer(21).uid>)
 src.sysmessageyellow O ferro deve estar fora de sua mala para aplicar a formula.
 return 1
elif (!<src.targ.canseelos>) 
 src.sysmessageyellow O lingote deve estar visivel.
 return 1
elif (<src.targ.distance> >= 2) 
 src.sysmessageyellow O lingote deve estar proximo.
 return 1
endif
src.targ.effect 2,i_fire_column,0,30,0
src.targ.effect 2,i_fx_sparkle,0,16,0
src.newitem=i_bottle_empty
src.act.bounce
sfx 01ec
if !(<R50>)// || (<src.gm>==1)
 src.newitem=i_gold
 src.act.amount=<R1,<eval <src.int>/8>>
elif !(<R20>)
 src.newitem=i_coin_copper
 src.act.amount=<R1,<eval <src.int>/3>>
else
 dorand (5)
  src.newitem={ i_r_perola_negra 1 i_r_musgo_sangue 1 i_r_alho 1 i_r_ginseng 1 i_r_raiz_de_mandrake 1 i_r_nightshade 1 i_r_cinza_sulfurosa 1 i_r_teia_de_aranha 1 }
  src.newitem={ i_horse_dung 1 0f3c 1 }
  src.newitem={ i_log 1 i_board 1 01f7e 1}
  src.newitem=i_potion_formula_21
  src.newitem={ i_plant_potted_small 1 i_flower_potted_small 1 i_tree_small_potted_2 1 }
 enddo
endif
src.new.p=<src.targ.p>
if (<src.new.type>!=t_gold)
 src.sysmessagered Algo de errado com a transmutacao... Tente denovo.
endif
src.targ.remove
src.consume 1 <baseid>
return 1

on=@targon_ground
src.sysmessage Alvo invalido.
return 1



// --- Poção da proteção ---

[ITEMDEF i_potion_Protection]
// This potion erects a temporary force field around you. Nobody approaching will be able to get within 1 tile of you, though you can move close to them if you wish.
ID=i_bottle_blue
NAME=pocao azulada
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_r_olho_morto_vivo, i_bottle_EMPTY
SKILLMAKE=ALCHEMY 62.2
DESCRIPTION=Poção da proteção
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@Create
    MORE1=s_Protection
    MORE2={500 890}

//A poção está muito forte. Atenuar.
ON=@DClick
more2=<eval <more2>/3>


// --- Poção da proteção maxima ---

[ITEMDEF i_potion_Protection_max]
// This potion erects a temporary force field around you. Nobody approaching will be able to get within 1 tile of you, though you can move close to them if you wish.
ID=i_bottle_green
NAME=pocao verde
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_r_olho_morto_vivo, i_bottle_EMPTY
SKILLMAKE=ALCHEMY 62.2
DESCRIPTION=Poção maxima proteção
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@DClick
    src.spelleffect s_Protection 650
    src.spelleffect s_reactive_armor 750
    src.spelleffect s_magic_reflection 1000

    src.sysmessageblue Uma aura lhe cobre bloqueando ataques fisicos e magicos!
    src.sfx 0216
    src.emotered pele brilhando


// --- Poção do usro cinzento (polimorfa com stats) ---

[ITEMDEF i_potion_poly_urso]
// Polymorpha temporariamente pra grizzly alterando algusn stats.
ID=i_bottle_orange
NAME=pocao alaranjada
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_r_olho_morto_vivo, i_bottle_EMPTY
SKILLMAKE=ALCHEMY 62.2
DESCRIPTION=Poção urso
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@DClick
src.f_polymorph c_bear_grizzly,30,40,30,-20


// --- Poção mascara da morte (polimorfa pra undead e evita ataques de undead) ---

[ITEMDEF i_potion_death_mask]
// Polymorpha temporariamente pra morto e não deixa os undead atacarem.
ID=i_bottle_purple
NAME=pocao rosea
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_r_olho_morto_vivo, i_bottle_EMPTY
SKILLMAKE=ALCHEMY 62.2
DESCRIPTION=Poção mascara da morte
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@DClick
DOSWITCH <eval <src.int>/200>
 local.body c_zombie
 local.body c_zombie
 local.body c_skeleton
 local.body c_liche
 local.body c_liche
ENDDO
src.f_polymorph <local.body>,30,-10,-30,0
src.f_death_mask 30


//########################################################
//##                            ##
//##            Poções piada            ##
//##                            ##
//########################################################

// --- veneno roxo (envenena e deixa roxo) ---

[itemdef i_potion_veneno_roxo]
ID=i_bottle_red
NAME=pocao escarlate
TYPE=T_POTION
TDATA1=i_bottle_empty
DESCRIPTION=Veneno roxo
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

on=@dclick
if (<topobj.uid> != <src.uid>)
 src.sysmessage Voce deve estar com esse tipo de pocao nas maos...
 return 1
endif
src.newitem i_mry_veneno_roxo
src.act.tag.ocolor=<src.color>
src.color=0f
src.act.equip
src.act.timer={30 120}
src.sysmessagered Voce esta roxo!!!
src.sfx 040
src.emotered quebrou a garrafa
src.spelleffect s_Poison 500
src.consume 1 <baseid>
return 1

[itemdef i_mry_veneno_roxo]
ID=i_memory
NAME=veneno roxo
TYPE=T_eq_script
layer=30

on=@timer
cont.color=<tag.ocolor>
cont.update
remove
return 1


[ITEMDEF i_potion_poly_galinha]
// Polymorpha temporariamente pra galinha diminuindo stats.
ID=i_bottle_white
NAME=pocao alva
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_r_olho_morto_vivo, i_bottle_EMPTY
SKILLMAKE=ALCHEMY 62.2
DESCRIPTION=Poção galinha
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

ON=@DClick
src.sysmessagered Grande coragem a sua!
src.f_polymorph c_chicken,150,-30,-30,-30


[ITEMDEF i_potion_poly_ancient_wyrm]
//Polimorpha temporariamente para Ancient Wyrm e cospe fogo.
ID=0f0a
NAME=pocao verde
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_bottle_empty
WEIGHT=1
DESCRIPTION=Poção Wyrm ancião
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

on=@create
more2=2000

on=@dclick
if !(<attr>&attr_identified)
 src.sysmessageyellow Voce fica com muito medo de tomar esta pocao.
 src.emotegreen treme
 return 1
else
 src.sysmessagegreen Clique duas vezes sobre personagens para cospir fogo!
 src.f_polymorph c_ancient_wyrm,60,50,40,20
 src.aid
 src.events +e_breath_o_fire
 src.timerf 61,hits,5
 src.timerf 60,events,-e_breath_o_fire
endif




[EVENTS e_breath_o_fire]
on=@CHARDCLICK
IF (<serv.time> < <ctag0.no_breath>)
 sysmessageyellow Aguarde ate poder cospir fogo novamente.
 return 1
ELIF (<act.flags>&01f)
 sysmessageyellow Voce nao pode afetar este alvo.
 return 1
ELIF (!<CanSeeLOS <act>>)
 sysmessagered Voce nao consegue ver o alvo.
 return 1
ELIF (<distance <act>> > 5)
 sysmessagered Muito longe...
 return 1
endif
anim 6
emotered cospe fogo
sfx 016A
act.effect 3 i_fire_column 26 26 26
act.damage <R20,50> DAM_FIRE|DAM_MAGIC <UID>
ctag.no_breath=<eval <serv.time>+25>
return 1


[ITEMDEF i_potion_poly_gazer]
//Polimorpha temporariamente para Gazer e paralysa
ID=i_bottle_red
NAME=pocao escarlate
TYPE=T_POTION
TDATA1=i_bottle_empty
RESOURCES=i_bottle_empty
WEIGHT=1
DESCRIPTION=Poção Gazer
SUBSECTION=Potions
CATEGORY=MyT - Provisions - Alchemy & Magic

on=@create
more2=2000

on=@dclick
if !(<attr>&attr_identified)
 src.sysmessageyellow Voce fica com muito medo de tomar esta pocao.
 src.emotegreen treme
 return 1
else
 src.sysmessagegreen Clique duas vezes sobre personagens para paraliza-lo!
 src.f_polymorph c_gazer,60,30,20,10
 src.aid
 src.events +e_breath_o_freeze
 src.timerf 61,hits,5
 src.timerf 60,events,-e_breath_o_freeze
endif




[EVENTS e_breath_o_freeze]
on=@CHARDCLICK
IF (<serv.time> < <ctag0.no_breath>)
 sysmessageyellow Aguarde ate poder petrificar novamente.
 return 1
ELIF (<act.flags>&01f)
 sysmessageyellow Voce nao pode afetar este alvo.
 return 1
ELIF (!<CanSeeLOS <act>>)
 sysmessagered Voce nao consegue ver o alvo.
 return 1
ELIF (<distance <act>> > 5)
 sysmessagered Muito longe...
 return 1
endif
anim 6
emotered olhar malvado
sfx 0179
act.petrify
act.timerf 6,stone
ctag.no_breath=<eval <serv.time>+85>
return 1

[FUNCTION petrify]
sfx 05A9
stone
serv.newitem i_gold
new.color 1
new.p=<p>
new.nudgeup 20
new.attr 012
new.timer 3
new.emoteyellow petrifica