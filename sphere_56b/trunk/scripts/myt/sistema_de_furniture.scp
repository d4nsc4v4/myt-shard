////////////////////////////////////
//Sistema de furniture e movelaria//
//   MyT - Mystical Tales Shard   //
//     Por: Galthar, o Errante    //
//              V 0.5             //
////////////////////////////////////


  //////////////////////////////////////////////////////
 //               Sistema de Furniture               //
//////////////////////////////////////////////////////


[PLEVEL 0]
moveis
furniture

[FUNCTION moveis]
src.furniture

[FUNCTION furniture]
newitem i_mry_furniture
src.act.equip
src.act.timer 15
src.act.attr=02
src.act.use
src.startfurn
src.sysmessageyellow Voce tem <eval 0<src.tag.furniact>-1> movel(is). Seu limite e de <eval 0<src.tag.furnimax>-1> moveis.
return 1

[FUNCTION startfurn]
if (<tag0.furnimax>==0)
 tag0.furnimax=4
 tag0.furniact=1
 if (strmatch(<src.tag.classe>,artesao))||(strmatch(<src.tag.classe>,lenhador))||(strmatch(<src.tag.classe>,ferreiro))
  src.tag0.furnimax=11
 endif
endif
return 1

[FUNCTION TryAddFurn]
if !( <tag0.furnimax> )
    startfurn
endif
if (<eval 0<tag.furniact>> >= <eval 0<tag.furnimax>>)
 sysmessagered Voce nao pode ter mais moveis. (<eval 0<tag.furniact>-1>/<eval 0<tag.furnimax>-1>)
 return 0
else
 return 1
endif

[FUNCTION AddFurn]
if !( <tag0.furnimax> )
    startfurn
endif
if (<src.TryAddFurn>==1)
 src.tag.furniact=(<src.tag.furniact> +1)
 src.sysmessageyellow Voce tem <eval 0<src.tag.furniact>-1)> movel(is). Seu limite e de <eval 0<src.tag.furnimax>-1)> moveis.
 return 1
endif
return 0


[FUNCTION SubFurn]
if !( <tag0.furnimax> )
    startfurn
endif
if (<eval 0<src.tag.furniact>>!=0)
 src.tag.furniact=(<src.tag.furniact> +(-1))
 src.sysmessageyellow Voce tem <eval 0<tag.furniact>-1> movel(is).Seu  limite e de <eval 0<tag.furnimax>-1> moveis.
 return 1
endif
return 0



[ITEMDEF i_mry_furniture]
id=i_memory
name=sistema de furniture
type=t_eq_script
layer=30

on=@dclick
if (<more1>==0)
 TARGET Selecione o seu movel:
 return 1
elif (<more1>==1)
 TARGET Entregar o movel para quem?
 return 1
endif

on=@targon_ground
cont.sysmessage Alvo invalido.
remove
return 1

on=@targon_char
if (<more1>==1) && (<cont.targ.brain>==0)
 cont.targ.startfurn
 if (<eval 0<cont.targ.tag.furniact>> >= <eval 0<cont.targ.tag.furnimax>>)
  cont.sysmessage O jogador selecionado nao opde ter mais moveis.
  remove
  return 1
 endif
 link.tag.dono=<cont.targ.uid>
 try cont.targ.tag.furniact=(<eval 0<cont.targ.tag.furniact>> +1)
 cont.subfurn
 cont.targ.sysmessage <link.name> agora pertence a voce.
 cont.sysmessage <link.name> nao pertence mais a voce.
 remove
 return 1
endif
cont.sysmessage Alvo invalido.
remove
return 1

on=@targon_item
if (<cont.targ.tag.dono>!=<cont.uid>)
 src.sysmessage Este movel nao lhe pertence.
 remove
 return 1
endif
link=<cont.targ.uid>
cont.menu m_furniture
return 1

on=@timer
cont.consume 30 i_mry_furniture
remove
return 1

[MENU m_furniture]
(MyT) O que deseja fazer? (MyT)
on=0 Fixar o movel no chao.
src.findid(i_mry_furniture).link.attr=(<src.findid(i_mry_furniture).link.attr>|010)
src.findid(i_mry_furniture).link.attr=(<src.findid(i_mry_furniture).link.attr>&~02)
src.findid(i_mry_furniture).link.update
src.findid(i_mry_furniture).link.timer=-1
src.update
src.sysmessageyellow Seu movel esta fixo.
src.sysmessageyellow Nunca sera apagado.
src.findid(i_mry_furniture).remove
return 1

on=0 Soltar o movel do chao.
src.findid(i_mry_furniture).link.attr=(<src.findid(i_mry_furniture).link.attr>&~010)
src.findid(i_mry_furniture).link.update
src.update
src.sysmessageyellow Seu movel NAO esta fixo.
src.sysmessageyellow Ele PODERA ser apagado quando o servidor salvar.
src.findid(i_mry_furniture).remove
return 1

on=0 Mudar a posicao do movel.
src.findid(i_mry_furniture).link.flip
src.sysmessage Seu movel foi virado.
src.findid(i_mry_furniture).remove
return 1

on=0 Doar o movel.
src.findid(i_mry_furniture).more1=1
src.findid(i_mry_furniture).use
//src.findid(i_mry_furniture).remove
return 1

on=0 DESTRUIR O MOVEL.
if 0(<src.findid(i_mry_furniture).link.link.type>==t_multi)
 src.findid(i_mry_furniture).link.link.remove
else
 src.findid(i_mry_furniture).link.remove
endif
src.findid(i_mry_furniture).remove
src.sysmessagered Seu movel foi apagado.
return 1


[MENU m_furniture_own]
(MyT) deseja ser proprietario deste movel? (MyT)
on=0 Sim
if (<TryAddFurn>==1)
tag.furniact=(<eval 0<tag.furniact>>+1)
try uid.<tag.furnuid>.tag.dono=<uid>
try uid.<tag.furnuid>.tag.makefurn=
try sysmessageyellow <uid.<tag.furnuid>.name> agora pertende a voce.
endif
tag.furnuid=
return 1

on=0 Nao
tag.furnuid=
Return 1

  //////////////////////////////////////////////////////
 //                      MÓVEIS                      //
//////////////////////////////////////////////////////

  ///////////////////
 //  ENCAIXOTADOS //
///////////////////

[FUNCTION pack_unpack]
if (<src.region.flags>&<def.region_flag_nobuilding>) && (!<src.IsGM>)
    src.sysmessagered Nao eh possovel colocar este item aqui por ordens superiores.
    return 1
endif
src.ctag.pack=<uid>
if (<IsEmpty <tag.south>>)
    if (<IsEmpty <tag.east>>)
        src.sysmessagered Item com problemas. Poste no post de bugs do forum.
        return 1
    else
        src.sysmessageorange O item ficara voltado para o leste.
        src.unpackToEast
        return 1
    endif
else
    if (<IsEmpty <tag.east>>)
        src.sysmessageorange O item ficara voltado para o sul.
        src.unpackToSouth
        return 1
    endif
endif
src.dialog d_unpack

[FUNCTION unpackToEast]
obj=<ctag.pack>
if (<obj.topobj>!=<src>)
    src.sysmessagered Isso deve estar em sua posse.
    return 1
endif
obj.type=t_deed
obj.more1=<obj.tag.east>
obj.use

[FUNCTION unpackToSouth]
obj=<ctag.pack>
if (<obj.topobj>!=<src>)
    src.sysmessagered Isso deve estar em sua posse.
    return 1
endif
obj.type=t_deed
obj.more1=<obj.tag.south>
obj.use

[TYPEDEF t_packed]
on=@Dclick
if (<topobj>!=<src>)
    src.sysmessagered Isso deve estar em sua posse.
    return 1
endif
pack_unpack
return 1

[ITEMDEF i_bancada_encaixotada]
ID=0e3c
NAME=bancada (desmontada)
type=t_packed
SKILLMAKE=Carpentry 91.2
RESOURCES=4 i_mesa_p, 8 i_suporte, 200 i_parafuso, 12 i_barra_metal

ON=@Create
tag.south=i_bancada_n
tag.east=i_bancada_w

[ITEMDEF i_moinho_encaixotado]
ID=0e3c
NAME=moinho (desmontado)
type=t_table
SKILLMAKE=Carpentry 62.2
RESOURCES=10 i_tabua, 2 i_trefila, 100 i_parafuso, 9 i_barra_metal

on=@dclick
src.addmoinho
return 1

  ///////////////////
 //     GERAL     //
///////////////////
[TYPEDEF t_escrivaninha]

[ITEMDEF 0b49]
NAME=escrivaninha
TYPE=t_escrivaninha


  ///////////////////
 //  MULTI ITENS  //
///////////////////

[TYPEDEF t_bancada]
on=@dclick
src.sysmessage Objeto nao funcional.
return 1

[ITEMDEF 019f1]
Name=bancada
type=t_bancada

[ITEMDEF 019f2]
Name=bancada
type=t_bancada

[ITEMDEF 019f3]
Name=bancada
type=t_bancada

[ITEMDEF 019f5]
Name=bancada
type=t_bancada

[ITEMDEF 019f6]
Name=bancada
type=t_bancada

[ITEMDEF 019f7]
Name=bancada
type=t_bancada

[ITEMDEF i_bancada_w]
name=bancada
ID=0eed
type=t_multi
MULTIREGION 0,0,0,0

COMPONENT=6641 0,1,0,0
COMPONENT=6642 0,0,0,0
COMPONENT=6643 0,-1,0,0

[ITEMDEF i_bancada_n]
name=bancada
ID=0eed
type=t_multi
MULTIREGION 0,0,0,0

COMPONENT=6645 1,0,0,0
COMPONENT=6646 0,0,0,0
COMPONENT=6647 -1,0,0,0

[TYPEDEF e_furniture]
on=@dclick
attr |= attr_move_always
update
if (!<tag0.dono>)
 if (<src.tryaddfurn>)
  src.ctag.furn = <UID>
  src.menu m_addfurn
 endif
elif (<type>==t_chair) || (<type>==t_table) || (<type>==t_normal)
 if (<tag0.dono>==<src.uid>)
  serv.newitem i_mry_furniture
  new.cont=<src.uid>
  new.link=<uid>
  src.sysmessageyellow Voce tem <eval <src.tag0.furniact>-1> movel(is).Seu  limite e de <eval <src.tag0.furnimax>-1> moveis.
  src.menu m_furniture
 else
  src.sysmessagered Este movel nao te pertence!
 endif
else
 if (<tag0.dono>==<src.uid>)
  src.sysmessagegreen Use o comando .moveis para interagir com o movel.
 endif
 return 0
endif
return 1

on=@clienttooltip
if (!<src.IsGM>)
 return 0
endif
f_sendTooltip <name>, Dono: <UID.<tag0.dono>.tag.name> [<UID.<tag0.dono>.account.name>] <DEF.BR> Moveis: <eval <UID.<tag0.dono>.tag.furniact>-1>/<eval <UID.<tag0.dono>.tag.furnimax>-1>
//return 1

on=@destroy
IF (<UID.<tag0.dono>.ISPLAYER>)
 obj=<UID.<tag0.dono>.UID>
 obj.tag.furniact -= 1 
// obj.say <UID> -- <obj.tag.furnitures>
 obj.tag.furnitures=<obj.f_RemoveList <UID>,<obj.tag.furnitures>>
 f_sendMessage <obj.uid>,Seu movel (<name>) em <region.name> foi destruido!,024
 serv.log [furniture] <name> (<UID>) de dono <obj.tag.name> [<obj.account.name>] - DESTRUIDO!
ENDIF


[MENU m_addfurn]
Voce tem <eval <tag0.furniact>-1>  moveis e o maximo permitido eh <eval <tag0.furnimax>-1>. Deseja fazer de <UID.<ctag0.furn>.name> seu movel?
on=0 Sim
obj=<src.ctag.furn>
IF (<src.AddFurn>)
 obj.tag.dono=<src.uid>
 IF (<ISEMPTY <src.tag.furnitures>>)
  src.tag.furnitures=<obj.uid>
 ELSE
  src.tag.furnitures=<obj.uid>;<src.tag.furnitures>
 obj.update
ELSE
    src.sysmessagered Voce nao pode ter mais moveis!
ENDIF
RETURN 1

on=0 Nao

[DIALOG d_unpack]
100,75
page 0
resizepic 50 31 2620 500 400
checkertrans 55 38 490 385
gumppic 0 0 10400
gumppic 0 160 10401
gumppic 0 356 10402
gumppic 518 0 10410
gumppic 518 160 10411
gumppic 518 356 10412
gumppic 225 45 2501
dtext 231 45 26 Mystical Tales Shard
resizepic 92 87 2620 416 310
resizepic 112 107 2620 376 106
dhtmlgump 120 115 360 89 0 0 <DEF.CENTER><DEF.BFONT_GREEN>Desempacotando<DEF.BR><DEF.DIV_E><DEF.BFONT_LGRAY>Voce esta prestes e desencaixotar este objeto. Selecione para qual direcao ele deve ficar voltado.<DEF.BR>Cuidado! Esta acao pode nao ter volta.
tilepic 190 220 2713
tilepic 361 220 2712
button 172 316 2444 2444 1 0 1
dtext 185 317 47 Leste
button 355 316 2444 2444 1 0 2
dtext 375 317 47 Sul

[DIALOG d_unpack text]

[DIALOG d_unpack button]
ON=1
// ToEast
unpackToEast

ON=2
//ToSouth
unpackToSouth
