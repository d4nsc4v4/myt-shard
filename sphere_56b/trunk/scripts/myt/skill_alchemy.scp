//*****************************************************************************
//*****************************************************************************
//
// SKILL ALCHEMY
//
//  Mestre Galthar
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************

//TODO:

//TAGS usadas:
//tag.espiriteira           indica a espiriteira q o recipiente esta em cima
//tag.raca

//REAGENTES (máscaras - more2)
//i_r_perola_negra    =0001
//i_r_musgo_sangue    =0002
//i_r_alho            =0004
//i_r_ginseng         =0008
//i_r_raiz_de_mandrake=0010
//i_r_nightshade      =0020
//i_r_cinza_sulfurosa =0040
//i_r_teia_de_aranha  =0080
//i_r_asa_morcego     =0100
//i_r_olho_morto_vivo =0200

[DEFNAME alch]
alch_dif_t_balao    51
alch_dif_t_destilador   61
alch_dif_t_misturador   75

//*****************************************************************************
//*****************************************************************************
// TYPEDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// t_reagente
//*****************************************************************************
[TYPEDEF t_reagente]
ON=@DCLICK
if <topobj>!=<src>
    bounce
else    
    src.sysmessageyellow Utilize o almofariz.
endif    
return 1

ON=@CLICK
message <amount> <name>
return 1

//*****************************************************************************
// t_tubo
//*****************************************************************************
[TYPEDEF t_tubo]
ON=@DCLICK
src.sysmessageyellow Utilize o almofariz.
return 1

ON=@STEP
src.emotered quebrou a vidraria
sfx SND_GLASS
remove
return 1

//*****************************************************************************
// t_tubo_cheio
//*****************************************************************************
[TYPEDEF t_tubo_cheio]

ON=@STEP
src.emotered quebrou a vidraria
sfx SND_GLASS
remove
return 1

//*****************************************************************************
// t_almofariz
//*****************************************************************************
[TYPEDEF t_almofariz]

//*****************************************************************************
// t_espiriteira
//*****************************************************************************
//link é quem está em cima
[TYPEDEF t_espiriteira]
ON=@DCLICK
link=<src>
if (<cont>)
    src.sysmessageyellow A vela nao pode ser acesa onde esta.
    return 1
endif
if (!<more1>) //se apagado
    if (<more2>>0) //se tem carga
        more1=1
        dispid=i_heating_stand_lit
        sfx=054
        timer=<more2>
        update
    else
        src.sysmessageyellow A vela acabou. Selecione uma nova.
        TARGET
    endif
else    //se aceso
    apagaEspiriteira
endif

return 1

ON=@TARGON_ITEM
if (<src.targ.baseid>=i_candle) || (<src.targ.baseid>=i_candle_lit) || (<src.targ.baseid>=i_candle_lg) || (<src.targ.baseid>=i_candle_lg_lit)
    src.targ.decrement
    more2=1000
    src.sysmessagegreen Vela trocada.
else
    src.sysmessageyellow Deve selecionar uma vela.
endif
return 1

ON=@PICKUP_GROUND
if (<more1>) //se aceso
    apagaEspiriteira
endif

ON=@TIMER
emotered apagou
apagaEspiriteira
return 1

//*****************************************************************************
// t_destilador
//*****************************************************************************
[TYPEDEF t_destilador]
 
ON=@STEP
src.emotered quebrou a vidraria
sfx SND_GLASS
remove
return 1

ON=@DROPON_ITEM
dest_setaCor

ON=@DROPON_GROUND
dest_setaCor

//*****************************************************************************
// t_balao
//*****************************************************************************
[TYPEDEF t_balao]

ON=@STEP
src.emotered quebrou a vidraria
sfx SND_GLASS
remove
return 1

ON=@DROPON_ITEM
balao_setaCor

ON=@DROPON_GROUND
balao_setaCor

//*****************************************************************************
// t_misturador
//*****************************************************************************
[TYPEDEF t_misturador]

ON=@STEP
src.emotered quebrou a vidraria
sfx SND_GLASS
remove
return 1

ON=@DROPON_ITEM
mist_setaCor

ON=@DROPON_GROUND
mist_setaCor

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// apagaEspiriteira
//*****************************************************************************
[FUNCTION apagaEspiriteira]
more1=0
dispid=i_heating_stand
sfx=02b
more2=<timer>
timer=-1
update

//*****************************************************************************
// f_checkExtrato( fezExtrato, uidExtrato )
//*****************************************************************************
[FUNCTION f_checkExtrato]
//se falhou, devolve tubo vazio
if (!<argn1>)
    serv.newitem i_tubo
    new.cont=<uid>
endif

//*****************************************************************************
// despeja_tubo( gera_tubo_vazio )
//*****************************************************************************
[FUNCTION despeja_tubo]
sfx 0240
if (<argv[0]>)
  serv.newitem i_tubo
  new.cont=<uid>
endif

//*****************************************************************************
// get_color
//*****************************************************************************
[function get_color]
if (<more1><2)
 return 0f
elif (<more1> < 4)
  return 028
elif (<more1> < 8)
 return 047e
elif (<more1> < 10)
 return 032
elif (<more1> < 20)
 return 02c
elif (<more1> < 40)
 return 016d
elif (<more1> < 80)
 return 09a
elif (<more1> < 100)
 return 03c1
elif (<more1> < 200)
 return 03be
else
 return 034
endif

//*****************************************************************************
// find_name
//*****************************************************************************
[function find_name]

DOSWITCH (<more2>)
    LOCAL.px=extrato de
    LOCAL.px=destilado de
    LOCAL.px=borra de
    LOCAL.px=composto de
    LOCAL.px=
    LOCAL.px=composto de
    LOCAL.px=composto de
ENDDO

if (<more1>=0001)
 return <LOCAL.px> perola negra
elif (<more1>=0002)
 return <LOCAL.px> musgo sangue
elif (<more1>=0004)
 return <LOCAL.px> alho
elif (<more1>=0008)
 return <LOCAL.px> ginseng
elif (<more1>=0010)
 return <LOCAL.px> raiz de mandrake
elif (<more1>=0020)
 return <LOCAL.px> nightshade
elif (<more1>=0040)
 return <LOCAL.px> cinza sulforoza
elif (<more1>=0080)
 return <LOCAL.px> teia de aranha
elif (<more1>=0100)
 return <LOCAL.px> asa de morcego
elif (<more1>=0200)
 return <LOCAL.px> olho de morto-vivo
 
elif (<more1>=03)
 return solucao rubra escura
elif (<more1>=05)
 return solucao negra
elif (<more1>=06)
 return solucao rubra
elif (<more1>=09)
 return composto de Deniz
elif (<more1>=0a)
 return composto benigno
elif (<more1>=0c)
 return composto de Belline
elif (<more1>=011)
 return sulucao alpha
elif (<more1>=012)
 return solucao raiz-de-fungo
elif (<more1>=014)
 return composto raiz forte
elif (<more1>=018)
 return composto de raizes
elif (<more1>=021)
 return solucao sombra de perola
elif (<more1>=022)
 return solucao sombra de musgo
elif (<more1>=024)
 return composto de Flamarion
elif (<more1>=028)
 return composto dos sonhos
elif (<more1>=030)
 return solucao de Pulliani
elif (<more1>=041)
 return solucao da chama
elif (<more1>=042)
 return composto maligno
elif (<more1>=044)
 return solucao Ohmega
elif (<more1>=048)
 return composto de Tearina
elif (<more1>=050)
 return sulucao de cinzadrake
elif (<more1>=060)
 return solucao fogo-da-noite
elif (<more1>=081)
 return composto de Simn
elif (<more1>=082)
 return solucao sangue-de-aranha
elif (<more1>=084)
 return solucao veneno de aranha
elif (<more1>=088)
 return composto 88
elif (<more1>=090)
 return solucao de aranha velha
elif (<more1>=0a0)
 return composto da aranha noturna
elif (<more1>=0c0)
 return solucao da teia de ouro
elif (<more1>=0101)
 return solucao vida voadora
elif (<more1>=0102)
 return composto de Vividus
elif (<more1>=0104)
 return solucao de Anelion
elif (<more1>=0108)
 return composto carnigolein
elif (<more1>=0110)
 return solucao de Hulier
elif (<more1>=0120)
 return composto sucarinus
elif (<more1>=0140)
 return solucao asa-morta
elif (<more1>=0180)
 return solucao asa de aranha
elif (<more1>=0201)
 return liquido olho-marinho
elif (<more1>=0202)
 return solucao de Anelion II
elif (<more1>=0204)
 return liquido ardente
elif (<more1>=0208)
 return ministrado de Sofia
elif (<more1>=0210)
 return composto firiudiplutal
elif (<more1>=0220)
 return solucao venen'Olhos
elif (<more1>=0240)
 return ministrado metoroidal
elif (<more1>=0280)
 return mistura de teia de aranha com olho de morto-vivo
elif (<more1>=0300)
 return coisa viscosa   
else
 return mistura
endif
update
return 1

//*****************************************************************************
// faz_pocao
//*****************************************************************************
//default eh o item com a pocao (more1-mascara ingredientes more2-tipo)
[function faz_pocao]
 if (<more1>==01)
  serv.newitem=i_bottle_red
  new.name=pocao rubra
 elif (<more1>==02)
  serv.newitem=i_bottle_blue
  new.name=pocao azul
 elif (<more1>==03) 
  serv.newitem=i_potion_magic_reflection
  more2=1
 elif (<more1>==04)
  serv.newitem=i_bottle_orange
  new.name=pocao alaranjada
 elif (<more1>==05)
  serv.newitem=i_potion_death_mask
  new.name=pocao mascara da morte
 elif (<more1>==08)
  serv.newitem=i_bottle_yellow
  new.name=pocao amarela
 elif (<more1>==09)
  serv.newitem=i_bottle_yellow
  new.name=pocao amarela
 elif (<more1>==0c)
  serv.newitem=i_bottle_orange
  new.name=pocao alaranjada
 elif (<more1>==010)
  serv.newitem=i_bottle_white
  new.name=pocao alva
 elif (<more1>==011) && (strmatch(<src.tag.raca>,Elfo))
  serv.newitem=i_potion_morvor
  more2=1
 elif (<more1>==012)
  serv.newitem=i_bottle_red
  new.name=pocao rubra
 elif (<more1>==014) && (<src.alchemy> >= 780)
  serv.newitem=i_potion_encolher
  more2=1
 elif (<more1>==018) && (strmatch(<src.tag.raca>,Orc))
  serv.newitem=i_potion_furor
  more2=1
 elif (<more1>==020)
  serv.newitem=i_bottle_green
  new.name=pocao verde
 elif (<more1>==021)
  serv.newitem=i_potion_veneno_roxo
  new.name=pocao roxa escura
 elif (<more1>==022)
  serv.newitem=i_bottle_white
  new.name=pocao alva
 elif (<more1>==028) && (strmatch(<src.tag.raca>,Humano))
  serv.newitem=i_potion_formula_21
  more2=1
 elif (<more1>==040)
  serv.newitem=i_bottle_purple
  new.name=pocao rozea
 elif (<more1>==041)
  serv.newitem=i_bottle_purple
  new.name=pocao rozea
 elif (<more1>==042)
  serv.newitem=i_bottle_blue
  new.name=pocao azul
 elif (<more1>==048) && (<src.alchemy> >= 925)
  serv.newitem=i_potion_sopro_de_fogo
  more2=1
 elif (<more1>==060)
  serv.newitem=i_potion_explosao_terrestre
  more2=1
 elif (<more1>==080)
  serv.newitem=i_bottle_black
  new.name=pocao negra
 elif (<more1>==084)
  serv.newitem=i_potion_invis
  new.name=pocao negra
  more2=1
 elif (<more1>==090)
  serv.newitem=i_potion_poly_urso
  new.name=pocao do urso cinzento
  more2=1
 elif (<more1>==0c0)
  serv.newitem=i_bottle_black
  new.name=pocao negra
 elif (<more1>==0100)
  serv.newitem=i_potion_mana
  new.name=pocao prateada
 elif (<more1>==0101)
  serv.newitem=i_potion_poly_galinha
  new.name=pocao da coragem
 elif (<more1>==0200)
  serv.newitem=i_potion_Protection
  new.name=pocao violeta
 elif (<more1>==0202)
  serv.newitem=i_potion_Protection_max
  new.name=pocao azulada
 else
  src.sysmessage Essa pocao nao parece ter utilidade alguma.
  serv.newitem i_tubo
  new.bounce
  //devolve garrafa consumida
  serv.newitem i_bottle_empty
  new.bounce
  return 1
endif

//Deixa as poções feitas como identificadas
new.attr |= attr_identified

doswitch <more2>
 new.more2=890
 new.more2=1000
 new.more2=1500
 new.more2=2000
enddo
new.bounce
return 1

//*****************************************************************************
// recpt_step( recipiente )
//*****************************************************************************
//anima destilador em uso
[FUNCTION recpt_step]
obj=<argv[0]>
ref1=<obj.tag.espiriteira>
//verifica se apagou a espiriteira ou se moveu
if (!<ref1.more1>) || (<obj.p.x>!=<ref1.p.x>) || (<obj.p.y>!=<ref1.p.y>)
    ref1=<findid.i_faz_item.uid>
    ref1.tag.falha=1
    return 1
endif

if (<obj.type>==t_destilador)
    DORAND (5)
        obj.link.emoteyellow destilando
        obj.emotered aquecendo
        obj.sfx 024
    ENDDO
elif (<obj.type>==t_balao)
    DORAND (5)
        obj.link.emoteyellow preparando
        obj.emotered aquecendo
        obj.sfx 020
    ENDDO
else
    DORAND (10)
        obj.link.emotegreen tenso
        obj.link.emotegreen preparando
        obj.link.emotegreen preparando
        obj.emoteyellow aquecendo
        obj.emoteyellow aquecendo
        obj.say @028 click!
        obj.sfx 050
        obj.sfx 020
        obj.sfx 050
        obj.sfx 024
    ENDDO
endif
//*****************************************************************************
// recpt_end( recipiente, sucesso, nada )
//*****************************************************************************
//termina destilacao
[FUNCTION recpt_end]
ref1=<argv[0]>
if (<argv[1]>)
    if (<ref1.type>==t_destilador)
        ref1.more2=2 //pronto
        ref1.name=destilador (<ref1.find_name>)
        ref1.link.emotegreen destilou
    elif (<ref1.type>==t_balao)
        ref1.more2=2 //pronto
        ref1.name=balao (<ref1.find_name>)
        ref1.link.emotegreen terminou
    elif (<ref1.type>==t_misturador)
        ref1.more2=7 //pronto
        ref1.name=misturador (<ref1.find_name>)
        ref1.link.emotegreen terminou
    endif
else
    if (!<R4>)
        if (<ref1.type>==t_misturador)
            ref1.sfx 011d
            serv.newitem=i_fx_explosion_2
            new.p=<ref1.p>
            new.timer=2
            new.attr=02
            ref1.link.damage {10 15} 0010 <ref1.link>
        endif
        ref1.link.emotered quebrou a vidraria
        ref1.sfx SND_GLASS
        ref1.remove
    else
        ref1.more1=0 //vazio
        ref1.more2=0 //vazio
        ref1.link.emotegreen falhou

        if (<ref1.type>==t_destilador)
            ref1.name=destilador
            ref1.dest_setaCor
        elif (<ref1.type>==t_balao)
            ref1.name=balao
            ref1.balao_setaCor
        elif (<ref1.type>==t_misturador)
            ref1.name=misturador
            ref1.mist_setaCor            
        endif
//        ref1.update
  endif
endif

//impede uso do .fazitem
obj=<findid.i_faz_item.uid>
obj.tag.abort=1

//*****************************************************************************
// alc_recptDclick
//*****************************************************************************
//funcao do dclick dos recipientes de alquimia (destilador,balao)
[FUNCTION alc_recptDclick]
 if (!<more2>) //vazio
    src.sysmessageyellow Utilize um tubo cheio.
 elif (<more2>==01)//com algo para destilar/aquecer
    src.sysmessageyellow Selecione a espiriteira
    TARGET
 elif (<more2>==02)//com destilado/borra
    src.sysmessageyellow Selecione outro recipiente
    TARGET
 endif

//*****************************************************************************
// alc_recptTarget
//*****************************************************************************
//funcao do target dos recipientes de alquimia (destilador,balao,misturador)
[FUNCTION alc_recptTarget]
if (<more2>==1) || (<more2>==6) //com algo para destilar/aquecer/misturar
    if (<src.targ.type>==t_espiriteira)
        if (<src.targ.cont>)
            src.sysmessageyellow Nao pode usar a espiriteira ai.
            return 1
        elif (!<src.targ.more1>)//espiriteira apagada?
            src.sysmessageyellow Acenda a espiriteira.
            return 1
        elif (<src.targ.distance>>1)//muito longe?
            src.sysmessageyellow Chegue mais perto.
            return 1
        endif
        //se ja tiver algo na espiriteira, tira antes.
        if (<src.targ.link>) && (<src.targ.link.topobj>!=<src>)
            src.targ.link.bounce
        endif
        p=<src.targ.p>
        src.targ.link=<uid>
        link=<src>
        tag.espiriteira=<src.targ>
        obj=<src.findid.i_faz_item.uid>
        obj.cleartags
        obj.tag.skill=alchemy
        obj.tag.dif=<eval <DEF.alch_dif_<type>>>
        obj.tag.delay=<eval 20-(<src.alchemy>/100)> //10 a 20
        obj.tag.item=1 
        obj.tag.f_step=recpt_step <uid>,
        obj.tag.f_done=recpt_end <uid>,
        src.fazitem     
        return 1
    else
        src.sysmessageyellow Selecione uma espiriteira acesa.
        return 1
    endif
elif (<more2>==2) || (<more2>==7)//com destilado/borra/mistura
    if (<src.targ.type>==t_tubo)
        src.targ.decrement
//      src.despeja_tubo 1
        sfx 0240
    serv.newitem i_tubo_cheio
        new.more1=<more1>
        more1=0//vazio
        more2=0//vazio

        if (<type>==t_destilador)
            new.more2=1 //destilado
            new.name=<NEW.find_name>
            name=destilador
            dest_setaCor
        elif (<type>==t_balao)
            new.more2=2 //borra
            new.name=<NEW.find_name>
            name=balao
            balao_setaCor
        elif (<type>==t_misturador)
            new.more2=3 //mistura
            new.name=<NEW.find_name>
            name=misturador
            mist_setaCor
        endif

        new.color=<new.get_color>
        new.cont=<src>
        new.update
    else
        src.sysmessageyellow Selecione um tubo vazio.
    endif
endif
return 1

//*****************************************************************************
// dest_setaCor
//*****************************************************************************
//seta dispid do destilador de acordo com conteudo
[FUNCTION dest_setaCor]
if (<more1>==01)            //bp
    dispid=i_beaker2_blue
elif (<more1>==02)      //bm
    dispid=i_beaker2_red
elif (<more1>==04)      //g
    dispid=i_beaker2_teal
elif (<more1>==08)      //g
    dispid=i_beaker2_red
elif (<more1>==010)     //mr
    dispid=i_beaker2_red
elif (<more1>==020)     //ns
    dispid=01837                //verde
elif (<more1>==040)     //sa
    dispid=01837                //verde
elif (<more1>==080)     //ss
    dispid=i_beaker2_blue
elif (<more1>==0100)    //am
    dispid=i_beaker2_blue
elif (<more1>==0200)    //om
    dispid=i_beaker2_blue
else
    dispid=i_beaker2_empty
endif
update

//*****************************************************************************
// balao_setaCor
//*****************************************************************************
//seta dispid do balao de acordo com conteudo
[FUNCTION balao_setaCor]
if (<more1>==01)            //bp
    dispid=0182a                //azul
elif (<more1>==02)      //bm
    dispid=0182c                //vermelho
elif (<more1>==04)      //g
    dispid=0182b                //amarelo
elif (<more1>==08)      //gi
    dispid=0182b                //amarelo
elif (<more1>==010)     //mr
    dispid=0182c                //vermelho
elif (<more1>==020)     //ns
    dispid=0182a                //azul
elif (<more1>==040)     //sa
    dispid=0182b                //amarelo
elif (<more1>==080)     //ss
    dispid=0182a                //azul
elif (<more1>==0100)    //bw
    dispid=0182c                //vermelho
elif (<more1>==0200)    //om
    dispid=0182a                //azul
else
    dispid=0182d                //cinza
endif
update

//*****************************************************************************
// mist_setaCor
//*****************************************************************************
//seta dispid do misturador de acordo com conteudo
[FUNCTION mist_setaCor]
if (<more1>==01)        //bp
    dispid=01839                //roxo
elif (<more1>==02)      //bm
    dispid=01838                //vermelho
elif (<more1>==04)      //g
    dispid=01839                //roxo
elif (<more1>==08)      //g
    dispid=01838                //vermelho
elif (<more1>==010)     //mr
    dispid=01839                //roxo
elif (<more1>==020)     //ns
    dispid=01838                //vermelho
elif (<more1>==040)     //sa
    dispid=01839                //roxo
elif (<more1>==080)     //ss
    dispid=0183a                //cinza
elif (<more1>==0100)    //bw
    dispid=01839                //roxo
elif (<more1>==0200)    //om
    dispid=01839                //roxo
elif (<more1>==0)       //vazio
    dispid=0183a                //cinza
else    
    dispid=01838                //vermelho
endif
update

//*****************************************************************************
// ALQUIMIANOW
//*****************************************************************************
[FUNCTION ALQUIMIANOW]
src.newitem=i_tubo
src.act.amount=5
src.act.bounce

src.newitem=i_destilador
src.act.amount=3
src.act.bounce

src.newitem=i_balao
src.act.amount=3
src.act.bounce

src.newitem=i_espiriteira
src.act.bounce

src.newitem=i_almofariz
src.act.bounce

src.newitem=i_bottle_empty
src.act.amount=5
src.act.bounce

src.newitem=i_misturador
src.act.amount=3
src.act.bounce

src.newitem=i_r_alho
src.act.amount=30
src.act.bounce

src.newitem=i_r_perola_negra
src.act.amount=30
src.act.bounce

src.newitem=i_r_ginseng
src.act.amount=30
src.act.bounce

src.newitem=i_r_musgo_sangue
src.act.amount=30
src.act.bounce

src.newitem=i_r_raiz_de_mandrake
src.act.amount=30
src.act.bounce

src.newitem=i_r_nightshade
src.act.amount=30
src.act.bounce

src.newitem=i_r_cinza_sulfurosa
src.act.amount=30
src.act.bounce

src.newitem=i_r_teia_de_aranha
src.act.amount=30
src.act.bounce


//*****************************************************************************
//*****************************************************************************
// ITEMDEFs - Reagentes
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_r_perola_negra
//*****************************************************************************
[ITEMDEF i_r_perola_negra]
NAME=perola negra
DEFNAME=i_r_perola_negra
ID=i_reag_black_pearl
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Pérola negra

ON=@CREATE
more1=001

//*****************************************************************************
// i_r_musgo_sangue
//*****************************************************************************
[ITEMDEF i_r_musgo_sangue]
NAME=musgo sangue
DEFNAME=i_r_musgo_sangue
ID=i_reag_blood_moss
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Musgo sangue

ON=@CREATE
more1=002

//*****************************************************************************
// i_r_alho
//*****************************************************************************
[ITEMDEF i_r_alho]
NAME=alho
DEFNAME=i_r_alho
ID=i_reag_garlic
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Alho

ON=@CREATE
more1=004

//*****************************************************************************
// i_r_ginseng
//*****************************************************************************
[ITEMDEF i_r_ginseng]
NAME=ginseng
DEFNAME=i_r_ginseng
ID=i_reag_ginseng
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=ginseng

ON=@CREATE
more1=008

//*****************************************************************************
// i_r_raiz_de_mandrake
//*****************************************************************************
[ITEMDEF i_r_raiz_de_mandrake]
NAME=raiz de mandrake
DEFNAME=i_r_raiz_de_mandrake
ID=i_reag_mandrake_root
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Mandrake root

ON=@CREATE
more1=010

//*****************************************************************************
// i_r_nightshade
//*****************************************************************************
[ITEMDEF i_r_nightshade]
NAME=nightshade
DEFNAME=i_r_nightshade
ID=i_reag_nightshade
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Nightshade

ON=@CREATE
more1=020

//*****************************************************************************
// i_r_cinza_sulfurosa
//*****************************************************************************
[ITEMDEF i_r_cinza_sulfurosa]
NAME=cinza sulfurosa
DEFNAME=i_r_cinza_sulfurosa
ID=i_reag_sulfur_ash
TYPE=t_reagente
SKILLMAKE=MINING 20.0
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Cinza sulfurosa

ON=@CREATE
more1=040

//*****************************************************************************
// i_r_teia_de_aranha
//*****************************************************************************
[ITEMDEF 0f8d]
NAME=teia de aranha
DEFNAME=i_r_teia_de_aranha
//ID=i_reag_spider_silk
TYPE=t_reagente
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Teia de aranha

ON=@CREATE
more1=080

//*****************************************************************************
// i_r_asa_morcego
//*****************************************************************************
[ITEMDEF 0f78]
//i_reag_BATWING
DEFNAME=i_r_asa_morcego
name=asa de morcego
TYPE=t_reagente
VALUE=3
PILE=1
WEIGHT=.1


CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Asa de morcego

ON=@CREATE
more1=0100

//*****************************************************************************
// i_r_olho_morto_vivo
//*****************************************************************************
[ITEMDEF 0f87]
// Eye of Newt
NAME=olho de morto-vivo
DEFNAME=i_r_olho_morto_vivo
TYPE=t_reagente
VALUE=3
PILE=1
WEIGHT=.1

CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Olho de morto-vivo

ON=@CREATE
more1=0200

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs - Acessorios/Ferramentas
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_tubo
//*****************************************************************************
[ITEMDEF i_tubo]
NAME=tubo de ensaio
ID=i_vial_empty
TYPE=t_tubo
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=tubo de ensaio

//*****************************************************************************
// i_tubo_cheio
//*****************************************************************************
//more1 reagente
//more2 tipo (extrato,destilado,borra,mistura)
[ITEMDEF i_tubo_cheio]
NAME=BUG!
ID=i_vial_empty
TYPE=t_tubo_cheio

ON=@DCLICK
src.sysmessageyellow Selecione o outro recipiente
TARGET
return 1

ON=@TARGON_ITEM
if (<src.targ.type>!=t_destilador) && (<src.targ.type>!=t_balao) && (<src.targ.type>!=t_misturador) && (<src.targ.type>!=t_potion_empty)
    src.sysmessageyellow Use em destiladores, baloes, misturadores ou frascos para pocao.
    return 1
endif

if (<src.targ.type>==t_destilador) || (<src.targ.type>==t_balao)
    if (<more2>)//se reagente do tubo nao for extrato
        src.sysmessageyellow Voce nao pode preparar isso.
        return 1
    endif
    if (<src.targ.more2>)
        src.sysmessageyellow Ja esta sendo usado.
        return 1
    endif

    decrement   
    src.despeja_tubo 1
 
    //passa dados do extrato para o destilador e diz que esta em uso
    src.targ.more1=<more1>
    src.targ.more2=1

    if (<src.targ.type>==t_destilador)
        src.targ.dest_setaCor
        src.targ.name=destilador (<name>)
    else
        src.targ.balao_setaCor
        src.targ.name=balao (<name>)
    endif
    
    src.targ.update
    return 1

elif (<src.targ.type>==t_misturador)
    if (!<more2>)
        src.sysmessageyellow Extratos sao muito impuros para serem misturados.
        return 1
    elif (<more2>==3)
        src.sysmessageyellow Voce nao pode misturar isso.
        return 1
    elif (<src.targ.more2>>=6)
        src.sysmessageyellow Nao pode adicionar um terceiro produto
        return 1
    endif

    decrement   
    src.despeja_tubo 1

    if (!<src.targ.more2>)
        src.targ.more2=5
    else
        src.targ.more2=6
    endif

    src.targ.more1=<src.targ.more1>|<more1>
    src.targ.name=misturador [ pre <src.targ.find_name> ]
    src.targ.mist_setaCor
    src.targ.update
    return 1
    
elif (<src.targ.type>==t_potion_empty)
    decrement   
    src.despeja_tubo 1
    src.targ.decrement
    faz_pocao
endif
return 1

//*****************************************************************************
// i_almofariz
//*****************************************************************************
[ITEMDEF i_almofariz]
NAME=almofariz
ID=i_mortar_pestle
TYPE=t_almofariz
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Almofariz

ON=@DCLICK
if (<topobj>!=<src.uid>)
    src.sysmessagered O almofariz precisa estar na sua mala
    return 1
endif
src.sysmessageyellow Selecione o reagente
TARGET 
return 1


ON=@TARGON_ITEM
if (<src.targ.type> == t_reagente)
 obj=<src.findid.i_faz_item.uid>
 obj.cleartags
 obj.tag.uid1=<src.targ.uid>
 obj.tag.amt1=1
 obj.tag.nam1=<src.targ.name>
 obj.tag.res2=i_tubo
 obj.tag.amt2=1
 obj.tag.nam2=tubo de ensaio vazio
 obj.tag.skill=alchemy
 obj.tag.dif=40
 obj.tag.emote=misturando <src.targ.name>
 obj.tag.sfx=578
 obj.tag.delay=<eval 13-(<src.alchemy>/100)> //3 a 13
 obj.tag.item=i_tubo_cheio
 obj.tag.color=<src.targ.get_color>
 obj.tag.more1=<src.targ.more1>
 obj.tag.more2=0//extrato
 obj.tag.name=extrato de <src.targ.name>
 obj.tag.f_end=despeja_tubo
 obj.tag.f_done=f_checkExtrato
 src.fazitem
elif (<src.targ.BaseID> == i_carvao_mineral)
 obj=<src.findid.i_faz_item.uid>
 obj.cleartags
 obj.tag.res1=i_carvao_mineral
 obj.tag.amt1=1
 obj.tag.nam1=<src.targ.name>
 obj.tag.res2=i_bottle_empty
 obj.tag.amt2=1
 obj.tag.nam2=garrafa alquimica
 obj.tag.skill=alchemy
 obj.tag.dif=40
 obj.tag.emote=misturando <src.targ.name>
 obj.tag.sfx=578
 obj.tag.delay=<eval 13-(<src.alchemy>/100)> //3 a 13
 obj.tag.item=i_pen_and_ink
 obj.tag.more1=<between 8,256,<src.alchemy>,2000>
 src.fazitem
else
 src.sysmessagered Use o almofariz em reagentes ou carvao.
endif
return 1

 
//*****************************************************************************
// i_destilador
//*****************************************************************************
//more1 mascara reagentes
//more2 status (vazio,a destilar,destilado)
[ITEMDEF i_destilador]
NAME=destilador
ID=i_beaker2_empty
TYPE=t_destilador
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Destilador

ON=@DCLICK
alc_recptDclick
return 1

ON=@TARGON_ITEM
alc_recptTarget
return 1

//*****************************************************************************
// i_balao
//*****************************************************************************
//more1 mascara reagentes
//more2 status (vazio,a aquecer,aquecido)
[ITEMDEF i_balao]
NAME=balao
ID=0182d
TYPE=t_balao
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Balão

ON=@DCLICK
alc_recptDclick
return 1

ON=@TARGON_ITEM
alc_recptTarget
return 1

//*****************************************************************************
// i_espiriteira
//*****************************************************************************
//more1 desligada/ligada
//more2 tempo de vida
[ITEMDEF i_espiriteira]
NAME=espiriteira
ID=i_heating_stand
TYPE=t_espiriteira
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Espiriteira

ON=@CREATE
more1=0
more2=1000
attr=attr_can_decay

//*****************************************************************************
// i_misturador
//*****************************************************************************
//more1 mascara reagentes
//more2 status (nada,nada,nada,nada,vazio,1reg,2rea a aquecer,aquecido)
[ITEMDEF i_misturador]
NAME=misturador
ID=0183a
TYPE=t_misturador
CATEGORY=MyT - Items by Professions
SUBSECTION=Alchemist
DESCRIPTION=Misturador

ON=@CREATE
more2=4

ON=@DCLICK
//more2=4 eh significa nada dentro (para poder usar mesmo scp com balao e destilador)
if (<more2><04)
    more2=4
endif
if (<src.alchemy> < 650)
    src.sysmessagered Voce nao tem experiencia para fazer isso.
elif (<more2><06)
    src.sysmessagegreen Coloque dois produtos para usar.
elif (<more2>==06)
    src.sysmessageyellow Selecione a espiriteira
    TARGET
elif (<more2>==07)
    src.sysmessageyellow Selecione outro recipiente
    TARGET
endif
return 1

ON=@TARGON_ITEM
alc_recptTarget
return 1

[EOF]