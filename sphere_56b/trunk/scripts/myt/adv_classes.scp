//*************************************************************
//			NECROMANTE
//*************************************************************

[ITEMDEF i_necronomico]
ID=i_spellbook_necromancy
NAME=necronomico
TYPE=t_wand
TEVENTS=e_focus

on=@create
tag.name=Necronomico
ATTR=020

on=@dclick
if (<topobj.uid>==<src.uid>) && (<p.z>==1)
 dialogclose d_book
 if (strmatch(<tag.author>,))
  tag.author=<src.tag.name>
  updatex
 endif
 src.cTAG.book=<UID>
 src.ctag.book.page=1
 src.DIALOG d_book
elif (<topobj.uid>==<src.uid>)
 src.equip <UID>
 src.mago
 return 1
endif

on=@ClientToolTip
src.addcliloc 1042971,<TAG.name>
src.addcliloc 1070722,De: <TAG.author>
return 1


//*************************************************************
//			DRUIDA
//*************************************************************

[ITEMDEF i_cajado_druida]
ID=i_staff_gnarled
NAME=cajado druida
WEIGHT=5
TYPE=T_WEAPON_MACE_STAFF
FLIP=1
DAM=15,17
SPEED=33
REQSTR=50
TWOHANDS=Y
TEVENTS=e_focus

CATEGORY=MyT Provisions - Weapons
SUBSECTION=Adv Class
DESCRIPTION=Cajado Druida

on=@create
ATTR=020
MORE1=03000300
COLOR=0b92

on=@dclick
src.equip <UID>
src.mago
return 1

//*************************************************************
//			Paladino
//*************************************************************

[DEFNAME adv_pala_timers]
pala_cure_time		15*60*10
pala_str_time		10*60*10
pala_reactive_timer	45*60*10
pala_bless_wpn		60*60*10
pala_cure_mana		20
pala_str_mana		20
pala_reactive_mana	30
pala_bless_wpn_mana	35


[SPEECH spk_paladin]

on=Ex Nox
_f_adv_pala_cure

on=In Bet Mani
_f_adv_pala_heal

on=Uus Bet Sanct
_f_adv_pala_strength

on=In Bet Sanct
_f_adv_pala_reactiveAR

on=In Ort Rel
_f_adv_pala_bless_wpn

on=In Ort Flam
_f_adv_pala_fire_wpn

on=In Ort Quas
_f_adv_pala_cold_wpn


[TYPEDEF e_adv_paladin]
on=@dclick
if ((<src.uid> == <topobj.uid>) && (STRMATCH(Paladino,<src.tag.advclass>))) || (<src.IsGM>)
 src.skillmenu sm_adv_paladin
endif
return 1

[skillmenu sm_adv_paladin]
Selecione um poder

on=i_rune_cure Curar enfermidade
_f_adv_pala_cure

on=i_rune_heal Curar ferimentos de batalha
_f_adv_pala_heal

on=i_rune_strength Fortalecer musculos
_f_adv_pala_strength

on=i_rune_reactive_armor Armadura magica
_f_adv_pala_reactiveAR

on=i_rune_bless Encantar Arma (bencao divina)
_f_adv_pala_bless_wpn

on=i_rune_summon_elem_fire Encantar Arma (fogo)
_f_adv_pala_fire_wpn

on=i_rune_summon_elem_water Encantar Arma (gelo)
_f_adv_pala_cold_wpn


//*************************************************************
//	Function paladino q mandam para as FUNCTIONS de magias
//*************************************************************
//    CTAG.castando_pala = Evita exploit de repeticao
//*************************************************************

[FUNCTION _f_adv_pala_cure]
IF (<src.findid.i_anel_paladino.tag0.curet> < <serv.time>) && (<src.CTAG.castando_pala> != 1)
anim 17
timerf 3,f_adv_pala_cure
src.CTAG.castando_pala = 1
ELSE
  LOCAL.tempo = <eval <eval <src.findid.i_anel_paladino.tag0.curet>-<serv.time>>/600)>
  src.sysmessageyellow Podera usar este poder novamente em <dLOCAL.tempo> minutos.
ENDIF 

[FUNCTION _f_adv_pala_heal]
IF (<src.findid.i_anel_paladino.tag0.heall> < <serv.time>) && (<src.CTAG.castando_pala> != 1)
anim 17
timerf 5,f_adv_pala_heal
src.CTAG.castando_pala = 1
ELSE
  LOCAL.tempo = <eval <eval <src.findid.i_anel_paladino.tag0.heall>-<serv.time>>/600)>
  src.sysmessageyellow Podera usar este poder novamente em <dLOCAL.tempo> minutos.
ENDIF 

[FUNCTION _f_adv_pala_strength]
IF (<src.findid.i_anel_paladino.tag0.strenghtt> < <serv.time>) && (<src.CTAG.castando_pala> != 1)
anim 17
timerf 3,f_adv_pala_strength
src.CTAG.castando_pala = 1
ELSE
  LOCAl.tempo = <eval <eval <src.findid.i_anel_paladino.tag0.strenghtt>-<serv.time>>/600)>
  src.sysmessageyellow Podera usar este poder novamente em <dLOCAL.tempo> minutos.
ENDIF 

[FUNCTION _f_adv_pala_reactiveAR]
IF (<src.findid.i_anel_paladino.tag0.reactiveart> < <serv.time>) && (<src.CTAG.castando_pala> != 1)
anim 17
timerf 5,f_adv_pala_reactiveAR
src.CTAG.castando_pala = 1
ELSE
  LOCAL.tempo = <eval <eval <src.findid.i_anel_paladino.tag0.reactiveart>-<serv.time>>/600)>
  src.sysmessageyellow Podera usar este poder novamente em <dLOCAL.tempo> minutos.
ENDIF 

[FUNCTION _f_adv_pala_bless_wpn]
IF (<src.findid.i_anel_paladino.tag0.bless.wpn> < <serv.time>) && (<src.CTAG.castando_pala> != 1)
anim 17
timerf 3,f_adv_pala_bless_wpn
src.CTAG.castando_pala = 1
ELSE
  LOCAL.tempo = <eval <eval <src.findid.i_anel_paladino.tag0.bless.wpn>-<serv.time>>/600)>
  src.sysmessageyellow Podera usar este poder novamente em <dLOCAL.tempo> minutos.
ENDIF 

[FUNCTION _f_adv_pala_fire_wpn]
IF (<src.findid.i_anel_paladino.tag0.fire.wpn> < <serv.time>) && (<src.CTAG.castando_pala> != 1)
anim 17
timerf 3,f_adv_pala_fire_wpn
src.CTAG.castando_pala = 1
ELSE
  LOCAL.tempo = <eval <eval <src.findid.i_anel_paladino.tag0.fire.wpn>-<serv.time>>/600)>
  src.sysmessageyellow Podera usar este poder novamente em <dLOCAL.tempo> minutos.
ENDIF 

[FUNCTION _f_adv_pala_cold_wpn]
IF (<src.findid.i_anel_paladino.tag0.cold.wpn> < <serv.time>) && (<src.CTAG.castando_pala> != 1)
anim 17
timerf 3,f_adv_pala_cold_wpn
src.CTAG.castando_pala = 1
ELSE
  LOCAL.tempo = <eval <eval <src.findid.i_anel_paladino.tag0.cold.wpn>-<serv.time>>/600)>
  src.sysmessageyellow Podera usar este poder novamente em <dLOCAL.tempo> minutos.
ENDIF 

//*************************************************************
//	Functions paladino com as MAGIAS e EFEITOS
//*************************************************************

////////////////////////////////////// MAGIA DE CURE ///////////////////////////////////////////////
[FUNCTION f_adv_pala_cure]
sysmessageyellow Quem voce deseja purificar o corpo?
src.targetf f_adv_pala_cure_

[FUNCTION f_adv_pala_cure_]
src.CTAG.castando_pala = 0
if (!<argo.ischar>) || (<argo.uid> == <uid>)
 sysmessageyellow Esse poder so pode ser usado em outros seres vivos a nao ser voce.
elif (!<f_consumeMana <DEF.pala_cure_mana>>)
 sysmessageyellow Voce nao tem energia magica o suficiente.
else
 findid.i_anel_paladino.tag.curet=<eval <serv.time>+<def.pala_cure_time>>
 argo.spelleffect s_cure,<tactics>,<uid>
 sfx snd_spell_cure
 sysmessageyellow Podera usar este poder novamente em <f_timestring <eval <def.pala_cure_time>/10>>.
endif
return 1

////////////////////////////////////// MAGIA DE HEALING ///////////////////////////////////////////////
[FUNCTION f_adv_pala_heal]
sysmessageyellow Quem voce deseja curar?
src.targetf f_adv_pala_heal_

[FUNCTION f_adv_pala_heal_]
src.CTAG.castando_pala = 0
if (!<argo.ischar>) || (<argo.uid> == <uid>)
 sysmessageyellow Esse poder so pode ser usado em outros seres vivos a nao ser voce.
elif (!<f_consumeMana <DEF.pala_cure_mana>>)
 sysmessageyellow Voce nao tem energia magica o suficiente.
else
 findid.i_anel_paladino.tag.heall=<eval <serv.time>+<def.pala_cure_time>>
 argo.spelleffect s_greater_heal 1500
 sfx snd_spell_cure
 sysmessageyellow Podera usar este poder novamente em <f_timestring <eval <def.pala_cure_time>/10>>.
endif
return 1

[FUNCTION f_adv_pala_strength]
src.CTAG.castando_pala = 0
if (!<f_consumeMana <DEF.pala_str_mana>>)
 sysmessageyellow Voce nao tem energia magica o suficiente.
 return 1
endif
findid.i_anel_paladino.tag.strenghtt=<eval <serv.time>+<def.pala_str_time>>
spelleffect s_strength {1500,2000}
sfx snd_spell_strength
sysmessageyellow Podera usar este poder novamente em <f_timestring <eval <def.pala_str_time>/10>>.

////////////////////////////////////// MAGIA REACTIVE ARMOR ///////////////////////////////////////////////
[FUNCTION f_adv_pala_reactiveAR]
src.CTAG.castando_pala = 0
if (!<f_consumeMana <DEF.pala_reactive_mana>>)
 sysmessageyellow Voce nao tem energia magica o suficiente.
 return 1
endif
findid.i_anel_paladino.tag.reactiveart=<eval <serv.time>+<def.pala_reactive_timer>>
spl_applyResBuff 5,5,5,5,0,12 )
sfx snd_spell_strength
sysmessageyellow Podera usar este poder novamente em <f_timestring <eval <def.pala_reactive_timer>/10>>.

////////////////////////////////////// ENCANTAMENTO DE BLESS ///////////////////////////////////////////////
[FUNCTION f_adv_pala_bless_wpn]
src.CTAG.castando_pala = 0
if (!<NPC_CanCast>)
 sysmessagered So pode usar um encantamento quando teminar o efeito do atual.
 return 0
endif
if (!<f_consumeMana <DEF.pala_bless_wpn_mana>>)
 sysmessageyellow Voce nao tem energia magica o suficiente.
 return 0
endif
if (!<weapon>)
 sysmessagered Voce nao tem arma para encantar.
 return 0
endif
findid.i_anel_paladino.tag.bless.wpn=<eval <serv.time>+<def.pala_bless_wpn>>
weapon.more2=17
weapon.morex=7
weapon.timerf 300,f_adv_pala_bless_wpn_		// 5 minutos 
weapon.tag.color=<weapon.color>
weapon.color=007f9
sfx 01f7
effect 2 i_fx_sparkle 0 16 0
spell_delay = 3000 					// 5 minutos 
sysmessageyellow Podera usar outro poder novamente em <f_timestring <eval <def.pala_bless_wpn>/10>>.

[FUNCTION f_adv_pala_bless_wpn_]
color=<tag0.color>
more2=0
morex=0
sfx snd_spell_weaken
updatex

////////////////////////////////////// ENCANTAMENTO DE FOGO ///////////////////////////////////////////////
[FUNCTION f_adv_pala_fire_wpn]
src.CTAG.castando_pala = 0
if (!<NPC_CanCast>)
 sysmessagered So pode usar um encantamento quando teminar o efeito do atual.
 return 0
endif
if (!<f_consumeMana <DEF.pala_bless_wpn_mana>>)
 sysmessageyellow Voce nao tem energia magica o suficiente.
 return 0
endif
if (!<weapon>)
 sysmessagered Voce nao tem arma para encantar.
 return 0
endif
findid.i_anel_paladino.tag.fire.wpn=<eval <serv.time>+<def.pala_bless_wpn>>
weapon.more2=51
weapon.morex=7
weapon.timerf 300,f_adv_pala_fire_wpn_		// 5 minutos 
weapon.tag.color=<weapon.color>
weapon.color=0054e
sfx 1482
effect 2 i_fx_sparkle 0 16 0 55
spell_delay = 3000 					// 5 minutos 
sysmessageyellow Podera usar outro poder novamente em <f_timestring <eval <def.pala_bless_wpn>/10>>.

[FUNCTION f_adv_pala_fire_wpn_]
color=<tag0.color>
more2=0
morex=0
sfx snd_spell_weaken
updatex

////////////////////////////////////// ENCANTAMENTO DE GELO ///////////////////////////////////////////////
[FUNCTION f_adv_pala_cold_wpn]
src.CTAG.castando_pala = 0
if (!<NPC_CanCast>)
 sysmessagered So pode usar um encantamento quando teminar o efeito do atual.
 return 0
endif
if (!<f_consumeMana <DEF.pala_bless_wpn_mana>>)
 sysmessageyellow Voce nao tem energia magica o suficiente.
 return 0
endif
if (!<weapon>)
 sysmessagered Voce nao tem arma para encantar.
 return 0
endif
findid.i_anel_paladino.tag.cold.wpn=<eval <serv.time>+<def.pala_bless_wpn>>
weapon.dmgcold += 3
weapon.timerf 60,f_adv_pala_cold_wpn_   	      // 1 minuto pq nao tem limite de cargas (morex)
weapon.tag.color=<weapon.color>
weapon.color=04f1
sfx 466
effect 2 i_fx_sparkle 0 16 0 0 0112
spell_delay = 600 					// 1 minuto 
sysmessageyellow Podera usar outro poder novamente em <f_timestring <eval <def.pala_bless_wpn>/10>>.

[FUNCTION f_adv_pala_cold_wpn_]
color=<tag0.color>
dmgcold -= 3
sfx snd_spell_weaken
updatex

//*************************************************************
//	ITEMS DO PALADINO
//*************************************************************

////////////////////////////////////// ANEL PALADINO ///////////////////////////////////////////////
[ITEMDEF i_anel_paladino]
ID=i_ring_gold
TYPE t_jewelry
NAME=anel paladino
TEVENTS=e_adv_paladin

CATEGORY=MyT Provisions - Weapons
SUBSECTION=Adv Class
DESCRIPTION=Anel Paladino

on=@create
ATTR=025

on=@equiptest
IF !(STRMATCH(<src.tag.advclass>,Paladino)) && (!<src.IsGM>)
 src.sysmessageyellow O anel nao entra no seu dedo.
 return 1
endif
src.dspeech +spk_paladin

on=@unequip
src.dspeech -spk_paladin

////////////////////////////////////// ESPADA PALADINO ///////////////////////////////////////////////
[ITEMDEF i_espada_paladino]
ID=026ce
NAME=Espada Sagrada
TYPE=T_WEAPON_SWORD
FLIP=1
TWOHANDS=Y
WEIGHT=10.0
DAM=25,45
SKILL=Swordsmanship
REQSTR=85
CATEGORY=MyT Provisions - Weapons
SUBSECTION=Adv Class
DESCRIPTION=Espada Sagrada (paladino)

dupelist=026cf

ON=@CREATE 
HITPOINTS={2000 2500}

////////////////////////////////////// ESCUDO PALADINO ///////////////////////////////////////////////
[ITEMDEF i_escudo_paladino]
ID=02b01
name=Escudo Sagrado
TYPE=t_shield
FLIP=1
WEIGHT=20
ARMOR=20
REQSTR=85
CATEGORY=MyT Provisions - Weapons
SUBSECTION=Adv Class
DESCRIPTION=Escudo Sagrado (paladino)

ON=@Create
HITPOINTS={2000 2500}
MORE1h=<hits>

ON=@equip
src.dex -= 3

ON=@unequip
src.dex += 3


[EOF]