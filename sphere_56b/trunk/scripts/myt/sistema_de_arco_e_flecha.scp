//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE ARCO E FLECHA por 
//
//    Galthar
//    Metus
//    Silver
//    UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:
//-corrigir RESOURCES e SKILLMAKE de todos os ITENS para novo sistema de craft
//-verificar como ficara as mascaras de uso dos barbantes/engrenagens com 
//a multiplicacao dos itens ou mesmo itemforge

//WEAPON
//more2                     Magia/Efeito
//morex                     Cargas de magia/efeito (não pode diminuir)
//morey                     Hits do barbante/engrenagem
//morez                     1 se coletável (do corpo). 0 se a flecha some pra sempre quando atirada.
//TAG.OVERRIDE.AMMOTYPE     Que flecha consome
//TAG.OVERRIDE.AMMOANIMHUE  Cor da animação da flecha disparada
//TAG.OVERRIDE.AMMOANIM     Animação da flecha disparada

//AMMO
//more1                     Magia/Efeito
//more2                     MODAR
//morex                     DMGFIRE
//morey                     DMGCOLD
//morez                     DMGPOISON
//morem                     DMGENERGY
//TAG.anim
//TAG.sfx
//TAG.colect
//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************

[DEFNAME arcos]
arrow_i_arco_curto          001
arrow_i_arco                001
arrow_i_arco_long           002
arrow_i_arco_composto       004
arrow_i_arco_elfico         008
arrow_i_besta               010
arrow_i_besta_pesada        020
arrow_i_besta_repeticao     020

//*****************************************************************************
//*****************************************************************************
// TYPEDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// t_weapon_bolt
//*****************************************************************************
[TYPEDEF t_weapon_bolt]
//MORE1 = Effeito de Spell no cara atingido
//MORE2 = Bônus de dano físico
//MOREX = Bônus de dano fire
//MOREY = Bônus de dano cold
//MOREZ = Bônus de dano poison
//MOREM = Bônus de dano energy
//TAG.COLECT=1 = Arrow PODE ficar no loot do alvo (nem sempre fica)
//tag.anim=i_fx_firesnake = Muda o anim da flecha
//tag.sfx=05c3 = Muda o som da flecha
ON=@DCLICK
correctammo
return 1

//*****************************************************************************
// t_weapon_arrow
//*****************************************************************************
[TYPEDEF t_weapon_arrow]
//MORE1 = Effeito de Spell no cara atingido
//MORE2 = Bônus de dano físico
//MOREX = Bônus de dano fire
//MOREY = Bônus de dano cold
//MOREZ = Bônus de dano poison
//MOREM = Bônus de dano energy
//TAG.COLECT=1 = Arrow PODE ficar no loot do alvo (nem sempre fica)
//tag.anim=i_fx_firesnake = Muda o anim da flecha
//tag.sfx=05c3 = Muda o som da flecha
ON=@DCLICK
correctammo
return 1

//*****************************************************************************
// t_preitem_bowcraft
//*****************************************************************************
[TYPEDEF t_preitem_bowcraft]
ON=@DClick
IF <src.restest 1 i_talhadeira>
    src.skillmenu sm_cr_flechas
else
    src.sysmessage Utilize uma talhadeira para montar as pontas de flecha nas varetas.
endif
return 1

[TYPEDEF t_ponta_flecha]
//Compatibilidade com vendar e crafting

//*****************************************************************************
// t_string
//*****************************************************************************
[TYPEDEF t_string]
//hits=hitmin
//maxhits=hitmax
//more2=bitmask

ON=@DCLICK
src.sysmessageyellow Selecione o arco para reparar...
targetf f_use_string <UID>
return 1

//*****************************************************************************
// t_weapon_ammo
//*****************************************************************************
[TYPEDEF t_weapon_ammo]

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************


//*****************************************************************************
// correctammo
//*****************************************************************************
//Corrige o arco de acordo com a munição
[FUNCTION correctammo] 

LOCAL.wtype=<src.weapon.type>

//garante que esta com arco/dardo e flecha correspondente
if <topobj>!=<src>
    return 1
elif (<LOCAL.wtype>!=t_weapon_bow) && (<LOCAL.wtype>!=t_weapon_xbow)
    return 1
elif  (<LOCAL.wtype>==t_weapon_bow) && (<type>!=T_WEAPON_ARROW)
    return 1
elif  (<LOCAL.wtype>==t_weapon_xbow) && (<type>!=T_WEAPON_BOLT)
    return 1
endif

src.weapon.TAG.OVERRIDE.AMMOTYPE=<baseid>
src.weapon.TAG.OVERRIDE.AMMOANIMHUE=<color>

src.weapon.more2=<more1>
if (<more1>)
    src.weapon.morex=500
else
    src.weapon.morex=0
endif

if (<tag0.anim>)
    src.weapon.TAG.OVERRIDE.AMMOANIM=<tag0.anim>
else
    src.weapon.TAG.OVERRIDE.AMMOANIM
endif

src.weapon.tag.sfx=<tag0.sfx>
src.weapon.tag.colect=<tag0.colect>
src.weapon.morez=<morez>
src.weapon.modAR=<more2>
src.weapon.dmgfire=<morex>
src.weapon.dmgcold=<morey>
src.weapon.dmgpoison=<morez>
src.weapon.dmgenergy=<morem>
src.sysmessageblue Agora usando <name>!

//*****************************************************************************
// f_use_string
//*****************************************************************************
[FUNCTION f_use_string]
OBJ=<argv0>
if !<argo>
    src.sysmessageyellow Deve selecionar um arco.
    return 1
elif (<argo.type>!=t_weapon_bow) && (<argo.type>!=t_weapon_xbow)
    src.sysmessageyellow Isso nao e um arco.
    return 1
elif (<argo.type>==t_weapon_bow) && (<obj.type>!=t_string)
    src.sysmessageyellow Use barbantes em arcos.
    return 1
elif (<argo.type>==t_weapon_xbow) && (<obj.baseid>!=i_gears)
    src.sysmessageyellow Use engrenagens em bestas.
    return 1
//tem barbante/engrenagem antiga?    
elif <argo.morey>
    if (<argo.type>==t_weapon_bow)
        src.sysmessagered Voce removeu o barbante antigo.
    else
        src.sysmessagered Voce removeu a engrenagem antiga.
    endif
    argo.name=<serv.resources.<argo.baseid>.name>
endif

//determinar tipo de arco
LOCAL.arco=<DEF.arrow_<argo.baseid>>

if !(<obj.more2>&<LOCAL.arco>)
    sysmessageyellow Voce nao pode por <obj.name> no <argo.name>
    return 1
endif

if <archery> > <bowcraft>
    LOCAL.skill=<archery>
else
    LOCAL.skill=<bowcraft>
endif

ARGO.morey=<f_rangeValue <obj.more1l>,<obj.more1h>,<LOCAL.skill>>
LOCAL.hits=<eval <argo.maxhits>-<argo.hits>>
LOCAL.hits=<f_rangeValue 0,<LOCAL.hits>,<bowcraft>>
argo.hits=<eval <argo.hits>+{1 <LOCAL.hits>}>

//Bônus para barbante abençoado
if (<obj.attr>&0401) //attr_blessed|attr_identified
    ARGO.morey=<eval (<ARGO.morey>*125)/100>
    obj.name=<obj.name> abencoado
    sfx 0f6
else
    sfx 04f
endif

LOCAL.name=<serv.resources.<argo.baseid>.name>
emoteorange reparando <LOCAL.name>
sysmessageblue <LOCAL.name> reparado com sucesso!
argo.name=<LOCAL.name> (<obj.name>)
obj.decrement
argo.update

//*****************************************************************************
// f_fire_arrow_consume
//*****************************************************************************
[FUNCTION f_fire_arrow_consume]
if <topobj.IsPlayer>
    topobj.sysmessageyellow As flechas de fogo se consumiram.
endif
remove

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs - DARDOS
//*****************************************************************************
//*****************************************************************************


//*****************************************************************************
// i_xbolt
//*****************************************************************************
[ITEMDEF 01bfb]
NAME=dardo%s%
DEFNAME=i_xbolt
TYPE=T_WEAPON_BOLT
FLIP=1
REPLICATE=1
RESOURCES=1 i_arrow_shaft, 1 i_feather
SKILLMAKE=17.8 BOWCRAFT
WEIGHT=0.1
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=dardo simples
DUPELIST=01bfc,01bfd,01bfe

//*****************************************************************************
// i_xbolt_iron
//*****************************************************************************
[ITEMDEF i_xbolt_iron]
ID=01bfb
NAME=dardo%s% (ponta de metal)
TYPE=T_WEAPON_BOLT
FLIP=1
REPLICATE=1
RESOURCES=1 i_arrow_shaft, 1 i_feather
SKILLMAKE=17.8 BOWCRAFT
WEIGHT=0.1
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=dardo de ferro
DUPELIST=01bfc,01bfd,01bfe

ON=@CREATE
color=0482
more2=3     //MODAR

//*****************************************************************************
// i_xbolt_silver
//*****************************************************************************
[ITEMDEF i_xbolt_silver]
ID=01bfb
NAME=dardo%s% (ponta de prata)
TYPE=T_WEAPON_BOLT
FLIP=1
REPLICATE=1
RESOURCES=1 i_arrow_shaft, 1 i_feather
SKILLMAKE=17.8 BOWCRAFT
WEIGHT=0.1
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=dardo de prata
DUPELIST=01bfc,01bfd,01bfe

ON=@CREATE
color=038a
more1=s_bless
tag.colect=1    //coletável

//*****************************************************************************
// i_xbolt_steel
//*****************************************************************************
[ITEMDEF i_xbolt_steel]
ID=01bfb
NAME=dardo%s% (metal tratado)
TYPE=T_WEAPON_BOLT
FLIP=1
REPLICATE=1
RESOURCES=1 i_arrow_shaft, 1 i_feather
SKILLMAKE=17.8 BOWCRAFT
WEIGHT=0.1
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=dardo de metal tratado
DUPELIST=01bfc,01bfd,01bfe

ON=@CREATE
color=03b0
more2=6         //MODAR
tag.colect=1    //coletável

//*****************************************************************************
// i_xbolt_stab
//*****************************************************************************
[ITEMDEF i_xbolt_stab]
ID=01bfb
NAME=dardo%s% de perfuracao
TYPE=T_WEAPON_BOLT
FLIP=1
REPLICATE=1
RESOURCES=1 i_arrow_shaft, 1 i_feather
SKILLMAKE=17.8 BOWCRAFT
WEIGHT=0.1
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=dardo de perfuracao
DUPELIST=01bfc,01bfd,01bfe

ON=@CREATE
color=03e7
more2=9         //MODAR
tag.colect=1    //coletável

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs - FLECHAS
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_arrow
//*****************************************************************************
[ITEMDEF 0f3f]
DEFNAME=i_arrow
NAME=flecha%s% de madeira
TYPE=T_WEAPON_ARROW
FLIP=1
REPLICATE=1
RESOURCES=1 i_vareta, 2 i_pena
SKILLMAKE=20.0 BOWCRAFT
WEIGHT=0.1
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=flecha de madeira
DUPELIST=0f40,0f41,0f42

ON=@CREATE
tag.colect=1            //coletável


//*****************************************************************************
// i_arrow_iron
//*****************************************************************************
[ITEMDEF i_arrow_iron]
ID=0f3f
NAME=flecha%s% com ponta de metal
RESOURCES=1 i_vareta, 2 i_pena, 1 i_ponta_ferro
SKILLMAKE=35.0 BOWCRAFT
WEIGHT=0.1
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=@

ON=@CREATE
color=0482
more2=3                 //MODAR
tag.colect=1            //coletável

//*****************************************************************************
// i_arrow_steel
//*****************************************************************************
[ITEMDEF i_arrow_steel]
ID=0f3f
NAME=flecha%s% com ponta de metal tratado
RESOURCES=1 i_vareta, 2 i_pena, 1 i_ponta_leve
SKILLMAKE=45.0 BOWCRAFT
WEIGHT=0.1
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=@

ON=@CREATE
color=03b0
more2=6                 //MODAR
tag.colect=1            //coletável

//*****************************************************************************
// i_arrow_crane
//*****************************************************************************
[ITEMDEF i_arrow_crane]
ID=0f3f
NAME=flecha%s% de pena de Crane
RESOURCES=1 i_vareta, 2 i_pena_crane
SKILLMAKE=30.0 BOWCRAFT
WEIGHT=0.0
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=@


ON=@CREATE
color=04
more2=7                 //MODAR
tag.colect=1            //coletável

//*****************************************************************************
// i_arrow_fire
//*****************************************************************************
[ITEMDEF i_arrow_fire]
ID=i_arrow
NAME=flecha%s% de Fogo 

ON=@CREATE
color=030
tag.anim=i_fx_fireball_small
tag.sfx=<def.snd_SPELL_FIREBALL>
morex=6                 //DMGFIRE

ON=@DCLICK
correctammo
src.weapon.TAG.OVERRIDE.AMMOANIMHUE
return 1

//*****************************************************************************
// i_arrow_stab
//*****************************************************************************
[ITEMDEF i_arrow_stab]
ID=i_arrow
NAME=flecha%s% de perfuracao
RESOURCES=1 i_vareta, 2 i_pena, 1 i_ponta_perfurante
SKILLMAKE=65.0 BOWCRAFT
WEIGHT=0.2
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=@

ON=@CREATE
more2=9                 //MODAR
color=03e7
tag.colect=1            //coletável

//*****************************************************************************
// i_arrow_silver
//*****************************************************************************
[ITEMDEF i_arrow_silver]
ID=i_arrow
NAME=flecha%s% de prata
RESOURCES=1 i_vareta, 2 i_pena, 1 i_ponta_prata
SKILLMAKE=75.0 BOWCRAFT
WEIGHT=0.1
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=@

ON=@CREATE
color=038a
more1=s_bless
tag.colect=1            //coletável

//*****************************************************************************
// i_arrow_poison
//*****************************************************************************
[ITEMDEF i_arrow_poison]
ID=i_arrow
NAME=flecha%s% (envenenadas)

ON=@CREATE
more1=s_poison
//morez=3 Nao precisa. Tem no Combat
color=0455

//*****************************************************************************
// i_arrow_ice
//*****************************************************************************
[ITEMDEF i_arrow_ice]
ID=i_arrow
NAME=flecha%s% de gelo

ON=@CREATE
more1=s_paralyze    //paralyze Trocar por CHILL quando existir
morey=8             //DMGCOLD
color=0480
attr=04020          //attr_can_decay|attr_magic

//*****************************************************************************
// i_arrow_stun
//*****************************************************************************
[ITEMDEF i_arrow_stun]
ID=i_arrow
NAME=flecha%s% de paralizia

ON=@CREATE
more1=s_paralyze
color=01a
attr=04020          //attr_can_decay|attr_magic

//*****************************************************************************
// i_arrow_energy
//*****************************************************************************
[ITEMDEF i_arrow_energy]
ID=i_arrow
NAME=flecha%s% de energia

ON=@CREATE
morem=16                    //DMGENERGY
color=0d3
tag.anim=i_fx_firesnake
tag.sfx=05c3
attr=04020          //attr_can_decay|attr_magic

//*****************************************************************************
// i_flecha_fogo_apagada
//*****************************************************************************
[ITEMDEF i_flecha_fogo_apagada]
ID=0f3f
NAME=flecha%s% (com oleo)
TYPE=t_weapon_ammo //somente para venda

ON=@CREATE
maxhits=1

ON=@DCLICK
if <topobj> != <src>
    src.sysmessagered Voce tem que ter as flechas em seu poder
    return 1
endif

src.emotered acende as flechas

if <src.restest i_arrow_fire>
    LOCAL.amount=<src.findid.i_arrow_fire.amount>
    src.findid.i_arrow_fire.remove
endif

serv.newitem i_arrow_fire
new.amount=<amount>+<LOCAL.amount>
new.timerf 240,f_fire_arrow_consume
src.bounce <new>
remove
return 1

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs - barbantes
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_barbante
//*****************************************************************************
[ITEMDEF 01420]
DEFNAME=i_barbante
RESOURCES=i_cloth
TYPE=t_string
NAME=barbante de algodao
WEIGHT=.1
DYE=0
VALUE=1000
SKILLMAKE=tailoring 10.0

ON=@CREATE
more2=arrow_i_arco|arrow_i_arco_long
more1l=5
more1h=15

//*****************************************************************************
// i_barbante_iron
//*****************************************************************************
[ITEMDEF i_barbante_iron]
ID=01420
TYPE=t_string
NAME=barbante de ferro
WEIGHT=0.2
DYE=0
VALUE=1000
SKILLMAKE=tinkering 10.0
RESOURCES=i_ingot_iron

ON=@CREATE
color=color_o_iron
more2=arrow_i_arco|arrow_i_arco_long|arrow_i_arco_composto
more1l=10
more1h=30

//*****************************************************************************
// i_barbante_silver
//*****************************************************************************
[ITEMDEF i_barbante_silver]
ID=01420
TYPE=t_string
NAME=Barbante de Prata
WEIGHT=0.5
DYE=0
VALUE=1000
SKILLMAKE=tinkering 20.0
RESOURCES=i_ingot_silver

ON=@CREATE
color=color_o_silver
more2=arrow_i_arco_long|arrow_i_arco_composto|arrow_i_arco_elfico
more1l=20
more1h=40

//*****************************************************************************
// i_barbante_gold
//*****************************************************************************
[ITEMDEF i_barbante_gold]
ID=01420
TYPE=t_string
NAME=Barbante de Ouro
WEIGHT=1.0
DYE=0
VALUE=1000
SKILLMAKE=tinkering 30.0
RESOURCES=i_ingot_gold

ON=@CREATE
color=color_o_gold
more2=arrow_i_arco_composto|arrow_i_arco_elfico
more1l=30
more1h=80

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs - engrenagens
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_gears
//*****************************************************************************
[ITEMDEF 01053]
DEFNAME=i_gears
NAME=engrenagens
RESOURCES=1 i_ingot_iron
WEIGHT=1
CATEGORY=Myt - Items by Professions
SUBSECTION=Tinker
DESCRIPTION=Gears
DUPELIST=01054
SKILLMAKE=TINKERING 14.7,t_tinker_tools

ON=@CREATE
more1l=50
more1h=100
more2=0ffff     //serve em todas bestas

ON=@DCLICK
src.sysmessageyellow Selecione o arco para reparar...
targetf f_use_string <UID>
return 1

[ITEMDEF 01054]
//gears
DUPEITEM=01053

//*****************************************************************************
// i_gears_especial
//*****************************************************************************
[ITEMDEF i_gears_especial] //01053
ID=i_gears
NAME=Engranagem especial
WEIGHT=1
CATEGORY=Myt - Items by professions
SUBSECTION=Tinker
DESCRIPTION=Engrenagem Especial

ON=@CREATE
more1l=50
more1h=100
color 003b3
more2=0ffff     //serve em todas bestas

ON=@DCLICK
src.sysmessageyellow Usado na construcao de bestas...
return 1

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs - PRE-ITENS
//*****************************************************************************
//*****************************************************************************

[ITEMDEF i_ponta_leve]
id=0f83
TYPE=t_ponta_flecha
NAME=ponta%s% de ferro tratado
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=ponta de ferro tratado

ON=@CREATE
color=003E0

[ITEMDEF i_ponta_ferro]
ID=0f83
TYPE=t_ponta_flecha
NAME=ponta%s% de ferro
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=ponta de ferro

ON=@CREATE
color=00320


[ITEMDEF i_ponta_perfurante]
ID=0f83
TYPE=t_ponta_flecha
NAME=ponta%s% de perfuracao
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=ponta de perfuracao

ON=@CREATE
color=0b11

[ITEMDEF i_ponta_precisao]
ID=0f83
TYPE=t_ponta_flecha
NAME=ponta%s% de precisao
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=ponta de precisao

ON=@CREATE
color=00b0c

[ITEMDEF i_ponta_prata]
ID=0f83
TYPE=t_ponta_flecha
NAME=ponta%s% de prata
CATEGORY=MyT - Provisions - Weapons
SUBSECTION=Bows
DESCRIPTION=ponta de prata

ON=@CREATE
color=color_o_silver

[EOF]
