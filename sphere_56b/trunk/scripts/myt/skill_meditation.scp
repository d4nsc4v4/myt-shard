//*****************************************************************************
//*****************************************************************************
//
// SKILL MEDITATION por
//
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//TODO:

//TAGS usadas:

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// sk_meditation
//*****************************************************************************
[FUNCTION sk_meditation]

if <src.findid.i_meditation>
    src.sysmessageyellow Ja esta meditando...
    return 1
elif (<src.flags>&statf_onhorse)
    src.sysmessageyellow Voce nao pode meditar em um cavalo...
    return 1
elif (<src.mana> >= <src.maxmana>) && (<src.stam> >= <src.maxstam>)
    src.sysmessageyellow Ja esta revigorado.
    return 1
endif

src.act=0
src.ctag.disturb=0
serv.newitem i_meditation
new.cont=<src>
new.morep=<src.p>
new.more2=<between 6,60,<src.meditation>,1000>
new.more2=<eval 600/<new.more2>>
return 1

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_meditation
//*****************************************************************************
//morex: active
//more1: counter
//more2: time between increases
[ITEMDEF i_meditation]
ID=i_deed
NAME=Meditation
TYPE=t_eq_script
LAYER=layer_special

ON=@CREATE
//not meditating
morex=0
more1=0
timer=1

ON=@TIMER
timer=1

//statf_dead|statf_freeze|statf_sleeping|statf_war|statf_poisoned|statf_polymorph|statf_stone|statf_hallucinating|statf_onhorse|statf_spiritspeak
if (<cont.flags>&0804414b6) || (<cont.ctag0.disturb>) || (<morex>!=<cont.p.x>) || (<morey>!=<cont.p.y>)
    cont.sysmessageyellow Voce perdeu a concentracao!!
    cont.meditation_leave
    remove
    return 1
endif

if !<morex>
    cont.sysmessagegreen Entrou em transe!
    cont.meditation_enter
    morex=1
elif <more1> < <serv.time>

    cont.skill_gain SKILL_MEDITATION
    more1 = <eval <serv.time>+<more2>>
    
    if <cont.mana> < <cont.maxmana>
        cont.mana += 1
        LOCAL.d=1
    endif
    
    if <cont.stam> < <cont.maxstam>
        cont.stam += 1
        LOCAL.d=1
    endif
    
    if !<LOCAL.d>
        cont.sysmessagegreen Voce se sente recuperado!!
        cont.meditation_leave
        remove
    else
        dorand 8
            cont.emoteyellow em transe
            cont.anim 17
        enddo
    endif
endif
return 1

//*****************************************************************************
// meditation_enter
//*****************************************************************************
[FUNCTION meditation_enter]
events +e_no_attack
//events +e_no_dclick
ctag.disturb=0
serv.newitem i_speak_disturb
new.cont=<uid>

//*****************************************************************************
// meditation_leave
//*****************************************************************************
[FUNCTION meditation_leave]
events -e_no_attack
//events -e_no_dclick
consume 1 i_speak_disturb

//*****************************************************************************
// setRegenBasePm hp,mana,stam
//*****************************************************************************
//configura regen base da raca com modificadores (meditation)
[FUNCTION setRegenBasePm]
setRegenBonusPm <argn1>,<eval <argn2>+(<meditation>/100)>,<argn3>

//*****************************************************************************
// setRegenBonusPm hp,mana,stam
//*****************************************************************************
//incrementa x hp, y mana a e z stam a cada minuto
[FUNCTION setRegenBonusPm]
setRegenBonus <eval 60/<argn1>>,<eval 60/<argn2>>,<eval 60/<argn3>>

//*****************************************************************************
// setRegenBonus hp,mana,stam
//*****************************************************************************
//incrementa hp a cada x segundos, mana a cada y e stam a cada z
[FUNCTION setRegenBonus]
consume 1 i_regen
consume 1 i_elfo_mana
serv.newitem i_regen
new.morex=<argn1>
new.morey=<argn2>
new.morez=<argn3>
new.more1h=<argn1>
new.more1l=<argn2>
new.more2h=<argn3>
new.cont=<uid>

//more1h=hp rate
//more1l=mana rate
//more2h=stam rate
//more2l=food rate
//morex=hp timer
//morey=mana timer
//morez=stam timer
[ITEMDEF i_regen]
id=i_deed
name=Regen
type=t_eq_script
layer=layer_special
weight=0

ON=@CREATE
timer=1
return 1

ON=@TIMER
timer=1

//hp
morex -= 1
if !<morex>
    morex=<more1h>
    if <cont.hits> < <cont.maxhits>
        cont.hits += 1
    endif
endif

//mana
morey -= 1
if !<morey>
    morey=<more1l>
    if <cont.mana> < <cont.maxmana>
        cont.mana += 1
    endif
endif

//stam
morez -= 1
if !<morez>
    morez=<more2h>
    if <cont.stam> < <cont.maxstam>
        cont.stam += 1
    endif
endif

return 1

[EOF]