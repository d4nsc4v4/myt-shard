[COMMENT ITEMFORGE]
Mystical Tales Shard
Itemforge v1.0		20/02/2009

Galthar, o Errante
US_Marine

Tags utilizadas:

- cTAG.ITEMFORGE_Tab		Aba que estamos navegando
- cTAG.ITEMFORGE_ID		ID (da DB) do item que está sendo editado. 0 se item novo
- cTAG.ITEMFORGE_Rev		Número da revisão do item. 0 se item novo
- cTAG.ITEMFORGE_Date		Data da criação do item. (pelo menos desta revisão)
- cTAG.itemforge_ItemDef
- cTAG.itemforge_DispID
- cTAG.itemforge_Name
- cTAG.itemforge_Category
- cTAG.itemforge_Subsection
- cTAG.itemforge_Weight
- cTAG.itemforge_Value
- cTAG.itemforge_Type
- cTAG.itemforge_TEvents
- cTAG.itemforge_Resources
- cTAG.itemforge_Resources2
- cTAG.itemforge_SkillMake
- cTAG.itemforge_Layer
- cTAG.itemforge_Pile
- cTAG.itemforge_TData1
- cTAG.itemforge_TData2
- cTAG.itemforge_TData3
- cTAG.itemforge_TData4
- cTAG.itemforge_Dam
- cTAG.itemforge_Armor
- cTAG.itemforge_Speed
- cTAG.itemforge_Skill
- cTAG.itemforge_ReqSTR
- cTAG.itemforge_TwoHands
- cTAG.itemforge_Color
- cTAG.itemforge_Create

ToDo:
- Completar itemforge_DBExists (função)
- Completar itemforge_DBLoad (função)
- Completar itemforge_DBSave (função)
- Completar itemforge_DBDelete (função)
- Completar itemforge_find (search tool)
- Browser (por categorias e subcategorias do Axis. Multipage. Rola copiar do sistema de vendas mole-mole.)
  - itemforge_DBListCats (lista categorias de Axis da DB) (função/gump)
  - itemforge_DBListSubs (lista subsections da category cTAG.ITEMFORGE_Category) (função/gump)
  - itemforge_DBListItems (lista os itens da categoria cTAG.ITEMFORGE_Category na subsection cTAG.ITEMFORGE_Subsection)(função/gump)
  - Caminho no topo
  - Voltar (de itens pra subsection pra category)
  - itemforge_Browse (função handler do browser)

[PLEVEL 4]
itemforge

[DEFNAME itemforge]
section [
sectione ]



////////////////////////////////////////////////////////////////
//
//			FUNÇÕES
//
////////////////////////////////////////////////////////////////

[FUNCTION slash2]
//Imprime os caracteres '//'
local.s=a/a
return <strsub 1,1,<local.s>><strsub 1,1,<local.s>>

///////////
//Funções do sistema Item Forge
//////////////////////

[FUNCTION itemforge]
// .itemforge [ browse | find {string} | new | insert | edit ]
//Função principal para client
if !(<eval strlen(<args>)>) || (STRMATCH(browse,<args>)
 itemforge_browse
elif (STRMATCH(new,<args>)
 itemforge_clear
 itemforge_open
elif (STRMATCH(find*,<args>)
 if (<STRLEN(<srteat <args>>)> > 3)
  itemforge_find <srteat <args>>
 else
  sysmessagered Palavra muito curta. Tente 4 letras ou mais.
 endif
elif (STRMATCH(insert,<args>)
 sysmessageyellow Selecione o ITEM a ser inserido:
 targetf itemforge_insert
elif (STRMATCH(edit,<args>)
 sysmessageyellow Selecione o ITEM a ser editado:
 targetf itemforge_edit
endif
return 1

[FUNCTION itemforge_clear]
//Limpa das cTAGS do cache do sistema
cTAG.ITEMFORGE_Tab
cTAG.ITEMFORGE_ID
cTAG.ITEMFORGE_Rev
cTAG.ITEMFORGE_Date
cTAG.itemforge_ItemDef
cTAG.itemforge_DispID
cTAG.itemforge_Name
cTAG.itemforge_Category
cTAG.itemforge_Subsection
cTAG.itemforge_Weight
cTAG.itemforge_Value
cTAG.itemforge_Type
cTAG.itemforge_TEvents
cTAG.itemforge_Resources
cTAG.itemforge_Resources2
cTAG.itemforge_SkillMake
cTAG.itemforge_Layer
cTAG.itemforge_Pile
cTAG.itemforge_TData1
cTAG.itemforge_TData2
cTAG.itemforge_TData3
cTAG.itemforge_TData4
cTAG.itemforge_Dam
cTAG.itemforge_Armor
cTAG.itemforge_Speed
cTAG.itemforge_Skill
cTAG.itemforge_ReqSTR
cTAG.itemforge_TwoHands
cTAG.itemforge_Color
cTAG.itemforge_Create

[FUNCTION itemforge_open]
//Abre o gump na tab <argn>
dialogclose d_itemforge
if (<argn>)
 cTAG.ITEMFORGE_Tab=<argn>
elif  (!<cTAG0.ITEMFORGE_Tab>)
 cTAG.ITEMFORGE_Tab=1
endif
DIALOG d_itemforge <cTAG0.ITEMFORGE_Tab>
RETURN 1

[FUNCTION itemforge_insert]
//TARGETF. Copia os atributos de um item do World pro cache.
if (<itemforge_DBExists <argo.defname>>)
 sysmessagered O item <argo.defname> ja existe no banco de dados. Crie uma nova revisao.
 itemforge_edit <argo>
 return 0
endif
itemforge_clear
cTAG.itemforge_ItemDef=<argo.defname>
cTAG.itemforge_DispID=<argo.dispiddec>
cTAG.itemforge_Name=<argo.name>
cTAG.itemforge_Weight=<argo.weight>
cTAG.itemforge_Value=<argo.value>
cTAG.itemforge_Type=<argo.type>
cTAG.itemforge_TEvents=<argo.tevents>
cTAG.itemforge_Resources=<argo.resources>
cTAG.itemforge_Resources2=<serv.resources.<argo.defname>.resources2>//<QVAL <IsEmpty <argo.resources2>>? :<argo.resources2>>
cTAG.itemforge_SkillMake=<argo.skillmake>
cTAG.itemforge_Layer=<argo.layer>
cTAG.itemforge_Pile=<QVAL !<IsEmpty <serv.resources.<argo.defname>.pile>>?1:0>
cTAG.itemforge_TData1=<argo.tdata1>
cTAG.itemforge_TData2=<argo.tdata2>
cTAG.itemforge_TData3=<argo.tdata2>
cTAG.itemforge_TData4=<argo.tdata4>
if (<f_IsWeapon <argo.uid>>)
 cTAG.itemforge_Dam=<argo.dam>
 cTAG.itemforge_Speed=<argo.speed>
 cTAG.itemforge_Skill=<serv.skill.<argo.skill>.name>
 cTAG.itemforge_ReqSTR=<argo.reqstr>
 cTAG.itemforge_TwoHands=<argo.twohands>
elif (<f_IsArmor <argo.uid>>) || (<f_IsWearable <argo.uid>>)
 cTAG.itemforge_ReqSTR=<argo.reqstr>
 cTAG.itemforge_Armor=<argo.armor>
endif
cTAG.itemforge_Color=<eval <argo.color>>
cTAG.itemforge_Create=<slash2>Criacao automatica;
if (<eval strlen(<argo.events>)>)
 cTAG.itemforge_Create .=events <argo.events>;
endif
if (<argo.more1>)
 cTAG.itemforge_Create .=more1 <argo.more1>;
endif
if (<argo.more2>)
 cTAG.itemforge_Create .=more2 <argo.more2>;
endif
if (<argo.morex>) || (<argo.morey>) || (<argo.morez>) || (<argo.morem>)
 cTAG.itemforge_Create .=morep <argo.morep>;
endif
if (<argo.tagcount>)
 LOCAL.c=<eval <argo.tagcount>-1>
 for i 0 <LOCAL.c>
  cTAG.itemforge_Create .= <argo.tagat.<local.i>>
 end
endif
src.itemforge_open
return 1

[FUNCTION itemforge_edit]
//TARGETF. Abre para edição um item com cache baseado numa instance no world.
if (!<itemforge_DBExists <argo.defname>>)
 sysmessageorange O item <argo.defname> NAO existe no banco de dados. Crie um novo item.
 itemforge_insert <argo>
 return 0
endif
itemforge_DBLoad <argo.defname>
return 1

[FUNCTION itemforge_find]
sysmessageyellow Procurando por <args> no banco de dados.
//Procura por strings parecidas com <args> entre os Name e DefNames da DB
//e retorna lista no ITEMFORGE_BROWSE

///////////
//Funções do banco de dados
//////////////////////

[FUNCTION itemforge_DBExists]
//Retorna se existe uma entrada na DB com o ITEMDEF <args>
return 0

[FUNCTION itemforge_DBLoad]
//Popula a lista de ctags para o cache gump
if (<argn>)
 //Fazer a query pela ID
else (!<IsEmpty <args>>)
 //Fazer a query pelo DEFNAME (revisão mais atual)
else
 return 0
endif
//Popular cache.
cTAG.ITEMFORGE_ID=<DB.ROW.0.
cTAG.ITEMFORGE_Rev=<DB.ROW.0.
cTAG.ITEMFORGE_Date=<DB.ROW.0.
cTAG.itemforge_ItemDef=<DB.ROW.0.
cTAG.itemforge_DispID=<DB.ROW.0.
cTAG.itemforge_Name=<DB.ROW.0.
cTAG.itemforge_Category=<DB.ROW.0.
cTAG.itemforge_Subsection=<DB.ROW.0.
cTAG.itemforge_Weight=<DB.ROW.0.
cTAG.itemforge_Value=<DB.ROW.0.
cTAG.itemforge_Type=<DB.ROW.0.
cTAG.itemforge_TEvents=<DB.ROW.0.
cTAG.itemforge_Resources=<DB.ROW.0.
cTAG.itemforge_Resources2=<DB.ROW.0.
cTAG.itemforge_SkillMake=<DB.ROW.0.
cTAG.itemforge_Layer=<DB.ROW.0.
cTAG.itemforge_Pile=<DB.ROW.0.
cTAG.itemforge_TData1=<DB.ROW.0.
cTAG.itemforge_TData2=<DB.ROW.0.
cTAG.itemforge_TData3=<DB.ROW.0.
cTAG.itemforge_TData4=<DB.ROW.0.
cTAG.itemforge_Dam=<DB.ROW.0.
cTAG.itemforge_Armor=<DB.ROW.0.
cTAG.itemforge_Speed=<DB.ROW.0.
cTAG.itemforge_Skill=<DB.ROW.0.
cTAG.itemforge_ReqSTR=<DB.ROW.0.
cTAG.itemforge_TwoHands=<DB.ROW.0.
cTAG.itemforge_Color=<DB.ROW.0.
cTAG.itemforge_Create=<DB.ROW.0.
itemforge_open
return 1

[FUNCTION itemforge_DBSave]
//Salva os dados das ctags na DB.
//Se o item já existe (cTAG.ITEMFORGE_ID != 0), criar revisão.
local.id=0	//TEM QUE RETORNAR A ID DEPOIS DE SALVAR!
return <local.id>

[FUNCTION itemforge_DBDelete]
//Elimina todos os dados de todas as verisões do item
if (<argn>)
 //Executar a query pela ID
else (!<IsEmpty <args>>)
 //Executar a query pelo DEFNAME
else
 return 0
endif



///////////
//Funções da página Sumario
//////////////////////

[FUNCTION f_itemforge_castSummary]
//Popula o HTML da Tab 'Sumário' com dados relevantes do item.
serv.newitem i_gold
new.type=<cTAG.ITEMFORGE_Type>
local.text=Item: <DEF.BFONT_SIZE5><DEF.BFONT_RED><cTAG.ITEMFORGE_Name> (<cTAG.ITEMFORGE_ItemDef>) <DEF.BR><DEF.BFONT_SIZE3><DEF.BFONT_BLACK>//<DEF.br>
if (<cTAG0.ITEMFORGE_ID>)
 local.text .=ID No. <dcTAG.ITEMFORGE_ID>; Revisao No. <dcTAG.ITEMFORGE_Rev> de <cTAG.ITEMFORGE_Date>.<DEF.BR><DEF.BR>
else
 local.text .=Novo item (nao encontrado no banco de dados)<DEF.BR><DEF.BR>
endif
//Armas
if (<f_IsWeapon <new>>)
 local.text .=Negociado pelo vendedor de armas <QVAL <IsEmpty <cTAG.itemforge_Value>>? : no melhor estado por <STRTRESURE <cTAG.itemforge_Value>>><DEF.BR><DEF.BR>
 float.mean=<fval (10*(<maximum <cTAG.ITEMFORGE_Dam>>+<minimum <cTAG.ITEMFORGE_Dam>>))/2>
 local.text .=Dano: <cTAG.ITEMFORGE_Dam> (Media de <fval <float.mean>/10@5>)<DEF.BR>
 local.text .=Velocidade: <dcTAG.ITEMFORGE_Speed><DEF.BR>
 local.dps = <fval (10*<float.mean>)/<floatval 75.0/<dcTAG.ITEMFORGE_Speed>>>
 local.text .=DPS: <local.dps> (
 if (<local.dps> < 9.0)
  local.text .= arma de mao rudimentar ou ferramenta)
 elif (<local.dps> < 11.5)
  local.text .= arma de mao leve ou de longo alcance regular)
 elif (<local.dps> < 13.0)
  local.text .= arma de long alcance forte ou arma de mao regular)
 elif (<local.dps> < 14.0)
  local.text .= arma de mao forte ou arma de alcance magica)
 else
  local.text .= arma de mao magica. Nao usar longo alcance)
 endif
//Armaduras
elif (<f_IsArmor <new>>)
 local.text .=Negociado pelo vendedor de armaduras <QVAL <IsEmpty <cTAG.itemforge_Value>>?.: no melhor estado por <STRTRESURE <cTAG.itemforge_Value>>><DEF.BR><DEF.BR>
 if (<IsNum <cTAG.ITEMFORGE_Layer>>)
  local.layer=<cTAG.ITEMFORGE_Layer>
 elif (!<IsEmpty <DEF.<cTAG.ITEMFORGE_Layer>>>)
  local.layer=<DEF.<cTAG.ITEMFORGE_Layer>>
 else
  local.layer=16	//Barba. Só pra dizer que tá errado.
 endif
 local.factor=0
 //Calculador de armadura: http://sphere.uozone.net/index.php?title=Armor_calculation
 DOSWITCH <local.layer>
  local.factor=0	//Sem layer 
  local.factor=0	//layer_hand1
  local.factor=0	//layer_hand2
  local.factor=5	//layer_shoes
  local.factor=20	//layer_pants
  local.factor=40	//layer_shirt
  local.factor=10	//layer_helm
  local.factor=10	//layer_gloves
  local.factor=0	//layer_ring
  local.factor=0	//layer_talisman
  local.factor=5	//layer_collar
  local.factor=0	//layer_hair
  local.factor=20	//layer_half_apron
  local.factor=40	//layer_chest
  local.factor=0	//layer_wrist
  local.factor=0	//layer_light
  local.factor=0	//layer_beard
  local.factor=40	//layer_tunic
  local.factor=0	//layer_ears
  local.factor=10	//layer_arms
  local.factor=20	//layer_cape
  local.factor=0	//layer_pack
  local.factor=70	//layer_robe
  local.factor=20	//layer_skirt
  local.factor=25	//layer_legs
 END
 local.text .=Armadura: <dcTAG.ITEMFORGE_Armor> (
 if (<local.factor>)
  local.text .=<eval (<local.factor>*<dcTAG.ITEMFORGE_Armor>)/100> real)
 else
  local.text .=layer errada)
 endif

elif (<f_isRegsOrMagic <new>>)
 local.text .=Negociado pelo vendedor de magia <QVAL <IsEmpty <cTAG.itemforge_Value>>?.: no melhor estado por <STRTRESURE <cTAG.itemforge_Value>>><DEF.BR><DEF.BR>
 
elif (<f_isFood <new>>)
 local.text .=Negociado pelo vendedor de alimentos <QVAL <IsEmpty <cTAG.itemforge_Value>>?.: no melhor estado por <STRTRESURE <cTAG.itemforge_Value>>><DEF.BR><DEF.BR>
 
elif (<f_isMisc <new>>)
 local.text .=Negociado pelo vendedor de provisoes <QVAL <IsEmpty <cTAG.itemforge_Value>>?.: no melhor estado por <STRTRESURE <cTAG.itemforge_Value>>><DEF.BR><DEF.BR>
 
elif (<f_isWearable <new>>)
 local.text .=Negociado pelo vendedor de roupas e acessorios <QVAL <IsEmpty <cTAG.itemforge_Value>>?.: no melhor estado por <STRTRESURE <cTAG.itemforge_Value>>><DEF.BR><DEF.BR>

elif (<f_isRaw <new>>)
 local.text .=Negociado pelo vendedor de materia prima <QVAL <IsEmpty <cTAG.itemforge_Value>>?.: no melhor estado por <STRTRESURE <cTAG.itemforge_Value>>><DEF.BR><DEF.BR>
 
else
 local.text .=Nao eh comercializavel.
 
endif
new.remove
return <local.text>

[FUNCTION f_itemforge_CopyItem]
//Cria uma nova entrada baseada nos dados em cache.
sysmessageorange Copiando...
cTAG.ITEMFORGE_ID=
cTAG.ITEMFORGE_Rev=
cTAG.ITEMFORGE_Date=
cTAG.itemforge_ItemDef=
sysmessageyellow Digite um novo DefName
src.promptconsole f_itemforge_changeItemdef

///////////
//Funções da página Geral
//////////////////////

[FUNCTION f_itemforge_changeItemdef]
//Troca o ItemDef do chache por <arg>
if (<serv.resources.<args>>)
 sysmessagered <args> ja existe.
elif (STRMATCH(* *,<args>)
 sysmessagered Itemdef nao pode conter espacos.
else
 cTAG.itemforge_itemdef=<args>
endif
itemforge_open 2
return 1

[FUNCTION f_itemforge_changeDispID]
//Troca o DispID do chache por <arg>
if (<IsNum <argv0>>)
 cTAG0.ITEMFORGE_DispID=<argn>
else
 sysmessagered <args> nao eh valido. Use numeros.
endif
itemforge_open 2
return 1

[FUNCTION f_itemforge_changeName]
//Troca o Name do chache por <arg>
if (STRMATCH([1-9]*,<args>)
 sysmessagered O nome nao pode comecar com numeros.
else
 cTAG0.ITEMFORGE_Name=<args>
endif
itemforge_open 2
return 1

[FUNCTION f_itemforge_changeCategory]
//Troca o Category do chache por <arg>
if (STRMATCH([1-9]*,<args>)
 sysmessagered A categoria nao pode comecar com numeros.
else
 cTAG0.ITEMFORGE_Category=<args>
endif
itemforge_open 2
return 1

[FUNCTION f_itemforge_changeSubsection]
//Troca o Subsection do chache por <arg>
if (STRMATCH([1-9]*,<args>)
 sysmessagered A sub-secao nao pode comecar com numeros.
else
 cTAG0.ITEMFORGE_Subsection=<args>
endif
itemforge_open 2
return 1

[FUNCTION f_itemforge_changeWeight]
//Troca o Weight do chache por <arg>
if (<IsNum <argv0>>)
 cTAG0.ITEMFORGE_Weight=<argn>
else
 sysmessagered <args> nao eh valido. Use numeros.
endif
itemforge_open 2
return 1

[FUNCTION f_itemforge_changeValue]
//Troca o Value do chache por <arg>
if (<IsNum <argv0>>)
 cTAG0.ITEMFORGE_Value=<argn>
else
 sysmessagered <args> nao eh valido. Use numeros.
endif
itemforge_open 2
return 1



///////////
//Funções da página Comportamento
//////////////////////

[FUNCTION f_itemforge_changeType]
//Troca o Type do chache por <arg>
if (!<serv.typedefs.<args>>)
 sysmessagered Nao existe o Type <args> no server.
else
 cTAG.ITEMFORGE_Type=<args>
endif
itemforge_open 3
return 1

[FUNCTION f_itemforge_changeTEvents]
//Troca o TEvents do chache por <arg>
cTAG.ITEMFORGE_TEvents=<args>
itemforge_open 3
return 1

[FUNCTION f_itemforge_changeResources]
//Troca o Resources do chache por <arg>
cTAG.ITEMFORGE_Resources=<args>
itemforge_open 3
return 1

[FUNCTION f_itemforge_changeResources2]
//Troca o Resources2 do chache por <arg>
cTAG.ITEMFORGE_Resources2=<args>
itemforge_open 3
return 1

[FUNCTION f_itemforge_changeSkillMake]
//Troca o SkillMake do chache por <arg>
if (!<serv.resources.<strarg <args>>>)
 sysmessagered <strarg <args>> nao eh uma skill vlaida.
elif (!<IsNum.<streat <args>>>)
 sysmessagered <streat <args>> nao eh uma um numero valido.
else
 cTAG.ITEMFORGE_SkillMake=<args>
endif
itemforge_open 3
return 1

[FUNCTION f_itemforge_changeLayer]
//Troca o Layer do chache por <arg>
if (<IsNum <args>>)
 if (<argn> > 30) || (<argn> < 1)
  sysmessagered <args> nao eh uma Layer valida.
  cTAG.ITEMFORGE_Layer=  
 else
  cTAG.ITEMFORGE_Layer=<argn>
 endif
else
 if !(STRMATCH(LAYER_*,<args>)
  sysmessagered <args> nao eh uma Layer valida.
 elif (<IsEmpty <DEF.<args>>>)
  sysmessagered <args> nao eh uma Layer valida.
 else
  cTAG.ITEMFORGE_Layer=<STRTOUPPER(<args>)>
 endif 
endif
itemforge_open 3
return 1

[FUNCTION f_itemforge_changePile]
//Troca o Pile do chache por <arg>
cTAG.ITEMFORGE_Pile=<QVAL <argn>?1:0>
itemforge_open 3
return 1

[FUNCTION f_itemforge_changeTData1]
//Troca o TData1 do chache por <arg>
IF (!<IsNum <args>>)
 sysmessagered <args> nao eh um numero. TDATA deve ser um numero.
else
 cTAG.ITEMFORGE_TData1=<argn>
ENDIF
itemforge_open 3
return 1

[FUNCTION f_itemforge_changeTData2]
//Troca o TData2 do chache por <arg>
IF (!<IsNum <args>>)
 sysmessagered <args> nao eh um numero. TDATA deve ser um numero.
else
 cTAG.ITEMFORGE_TData2=<argn>
ENDIF
itemforge_open 3
return 1

[FUNCTION f_itemforge_changeTData3]
//Troca o TData3 do chache por <arg>
IF (!<IsNum <args>>)
 sysmessagered <args> nao eh um numero. TDATA deve ser um numero.
else
 cTAG.ITEMFORGE_TData3=<argn>
ENDIF
itemforge_open 3
return 1

[FUNCTION f_itemforge_changeTData4]
//Troca o TData4 do chache por <arg>
IF (!<IsNum <args>>)
 sysmessagered <args> nao eh um numero. TDATA deve ser um numero.
else
 cTAG.ITEMFORGE_TData4=<argn>
ENDIF
itemforge_open 3
return 1



///////////
//Funções da página Características
//////////////////////

[FUNCTION f_itemforge_changeDam]
//Troca o Dam do chache por <arg>
IF (<IsNum <argv0>>) && (<IsNum <argv1>>) && (<IsEmpty <argv2>>)
 cTAG.ITEMFORGE_Dam=<args>
else
 sysmessagered Valor de dano invalido.
endif
itemforge_open 4
return 1

[FUNCTION f_itemforge_changeArmor]
//Troca o Armor do chache por <arg>
if (!<IsNum <argn>>)
 sysmessagered Armor deve ser um numero.
else
 cTAG0.ITEMFORGE_Armor=<args>
endif
itemforge_open 4
return 1

[FUNCTION f_itemforge_changeSpeed]
//Troca o Speed do chache por <arg>
if (!<IsNum <argn>>)
 sysmessagered Speed deve ser um numero.
else
 cTAG0.ITEMFORGE_Speed=<args>
endif
itemforge_open 4
return 1

[FUNCTION f_itemforge_changeSkill]
//Troca o Skill do chache por <arg>
if (!<serv.skill.<argv0>>)
 sysmessagered Skill invalida.
else
 cTAG0.ITEMFORGE_Skill=<serv.skill.<argv0>.name>
endif
itemforge_open 4
return 1

[FUNCTION f_itemforge_changeReqSTR]
//Troca o ReqSTR do chache por <arg>
cTAG0.ITEMFORGE_ReqSTR=<argn>
itemforge_open 4
return 1

[FUNCTION f_itemforge_changeTwoHands]
//Troca o TwoHands do chache por <arg>
cTAG0.ITEMFORGE_TwoHands=<QVAL 0<argn>?1:0>
itemforge_open 4
return 1



///////////
// Funções da página @Create
//////////////////////

[FUNCTION f_itemforge_changeColor]
//Troca o Color do chache por <arg>
if (<IsNum <argv0>>)
 cTAG0.ITEMFORGE_Color=<argn>
else
 sysmessagered <args> nao eh valido. Use numeros.
endif
itemforge_open 5
return 1

[FUNCTION f_itemforge_refreshCreate]
//Troca o Create do chache por <arg>
cTAG0.ITEMFORGE_Create=<args>
itemforge_open 5
return 1



////////////////////////////////////////////////////////////////
//
//			GUMPS
//
////////////////////////////////////////////////////////////////

// Created 26/2/2009 04:59:38, with Gump Studio.
// Exported with with SphereExporter ver 1.1.
// Script for 0.56/Revisions

[DIALOG d_itemforge]
100,75
nomove
page 0
resizepic 0 0 2620 600 450		//BG
//<head>
tilepic 513 23 4015//		\
tilepic 551 6 4017//		 |
tilepic 517 31 4023//		  > 	LOGO
tilepic 486 9 4021//		 |
tilepichue 510 0 3149 42//	/
//<title>
dtext 9 10 32 Item Forge -> <QVAL <IsEmpty <cTAG.itemforge_Category>>?sem categoria:<cTAG.itemforge_Category>> -> <QVAL <IsEmpty <cTAG.itemforge_Subsection>>?sem sub-secao:<cTAG.itemforge_Subsection>> -> <QVAL <IsEmpty <cTAG.itemforge_Name>>?sem nome:<cTAG.itemforge_Name>>
//</head>
//<tabs>
button 15 35 2443 2443 1 0 1
dtext 25 36 <QVAL <cTAG.ITEMFORGE_Tab>==1?32:2100> Sumario
button 85 35 2443 2443 1 0 2
dtext 100 36 <QVAL <cTAG.ITEMFORGE_Tab>==2?32:2100> Geral
button 154 35 2445 2445 1 0 3
dtext 165 36 <QVAL <cTAG.ITEMFORGE_Tab>==3?32:2100> Comportamento
button 267 35 2440 2440 1 0 4
dtext 306 36 <QVAL <cTAG.ITEMFORGE_Tab>==4?32:2100> Caracteristicas
button 437 35 2443 2443 1 0 5
dtext 443 36 <QVAL <cTAG.ITEMFORGE_Tab>==5?32:2100> @Create
//</tabs>
//<item pic>
resizepic 20 70 2620 112 162
tilepichue 32 82 <cTAG0.ITEMFORGE_DispID> <dcTAG0.ITEMFORGE_Color>



page 1//Sumário
dhtmlgump 140 70 440 350 1 1 <f_itemforge_castSummary>
button 23 240 2445 2445 1 0 101
dtext 47 241 2100 Add Item
button 23 292 2445 2445 1 0 102
dtext 54 293 4 Copiar
button 23 344 2445 2445 1 0 103
dtext 56 345 52 Salvar
button 23 396 2445 2445 1 0 104
dtext 53 397 37 Excluir



page 2//Geral
dorigin 190 83
dtext - - 47 ItemDef
button +58 -3 1141 1142 1 0 201
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_itemdef>>? :<cTAG.itemforge_itemdef>>

dtext - *30 47 ID
button +58 -3 1141 1142 1 0 202
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_DispID>>? :<cTAG.itemforge_DispID>>

dtext - *30 47 Name
button +58 -3 1141 1142 1 0 203
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_Name>>? :<cTAG.itemforge_Name>>

dtext - *30 47 Category
button +58 -3 1141 1142 1 0 204
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_Category>>? :<cTAG.itemforge_Category>>

dtext - *30 47 Subsection
button +58 -3 1141 1142 1 0 205
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_Subsection>>? :<cTAG.itemforge_Subsection>>

dtext - *30 47 Weight
button +58 -3 1141 1142 1 0 206
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_Weight>>? :<dcTAG.itemforge_Weight>>

dtext - *30 47 Value
button +58 -3 1141 1142 1 0 207
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_Value>>? :<STRTRESURE <cTAG.itemforge_Value>>>



page 3//Comportamento
dorigin 190 83
dtext -6 - 47 Type
button +58 -3 1141 1142 1 0 301
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_type>>? :<cTAG.itemforge_type>>

dtext -6 *30 47 TEvents
button +58 -3 1141 1142 1 0 302
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_TEvents>>? :<cTAG.itemforge_TEvents>>

dtext -6 *30 47 Resources
button +58 -3 1141 1142 1 0 303
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_Resources>>? :<cTAG.itemforge_Resources>>

dtext -6 *30 47 Resources2
button +58 -3 1141 1142 1 0 304
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_Resources2>>? :<cTAG.itemforge_Resources2>>

dtext -6 *30 47 SkillMake
button +58 -3 1141 1142 1 0 305
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_SkillMake>>? :<cTAG.itemforge_SkillMake>>

dtext -6 *30 47 Layer
button +58 -3 1141 1142 1 0 306
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_Layer>>? :<QVAL <IsNum <cTAG.itemforge_Layer>>?<dcTAG.itemforge_Layer>:<cTAG.itemforge_Layer>>>

dtext -6 *30 47 Pile
button +58 -3 1141 1142 1 0 307
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_Pile>>? :<dcTAG.itemforge_Pile>>

dtext -6 *30 47 TData1
button +58 -3 1141 1142 1 0 308
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_TData1>>? :<cTAG.itemforge_TData1>>

dtext -6 *30 47 TData2
button +58 -3 1141 1142 1 0 309
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_TData2>>? :<cTAG.itemforge_TData2>>

dtext -6 *30 47 TData3
button +58 -3 1141 1142 1 0 310
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_TData3>>? :<cTAG.itemforge_TData3>>

dtext -6 *30 47 TData4
button +58 -3 1141 1142 1 0 311
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_TData4>>? :<cTAG.itemforge_TData4>>



page 4//Características
dorigin 190 83
dtext - - 47 Dam
button +58 -3 1141 1142 1 0 401
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_Dam>>? :<cTAG.itemforge_Dam>>

dtext - *30 47 Armor
button +58 -3 1141 1142 1 0 402
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_Armor>>? :<dcTAG.itemforge_Armor>>

dtext - *30 47 Speed
button +58 -3 1141 1142 1 0 403
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_Speed>>? :<dcTAG.itemforge_Speed>>

dtext - *30 47 Skill
button +58 -3 1141 1142 1 0 404
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_Skill>>? :<cTAG.itemforge_Skill>>

dtext - *30 47 ReqSTR
button +58 -3 1141 1142 1 0 405
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_ReqSTR>>? :<dcTAG.itemforge_ReqSTR>>

dtext - *30 47 TwoHands
button +58 -3 1141 1142 1 0 406
dtext +71 - 2100 <QVAL <IsEmpty <cTAG.itemforge_TwoHands>>? :<dcTAG.itemforge_TwoHands>>



page 5//@Create
dtext 212 82 47 Color
button 248 80 1141 1142 1 0 501
dtext 261 83 2100 <dcTAG0.itemforge_Color>
resizepic 150 148 9350 431 285
dtextentry 159 158 412 265 2100 0 <QVAL <IsEmpty <cTAG.ITEMFORGE_Create>>? :<cTAG0.ITEMFORGE_Create>>
dtext 153 124 47 Bloco de intrucoes @CREATE. Use ';' para pular linha.
dhtmlgump 18 239 120 160 1 0 Apos editar o bloco @CREATE, pressione o botao 'Modificar' para que as mudanÃ§as tenham efeito.
button 24 405 2445 2445 1 0 502
dtext 46 406 4 Modificar

[DIALOG d_itemforge text]


[DIALOG d_itemforge button]
ON=1 5
// Tabs
itemforge_open <argn>
RETURN 1

ON=101
// AddItem
if (!<serv.resources.<cTAG.itemforge_itemdef>>)
 sysmessagered Item nao existente como script.
else
 src.newitem <cTAG.itemforge_itemdef>
 new.bounce
endif
itemforge_open
return 1

ON=102
// Copy Item
f_itemforge_CopyItem
return 1

ON=103
// Submit to DB
local.save=<itemforge_DBSave>
itemforge_clear
itemforge_DBLoad <local.save>
itemforge_open
return 1

ON=104
// Delete from DB
if (!<itemforge_DBExists <cTAG.itemforge_itemdef>>)
 sysmessagered O item '<cTAG.itemforge_itemdef>' nao foi encontrado no banco de dados.
else
 itemforge_DBDelete <cTAG.itemforge_itemdef>
 sysmessagegreen O item foi '<cTAG.itemforge_itemdef>' deletado do banco de dados.
 sysmessagegreen Os dados originais continuam no Item Forge.
 sysmessagegreen Voce pode altera-los e salva-lo de novo.
endif
itemforge_open
return 1

ON=200 299 		//Geral
DOSWITCH <eval <argn>-200>
 src.sysmessagered ERRO! Nao ha botao 200!
 src.sysmessageyellow Digite um ITEMDEF valido//21
 src.sysmessageyellow Digite o numero de um ID valido (InsideUO)//22
 src.sysmessageyellow Digite o nome do item
 src.sysmessageyellow Digite o nome de uma categoria do Axis para o item.
 src.sysmessageyellow Digite o nome de uma sub-secao de <cTAG0.ITEMFORGE_Category> do Axis para o item.
 src.sysmessageyellow Digite um peso (em DECIMOS de Stones) para o item.
 src.sysmessageyellow Digite um valor em Moedas de Cobre para o item.
END
DOSWITCH <eval <argn>-200>
 src.itemforge_open				//200
 src.promptconsole f_itemforge_changeItemdef	//201
 src.promptconsole f_itemforge_changeDispID	//202
 src.promptconsole f_itemforge_changeName	//203
 src.promptconsole f_itemforge_changeCategory	//204
 src.promptconsole f_itemforge_changeSubsection	//205
 src.promptconsole f_itemforge_changeWeight	//206
 src.promptconsole f_itemforge_changeValue	//207
END
return 1

ON=300 399		//Comportamento
DOSWITCH <eval <argn>-300>
 src.sysmessagered ERRO! Nao ha botao 300!
 src.sysmessageyellow Digite um Type valido:
 src.sysmessageyellow Digite todos os TEVENTS separados por ',':
 src.sysmessageyellow Digite todos os Resources separados por ',' Ex.:2 i_pena, 1 i_ponta_ferro, 1 i_vareta:
 src.sysmessageyellow Digite um valor para Resources2:
 src.sysmessageyellow Digite Skillmake no formato 'Skill Valor'. Ex.: ALCHEMY 53.0
 src.sysmessageyellow Digite uma valor para Layer:
 src.sysmessageyellow Digite 0 ou 1 para Pile:
 src.sysmessageyellow Digite um valor para TDATA1:
 src.sysmessageyellow Digite um valor para TDATA2:
 src.sysmessageyellow Digite um valor para TDATA3:
 src.sysmessageyellow Digite um valor para TDATA4:
END
DOSWITCH <eval <argn>-300>
 src.itemforge_open
 src.promptconsole f_itemforge_changeType	//301
 src.promptconsole f_itemforge_changeTEvents	//302
 src.promptconsole f_itemforge_changeResources	//303
 src.promptconsole f_itemforge_changeResources2	//304
 src.promptconsole f_itemforge_changeSkillMake	//305
 src.promptconsole f_itemforge_changeLayer	//306
 src.promptconsole f_itemforge_changePile	//307
 src.promptconsole f_itemforge_changeTData1	//308
 src.promptconsole f_itemforge_changeTData2	//309
 src.promptconsole f_itemforge_changeTData3	//310
 src.promptconsole f_itemforge_changeTData4	//311
END
return 1

ON=400 499		//Características
DOSWITCH <eval <argn>-400>
 src.sysmessagered ERRO! Nao ha botao 400!
 src.sysmessageyellow Digite um dano para esta arma no 'formato min,max'. Ex.: 7,14
 src.sysmessageyellow Digite um valor de 0 a 100 para esta armadura.
 src.sysmessageyellow Digite um valor para velocidade de ataque.
 src.sysmessageyellow Digite o nome de uma skill para usar esta arma.
 src.sysmessageyellow Digite o valor minimo de STR para usar esta arma.
 src.sysmessageyellow Digite 1 ou 0 para o uso de 2 maos desta arma.
END
DOSWITCH <eval <argn>-400>
 src.itemforge_open
 src.promptconsole f_itemforge_changeDam	//401
 src.promptconsole f_itemforge_changeArmor	//402
 src.promptconsole f_itemforge_changeSpeed	//403
 src.promptconsole f_itemforge_changeSkill	//404
 src.promptconsole f_itemforge_changeReqSTR	//405
 src.promptconsole f_itemforge_changeTwoHands	//406
END
return 1

ON=500 599		//@Create
DOSWITCH <eval <argn>-500>
 src.sysmessagered ERRO! Nao ha botao 500! 
 src.sysmessageyellow Digite o numero de uma cor:
 src.sysmessageblue Bloco @Create alterado.
END
DOSWITCH <eval <argn>-500>
 src.itemforge_open
 src.promptconsole f_itemforge_changeColor
 src.f_itemforge_refreshCreate <ARGTXT[0]>
END

[EOF]
