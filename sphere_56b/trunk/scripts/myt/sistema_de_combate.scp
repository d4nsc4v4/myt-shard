//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE COMBATE por
//  Kell
//  Galthar
//  Silver
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:

//@HIT
//
//Verifica Parrying - ??
//  Danifica Escudo
//  Fim
//Tenta esquivar - ??
//  Esquiva
//  Fim
//Tenta envenenar - (atacante é animal venenoso && poisoning > 30% && nao envenenado)
//  Envenena com 10% poisoning e duração de 20 a 30 ataques.
//Tenta envenenar - (armas envenenadas && ( 50% swords || 50% fencing || 76% poison ) )
//  Envenena com spell poison, 30% de skill.
//Tenta queimar - (armas com fogo)
//  Queima (fire collumn) - 3 a 6 de dano
//Ajusta dano
//  STR 
//    >= 95% +3
//    >= 90% +2
//    >= 85% +1
//  ANATOMY
//    + ANATOMY / 25 (0 a 4)
//  SPECIAL_MOVE
//    + x
//  ATACANTE ENVENENADO
//    - (more2 / 15)
//  STAMINA
//    - (3 - (STAM/DEX)*4)
//  PESO
//    - (WEIGHT / MAXWEIGHT) * 4 (0 a 4)
//  QUALIDADE DA ARMA DE ATAQUE
//    - (3 - (HITS/MAXHITS)*4)
//  SKILL DE ATAQUE (afeta dano original: argn1)
//    + ??
//Define local do ataque ( rand 0 a 10% tactics )
//  torax
//  torax
//  mao
//  braco
//  perna
//  perna
//  torax
//  torax
//  pescoco
//  cabeca
//  cabeca
//
//Danifica armadura protegendo local do ataque - 1 a 4
//Danifica armas utilizadas - 0 a 1
//Emite sons
//Cansa atacante: - 0 a 2 de stamina
//Desconcentra vitima: - 0 a 2 de mana

//  ARMADURA ALVO
//    - (dano * AR%)

//Aplica dano

// POISON por magia: i_veneno.MORE2 = 50% magery repeticoes e 7% magery dano
// HEAL POISON por magia: i_veneno.MORE2 -= 33% magery
// POISON por pocao: pocao.MORE2 = (890,1000,1500,2000) (fraca,normal,concentrada,forte)
//   flecha envenenada: dano constante: i_poison_arrow
//   arma envenenada: 4,5% more2 e 1,5% na recarga (recargas de dano constante:2)
//   alvo envenenado: 5% more2 repeticoes e 0.71% more2 dano
//   chao envenenado: 2,5% more2 repeticoes e 0.36% more2 dano
// POISON NPC: i_veneno.MORE2 = ( 10% poisoning ) repeticoes e 1 a ( 10% poisoning * 14%) 

//TAGS usadas:
//tag.agr       nível de agressividade do combate (0 a 10)
//tag.moddam    modificado fixo de dano

//tag.fRP   pontos de rp
//tag.lastkillname nome da ultima vitima fatal
//tag.lastkillUID  uid da ultima vitima fatal
//tag.fpk   contador de vitimas fatais
//tag.lastagr2uid  uid do penultimo agressor
//tag.lastagr2name nome do penultimo agressor
//tag.lastagr1uid  uid do ultimo agressor
//tag.lastagr1name nome do ultimo agressor

//item.tag.name nome original do item antes de ser envenenado
//*****************************************************************************

[PLEVEL 1]
combate

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************
[EVENTS e_combat] 
//*************************************
// @KILL
//*************************************
ON=@KILL
event_kill

//*************************************
// @HITTRY
//*************************************
ON=@HITTRY
//sai do modo batalha se alvo invul, morto, desmaiado, escondido, etc
if ( <src.flags>&(statf_invul|statf_dead|statf_invisible|statf_insubstantial|statf_stone|statf_hidden))
 flags &= ~statf_war
 update
 return 1
endif

//*************************************
// @HITMISS - somente evita mensagem de miss
//*************************************
ON=@HITMISS
return 1

//*************************************
// @HIT
//*************************************
//SRC esta apanhando
//DEFAULT esta atacando
ON=@HIT
//MORE1H = maxItemHits
//MORE1L = itemHits

LOCAL.bonus = 0
LOCAL.penal = 0
//fator de agressividade conjunto (ataque e defesa)
LOCAL.agm=<eval <tag0.agr>+<src.tag0.agr>> //0 a 9 = defensivo, 10 = neutro, 11 a 20, agressivo
//fator de agressividade de ataque (proporcional ao tag.agr)
LOCAL.aga=<eval <between 0,100,<LOCAL.agm>,20>+50> //50 a 150
//fator de agressividade de defesa (inversamente proporcional ao tag.agr)
LOCAL.agd=<eval <between2 0,100,<LOCAL.agm>,20>+50> //150 a 50

//toda acao que gera tentativa de esquiva ganha tatics
//salva skill usada em LOCAL.sg
LOCAL.tact=0
if (<action> == SKILL_WRESTLING)
    LOCAL.sg=<wrestling>
    LOCAL.tact=1
elif (<action> == SKILL_FENCING)
    LOCAL.sg=<fencing>
    LOCAL.tact=1
elif (<action> == SKILL_SWORDSMANSHIP)
    LOCAL.sg=<swordsmanship>
    LOCAL.tact=1
elif (<action> == SKILL_MACEFIGHTING)
    LOCAL.sg=<macefighting>
    LOCAL.tact=1
elif (<action> == SKILL_ARCHERY)
    LOCAL.sg=<archery>

    //atacante archer precisa ganhar tactics aqui pq nao toma melee pra desviar
    skill_gain SKILL_ARCHERY
    skill_gain SKILL_TACTICS
else
    LOCAL.sg=500
endif

///////////////////////////////////////////////
////////////Tentar usar Special Move///////////
///////////////////////////////////////////////

LOCAL.specialmove=<f_trySpecialMove>

///////////////////////////////////////////////
////////////Tentar aparar com escudo///////////
///////////////////////////////////////////////

//se tiver escudo tenta parrying
if (<src.findlayer.layer_hand2.type>==t_shield)

 //se o special move nao for IGNORE AR ou ARMOR PIERCE
 if (<LOCAL.specialmove>!=1) && (<LOCAL.specialmove>!=23)
  //salva escudo para uso mais rapido
  REF1=<src.findlayer.layer_hand2>
  //ganha parrying independente se defende ou nao
  //mas evita macros
  if <argn1> > 5 
     src.skill_gain skill_parrying 
  endif
  LOCAL.p=<eval (<src.parrying>-<LOCAL.sg>)+1000> //0 a 2000
  LOCAL.s=<eval <LOCAL.p>*<LOCAL.agd>>
  //serv.log [PARRYING] PAR:<eval <dLOCAL.s>/4000>% / A:<dLOCAL.aga>% / D:<dLOCAL.agd>%
  if <R400000> <= <LOCAL.s>
//   if (<NPC>==0) // se não for NPC...
//    sysmessageyellow <src.name> apara seu golpe!
//   endif
//   if (<src.NPC>==0) // Se o defensor não for NPC...
//    src.sysmessageyellow Voce apara o golpe!
//   endif
 
   //danifica escudo (mace da maior dano)
   if (<action> == Skill_Macefighting)
    REF1.hits -= <eval <argn1>/3>
   else
    REF1.hits -= <eval <argn1>/6>
   endif
 
   //caso escudo destruido
   if (<REF1.hits> > 08000 ) && !(<REF1.attr> & attr_newbie)
    if (<NPC>==0) // se não for NPC...
     sysmessageorange O escudo de <src.name> foi totalmente arruinado!
    endif
    if (<src.NPC>==0) // Se o defensor não for NPC...
     src.sysmessageorange Seu escudo foi totalmente arruinado!
    endif
    REF1.remove
   else
    REF1.update
   endif
   //som e animacao
   dorand 4
    sfx 0237
    sfx 0236
    sfx 023c
    sfx 023b
   enddo
   anim 19
   return 1
  endif
 endif


///////////////////////////////////////////////
////////////Tentar esquivar o golpe////////////
///////////////////////////////////////////////
//senao tenta esquiva
else 

 LOCAL.p=<eval (<src.tactics>-<LOCAL.sg>)+1000> //0 a 2000
 LOCAL.s=<eval <LOCAL.p>*<LOCAL.agd>>
//serv.log [ESQUIVA] TAC:<eval <dLOCAL.s>/4000>% / A:<dLOCAL.aga>% / D:<dLOCAL.agd>%

 //quando o atacado for um player ou npc e não estiver usando Special Move nem arquery
 if (<R400000> <= <LOCAL.s>) && (!<LOCAL.specialmove>) && (<action> != Skill_Archery) 
//  if (<NPC>==0) // se não for NPC e nem desvia-se de flechas como o Neo...
//   sysmessageyellow <src.name> esquiva-se de seu golpe!
//  endif
//  if (<src.NPC>==0)// Se o defensor não for NPC e nem desvia-se de flechas como o Neo......
//   src.sysmessageyellow Voce se esquiva do golpe!
//  endif
  //som e animação
  dorand 5
   sfx 0239
   sfx 023a
   sfx 0233
   sfx 0238
   sfx 0232
  enddo 
  anim 18
  return 1
 endif
 
 
endif
///////////////////////////////////////////////
//Tentar usar Poison///////////////////////////
///////////////////////////////////////////////

// Se eu for um animal venenoso e a minha presa não está envenenada...
if (<poisoning> >= 300) && (<NPC>!=0) && (!<src.ispoisoned>)
 if ({1 1000} < <poisoning>) && (RAND(4)==0)
  src.veneno <poisoning>
  emotered morde profundamente
 endif
endif

//ARMAS ENVENENADAS
//MORE2=020
//MOREX=carga de veneno
//Se a arma na HAND1 estiver envenenada // && (<src.npc>!=12)
if (<argo.more2>==20)
 src.damage <eval <argo.morex>/100>> dam_poison <UID>
 IF (<local.specialmove>==8)
  src.veneno 2000
  f_gmLog [SPECIAL MOVES] <tag.name>: Infecting aplicado com sucesso.
 ENDIF
 if (<R20000> < <swordsmanship>) || (<R20000> < <fencing>) || (<R13000> < <poisoning>) || (<R13000> < <archery>)
  src.veneno <argo.morex>
 ENDIF
 if ((<argo.morex> <= 150) || (<argo.morex> > 2000))
  argo.more2=0
  argo.morex=0
  if !(strmatch(<argo.tag.name>,)
   argo.name <argo.tag.name>
  endif
  argo.color=<argo.tag0.color>
  sysmessageorange Sua arma nao esta mais envenenada.
  argo.updatex
 ELIF (<action>!=skill_archery)
  argo.morex=<eval (<argo.morex>*11)/12>
 ENDIF
//Se a arma na HAND1 estiver encantada com Flamestrike
elif (<argo.more2>==51)
 src.effect 2 i_fire_column 0 32 0
 src.damage <R5,10> dam_fire <uid>
 argo.morex=<argo.morex>-1
 if (<argo.morex>==0)
  argo.more2=0
 endif
//Se a arma na HAND1 estiver encantada com Bless
elif (<argo.more2>==17)
 src.effect 2 i_fx_heal_effect 0 32 0
 src.spelleffect s_bless <tactics> <UID>
 argo.morex=<argo.morex>-1
 if (<argo.morex>==0)
  argo.more2=0
 endif
//Se a arma na HAND1 estiver encantada com Paralyse
elif (<argo.more2>==38) && !(RAND(6))
 src.effect 2 i_fx_heal_effect 0 32 0 09c
 src.stun <eval {3 6}>
 src.emotered paralizado
 argo.morex=<argo.morex>-1
 if (<argo.morex>==0)
  argo.more2=0
 endif
//Se a arma na HAND1 estiver encantada com Chill
elif (<argo.more2>==350) && !(RAND(6))
 src.spelleffect_chill <tactics>
 argo.morex=<argo.morex>-1
 if (<argo.morex>==0)
  argo.more2=0
 endif
//Se a arma na HAND1 estiver encantada com Lightning
elif (<argo.more2>==30) && (!<R4>)
 src.spelleffect 30 75.0 <UID>
 src.sfx SND_THUNDER
 argo.morex=<argo.morex>-1
 if (<argo.morex>==0)
  argo.more2=0
 endif
endif

///////////////////////////////////////////////
//Aplicando correção de dano por STR///////////
///////////////////////////////////////////////
if (<str> >= 95)
 LOCAL.bonus += 3
elif (<str> >= 90)
 LOCAL.bonus += 2
elif (<str> >= 85)
 LOCAL.bonus += 1
endif
//SERV.log [BONUS-STR] <dLOCAL.bonus>

///////////////////////////////////////////////
//Aplicando correção de dano por ANATOMY///////
///////////////////////////////////////////////
LOCAL.bonus += <eval <anatomy>/250>
//SERV.log [BONUS-ANATOMY] <dLOCAL.bonus>

///////////////////////////////////////////////
//////////Aplicar special moves////////////////
///////////////////////////////////////////////
TRY LOCAL.sm = <<DEF.SMO<dLOCAL.specialmove>_FUNC>>
LOCAL.bonus += <LOCAL.sm>
//serv.log [BONUS-SPMOVE]<dLOCAL.sm>

///////////////////////////////////////////////
//Aplicando correção de dano por POISON////////
///////////////////////////////////////////////
if (<restest 1 i_veneno>)
 LOCAL.penal += <eval <findid.i_veneno.more2>/15>
 if !<R3>
    sysmessagegreen O veneno esta te enfraquecendo. Seus golpes estao fracos!
 endif
// SERV.log [PENAL-VENENO] <dLOCAL.penal>
endif

///////////////////////////////////////////////
//Aplicando correção de dano por STAM//////////
///////////////////////////////////////////////
LOCAL.q = <eval (4*<stam>)/<dex>>
LOCAL.penal += <eval 3-<LOCAL.q>>

//SERV.log [PENAL-STAM] <dLOCAL.penal>

///////////////////////////////////////////////
//Aplicando correção de dano por PESO//////////
///////////////////////////////////////////////
LOCAL.penal += <eval (4*<weight>)/<maxweight>>
//SERV.log [PENAL-WEIGHT] <dLOCAL.penal>

//////////////////////////////////////////////////////////////
//Aplicando correção de dano pela qualidade da arma///////////
//////////////////////////////////////////////////////////////
//MORE1H=qualidade total
//MOTE1L=qualidade atual

if (<argo>)
 LOCAL.q = <eval (4*<argo.hits>)/<argo.maxhits>>
 LOCAL.penal += <eval 3-<LOCAL.q>>
endif
//SERV.log [PENAL-QUALITY] <dLOCAL.penal>

//////////////////////////////////////////////////////////////
//Aplicando correção de dano pela skill de luta //////////////
//////////////////////////////////////////////////////////////

//evita macros pra ganhar tactics
if (<argn1> > 5) && <LOCAL.tact>
    src.skill_gain SKILL_TACTICS 
endif

LOCAL.mod = <eval (<LOCAL.sg>*<LOCAL.aga>)/1000> // 0 a 150
LOCAL.mod += 50 //50 a 200
//SERV.log [MOD-SKILL] <dLOCAL.mod>%
argn1 = <eval (<argn1>*<LOCAL.mod>)/100>

//////////////////////////////////////////////////////////////
// Determinando tipo de dano//////////////////////////////////
//////////////////////////////////////////////////////////////

//<ARGO> == arma usada

//Determina tipo de dano por arma
if (<argo.type>==t_weapon_fence)||(<argo.type>==t_weapon_bow)||(<argo.type>==t_weapon_mace_pick)||(<argo.type>==t_weapon_arrow)||(<argo.type>==t_weapon_xbow)||(<argo.type>==t_weapon_bolt)
 LOCAL.tipo=dam_pierce
elif (<argo.type>==t_weapon_mace_sharp)||(<argo.type>==t_weapon_sword)||(<argo.type>==t_weapon_axe)
 LOCAL.tipo=dam_slash
else
 LOCAL.tipo=dam_physical
endif

//Determinar tipo de dano especial
if (<argo.dmgfire>)
 src.damage <argo.dmgfire> dam_fire <uid>
endif
if (<argo.dmgcold>)
 src.damage <argo.dmgcold> dam_cold <uid>
endif
if (<argo.dmgenergy>)
 src.damage <argo.dmgenergy> dam_energy <uid>
endif
if (<argo.dmgpoison>)
 src.damage <argo.dmgpoison> dam_poison <uid>
endif

///////////////////////////////////////////////
//Determinar região do golpe///////////////////
///////////////////////////////////////////////

//IF (<action> == Skill_Wrestling)
//  dorand (<eval <wrestling>/100>)
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=no braco
//   LOCAL.loc=na perna
//   LOCAL.loc=na perna
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no cabeca
//   LOCAL.loc=no pescoco
//  enddo
//ELIF (<action> == Skill_fencing)
//  dorand (<eval <fencing>/100>)
//   LOCAL.loc=na mao
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=no braco
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no pescoco
//   LOCAL.loc=no pescoco
//   LOCAL.loc=na cabeca
//  enddo
//elIF (<action> == Skill_Macefighting)
//  dorand (<eval <macefighting>/100>)
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=no torax
//   LOCAL.loc=no braco
//   LOCAL.loc=no braco
//   LOCAL.loc=na perna
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no cabeca
//  enddo
//elif (<action> == Skill_Swordsmanship)
//  dorand (<eval <swordsmanship>/100>)
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=no braco
//   LOCAL.loc=na perna
//   LOCAL.loc=na perna
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no cabeca
//   LOCAL.loc=no pescoco
//  enddo
//elif (<action> == Skill_Archery)
//  dorand (<eval <archery>/100>)
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=na mao
//   LOCAL.loc=no braco
//   LOCAL.loc=na perna
//   LOCAL.loc=na perna
//   LOCAL.loc=no torax
//   LOCAL.loc=no torax
//   LOCAL.loc=no cabeca
//   LOCAL.loc=no pescoco
//  enddo
//endif

///////Trocando nomes engraçados.
//if (<src.body>==c_snake_giant) || (<src.body>==c_snake)
// dorand 2
//  LOCAL.loc=no corpo
//  LOCAL.loc=na cabeca
// enddo
//endif
//if (<src.body>==c_headless) && (strmatch(<LOCAL.loc>, na cabeca))
// LOCAL.loc=entre os ombros
//endif
//if (<src.body>==c_headless) && (strmatch(<LOCAL.loc>, no pescoco))
// LOCAL.loc=entre os ombros
//endif

///////////////////////////////////////////////
//Determinar força do golpe////////////////////
///////////////////////////////////////////////

//if (<eval <argn1>+<LOCAL.bonus>> >= 30)
// LOCAL.cbt.for=monstruoso
//elif (<eval <argn1>+<LOCAL.bonus>> >= 25)
// LOCAL.cbt.for=destruidor
//elif (<eval <argn1>+<LOCAL.bonus>> >= 20)
// LOCAL.cbt.for=potente
//elif (<eval <argn1>+<LOCAL.bonus>> >= 15)
// LOCAL.cbt.for=muito forte
//elif (<eval <argn1>+<LOCAL.bonus>> >= 10)
// LOCAL.cbt.for=forte
//elif (<eval <argn1>+<LOCAL.bonus>> >= 5)
// LOCAL.cbt.for=fraco
//else
// LOCAL.cbt.for=patetico
//endif

///////////////////////////////////////////////
//Danifica armaduras 
///////////////////////////////////////////////

dorand (<eval (<tactics>)/100>)
 LOCAL.loc=1    //torax
 LOCAL.loc=1    //torax
 LOCAL.loc=2    //mao
 LOCAL.loc=3    //braco
 LOCAL.loc=4    //perna
 LOCAL.loc=4    //perna
 LOCAL.loc=1    //torax
 LOCAL.loc=1    //torax
 LOCAL.loc=5    //pescoco
 LOCAL.loc=6    //cabeca
 LOCAL.loc=6    //cabeca
enddo

//se defensor for player
if !<SRC.NPC>
    doswitch <LOCAL.loc>
        src.emotered 1<LOCAL.loc>
        cmb_damageSrcArmor layer_chest,<argn1>
        cmb_damageSrcArmor layer_gloves,<argn1>
        cmb_damageSrcArmor layer_pants,<argn1>
        cmb_damageSrcArmor layer_arms,<argn1>
        cmb_damageSrcArmor layer_collar,<argn1>
        cmb_damageSrcArmor layer_helm,<argn1>
    enddo        
endif 

///////////////////////////////////////////////
//Danificar armas /////////////////////////////
///////////////////////////////////////////////

if (<NPC>==0) && (<action> != Skill_Wrestling)
 argo.more1l -= <R1>
 if (<argo.more1l> > 08000 )
  sysmessageorange Sua arma foi destruida!
  if (<src.NPC>==0) // Se o defensor não for NPC...
   src.sysmessageorange A arma de <src.name> foi destruida!
  endif
  argo.remove
 else
  if <argo.hits> > <argo.maxhits>
    serv.log [COMBATE] arma <argo> com hits <argo.hits> > <argo.maxhits>
    argo.hits=<argo.maxhits>
  endif
  argo.update
 endif
 
 if (<action> == Skill_Archery)
  argo.morey -= 1
  if (!<argo.morey>)
   argo.cont=<findlayer.layer_pack>
   if <argo.type>==t_weapon_bow
       sysmessageorange O barbante do seu arco se partiu!
       emoteorange barbante quebrou
       argo.name=<argo.tag.name> (sem barbante)
   else
       sysmessageorange A engrenagem de sua besta se quebrou!
       emoteorange engrenagem quebrou
       argo.name=<argo.tag.name> (sem engrenagem)
   endif   
  endif
  argo.update
 endif
endif

///////////////////////////////////////////////
/////////////Produtos finais///////////////////
///////////////////////////////////////////////

///// Falas bonitinhas
//if (<NPC>==0) //Se for um jogador e não um NPC...
// dorand 3
//  sysmessageblue Um <LOCAL.cbt.for> golpe acerta <src.name> <LOCAL.loc>.
//  sysmessageblue Voce desfere um <LOCAL.cbt.for> golpe <LOCAL.loc> de <src.name>.
//  sysmessageblue Seu <LOCAL.cbt.for> golpe pega em cheio <LOCAL.loc> de <src.name>.
// enddo
//endif
//if (<src.NPC>==0) //Se o alvo for um jogador...
// dorand 3
//  src.sysmessagered <name> acerta um <LOCAL.cbt.for> golpe em voce <LOCAL.loc>.
//  src.sysmessagered O <LOCAL.cbt.for> golpe de <name> te acerta <LOCAL.loc>.
//  src.sysmessagered Um golpe <LOCAL.cbt.for> de <name> te pega desprevinido <LOCAL.loc>
// enddo
//endif

/////Produzir sons
IF (<action> == Skill_wrestling)
 dorand 4
  sfx 014a
  sfx 013d
  sfx 0137
  sfx 013b
 enddo
ELIF (<action> == Skill_Swordsmanship) || (<action> == Skill_Fencing)
 dorand 4
  sfx 0237
  sfx 0236
  sfx 023c
  sfx 023b
 enddo
ELIF (<action> == Skill_Macefighting)
 dorand 4
  sfx 0149
  sfx 0143
  sfx 0146
  sfx 023b
 enddo
ELIF (<action> == Skill_Archery)
//Doubleshot?
 IF (<LOCAL.specialmove>==22)
  sfx 0523
 ELIF (<weapon.tag0.sfx>)
  sfx <weapon.tag0.sfx>
 ELSE
  sfx 0234
 ENDIF
//flecha coletável?
 IF (<weapon.tag0.colect>) && (!<R3>)
  serv.newitem <weapon.TAG.OVERRIDE.AMMOTYPE>
  new.cont=<src.UID>
 ENDIF
endif

///// APLICA Modificadores de dano
//serv.log [HIT-<account>]<dLOCAL.bonus> / <dLOCAL.penal> / <eval <argn1>> :: <Action>
argn1 += <LOCAL.bonus>
argn1 += <tag0.moddam>
argn1 -= <LOCAL.penal>


///////////////////////////////////////////////////
//Aplicando correção de dano por AR no que sobrou//
///////////////////////////////////////////////////
//DEVERIA estar no @GETHIT, mas a informacao do 
//special move esta aqui
if (<argn1> > 0) && (<LOCAL.specialmove>!=1) && (<LOCAL.specialmove>!=23)    //Não permite se usando IgnoreAR ou ARPierce
 //LOCAL.ar=<R<R0,<src.ar>>,<src.ar>>
 //LOCAL.ar=<R<src.ar>>
 LOCAL.ar=<src.ar>
// serv.log [ARMOR] <eval <argn1>> / <dLOCAL.ar> / <eval <BETWEEN2 1,<argn1>,<LOCAL.ar>,100>>
 argn1=<BETWEEN2 1,<argn1>,<LOCAL.ar>,100>
endif

//dano minimo é 1
if (<argn1> <= 0)
 argn1=1
endif

///// Transfere e aplica dano
src.damage <argn1> <LOCAL.tipo> <uid>

///// Double strike? Double shot?
IF (<LOCAL.specialmove>==7) || (<LOCAL.specialmove>==22)
 src.timerf 1,damage,<argn1>,<LOCAL.tipo>,<uid>
ENDIF

/////cansar atacante
if (<stam> >= 6)
 stam -= <R2>
endif

/////desconcentrar vítima
if (<src.mana> >= 6)
 src.mana -= <R2>
endif

///// Remove Special Moves
if (<LOCAL.specialmove>)
 f_clrSpecialMove
endif

///// Marca assassino (sistema de morte)
//if (<argn1> >= <src.hits>)
// if (<NPC>==0)
//  tag.lastkillname=<src.tag.name>
//  tag.lastkillUID=<src.uid>
//  tag.fpk=(<tag0.fpk>+1)
// endif
//endif

///// Salvar nomes para .ficha
IF (<src.npc>==0) && (<npc>==0)
 IF (<src.tag.lastagr1uid> != <uid>)
  src.tag.lastagr2uid=<src.tag.lastagr1uid>
  src.tag.lastagr2name=<src.tag.lastagr1name>
  src.tag.lastagr1uid=<uid>
  src.tag.lastagr1name=<tag.name>
 endif
endif

/////Executar script do HIT da arma
IF (<argo>)
 argo.trigger=@hit
endif

return 1


//*************************************
ON=@GETHIT
//*************************************
//e_combat deve estar como TEVENT para ser chamado sempre por ultimo
//nao pode haver GetHit direto nos CHARDEF por que eles nao serao chamados
//usar como EVENT ou como TEVENT desde que seja antes do e_combat
//argn1 = damage
//argn2 = type
//serv.log [COMBAT] <name>
/////Se estiver invulnerável, deixa queto.
if (<flags>&statf_invul)
 return 1
///// Bloquear dano se imune
elif (<argn2>&<src.tag0.immune>)
 DORAND 2
  src.sysmessageorange <name> parece ser imune a este tipo de ataque.
  emotered ileso
 ENDDO
 return 1
///// Piorar dano se agravante
elif (<tag0.aggravate>&<argn2>)
 LOCAL.agg=<R150,325>         //Agrava sempre de 50 a 225% de dano
 argn1 = <eval (<argn1>*<LOCAL.agg>)/100>
 DORAND 2
  act.sysmessageorange <name> parece muito assustado com este tipo de ataque.
  emotered dor
 ENDDO
///// Adicionar HITS se regenera com dano
elif (<tag0.regendam>&<argn2>)
 hits += <argn1>
 DORAND 2
  act.sysmessageorange <name> parece melhor agora do que antes do ataque.
  emotered aliviado
 ENDDO
 return 1
endif

disturb

///// Amenizar dano se resistente (compativel com OSI e Sphere original)
LOCAL.res=0
if (<argn2>&dam_fire)
 LOCAL.res=<RESFIRE>
elif (<argn2>&dam_cold)
 LOCAL.res=<RESCOLD>
elif (<argn2>&dam_poison)
 LOCAL.res=<RESPOISON>
elif (<argn2>&dam_energy)
 LOCAL.res=<RESENERGY>
endif

LOCAL.res=<eval 100-<LOCAL.res>>
argn1=<eval (<argn1>*<LOCAL.res>)/100>

/////aos damage (show counter above player head)
if <argn1> > 0
 LOCAL.damaged=<uid>
 FORPLAYERS 10
   if <isonline>
      SENDPACKET  0bf W11 W022 01 D<LOCAL.damaged> B<argn1>
   endif
 ENDFOR

// if (0<npc> == 0) && (<hits> > 0)
//  SENDPACKET  0bf W11 W022 01 D<UID> B<argn1> 
// endif
// if (<src> > 0 ) && (0<src.npc> == 0) && (<hits> > 0) && (<src.uid> != <uid>)
//  SRC.SENDPACKET  0bf W11 W022 01 D<UID> B<argn1> 
// endif

 //// anima
 cry
endif

////perpetuar agreção
if <NPC> && (!<IsMyEnemy <src>>) && (!<src.flags>&statf_invul) //&& (<SRC> != <UID>)
 attack <src>
endif

////decrementa pontos
hits -= <argn1>

if (<hits><=0)
    src.trigger @kill
endif

return 1

//*************************************
// @SPELLEFFECT
//*************************************
ON=@SPELLEFFECT
//argn1=spell
//argn2=magery skill

//so permite ganhar skill se a magia for corretamente encantada e nao foi 'self cast'
//serv.log [SPELLEFFECT] <src.name> -- <name>
if (<src>!=<uid>)
    src.skill_gain SKILL_MAGERY
endif

//s_Cure e veneno
if (<argn1>==11) && (<restest 1 i_veneno>)
 LOCAL.poisoning=<argn2>
 LOCAL.poisoning=<eval <LOCAL.poisoning>/30>
 findid.i_veneno.more2=<eval <findid.i_veneno.more2>-<LOCAL.poisoning>>
 if (<eval <findid.i_veneno.more2>> >= 08000) || (<eval <findid.i_veneno.more2>> <= 0)
  findid.i_veneno.remove
  flags=<flags>&~statf_poisoned
  update
  sysmessageblue O veneno foi removido do seu corpo!
 endif
 sysmessageblue Voce se sente um tanto melhor. 
 return 1
endif

//s_Cure e peste
if (<argn1>==11) && (<restest 1 i_mry_peste>)
 if (<argn2> < 1001)
  sysmessagered A cura foi muito fraca para amenizar a peste.
 else
  src.findid.i_mry_peste.more2=(<src.findid.i_mry_peste.more2>/3)
  sysmessageblue Voce se sente um pouco melhor.
 endif
 return 1
endif

//s_Poison
if (<argn1>==20)
 veneno <argn2>
 effect 2 i_fx_curse 0 30 0
 update
 return 1
endif

//s_Fireball. Manter dano somente dam_magic|dam_fire, tirando o dam_physical.
if (<argn1>==18)
 DAMAGESPELL s_Fireball,dam_magic|dam_fire,<src.magery>,<src.uid>
 RETURN 1
endif

//*************************************
// @ITEMDCLICK
//*************************************
//ON=@ITEMDCLICK
//if (<act.Baseid>==i_bottle_green) && (<act.more1>==s_poison)
// LOCAL.actuid=<act.uid>
// newitem i_mry_usepoison
// new.link <LOCAL.actuid>
// new.equip
// new.use
// return 1
//endif

//*************************************
// @USERSPECIALMOVE
//*************************************
ON=@USERSPECIALMOVE
tag.specialmove=<argn1>
sysmessagegreen Usando Moviemento Especial: <DEF.SMO<eval <tag0.specialmove>>_NAME>
f_gmLog [SPECIAL MOVES] <account.name>:<tag.name> seleciona SM <DEF.SMO<eval <tag0.specialmove>>_NAME>

//*************************************
// @ITEMUNEQUIP
//*************************************
ON=@ITEMUNEQUIP
//Cancela o special move se desequipar a arma
IF (<f_isWeapon <act>>)
 f_clrSpecialMove
endif



//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// combate
//*****************************************************************************
[FUNCTION combate]
ctag.roll=<eval (<tag0.agr>*24)+70>
src.dialog d_estrategia
return 1

//*****************************************************************************
// cmb_damageSrcArmor
//*****************************************************************************
[FUNCTION cmb_damageSrcArmor]
REF1=<src.findlayer.<argv0>>
if !<REF1>
    return 1
endif
REF1.more1l -= <eval <argv1>/6>
if <REF1.more1l> > 08000
    if !<npc> // se não for NPC...
        sysmessageorange A protecao de <src.name> [<REF1.name>] foi totalmente arruinada!
    endif
    if !<src.npc> // Se o defensor não for NPC...
        src.sysmessageorange Sua protecao [<REF1.name>] foi totalmente arruinada!
    endif
    REF1.remove
else
    REF1.update
endif 

//*****************************************************************************
//*****************************************************************************
// GUMPs
//*****************************************************************************
//*****************************************************************************

[DIALOG d_estrategia]
200, 150
noclose
nomove
PAGE 0
gumppic 0 0 1140
gumppic 61 88 1143
gumppic 136 41 2445
text 153 41 1101 0
text 65 69 455 1
text 263 69 455 2
button <eval <ctag0.roll>> 94 2117 2118 0 0 0
button 45 95 2223 2223 1 0 1
button 328 95 2224 2224 1 0 2
button 137 118 2117 2118 1 0 3
button 137 139 2117 2118 1 0 4
button 137 161 2117 2118 1 0 5
text 159 114 1345 3
text 159 136 1345 4
text 158 156 1345 5
button 156 196 249 248 1 0 6

[DIALOG d_estrategia TEXT]
Estrategia
Defensiva
Agressiva
Agressiva
Defensiva
Neutra

[DIALOG d_estrategia BUTTON]
on=1
if (<tag0.agr><=0)
 tag.agr=0
 DIALOG d_estrategia
 return 1
endif
tag.agr=<eval (<tag0.agr>-1)>
ctag.roll=<eval (<tag0.agr>*24)+70>
DIALOG d_estrategia
return 1

on=2
if (<tag0.agr>>=10)
 tag.agr=10
 DIALOG d_estrategia
 return 1
endif
tag.agr=<eval (<tag0.agr>+1)>
ctag.roll=<eval (<tag0.agr>*24)+70>
DIALOG d_estrategia
return 1

on=3
tag.agr=10
ctag.roll=(<tag.agr>*24)+70
DIALOG d_estrategia
return 1

on=4
tag.agr=0
ctag.roll=(<tag.agr>*24)+70
DIALOG d_estrategia
return 1

on=5
tag.agr=5
ctag.roll=(<tag.agr>*24)+70
DIALOG d_estrategia
return 1

on=6
return 1

[EOF]
