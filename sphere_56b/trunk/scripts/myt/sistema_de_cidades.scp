//############################################################################
//
//              TOWN
//
//############################################################################


//TAGS da region de Town
//TAG.TOWN_TYPE
//  1   Cpital
//  2   Cidade
//  3   Vila
//TAG.TOWN_RACE
//  Elfo
//  Anao
//  Humano
//  Drow
//  Orc
//  Misc
//TAG.TOWN_CAPITAL      AREADEF da área que comanda as leis. Se esta comandar, então é o nome do reino.
//TAG.TOWN_TRESURE      Quantidade de mc que a town possue.
//TAG.TOWN_RACE_FORBIDDEN   Raças separadas por ;
//TAG.TOWN_RACE_FRIEND      Raças separadas por ;
//TAG.TOWN_UID_FORBIDDEN    UIDs sepadaras por ;
//TAG.TOWN_UID_FRIEND       UIDs separadas por ;
//TAG.TOWN_MASTER       UID do mestre da Town
//TAG.TOWN_COUNSELORS       UIDs dos conselheiros da Town separadas por ;
//TAG.TOWN_SERVANTS     UIDs dos funcionarios NPC separadas por ;
//TAG.TOWN_CHPRICE      Preço do tile² do terreno para construção. Proibido contruir se empty

[DEFNAME TOWN]
TOWN_TYPE_CAPITAL   1
TOWN_TYPE_CITY      2
TOWN_TYPE_VILLAGE   3

TOWN_SERVANT_GATE   1
TOWN_SERVANT_GUARD  2
TOWN_SERVANT_SENESCAL   3

TOWN_LIST_COUNSELORS    1
TOWN_LIST_FRIENDS   2
TOWN_LIST_FORBIDDEN 3

//Tempo que demora para o Senescal rodar a função TOWN_TRIG
TOWN_TRIG_TIME      22*60*60

//Razão da fama de um guarda pelo custo em MC para compra.
//Ex.: Um guarda com 5000 de fama com este número a 10 custaria 500 mc
FAME_COPPER_RATE_BUY    20

//Razão da fama de um guarda pelo custo em MC para salário.
//Ex.: Um guarda com 5000 de fama com este número a 50 custaria 50 mc
FAME_COPPER_RATE_ORD    120

//Mínimo que pode custar um funcionário em mc
SERVANT_MIN_COST    40

//Salário mínimo do funcionário em mc
SERVANT_MIN_ORD     12

[DEFNAME guards_task]
//TAG.TASK:
GUARD_TASK_WANDER   0
GUARD_TASK_STAND    1
GUARD_TASK_WARN     2
GUARD_TASK_ATTACK   3
GUARD_TASK_GO       4
GUARD_TASK_FOLLOW   5
GUARD_TASK_GUARD    6
GUARD_TASK_POST     7
GUARD_TASK_PATROL   8
GUARD_TASK_DISMISSING   9
GUARD_TASK_REMOVE   10

GUARD_WARN_WARMODE  1
GUARD_WARN_WEAPON   2
GUARD_WARN_LOCKPICK 3
GUARD_WARN_FORBIDDEN    4
GUARD_ATTACK_MONSTER    5

//Máximo de Waypoints que os guardas podem ter.
GUARD_WP_MAX        10




//******************************************************************************
//******************************************************************************
//############################################################################**
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$**
//                                        **
//############################################################################**
//                                        **
//                  TOWN                      **
//                                        **
//#############################################################################*
//                                        **
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$**
//############################################################################**
//******************************************************************************
//******************************************************************************


//****************************************************************************
//                  ITEMDEFS
//****************************************************************************
[ITEMDEF I_BOX_TOWN_TRESURE]
ID=i_box_brass
TYPE=t_container
NAME=Tesouro do governo

on=@ClientToolTip
f_sendTooltip <name>, <TOWN_GET_STRTRESURE>
return 1

on=@dclick
IF (<findcont.0>)
 timerd=1
 src.timerf 1,dclick,<uid>
 src.sysmessagegreen Calculando tesouro
 return 1
ENDIF
IF (<region.tag0.TOWN_TRESURE>)
 CALL TOWN_GET_STRTRESURE
 IF (<LOCAL.g>)
  serv.newitem i_gold
  new.amount=<LOCAL.g>
  new.cont=<UID>
 ENDIF
 IF (<LOCAL.s>)
  serv.newitem i_coin_silver
  new.amount=<LOCAL.s>
  new.cont=<UID>
 ENDIF
 IF (<LOCAL.c>)
  serv.newitem i_coin_copper
  new.amount=<LOCAL.c>
  new.cont=<UID>
 ENDIF
 region.tag.town_tresure=
 timer=60
 update
ENDIF

on=@timer
FORCONT <UID>
 if (<baseid>==i_gold)
  region.tag0.town_tresure += <eval <amount>*100>
 elif (<baseid>==i_coin_silver)
  region.tag0.town_tresure += <eval <amount>*10>
 elif (<baseid>==i_coin_copper)
  region.tag0.town_tresure += <amount>
 endif
endfor
empty
update
return 1

//****************************************************************************
//                  FUNCTIONS
//****************************************************************************


//*****************************************************************************
[FUNCTION town]
//*****************************************************************************
//Configura town
if (STRMATCH(*world*,<region.defname>))
 sysmessagered O mundo eh selvagem...
 return 1
elif (!<region.tag0.town_type>)
 MENU m_new_town
else
 IF (!<Isempty <region.tag.TOWN_LASTTIME>>)
  sysmessageorange Ultimo trabalho do senescal em <region.tag.TOWN_LASTTIME>.
 ELSE
  sysmessagered O senescal nunca efetuou pagamento ou limpou listas..
 ENDIF
 dialog d_town
endif

//*****************************************************************************
[FUNCTION TOWN_GET_STRTRESURE]
//*****************************************************************************
//Retorna uma string representando o tesouro de uma town.
LOCAL.c=<region.tag0.town_tresure>
WHILE (<LOCAL.c> >= 100)
 LOCAL.g += 1
 LOCAL.c -= 100
ENDDO
WHILE (<LOCAL.c> >= 10)
 LOCAL.s += 1
 LOCAL.c -= 10
ENDDO
return <dLOCAL.g>mo <dLOCAL.s>mp <dLOCAL.c>mc


//*****************************************************************************
[FUNCTION TOWN_RESOLV_DEMENCES]
//*****************************************************************************
//Seta LOCAL.KINGDOM, LOCAL.CITY e LOCAL.VILLAGE pro gump d_town
IF (<region.tag.TOWN_TYPE>==TOWN_TYPE_CAPITAL)
 LOCAL.Kingdom=do reino de <region.tag.TOWN_KINGDOM>
 call TOWN_RESOLV_CITIES <EXPLODE ;,<region.tag.town_demences>>
 call TOWN_RESOLV_VILLAGES <EXPLODE ;,<region.tag.town_demences>> 
ELIF (<region.tag.TOWN_TYPE>==TOWN_TYPE_CITY)
 IF (<serv.resources.<region.tag.TOWN_CAPITAL>>)
  LOCAL.KINGDOM=<serv.resources.<region.tag.TOWN_CAPITAL>.name>
 ELSE
  LOCAL.KINGDOM=Autonomo
 ENDIF
 LOCAL.CITY=<region.name>
 CALL TOWN_RESOLV_VILLAGES <EXPLODE ;,<region.tag.town_demences>> 
ENDIF

//*****************************************************************************
[FUNCTION TOWN_RESOLV_CITIES]
//*****************************************************************************
//Conta o número de cities da town. ARGS é a lista de towns separadas por ;
FOR i 1 <argv>
 IF (<serv.resources.<argv[<LOCAL.i>]>.tag.TOWN_TYPE>==TOWN_TYPE_CITY)
  LOCAL.CITY += 1
 ENDIF
ENDFOR

//*****************************************************************************
[FUNCTION TOWN_RESOLV_VILLAGES]
//*****************************************************************************
//Conta o número de villages da town. ARGS é a lista de towns separadas por ;
FOR i 1 <argv>
 IF (<serv.resources.<argv[<LOCAL.i>]>.tag.TOWN_TYPE>==TOWN_TYPE_VILLAGE)
  LOCAL.VILLAGE += 1 
 ENDIF
ENDFOR

//*****************************************************************************
[FUNCTION TOWN_CHECK_SPK_PRIVS]
//*****************************************************************************
//Checa se o item DEFAULT tem privilégios (nível 0, 1, 2 e 3) para comandar um NPC.
obj=<argv[0]>
IF (<ISGM>)
 OBJ.SETMEMORY memory_friend,<uid>
 RETURN 1
ENDIF
IF (0<argv1>==2)
 IF !(STRMATCH(*<UID>*,<obj.GET_TOWN_COUNSELORS>))
  obj.timerf 1,say,Esta ordem so pode ser dada pelo conselho de <serv.resources.<obj.GET_TOWN>.name>.
  return 0
 ELSE
  OBJ.SETMEMORY memory_friend,<uid>
  RETURN 1
 ENDIF
ELIF (0<argv1>==3)
 IF !(STRMATCH(*<UID>*,<obj.GET_TOWN_MASTER>))  
  obj.timerf 1,say,Esta ordem so pode ser dada por <uid.<obj.GET_TOWN_MASTER>.tag.name>
  return 0
 ELSE
  OBJ.SETMEMORY memory_friend,<uid>
  RETURN 1
 ENDIF
ENDIF
IF (<tag0.comcapuz>)
 obj.timerf 1,say,E quem eh voce? Nao posso ver seu rosto!
 return 0
ELIF (STRMATCH(*<UID>*,<obj.TOWN_GET_FORB_UID>))
 obj.timerf 1,say,Voce esta <sex proibido/proibida> de entrar em <obj.region.name>. Va embora.
 return 0
ELIF (STRMATCH(*<UID>*,<obj.TOWN_GET_FRIEND_UID>))
 return 1
ELIF (STRMATCH(*<tag.raca>*,<obj.TOWN_GET_FORB_RACE>))
 obj.timerf 1,say,Nenhum <tag.raca> deve entrar em <obj.region.name>. Va embora.
 return 0
endif
return 1

//*****************************************************************************
[FUNCTION TOWN_CHECK_PRIVS]
//*****************************************************************************
//Checa se o item DEFAULT tem privilégios (nível 0, 1, 2 e 3) na town.
IF (<ISGM>)
 RETURN 4
ELIF (STRMATCH(*<UID>*,<GET_TOWN_MASTER>))  
 RETURN 3
ELIF (STRMATCH(*<UID>*,<GET_TOWN_COUNSELORS>))
 return 2
ELIF (STRMATCH(*<UID>*,<region.tag.TAG.TOWN_UID_FRIEND>))
 return 1
ELIF (STRMATCH(*<UID>*,<region.tag.TAG.TOWN_UID_FORBIDDEN>))
 return 0
ELIF (STRMATCH(*<tag.raca>*,<region.tag.TAG.TOWN_RACE_FORBIDDEN>))
 return 0
endif
return 1

//*****************************************************************************
[FUNCTION GET_TOWN]
//*****************************************************************************
//Retronar DEFNAME da town do NPC
IF (!<IsEmpty <tag.town>>)
 return <tag.town>
ELIF (<region.tag0.TOWN_TYPE>)
 return <region.defname>
endif

//*****************************************************************************
[FUNCTION TOWN_COUNSELORS]
//*****************************************************************************
//Retorna quantos counselors tem a town. Se <argn>, retorna um dos membros
LOCAL.town=<GET_TOWN>
IF (!<serv.resources.<LOCAL.town>>)
 return 0
ELIF (<IsEmpty <serv.resources.<LOCAL.town>.tag.town_counselors>>)
 return 0
ENDIF
IF (!<argn>)
 call _TOWN_COUNSELORS <explode ;,<serv.resources.<LOCAL.town>.tag.town_counselors>>
ELSE
 LOCAL.i=<argn>
 call _TOWN_COUNSELORS_ <explode ;,<serv.resources.<LOCAL.town>.tag.town_counselors>>
ENDIF
return <LOCAL.ret>

[FUNCTION _TOWN_COUNSELORS]
LOCAL.ret=<eval <argv>-1>

[FUNCTION _TOWN_COUNSELORS_]
LOCAL.ret=<argv[<LOCAL.i>]>

//*****************************************************************************
[FUNCTION TOWN_FORBIDDEN_UIDs]
//*****************************************************************************
//Retorna quantos UIDs banidos tem a town. Se <argn>, retorna um dos UIDs
LOCAL.town=<GET_TOWN>
IF (!<serv.resources.<LOCAL.town>>)
 return 0
ELIF (<IsEmpty <serv.resources.<LOCAL.town>.tag.town_UID_FORBIDDEN>>)
 return 0
ENDIF
IF (!<argn>)
 call _TOWN_FORBIDDEN_UIDs <explode ;,<serv.resources.<LOCAL.town>.tag.town_UID_FORBIDDEN>>
ELSE
 LOCAL.i=<argn>
 call _TOWN_FORBIDDEN_UIDs_ <explode ;,<serv.resources.<LOCAL.town>.tag.town_UID_FORBIDDEN>>
ENDIF
return <LOCAL.ret>

[FUNCTION _TOWN_FORBIDDEN_UIDs]
LOCAL.ret=<eval <argv>-1>

[FUNCTION _TOWN_FORBIDDEN_UIDs_]
LOCAL.ret=<argv[<LOCAL.i>]>


//*****************************************************************************
[FUNCTION TOWN_FRIEND_UIDs]
//*****************************************************************************
//Retorna quantos UIDs friend tem a town. Se <argn>, retorna um dos UIDs
LOCAL.town=<GET_TOWN>
IF (!<serv.resources.<LOCAL.town>>)
 return 0
ELIF (<IsEmpty <serv.resources.<LOCAL.town>.tag.town_UID_FRIEND>>)
 return 0
ENDIF
IF (!<argn>)
 call _TOWN_FRIEND_UIDs <explode ;,<serv.resources.<LOCAL.town>.tag.town_UID_FRIEND>>
ELSE
 LOCAL.i=<argn>
 call _TOWN_FRIEND_UIDs_ <explode ;,<serv.resources.<LOCAL.town>.tag.town_UID_FRIEND>>
ENDIF
return <LOCAL.ret>

[FUNCTION _TOWN_FRIEND_UIDs]
LOCAL.ret=<eval <argv>-1>

[FUNCTION _TOWN_FRIEND_UIDs_]
LOCAL.ret=<argv[<LOCAL.i>]>


//*****************************************************************************
[FUNCTION GET_TOWN_MASTER]
//*****************************************************************************
//Retorna a UID do MASTER da town do NPC
LOCAL.town=<GET_TOWN>
IF (!<serv.resources.<LOCAL.town>>)
 return 0
ELIF (<IsEmpty <serv.resources.<LOCAL.town>.tag.town_master>>)
 return 0
ENDIF
return <serv.resources.<LOCAL.town>.tag.town_master>

//*****************************************************************************
[FUNCTION GET_TOWN_COUNSELORS]
//*****************************************************************************
//Retorna uma string com todos os UIDs dos conselors + do master da town
LOCAL.town=<GET_TOWN>
IF (!<serv.resources.<LOCAL.town>>)
 return 0
ELIF (<IsEmpty <serv.resources.<LOCAL.town>.tag.town_counselors>>)
 return <GET_TOWN_MASTER>
ENDIF
return <serv.resources.<LOCAL.town>.tag.town_master>;<serv.resources.<LOCAL.town>.tag.town_counselors>

//*****************************************************************************
[FUNCTION TOWN_GET_FRIEND_RACE]
//*****************************************************************************
//Retorna a lista de raças amigas da town deste NPC.
LOCAL.town=<GET_TOWN>
IF (!<serv.resources.<LOCAL.town>>)
 return 0
ELIF (<IsEmpty <serv.resources.<LOCAL.town>.tag.town_race_friend>>)
 return <serv.resources.<LOCAL.town>.tag.town_race>
ENDIF
RETURN <serv.resources.<LOCAL.town>.tag.town_race>;<serv.resources.<LOCAL.town>.tag.town_race_friend>

//*****************************************************************************
[FUNCTION TOWN_GET_TRESURE]
//*****************************************************************************
//Retorna o tesouro da town em mc. Se existe <argv[0]>, é o defname da town. Se não, usa a town do NPC.
IF (!<IsEmpty <argv[0]>>)
 IF (<serv.resources.<argv[0]>.tag0.town_type>)
  LOCAL.town=<argv[0]>
 ELSE
  return -1
 ENDIF
ELSE
 LOCAL.town=<GET_TOWN>
ENDIF
RETURN <serv.resources.<LOCAL.town>.tag0.town_tresure>

//*****************************************************************************
[FUNCTION TOWN_CONSUME_TRESURE]
//*****************************************************************************
//Consome <argn> do tesouro da town em mc.
LOCAL.town=<GET_TOWN>
IF (!<serv.resources.<LOCAL.town>>)
 IF (!<IsEmpty <region.tag0.town_type>>)
  LOCAL.town=<region.defname>
 ELSE
  serv.log ALOOOW! <name> (<UID>) nao tem town para consumir tesouro!
  return 0
 ENDIF
ENDIF
serv.resources.<LOCAL.town>.tag0.town_tresure -= <argn>

//*****************************************************************************
[FUNCTION TOWN_GET_FRIEND_UID]
//*****************************************************************************
//Retorna a lista de UIDs amigas da town deste NPC.
LOCAL.town=<GET_TOWN>
IF (!<serv.resources.<LOCAL.town>>)
 return 0
ELIF (<IsEmpty <serv.resources.<LOCAL.town>.tag.town_UID_friend>>)
 return 0
ENDIF
RETURN <serv.resources.<LOCAL.town>.tag.town_UID_friend>

//*****************************************************************************
[FUNCTION TOWN_GET_FORB_RACE]
//*****************************************************************************
//Retorna a lista de raças proibidas na town deste NPC.
LOCAL.town=<GET_TOWN>
IF (!<serv.resources.<LOCAL.town>>)
 return 0
ELIF (<IsEmpty <serv.resources.<LOCAL.town>.tag.town_race_forbidden>>)
 return 0
ENDIF
RETURN <serv.resources.<LOCAL.town>.tag.town_race_forbidden>

//*****************************************************************************
[FUNCTION TOWN_GET_FORB_UID]
//*****************************************************************************
//Retorna a lista de UIDs proibidas na town deste NPC.
LOCAL.town=<GET_TOWN>
IF (!<serv.resources.<LOCAL.town>>)
 return 0
ELIF (<IsEmpty <serv.resources.<LOCAL.town>.tag.town_UID_forbidden>>)
 return 0
ENDIF
RETURN <serv.resources.<LOCAL.town>.tag.town_UID_forbidden>

//*****************************************************************************
[FUNCTION TOWN_AZILUM]
//*****************************************************************************
//Faz um NPC servo espatriado pertencer a esta town
if (<IsEmpty <tag.town>>)
 if !(STRMATCH(*world*,<region.defname>))
  if (<region.tag.town_type>)
   DORAND 3
    say <region.name> eh meu novo lar.
    say Eu tenho um novo lar. Seu nome eh <region.name>
    say Ah, eh bom estar em <region.name>. Farei daqui meu lar
   ENDDO
   tag.town=<region.defname>
   home=<p>
  else
   say <region.name> nao eh uma capital, nem cidade e nem vila.
  endif
 else
  say O mundo eh selvagem...
 endif
else
 say Nao posso ficar aqui. Meu lar eh <serv.resources.<GET_TOWN>.name>.
endif

//*****************************************************************************
[FUNCTION TOWN_REG_SERVANT]
//*****************************************************************************
//Registra este NPC como funcionário da town dele
IF (!<GET_TOWN>)
 serv.log ALOOOW! <name> (<uid>) nao tem town! Nao pode ser registrado!
ELIF (STRMATCH(*<UID>*,<serv.resources.<GET_TOWN>.tag.TOWN_SERVANTS>)
 serv.log ALOOOW! <name> (<uid>) Ja eh registrado em sua town!
ELSE
 if (<IsEmpty <serv.resources.<GET_TOWN>.tag.TOWN_SERVANTS>>)
  serv.resources.<GET_TOWN>.tag.TOWN_SERVANTS = <UID>
 else
  serv.resources.<GET_TOWN>.tag.TOWN_SERVANTS .= ;<UID>
 endif
 serv.log Hey! Hey! Hey!  <name> (<uid>) registrado como funcionario em <serv.resources.<GET_TOWN>.name>!
ENDIF

//*****************************************************************************
[FUNCTION TOWN_UNREG_SERVANT]
//*****************************************************************************
//Remove o registro deste NPC como funcionário da town dele
IF (!<GET_TOWN>)
 serv.log ALOOOW! <name> (<uid>) nao tem town! Nao pode ser desregistrado!
ELIF !(STRMATCH(*<UID>*,<serv.resources.<GET_TOWN>.tag.TOWN_SERVANTS>)
 serv.log ALOOOW! <name> (<uid>) Nao eh registrado em sua town!
ELSE
 serv.resources.<GET_TOWN>.tag.TOWN_SERVANTS = <f_RemoveList <uid>,<serv.resources.<GET_TOWN>.tag.TOWN_SERVANTS>>
 serv.log Hey! Hey! Hey!  <name> (<uid>) perdeu registro de funcionario em <serv.resources.<GET_TOWN>.name>!
ENDIF

//*****************************************************************************
[FUNCTION TOWN_GET_SERVANT_PRICE]
//*****************************************************************************
//Pega o preço de um funcionário pela fama dele. <argv[0]> é a fama e <argv[1]> é o tipo de
//funcionário de acordo com <DEF.TOWN_SERVANT_*>
LOCAL.type=<DEF.<argv[1]>>
LOCAL.FAME=<argv[0]>
IF (<LOCAL.type>==TOWN_SERVANT_GUARD)
 RETURN <eval <DEF.SERVANT_MIN_COST>+(<LOCAL.FAME>/<DEF.FAME_COPPER_RATE_BUY>)>
ELIF (<LOCAL.type>==TOWN_SERVANT_GATE)
 RETURN <DEF.SERVANT_MIN_COST>
ELIF (<LOCAL.type>==TOWN_SERVANT_SENESCAL)
 RETURN <eval <DEF.SERVANT_MIN_COST>*5>
ENDIF

//*****************************************************************************
[FUNCTION TOWN_GET_SERVANT_ORD]
//*****************************************************************************
//Pega o ordenado de um funcionário pela fama dele. <argv[0]> é a fama e <argv[1]> é o tipo de
//funcionário de acordo com <DEF.TOWN_SERVANT_*>
LOCAL.type=<DEF.<argv[1]>>
LOCAL.FAME=<argv[0]>
IF (<LOCAL.type>==TOWN_SERVANT_GUARD)
 RETURN <eval <DEF.SERVANT_MIN_ORD>+(<LOCAL.FAME>/<DEF.FAME_COPPER_RATE_ORD>)>
ELIF (<LOCAL.type>==TOWN_SERVANT_GATE)
 RETURN <DEF.SERVANT_MIN_ORD>
ELIF (<LOCAL.type>==TOWN_SERVANT_SENESCAL)
 RETURN <eval <DEF.SERVANT_MIN_ORD>*5>
ENDIF


//*****************************************************************************
[MENU m_new_town]
//*****************************************************************************
//Menu de criação de town chamado pelo comando .town, caso a region não tenha town
<region.name> nao eh uma vila. Que vila faremos?
on=0 Capital
region.tag.town_type=<DEF.TOWN_TYPE_CAPITAL>
MENU m_NEW_TOWN_RACE

on=0 Cidade
region.tag.town_type=<DEF.TOWN_TYPE_CITY>
MENU m_NEW_TOWN_RACE

on=0 Vila
region.tag.town_type=<DEF.TOWN_TYPE_VILLAGE>
MENU m_NEW_TOWN_RACE

//*****************************************************************************
[MENU m_NEW_TOWN_RACE]
//*****************************************************************************
//Continuação de m_new_town para configurar a raça da town
Que habitantes tera <region.name>?
on=0 Elfo
region.TAG.TOWN_RACE=Elfo
sysmessageorange Digite o DEFNAME da regiao que manda nesta town ou nome do reino.
promptconsole new_town_capital

on=0 Anao
region.TAG.TOWN_RACE=Anao
sysmessageorange Digite o DEFNAME da regiao que manda nesta town ou nome do reino.
promptconsole new_town_capital

on=0 Humano
region.TAG.TOWN_RACE=Humano
sysmessageorange Digite o DEFNAME da regiao que manda nesta town ou nome do reino.
promptconsole new_town_capital

on=0 Drow
region.TAG.TOWN_RACE=Drow
sysmessageorange Digite o DEFNAME da regiao que manda nesta town ou nome do reino.
promptconsole new_town_capital

on=0 Orc
region.TAG.TOWN_RACE=Orc
sysmessageorange Digite o DEFNAME da regiao que manda nesta town ou nome do reino.
promptconsole new_town_capital

on=0 Todas
region.TAG.TOWN_RACE=Elfo;Anao;Humano;Drow;Orc
sysmessageorange Digite o DEFNAME da regiao que manda nesta town ou nome do reino.
promptconsole new_town_capital

//*****************************************************************************
[FUNCTION new_town_capital]
//*****************************************************************************
//Function de prompt para a capital da town.
IF (<serv.resources.<args>.tag0.TOWN_TYPE>)
 region.tag.town_capital=<args>
 region.tag.town_kingdom=<serv.resources.<args>.tag0.TOWN_KINGDOM>
 if (<IsEmpty <serv.resources.<args>.tag.TOWN_DEMENCES>>)
  serv.resources.<args>.tag.TOWN_DEMENCES=<region.defname>
 else
  serv.resources.<args>.tag.TOWN_DEMENCES .= ;<region.defname>
 endif 
ELSE
 region.tag.town_kingdom=<args>
 region.tag.town_capital=<region.defname>
endif
region.tag.TOWN_MASTER=<UID>
sysmessagegreen Sua town foi configurada com sucesso! A capital eh <serv.resources.<region.tag.town_capital>.name> e o reino <serv.resources.<region.defname>.tag.TOWN_KINGDOM>.
TOWN_ADD_SENESCAL

//*****************************************************************************
[FUNCTION TOWN_SERVANTS]
//*****************************************************************************
//Retorna o número de funionários da TOWN.
//Se usado com CALL também seta LOCAL.GUARDS, LOCAL.SENESCAL e LOCAL.GATE
//ARGS=Defname da TOWN.
IF (!<serv.resources.<args>>)
 return 0
ENDIF
LOCAL.servants=<explode ;,<serv.resources.<args>.tag.TOWN_SERVANTS>>
call _TOWN_SERVANTS <LOCAL.servants>
RETURN <LOCAL.SERVANTS>

[FUNCTION _TOWN_SERVANTS]
for s 1 <eval <argv>-1>
 IF (<uid.<argv[<LOCAL.s>]>.tag.servant>==TOWN_SERVANT_GUARD)
  LOCAL.GUARDS += 1
 ELIF (<uid.<argv[<LOCAL.s>]>.tag.servant>==TOWN_SERVANT_GATE)
  LOCAL.GATE += 1
 ELIF (<uid.<argv[<LOCAL.s>]>.tag.servant>==TOWN_SERVANT_SENESCAL)
  LOCAL.SENESCAL += 1
 ENDIF
endfor
LOCAL.servants=<eval <argv>-1>

//*****************************************************************************
[FUNCTION TOWN_PAY_SERVANTS]
//*****************************************************************************
//Paga o que é devido aos funcionários da Town <args>. Retorna a quantidade de mc usada.
IF (!<serv.resources.<args>>)
 return 0
ENDIF
LOCAL.servants=<explode ;,<serv.resources.<args>.tag.TOWN_SERVANTS>>
call _TOWN_PAY_SERVANTS <LOCAL.servants>
RETURN <LOCAL.PAY>

[FUNCTION _TOWN_PAY_SERVANTS]
for s 1 <eval <argv>-1>
 LOCAL.ORD=<TOWN_GET_SERVANT_ORD <uid.<argv[<LOCAL.s>]>.fame>,<uid.<argv[<LOCAL.s>]>.tag.servant>>
 IF (!<LOCAL.ord>)
  serv.log ALOOOW! <uid.<argv[<LOCAL.s>]>.name> (<argv[<LOCAL.s>]>) nao recebe ordenado?
  LOCAL.ORD=1
 ENDIF
 IF (<TOWN_GET_TRESURE> >= <LOCAL.ORD>)
  serv.newitem i_coin_copper
  new.amount=<LOCAL.ord>
  new.cont=<argv[<LOCAL.s>]>
  IF (<uid.<argv[<LOCAL.s>]>.tag.servant>!=TOWN_SERVANT_SENESCAL)
   DORAND 4
    LOCAL.message=Meu dinheiro suado. <dLOCAL.ord> cobres.
    LOCAL.message=<dLOCAL.ord> moedas pelo meu servico? Nao acho o suficiente.
    LOCAL.message=Chegou o pagamento! <dLOCAL.ord> moedas de cobre.
    LOCAL.message=Como podem me pagar apenas <dLOCAL.ord> moedas de cobre?
   ENDDO
   uid.<argv[<LOCAL.s>]>.timerf <R2,5>,say,<LOCAL.message>
  ENDIF
  TOWN_CONSUME_TRESURE <LOCAL.ord>
  LOCAL.pay += <LOCAL.ord>
 ELSE
  uid.<argv[<LOCAL.s>]>.tag0.nopay += 1
  DORAND 4
   LOCAL.message=Nao me pagaram o ordenado. Vou sair de <serv.resources.<GET_TOWN>.name> logo!
   LOCAL.message=Fiquei sem pagamento... <serv.resources.<GET_TOWN>.name> deve estar sem fundos!
   LOCAL.message=Ah! Vou reclamar com <uid.<serv.resources.<GET_TOWN>.tag.town_master>.tag.name>! Como deixam de pagr meu ordanado?
   LOCAL.message=Sem pagamento desta vez? Vou mendigar comida nas portas de <serv.resources.<GET_TOWN>.name> se continuar assim.
  ENDDO
  uid.<argv[<LOCAL.s>]>.timerf <R2,5>,say,<LOCAL.message>  
 ENDIF
 IF (<uid.<argv[<LOCAL.s>]>.tag.servant>==TOWN_SERVANT_GUARD)
  uid.<argv[<LOCAL.s>]>.TIMERF <eval <DEF.TOWN_TRIG_TIME>/3>,GUARD_BUY
  uid.<argv[<LOCAL.s>]>.TIMERF <eval (<DEF.TOWN_TRIG_TIME>/3)*2>,GUARD_BUY
  uid.<argv[<LOCAL.s>]>.FAME += <eval <DEF.TOWN_TRIG_TIME>/1800>
  uid.<argv[<LOCAL.s>]>.GUARD_LEVEL
 ENDIF
endfor


//*****************************************************************************
[FUNCTION TOWN_TRIG]
//*****************************************************************************
//O senescal roda esta função a cada <DEF.TOWN_TRIG_TIME> segundos.
//funcionário de acordo com <DEF.TOWN_SERVANT_*>
DORAND 3
 say Um momento. Vou pagar os funcionarios de <serv.resources.<tag.town>.name>.
 say Bem, eh hora de pagar os servidores de <serv.resources.<tag.town>.name>.
 say Pronto! O proximo servico sera pagar os servidores de <serv.resources.<tag.town>.name>.
ENDDO
LOCAL.pay = <TOWN_PAY_SERVANTS <tag.town>>
DORAND 3
 LOCAL.message=<dLOCAL.pay> moedas de cobre no total. Agora vamos contabilizar os animais da fazernda.
 LOCAL.message=Foram <dLOCAL.pay> cobres. Vou anotar e voltar ao relatorio de vendas.
 LOCAL.message=Pagando <dLOCAL.pay> cobres, com o atual tesouro so poderei fazer o pagamento por mais <eval <TOWN_GET_TRESURE>/<LOCAL.pay>> vezes.
ENDDO
region.tag.TOWN_LASTTIME=<serv.rtime>
region.tag.TOWN_LASTPAY=<LOCAL.pay>
TIMERF 3,say,<LOCAL.message>
TIMERF <DEF.TOWN_TRIG_TIME>,TOWN_TRIG

//*****************************************************************************
[FUNCTION MyTownColor]
//*****************************************************************************
//Retorna a cor da town do NPC
if (<IsEmpty <tag.town>>)
 return colors_all
elif (<IsEmpty <serv.resources.<tag.town>.tag.town_color>>)
 return colors_all
endif
return <serv.resources.<tag.town>.tag.town_color>

//******************************************************************************
//******************************************************************************
//############################################################################**
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$**
//                                        **
//############################################################################**
//                                        **
//              SENESCAIS                     **
//                                        **
//#############################################################################*
//                                        **
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$**
//############################################################################**
//******************************************************************************
//******************************************************************************

//*****************************************************************************
// Senescal Homem
//*****************************************************************************
[CHARDEF c_h_senescal]
ID=0190
Name=#NAMES_HUMANMALE, o Senescal
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff
BLOODCOLOR=0
SOUND=snd_human_moomph01

TEVENTS=e_combat 
TSPEECH=spk_senescal
TEVENTS=e_Human_HearUnk

CATEGORY=MYT - Town
SUBSECTION=Senescal
DESCRIPTION=Senescal (Humano)

ON=@Create
NPC=brain_towncrier
COLOR=colors_skin

FENCING={15.0 38.0}
PARRYING={15.0 38.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={15.0 38.0}
TACTICS={15.0 38.0}

STR={40 60}
DEX={50 70}
INT={60 80}

timerf 1,TOWN_START_SENESCAL

SPEECHCOLOR=colors_blue
TAG.RACA=Humano
tag.servant=TOWN_SERVANT_SENESCAL

ON=@NPCRestock
    ITEM={ i_hair_ponytail 1 i_hair_short 1 i_hair_pageboy 1}
    COLOR=colors_hair
    ITEM { i_beard_vandyke 1 i_beard_goatee 1 i_beard_mustache 1 0 2}
    COLOR=match_hair
    ITEM=RANDOM_LIGHT
    ITEM=i_shirt_fancy
    COLOR=colors_all
    ITEM=i_pants_long
    COLOR=colors_all
    ITEM=i_cape
    ITEM=i_SASH
    ITEM=random_boots
    ITEM={ i_hat_feather 1 i_cap 1 0 2}
    COLOR=COLORS_ALL
    
on=@Destroy
TOWN_UNREG_SERVANT
serv.log Hey! Hey! Hey! Senescal <name> (<uid>) foi destruido!!!

//*****************************************************************************
// Senescal Elfo
//*****************************************************************************
[CHARDEF c_e_senescal]
ID=0190
Name=#NAMES_HUMANMALE, o Senescal
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff
BLOODCOLOR=0
SOUND=snd_human_moomph01

TEVENTS=e_combat 
TSPEECH=spk_senescal
TEVENTS=e_Human_HearUnk

CATEGORY=MYT - Town
SUBSECTION=Senescal
DESCRIPTION=Senescal (Elfo)

ON=@Create
NPC=brain_towncrier
COLOR=083EA

FENCING={15.0 38.0}
PARRYING={15.0 38.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={15.0 38.0}
TACTICS={15.0 38.0}

STR={40 60}
DEX={50 70}
INT={60 80}

timerf 1,TOWN_START_SENESCAL

SPEECHCOLOR=colors_blue
TAG.RACA=Elfo
tag.servant=TOWN_SERVANT_SENESCAL

ON=@NPCRestock
    ITEM=i_hair_long
    COLOR=035
    ITEM=i_shirt_plain
    COLOR=colors_all
    ITEM=i_pants_short
    COLOR=colors_all
    ITEM=i_cape
    ITEM=i_SASH
    ITEM=i_sandals
    
on=@Destroy
TOWN_UNREG_SERVANT
serv.log Hey! Hey! Hey! Senescal <name> (<uid>) foi destruido!!!

//*****************************************************************************
// Senescal Anão
//*****************************************************************************
[CHARDEF c_a_senescal]
ID=0190
Name=#NAMES_HUMANMALE, o Senescal
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff
BLOODCOLOR=0
SOUND=snd_human_moomph01

TEVENTS=e_combat 
TSPEECH=spk_senescal
TEVENTS=e_Human_HearUnk

CATEGORY=MYT - Town
SUBSECTION=Senescal
DESCRIPTION=Senescal (Anao)

ON=@Create
NPC=brain_towncrier
COLOR=083EA

FENCING={15.0 38.0}
PARRYING={15.0 38.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={15.0 38.0}
TACTICS={15.0 38.0}

STR={40 60}
DEX={50 70}
INT={60 80}

timerf 1,TOWN_START_SENESCAL

SPEECHCOLOR=colors_blue
TAG.RACA=Anao
tag.servant=TOWN_SERVANT_SENESCAL

ON=@NPCRestock
    ITEM={ i_hair_long 1 i_hair_ponytail 1  i_hair_2_pigtails 1 }
    COLOR=COLORS_HAIR
    ITEM={ i_beard_long 1 i_beard_short_med 1 i_beard_long_med 1 }
    COLOR=match_hair
    ITEM=i_shirt_fancy
    COLOR=colors_all
    ITEM=i_pants_long
    COLOR=colors_all
    ITEM=i_cape
    ITEM=i_SASH
    ITEM=RANDOM_BOOTS
    
on=@Destroy
TOWN_UNREG_SERVANT
serv.log Hey! Hey! Hey! Senescal <name> (<uid>) foi destruido!!!

[FUNCTION TOWN_START_SENESCAL]
TOWN_AZILUM
TOWN_REG_SERVANT
FINDID.i_cape.color=<region.tag0.TOWN_COLOR>
FINDID.i_sash.color=<region.tag0.TOWN_COLOR>

[SPEECH spk_senescal]
on=*venha*
on=*vamos*
on=ANDE
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 flags=<flags>&~04
 timerf 1,say,Certamente, <src.sex senhor/senhora>.
 act=<src.uid>
 action=064
ENDIF
return 1

on=*fique*
on=*permaneca*
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 flags = <flags>|04
 timerf 1,say,De certo que ficarei aqui, <src.sex senhor/senhora>.
 act=
 action=065
ENDIF
return 1

on=*como*esta*reino*
on=*como*esta*cidade*
on=*como*esta*vila*
timerf 1,say Oh, sim! Veja meus relatos.
timerf 2,emoteyellow,entrega uns papeis
src.timerf 2,town

on=*contratar*
on=*contrate*
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>, 2>)
 say Vamos ver o que temos disponivel entao.
 src.menu M_TOWN_HIRE
ENDIF

//*****************************************************************************
[MENU M_TOWN_HIRE]
//*****************************************************************************
O que deseja contratar?
on=0 Porteiro
TOWN_BUY_GATEKEEPER

on=0 Guarda
MENU M_TOWN_HIRE_GUARD

//*****************************************************************************
[MENU M_TOWN_HIRE_GUARD]
//*****************************************************************************
Que tipo de guarda?
on=0 Recruta por <eval <TOWN_GET_SERVANT_PRICE 0,TOWN_SERVANT_GUARD>>mc (ordenado de <eval <TOWN_GET_SERVANT_ORD 0,TOWN_SERVANT_GUARD>>mc)
TOWN_BUY_GUARD

on=0 Soldado por <eval <TOWN_GET_SERVANT_PRICE 1001,TOWN_SERVANT_GUARD>>mc (ordenado de <eval <TOWN_GET_SERVANT_ORD 1001,TOWN_SERVANT_GUARD>>mc)
TOWN_BUY_GUARD 1001

on=0 Tenente por <eval <TOWN_GET_SERVANT_PRICE 2501,TOWN_SERVANT_GUARD>>mc (ordenado de <eval <TOWN_GET_SERVANT_ORD 2501,TOWN_SERVANT_GUARD>>mc)
TOWN_BUY_GUARD 2501

on=0 Capitao por <eval <TOWN_GET_SERVANT_PRICE 5001,TOWN_SERVANT_GUARD>>mc (ordenado de <eval <TOWN_GET_SERVANT_ORD 5001,TOWN_SERVANT_GUARD>>mc)
TOWN_BUY_GUARD 5001

//******************************************************************************
//******************************************************************************
//############################################################################**
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$**
//                                        **
//############################################################################**
//                                        **
//              PORTEIROS                     **
//                                        **
//#############################################################################*
//                                        **
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$**
//############################################################################**
//******************************************************************************
//******************************************************************************



//*****************************************************************************
// Porteiro Homem
//*****************************************************************************
[CHARDEF c_h_gatekeeper]
ID=0190
Name=#NAMES_HUMANMALE, o Porteirol
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff
BLOODCOLOR=0
SOUND=snd_human_moomph01

TEVENTS=e_combat 
TSPEECH=jobTOWNdoor
TEVENTS=e_Human_HearUnk
TEVENTS=e_town_gatekeeper

CATEGORY=MYT - Town
SUBSECTION=Porteiros
DESCRIPTION=Porteiro (Humano)

ON=@Create
NPC=brain_towncrier
COLOR=colors_skin

FENCING={15.0 38.0}
PARRYING={15.0 38.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={15.0 38.0}
TACTICS={15.0 38.0}

STR={40 60}
DEX={50 70}
INT={60 80}

timerf 1,TOWN_START_GATEKEEPER

SPEECHCOLOR=colors_blue
TAG.RACA=Humano
tag.servant=TOWN_SERVANT_GATE

ON=@NPCRestock
    ITEM={ i_hair_ponytail 1 i_hair_short 1 i_hair_pageboy 1}
    COLOR=colors_hair
    ITEM { i_beard_vandyke 1 i_beard_goatee 1 i_beard_mustache 1 0 2}
    COLOR=match_hair
    ITEM=RANDOM_LIGHT
    ITEM=i_shirt_fancy
    COLOR=colors_all
    ITEM=i_pants_long
    COLOR=colors_all
    ITEM=i_SASH
    ITEM=random_boots
    ITEM={ i_hat_feather 1 i_cap 1 0 2}
    COLOR=COLORS_ALL
    

//*****************************************************************************
// Porteiro Elfo
//*****************************************************************************
[CHARDEF c_e_gatekeeper]
ID=0190
Name=#NAMES_HUMANMALE, o Porteiro
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff
BLOODCOLOR=0
SOUND=snd_human_moomph01

TEVENTS=e_combat 
TSPEECH=jobTOWNdoor
TEVENTS=e_Human_HearUnk
TEVENTS=e_town_gatekeeper

CATEGORY=MYT - Town
SUBSECTION=Porteiros
DESCRIPTION=Porteiro (Elfo)

ON=@Create
NPC=brain_towncrier
COLOR=083EA

FENCING={15.0 38.0}
PARRYING={15.0 38.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={15.0 38.0}
TACTICS={15.0 38.0}

STR={40 60}
DEX={50 70}
INT={60 80}

timerf 1,TOWN_START_GATEKEEPER

SPEECHCOLOR=colors_blue
TAG.RACA=Elfo
tag.servant=TOWN_SERVANT_GATE

ON=@NPCRestock
    ITEM=i_hair_long
    COLOR=035
    ITEM=i_shirt_plain
    COLOR=colors_all
    ITEM=i_pants_short
    COLOR=colors_all
    ITEM=i_SASH
    ITEM=i_sandals
    

//*****************************************************************************
// Porteiro Anão
//*****************************************************************************
[CHARDEF c_a_gatekeeper]
ID=0190
Name=#NAMES_HUMANMALE, o Porteiro
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff
BLOODCOLOR=0
SOUND=snd_human_moomph01

TEVENTS=e_combat 
TSPEECH=jobTOWNdoor
TEVENTS=e_Human_HearUnk
TEVENTS=e_town_gatekeeper

CATEGORY=MYT - Town
SUBSECTION=Porteiros
DESCRIPTION=Porteiro (Anao)

ON=@Create
NPC=brain_towncrier
COLOR=083EA

FENCING={15.0 38.0}
PARRYING={15.0 38.0}
SWORDSMANSHIP={15.0 38.0}
WRESTLING={15.0 38.0}
MACEFIGHTING={15.0 38.0}
MAGICRESISTANCE={15.0 38.0}
TACTICS={15.0 38.0}

STR={40 60}
DEX={50 70}
INT={60 80}

timerf 1,TOWN_START_GATEKEEPER

SPEECHCOLOR=colors_blue
TAG.RACA=Anao
tag.servant=TOWN_SERVANT_GATE

ON=@NPCRestock
    ITEM={ i_hair_long 1 i_hair_ponytail 1  i_hair_2_pigtails 1 }
    COLOR=COLORS_HAIR
    ITEM={ i_beard_long 1 i_beard_short_med 1 i_beard_long_med 1 }
    COLOR=match_hair
    ITEM=i_shirt_fancy
    COLOR=colors_all
    ITEM=i_pants_long
    COLOR=colors_all
    ITEM=i_SASH
    ITEM=RANDOM_BOOTS
    
[EVENTS e_town_gatekeeper]
on=@Destroy
TOWN_UNREG_SERVANT
TOWN_RELEASE_DOORS
serv.log Hey! Hey! Hey! Porteiro <name> (<uid>) foi destruido!!!

on=@Hunger
IF (<food> < 30)
 IF (!<GUARD_FOOD>)
  IF (<restest 2 i_coin_copper>)
   consume 2 i_coin_copper
   serv.newitem i_ribs_cooked,18,<UID>
  ELIF (!<food>) && (<tag0.nopay> > 2)
   say Chega disso! Me demito!
   timerf 15,remove
  ENDIF
 ENDIF
ENDIF


[FUNCTION TOWN_START_GATEKEEPER]
TOWN_AZILUM
TOWN_REG_SERVANT
FINDID.i_sash.color=<region.tag0.TOWN_COLOR>

[SPEECH jobTOWNdoor]
on=*venha*
on=*vamos*
on=ANDE
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 flags=<flags>&~04
 timerf 1,say,Pois nao, <src.sex senhor/senhora>.
 act=<src.uid>
 action=064
ENDIF
return 1

on=*fique*
on=*permaneca*
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 flags = <flags>|04
 timerf 1,say,Cuidarei dos portoes aqui entao, <src.sex senhor/senhora>.
 act=
 action=065
ENDIF
return 1

on=*vigie*porta
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>, 2>)
 SAY Que porta quer que eu vigie?
 targetf TOWN_ADD_DOOR <UID>
ENDIF
return 1

on=*pare*vigiar*porta
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>, 2>)
 SAY Que porta quer que eu deixe de vigiar?
 targetf TOWN_REM_DOOR <UID>
ENDIF
return 1

on=*abra*porta*
on=*abra*portao*
on=*abra*portal*
on=*abra*portoes*
on=*abra*portais*
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,1>)
 TOWN_OPEN_DOOR
 timerf 7,TOWN_CLOSE_DOOR
ENDIF
return 1

on=*mantenha*porta*aberta
on=*mantenha*portao*aberto
on=*mantenha*portal*aberto
on=*mantenha*portoes*abertos
on=*mantenha*portais*abertos
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 TOWN_OPEN_DOOR
ENDIF
return 1

on=*feche*porta*
on=*feche*portao*
on=*feche*portal*
on=*feche*portoes*
on=*feche*portais*
IF (<SRC.TOWN_CHECK_SPK_PRIVS <UID>,1>)
 TOWN_CLOSE_DOOR
ENDIF
return 1

//*****************************************************************************
[FUNCTION TOWN_ADD_DOOR]
//*****************************************************************************
//Adiciona a porta TARGER (argo) a lista de portas do NPC
OBJ=<argv[0]>
say Esta aqui.
if (<argo.type> != t_door) && (<argo.type> != t_door_locked) && (<argo.type> != t_portculis)
 obj.timerf 1,say,Isso nao me parece com uma porta <sex senhor/senhora>.
 return 1
endif
IF (STRMATCH(*<argo.uid>*,<obj.tag.TOWN_DOORS>)
 Obj.timerf 1,say,Esta porta ja esta sendo vigiada.
 return 1
endif
obj.timerf 1,say,Muito bem <sex senhor/senhora>. Guardarei para que seja segura.
obj.tag.TOWN_DOORS .=;<argo.uid>

[FUNCTION TOWN_REM_DOOR]
OBJ=<argv[0]>
say Esta aqui.
if (<argo.type> != t_door) && (<argo.type> != t_door_locked) && (<argo.type> != t_portculis)
 obj.timerf 1,say,Isso nao me parece com uma porta <sex senhor/senhora>.
 return 1
endif
IF !(STRMATCH(*<argo.uid>*,<obj.tag.TOWN_DOORS>)
 Obj.timerf 1,say,Mas eu nao estou vigiando esta porta...
 return 1
endif
obj.timerf 1,say,Certo <sex senhor/senhora>. Nao guardarei mais esta porta.
obj.tag.TOWN_DOORS =<f_RemoveList <argo.uid>,<obj.tag.TOWN_DOORS>>

//*****************************************************************************
[FUNCTION TOWN_OPEN_DOOR]
//*****************************************************************************
//Abre as portas da lista de portas do NPC porteiro
LOCAL.doors=<EXPLODE ;,<tag.TOWN_DOORS>>
LOCAL.doors=<STRSUB 1 <eval STRLEN(<LOCAL.doors>)> <LOCAL.doors>>
_TOWN_OPEN_DOOR <LOCAL.doors>

[FUNCTION _TOWN_OPEN_DOOR]
for i 0 <eval <argv>-1>
 IF (<UID.<argv[<LOCAL.i>]>.type> != t_portculis)
  if (!<UID.<argv[<LOCAL.i>]>.tag0.open>)
   UID.<argv[<LOCAL.i>]>.timerd=1
   UID.<argv[<LOCAL.i>]>.tag.open=1
   UID.<argv[<LOCAL.i>]>.timerf 1,timer,-1
  endif
 ELSE
  IF (!<UID.<argv[<LOCAL.i>]>.moreX>) && (!<UID.<argv[<LOCAL.i>]>.morey>)
   //Esta fechada E nao esta em uso
   UID.<argv[<LOCAL.i>]>.f_portculis
  ENDIF
 ENDIF
endfor

//*****************************************************************************
[FUNCTION TOWN_RELEASE_DOORS]
//*****************************************************************************
//Abre as portas da lista de portas do NPC porteiro para sempre.
LOCAL.doors=<EXPLODE ;,<tag.TOWN_DOORS>>
LOCAL.doors=<STRSUB 1 <eval STRLEN(<LOCAL.doors>)> <LOCAL.doors>>
_TOWN_RELEASE_DOOR <LOCAL.doors>

[FUNCTION _TOWN_RELEASE_DOOR]
for i 0 <eval <argv>-1>
 IF (<UID.<argv[<LOCAL.i>]>.type> != t_portculis)
  UID.<argv[<LOCAL.i>]>.type=t_door
 ELSE
  IF (!<UID.<argv[<LOCAL.i>]>.moreX>) && (!<UID.<argv[<LOCAL.i>]>.morey>)
   //Esta fechada E nao esta em uso
   UID.<argv[<LOCAL.i>]>.f_portculis
  ENDIF
  UID.<argv[<LOCAL.i>]>.timerf 10,type,t_quebrado
 ENDIF
endfor

//*****************************************************************************
[FUNCTION TOWN_CLOSE_DOOR]
//*****************************************************************************
//Fecha as portas da lista de portas do NPC porteiro
LOCAL.doors=<EXPLODE ;,<tag.TOWN_DOORS>>
LOCAL.doors=<STRSUB 1 <eval STRLEN(<LOCAL.doors>)> <LOCAL.doors>>
_TOWN_CLOSE_DOOR <LOCAL.doors>

[FUNCTION _TOWN_CLOSE_DOOR]
for i 0 <eval <argv>-1>
 IF (<UID.<argv[<LOCAL.i>]>.type> != t_portculis)
  if (<UID.<argv[<LOCAL.i>]>.tag0.open>)
   UID.<argv[<LOCAL.i>]>.timerd=1
   UID.<argv[<LOCAL.i>]>.tag.open=
  endif
 ELSE
  IF (<UID.<argv[<LOCAL.i>]>.moreX>) && (!<UID.<argv[<LOCAL.i>]>.morey>)
   //Esta aberta E nao esta em uso
   UID.<argv[<LOCAL.i>]>.f_portculis
  ENDIF
 ENDIF
endfor




//******************************************************************************
//******************************************************************************
//############################################################################**
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$**
//                                        **
//############################################################################**
//                                        **
//                  GUARDAS                   **
//                                        **
//#############################################################################*
//                                        **
//$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$**
//############################################################################**
//******************************************************************************
//******************************************************************************

//*****************************************************************************
[FUNCTION TOWN_BUY_GUARD]
//*****************************************************************************
//Testa dinheiro, cria e configura um guarda para a town da REGION de quem o chama.
IF (<IsEmpty <argv[0]>>)
 LOCAL.fame=0
ELSE
 LOCAL.fame=<argv[0]>
ENDIF

LOCAL.price=<TOWN_GET_SERVANT_PRICE <LOCAL.fame>,TOWN_SERVANT_GUARD>
IF (<LOCAL.price> > <TOWN_GET_TRESURE <region.defname>>)
 sysmessagered <region.name> nao tem <dLOCAL.price>mc para contratar este funcionario.
 return 1
ELSE
 TOWN_CONSUME_TRESURE <LOCAL.price>
ENDIF

if (STRMATCH(Anao,<region.tag.town_race>))
 LOCAL.guard=c_a_guard
elif (STRMATCH(Humano,<region.tag.town_race>))
 LOCAL.guard=c_h_guard
elif (STRMATCH(Elfo,<region.tag.town_race>))
 LOCAL.guard=c_e_guard
ELSE
 DORAND 5
  LOCAL.guard=c_a_guard
  LOCAL.guard=c_h_guard
  LOCAL.guard=c_a_guard
  LOCAL.guard=c_e_guard
  LOCAL.guard=c_h_guard
 ENDDO
endif

serv.newnpc <LOCAL.guard>

new.movenear <UID>,2
new.GUARD_FOLLOW <UID>
if (0<argv[0]>)
 new.fame=<argv[0]>
endif
new.TOWN_AZILUM
new.TOWN_REG_SERVANT
new.GUARD_LEVEL 1

//*****************************************************************************
[FUNCTION TOWN_BUY_GATEKEEPER]
//*****************************************************************************
//Testa dinheiro, cria e configura um guarda para a town da REGION de quem o chama.
LOCAL.price=<TOWN_GET_SERVANT_PRICE 0,TOWN_SERVANT_GATE>
IF (<LOCAL.price> > <TOWN_GET_TRESURE <region.defname>>)
 sysmessagered <region.name> nao tem <dLOCAL.price>mc para contratar este funcionario.
 return 1
ELSE
 TOWN_CONSUME_TRESURE <LOCAL.price>
ENDIF

if (STRMATCH(Anao,<region.tag.town_race>))
 LOCAL.guard=c_a_gatekeeper
elif (STRMATCH(Humano,<region.tag.town_race>))
 LOCAL.guard=c_h_gatekeeper
elif (STRMATCH(Elfo,<region.tag.town_race>))
 LOCAL.guard=c_e_gatekeeper
ELSE
 DORAND 5
  LOCAL.guard=c_a_gatekeeper
  LOCAL.guard=c_h_gatekeeper
  LOCAL.guard=c_a_gatekeeper
  LOCAL.guard=c_e_gatekeeper
  LOCAL.guard=c_h_gatekeeper
 ENDDO
endif

serv.newnpc <LOCAL.guard>

new.movenear <UID>,2
new.act <UID>
new.action 064


//*****************************************************************************
[FUNCTION TOWN_ADD_SENESCAL]
//*****************************************************************************
//Cria e configura um senescal para a town da REGION de quem o chama.
if (STRMATCH(Anao,<region.tag.town_race>))
 LOCAL.guard=c_a_senescal
elif (STRMATCH(Humano,<region.tag.town_race>))
 LOCAL.guard=c_h_senescal
elif (STRMATCH(Elfo,<region.tag.town_race>))
 LOCAL.guard=c_e_senescal
ELSE
 DORAND 5
  LOCAL.guard=c_a_senescal
  LOCAL.guard=c_h_senescal
  LOCAL.guard=c_a_senescal
  LOCAL.guard=c_e_senescal
  LOCAL.guard=c_h_senescal
 ENDDO
endif

serv.newnpc <LOCAL.guard>

new.movenear <UID>,2
new.act <UID>
new.action 064

//*****************************************************************************
[FUNCTION GUARD_LEVEL]
//*****************************************************************************
//Verifica se é necessário avançar o nível do guarda
//ARGN=Iniciar
IF (<tag0.level>==3)
 return 0
ELIF (<tag0.level>==2) && (<FAME> < 5000)
 return 0
ELIF (<tag0.level>==1) && (<FAME> < 2500)
 return 0
ELIF (!<tag0.level>) && (<FAME> < 1000) && (<FAME> > 0)
 return 0
ELIF (!<FAME>)
 FAME=1
ELIF (<FAME>) && (<argn>) && (!<tag0.level>)
 if (<FAME> > 4999)
  tag.level 3
 ELIF (<FAME> > 2499)
  tag.level 2
 ELIF (<FAME> > 999)
  tag.level 1
 ENDIF
ELSE
 tag0.level += 1
ENDIF

IF (<argn>) && (<tag0.level>)
 str += <BETWEEN 0,50,<FAME>,5001>
 dex += <BETWEEN 0,50,<FAME>,5001>
 int += <BETWEEN 0,50,<FAME>,5001>
 maxhits += <BETWEEN 0,400,<FAME>,5001>
 LOCAL.bonus 700
 for i 0 3
  LOCAL.skill=<skillbest.<dLOCAL.i>>
  IF (<LOCAL.skill>==31)
   archery += <BETWEEN 0,<LOCAL.bonus>,<FAME>,5001>
   LOCAL.bonus -= 100
  ELIF (<LOCAL.skill>==42)
   fencing += <BETWEEN 0,<LOCAL.bonus>,<FAME>,5001>
   LOCAL.bonus -= 100
  ELIF (<LOCAL.skill>==41)
   macefighting += <BETWEEN 0,<LOCAL.bonus>,<FAME>,5001>
   LOCAL.bonus -= 100
  ELIF (<LOCAL.skill>==40)
   swordsmanship += <BETWEEN 0,<LOCAL.bonus>,<FAME>,5001>
   LOCAL.bonus -= 100
  ENDIF
 endfor
ENDIF

for layer 1 24
 if (<LOCAL.layer>!= LAYER_HAIR) && (<LOCAL.layer>!= LAYER_BEARD) && (<LOCAL.layer>!= 21)
  findlayer.<LOCAL.layer>.remove
 ENDIF
endfor
DOSWITCH <tag0.level>
 take_item i_pescoco_courom
 take_item i_pescoco_anel
 take_item i_pescoco_malha
 take_item i_pescoco_placa
ENDDO

DOSWITCH <tag0.level>
 take_item i_braco_courom
 take_item i_braco_anel
 take_item i_braco_malha
 take_item i_braco_placa
ENDDO

DOSWITCH <tag0.level>
 take_item i_luva_courom
 take_item i_luva_anel
 take_item i_luva_malha
 take_item i_luva_placa
ENDDO

DOSWITCH <tag0.level>
 take_item i_peito_courom
 take_item i_peito_anel
 take_item i_peito_malha
 take_item i_peito_placa
ENDDO

DOSWITCH <tag0.level>
 take_item i_perna_courom
 take_item i_perna_anel
 take_item i_perna_malha
 take_item i_perna_placa
ENDDO

DOSWITCH <tag0.level>
 take_item i_doublet
 take_item i_sash
 take_item i_cape
 take_item i_cape
ENDDO
new.color=<MyTownColor>

if (<tag0.level> == 3)
 take_item i_sash
 new.color=<MyTownColor>
endif

take_weapon

take_item i_boots_calf

[FUNCTION take_item]
serv.newitem <args>
equip <new.uid>

//*****************************************************************************
[FUNCTION take_weapon]
//*****************************************************************************
//Dá armas conforme a skill de quem o chama
for i 0 53
 LOCAL.skill=<skillbest.<dLOCAL.i>>
 IF (<LOCAL.skill>==31)
  DOSWITCH <TAG0.level>
   take_item i_bow
   take_item i_bow_long
   take_item i_bow_composite
   take_item i_bow_elven
  ENDDO
  new.morey 600
  equip <new.uid>
  take_item i_arrow
  new.amount 150
  return 1
 ELIF (<LOCAL.skill>==42)
  DOSWITCH <TAG0.level>
   take_item random_faca
   take_item i_lanca_curta
   take_item i_garfo_guerra
   take_item i_lanca
  ENDDO
  return 1
 ELIF (<LOCAL.skill>==41)
  take_item i_random_maca
  return 1
 ELIF (<LOCAL.skill>==40)
  take_item i_random_espada
  return 1
 ENDIF
endfor



//*****************************************************************************
//*****************************************************************************
//
//                 CHARDEFS
//
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// guarda Humano
//*****************************************************************************
[CHARDEF c_h_guard]
ID=c_man
NAME=#NAMES_HUMANMALE
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff,t_tar,t_fire
BLOODCOLOR=0
SOUND=snd_human_moomph01
TEVENTS=e_guard_town
TEVENTS=e_PvM
TEVENTS=e_combat 

TSPEECH=jobTOWNGuard

CATEGORY=MYT - Town
SUBSECTION=Guardas
DESCRIPTION=Guarda (Humano)

ON=@Create
NPC=brain_human
COLOR=083ea
KARMA=5000

timerf 5,GUARD_AI

TAG.RACA=Humano
TAG.SERVANT=TOWN_SERVANT_GUARD

speechcolor={ colors_red 1 colors_orange 1 colors_blue 1 }

MACEFIGHTING={50.0 65.0}
SWORDSMANSHIP={50.0 65.0}
ARCHERY={40.0 58.0}
FENCING={50.0 60.0}
TACTICS=60.0

STR=60
DEX=70
INT=80
MAXHITS=150

ITEM=i_coin_copper,{3 10}
ITEMNEWBIE={ i_hair_short 1 i_hair_long 1 i_hair_ponytail 1 i_hair_receding 1 }
COLOR=colors_brown
ITEMNEWBIE={ i_beard_short 1 i_beard_goatee 1 i_beard_mustache 3 i_beard_short_med 1 i_beard_vandyke 1 }
COLOR=match_hair
ITEM=i_ribs_cooked
amount=12

//*****************************************************************************
// guarda Elfo
//*****************************************************************************
[CHARDEF c_e_guard]
ID=c_man
NAME=#NAMES_HUMANMALE
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff,t_tar,t_fire
BLOODCOLOR=0
SOUND=snd_human_moomph01
TEVENTS=e_guard_town
TEVENTS=e_PvM
TEVENTS=e_combat 

TSPEECH=jobTOWNGuard

CATEGORY=MYT - Town
SUBSECTION=Guardas
DESCRIPTION=Guarda (Humano)

ON=@Create
NPC=brain_human
COLOR=colors_skin
KARMA=5000

timerf 5,GUARD_AI

TAG.RACA=Elfo
TAG.SERVANT=TOWN_SERVANT_GUARD

speechcolor={ colors_red 1 colors_orange 1 colors_blue 1 }

SWORDSMANSHIP={50.0 60.0}
ARCHERY={53.0 62.0 }
FENCING={50.0 60.0}
TACTICS=60.0

STR=60
DEX=70
INT=80
MAXHITS=150

ITEM=i_coin_copper,{3 10}
ITEMNEWBIE={ i_hair_short 1 i_hair_long 2 i_hair_ponytail 2 }
COLOR=035
ITEM=i_ribs_cooked
amount=12


//*****************************************************************************
// guarda anão
//*****************************************************************************
[CHARDEF c_a_guard]
ID=c_man
NAME=#NAMES_HUMANMALE
ICON=i_pet_MAN
CAN=MT_EQUIP|MT_WALK|MT_RUN|MT_USEHANDS
RESOURCES=i_flesh_head, i_flesh_torso, i_flesh_right_arm, i_flesh_left_arm, i_flesh_left_leg, i_flesh_right_leg, i_blood_2
FOODTYPE=100 t_food, t_fruit
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff,t_tar,t_fire
BLOODCOLOR=0
SOUND=snd_human_moomph01
TEVENTS=e_guard_town
TEVENTS=e_PvM
TEVENTS=e_combat 

TSPEECH=jobTOWNGuard

CATEGORY=MYT - Town
SUBSECTION=Guardas
DESCRIPTION=Guarda (Anao)

ON=@Create
NPC=brain_human
COLOR=colors_skin
KARMA=5000

timerf 5,GUARD_AI

TAG.RACA=Anao
TAG.SERVANT=TOWN_SERVANT_GUARD

speechcolor={ colors_red 1 colors_orange 1 colors_blue 1 }

MACEFIGHTING={50.0 70.0}
SWORDSMANSHIP={50.0 60.0}
TACTICS=60.0
FENCING={50.0 60.0}

STR=60
DEX=70
INT=80
MAXHITS=150

ITEM=i_coin_copper,{3 10}
ITEMNEWBIE={i_hair_long 1 i_hair_2_pigtails 1}
COLOR=colors_brown
ITEMNEWBIE={i_beard_long_med 1 i_beard_long}
COLOR=match_hair
ITEM=i_ribs_cooked
amount=12


//*****************************************************************************
//*****************************************************************************
//
//                 SPEECH
//
//*****************************************************************************
//*****************************************************************************


[SPEECH jobTOWNGuard]
on=pare*patrulhar
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 timerf 1,say Nao patrulharei mais meus pontos entao, <sex senhor/senhora>.
 GUARD_CLEAR_WAYPOINTS
 action=06D
 GUARD_FOLLOW
endif
return 1

on=sua*patrulha
on=va*patrulha
on=va*patrulhar
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 timerf 1,say,Entendido!
 GUARD_PATROL
 TAG.TASK=GUARD_TASK_PATROL
endif
return 1

on=*pare*
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 GUARD_STOP
 TAG.TASK=GUARD_TASK_STAND
 timerf 1,say,Sim, <src.sex senhor/senhora>.
endif
return 1

on=*venha*
on=*vamos*
on=ANDE
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 say Sim, <src.sex senhor/senhora>.
 TAG.TASK=GUARD_TASK_FOLLOW
 GUARD_FOLLOW
ENDIF
return 1

on=*ataque*
on=*atacar*
on=*mate*
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 say Qual eh o meu alvo, <src.sex senhor/senhora>?
 TAG.TASK=GUARD_TASK_ATTACK
 src.targetf GUARD_ATTACK <UID>
ENDIF
return 1

on=guarde
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 say Mas o que eu devo guardar?
 TAG.TASK=GUARD_TASK_GUARD
 src.targetf GUARD_GUARD <UID>
ENDIF
return 1

on=para*seu*posto
on=no*seu*posto
on=ao*seu*posto
on=para*seus*postos
on=nos*seus*postos
on=aos*seus*postos
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 timerf 1,Say,Imediatamente!
 GUARD_CORRECT_HOME
 action=06D
 TAG.TASK=GUARD_TASK_POST
ENDIF
return 1

on=este*seu*posto
on=*novo*posto*aqui
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 timerf 1,Say,Ficarei de olho por aqui entao.
 home=<src.p>
 tag.home=<src.p>
 HOMEDIST=5
 action=06D
 TAG.TASK=GUARD_TASK_POST
ENDIF
return 1

on=patrulhe*ponto
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 say Qual ponto devo guardar?
 GUARD_FOLLOW <src.uid>
 src.targetfg GUARD_ADD_WAYPOINT <UID>
endif

on=*volte*trabalho*
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 if (<IsEmpty <tag.oldtask>>)
  timerf 1,say E qual era meu trabalho?
 else
  say Agora mesmo, <sex senhor/senhora>.
  GUARD_RETRIEVE_TASK
 endif
endif
return 1

on=*esta*demitido*
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,3>)
 say Tem certeza, <sex senhor/senhora>?
 TAG.TASK=GUARD_TASK_DISMISSING
 act=<src.uid>
ENDIF

on=*va*
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,2>)
 say Ate aonde, <src.sex senhor/senhora>?
 TAG.TASK=GUARD_TASK_GO
 src.targetfg GUARD_GO <uid>,1
endif
return 1

on=sim
on=tenho
on=absoluta
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,3>)
 IF (<TAG.TASK>==GUARD_TASK_DISMISSING)
  say Muito bem. Estou indo embora entao.
  SPEECH -jobTOWNGuard
  TAG.TASK=GUARD_TASK_REMOVE
  timerf 120,remove
 ELSE
  say Seja mais <sex especifico/especifica>, <sex meu/minha> <sex senhor/senhora>.
 ENDIF
ENDIF

on=nao
on=esqueca
IF (<src.TOWN_CHECK_SPK_PRIVS <UID>,3>)
 IF (<TAG.TASK>==GUARD_TASK_DISMISSING)
  say E o que farei agora entao?
  TAG.TASK=GUARD_TASK_FOLLOW
  timerf 60,remove
  act=<src.uid>
 ELSE
  say Seja mais <sex especifico/especifica>, <sex meu/minha> <sex senhor/senhora>.
 ENDIF
ENDIF

//*****************************************************************************
//*****************************************************************************
//
//                 EVENTS
//
//*****************************************************************************
//*****************************************************************************

[EVENTS e_guard_town]
on=@Kill
FAME += <eval <argo.fame>/20>
IF (<argo.karma> > 0)
 KARMA += <eval <argo.KARMA>/20>
ELIF (<argo.karma> < -2000)
 KARMA -= <eval <argo.KARMA>/20>
endif
return 0
DORAND 5
 TACTICS += <R5,25>
 FENCING += <R5,25>
 ARCHERY += <R5,25>
 MACEFIGHTING += <R5,25>
 SWORDSMANSHIP += <R5,25>
ENDDO
DORAND 3
 STR += 2
 DEX += 2
 INT += 1
ENDDO
MAXHITS += <R1,5>

on=@Hunger
tag.lastfood=<serv.time>
if (<food> < 25)
 if (!<GUARD_FOOD>)
  if (!<food>) && (<tag0.nopay> > 2)
   say Chega disto! Estou me demitindo!
   timerf 15,remove
   return 1
  endif
  GUARD_BUY
 else
  return 1
 endif
endif

on=@NPCActFight
argn2=1000

ON=@NPCLookAtChar
//Medida drástica para evitar lag:
//say vendo <src.name>
if (<R<sector.complexity>> > 4) || (<tag0.task>==GUARD_TASK_ATTACK) || (<tag0.task>==GUARD_TASK_WARN) || (<tag0.task>==GUARD_TASK_REMOVE) || (<region.defname> != <tag0.town>)
 return 0
endif
LOCAL.Warn=<src.GuardMustWarn <UID>>
IF (<LOCAL.Warn>)
 GUARD_WARN <src.UID>,<LOCAL.WARN>
ENDIF

on=@NPCLOSTTELEPORT
//serv.log <name> tentou teleportar por estar perdido. Dist: <argn1> - Try: <eval <actarg1>>
IF (<actarg1> > 24)
 actarg1=
 return 0
endif
actarg1 += 1
return 1

on=@Destroy
TOWN_UNREG_SERVANT

on=@GetHit
IF (<src.isplayer>) && (<src.TOWN_CHECK_PRIVS>==1) && (<region.defname>==<GET_TOWN>)
 TOWN_BAN_PLAYER <src.uid>
 return 0
endif
if (<argn1> >= <hits>)
 TOWN_BAN_PLAYER <src.uid>
 f_sendMessage <GET_TOWN_MASTER>, Guarda <name> foi morto por <uid.<GET_TOWN_MASTER>.rec_fGetName <src.uid>> as <serv.rtime>.,026
endif
if (<src.ischar>)
 if (<src.isgm>) || (<src.flags>&01f) || (<memoryfindtype.memory_war_targ>)
  return 0
 endif
 act=<src.uid>
 GUARD_ATTACK <uid> 
endif




//*****************************************************************************
//*****************************************************************************
//
//                 FUNCTIONS
//
//*****************************************************************************
//*****************************************************************************






//*****************************************************************************
[FUNCTION GUARD_STOP]
//*****************************************************************************
//Para um NPC guarda
tag.task=GUARD_TASK_WANDER
act=
actp=
flags =<flags>&~statf_war
update
action=065

//*****************************************************************************
[FUNCTION GUARD_CORRECT_HOME]
//*****************************************************************************
//Salva ou usa a TAG.HOME para o NPC nunca perder a home.
//Para quase todos os comandos de andar até um ponto se usa o HOME para evitar teleport
IF (<IsEmpty <tag.home>>)
 IF (<IsEmpty <home>>)
  home=<p>
 endif
 tag.home=<home>
ELSE
 HOME=<tag.home>
ENDIF


//*****************************************************************************
[FUNCTION GUARD_FOLLOW]
//*****************************************************************************
//Faz um NPC seguir ARGN
GUARD_SAVE_TASK
tag.task=GUARD_TASK_FOLLOW
IF (<argn>)
 act=<argn>
else
 act=<src.uid>
endif
action=064

//*****************************************************************************
[FUNCTION GUARD_ATTACK]
//*****************************************************************************
//Faz o guarda de UID <argv[0]> atacar o TARGET <ARGO>. Se não houver <ARGO>, acata ACT.
ref1=<argv[0]>
IF (<argo>)
 ref1.GUARD_SAVE_TASK
 ref1.tag.task.GUARD_TASK_ATTACK
 ref1.attack <argo>
ELSE
 ref1.tag.task=GUARD_TASK_ATTACK
 ref1.attack <ref1.act>
ENDIF

//*****************************************************************************
[FUNCTION GUARD_GO]
//*****************************************************************************
//ARGV0=UID do guarda
//ARGV1=Se responde por voz ou não.
ref1=<argv[0]>
ref1.actp=<targp>
ref1.action=066
ref1.tag.task=GUARD_TASK_GO
if (0<argv[1]>)
 ref1.say Agora mesmo, <sex senhor/senhora>
endif

//*****************************************************************************
[FUNCTION GUARD_GUARD]
//*****************************************************************************
//Nem eu sei... Seria pra guardar algo ou alguém.
ref1=<argv[0]>
if (<argo>)
 ref1.guard <argo.uid>
 ref1.say Guardarei para que <argo.name> esteja em seguranca.
else
 ref1.say O que exatamente terei de guardar?
endif

//*****************************************************************************
[FUNCTION GUARD_PATROL]
//*****************************************************************************
//Faz o guarda patrulhar pelos WAYPOINTS dele.
IF (<IsEmpty <tag.waypoint>>)
 IF (<IsEmpty <tag.wp_00>>)
  tag.task=GUARD_TASK_WANDER
  action=06D
  timerf 2,say Mas eu nao tenho o que patrulhar...
 ELSE
  tag.waypoint=0
  home=<tag.wp_00>
  GAURD_CORRECT_HOME
  action=06D
 ENDIF
 return 1
ENDIF

IF (<distance <home>> < 2)
 IF (!<IsEmpty <tag.wp_<hval <tag.waypoint>+1>>>)
  tag.waypoint += 1
  home=<tag.wp_<tag.waypoint>>
 ELSE
  tag.waypoint=0
  home=<tag.wp_00>
 ENDIF
 action=06D
ENDIF

//*****************************************************************************
[FUNCTION GUARD_SAVE_TASK]
//*****************************************************************************
//Salva a ultima task do guarda
tag.oldtask=<tag.task>
tag.oldaction=<action>
tag.oldact=<act>
tag.oldactp=<actp>
tag.oldhome=<home>

//*****************************************************************************
[FUNCTION GUARD_RETRIEVE_TASK]
//*****************************************************************************
//Salva a ultima satk do guarda
IF (!<IsEmpty <tag.oldtask>>)
 tag.task=<tag.oldtask>
 action=<tag0.oldaction>
 act=<tag0.oldact>
 actp=<tag.oldactp>
 home=<tag.oldhome>
 tag.oldtask=
 tag.oldaction=
 tag.oldact=
 tag.oldactp=
 tag.warn=
 tag.subject=
 tag.target=
 flags = <flags>&~statf_war
ELSE
 tag.task=GUARD_TASK_WANDER
ENDIF

//*****************************************************************************
[FUNCTION GUARD_WARN]
//*****************************************************************************
//Persegue e avisa <argv[0]> por alguma coisa errada que ele esteja fazendo.
obj=<argv[0]>
GUARD_SAVE_TASK
tag.task=GUARD_TASK_WARN
action=064
act=<obj.uid>
tag.target=<obj.uid>
tag.subject=<argv[1]>
DORAND 4
 say O que eh isso, <obj.name>?
 say Hey! O que esta fazendo?
 say Voce, <obj.name>!
 say Auto!
ENDDO
DORAND 3
 obj.sysmessagered O guarda esta ralhando com voce.
 obj.sysmessagered <name> grita com voce.
 obj.sysmessagered Voce esta sendo abordado pelo guarda <name>.
ENDDO
flags |= statf_war
timerf clear
timerf 5,GUARD_AI

[FUNCTION GUARD_WARN_STEP]
//Cada passo da AI com GUARD_TASK_WARN ele fará isso.
act=<tag0.target>
action=064

IF (!<act.ischar>) || ( (<distance <act.p>> > 11) && (!<CanSeeLOS <act>>) ) || ( (<act.isplayer>) && (!<act.isonline>) )
 act=04fffffff
 GUARD_RETRIEVE_TASK
 DORAND 3
  say Otimo...
  emotered resmunga
  say Oras!
 ENDDO

ELIF (<tag.subject>==GUARD_WARN_WARMODE)
 IF !(<act.flags>&statf_war) || (<act.region.defname> != <GET_TOWN>) || (!<act.IsOnline>)
  DORAND 3
   say E que sirva de aviso para os outros!
   say Obrigado pela cooperacao.
   say Por favor, nao me obrigue a fazer isto de novo.
  ENDDO
  act=04fffffff
  GUARD_RETRIEVE_TASK
 ELSE
  DOSWITCH <tag0.warn>
   say Acalme-se! Baixe esta arma!
   say Vamos! Abaixe esta arma!
   say Vou avisar pela ultima vez: Abaixe a arma!
   GUARD_ATTACK <uid>
  ENDDO
  timerf clear
  timerf 5,GUARD_AI
  tag0.warn += 1
 ENDIF

ELIF (<tag.subject>==GUARD_ATTACK_MONSTER)
 DORAND 3
  say Morra, <act.name> imund<act.sex o/a>
  say Fora de <region.name> sua desgraca!
  say <region.name> deve ser segura!
 ENDDO
 GUARD_ATTACK <uid>

ELIF (<tag.subject>==GUARD_WARN_WEAPON)
 IF (<act.weapon>)
  DORAND 3
   say <uid.<GET_TOWN_MASTER>.tag.name> nao quer pessoas andando armadas pela cidade!
   say Pessoas andando armadas por <region.name> nao sao bem vistas.
   say Por que anda armado por <region.name>?
  ENDDO
 ELSE
  act=04fffffff
  GUARD_RETRIEVE_TASK 
 ENDIF

ELIF (<tag.subject>==GUARD_WARN_LOCKPICK)
 DORAND 3
  say Minha funcao eh fazer de <region.name> um lugar segudo!
  say Ladroes! Guardas! Todos aos seus postos!
  say Ladroes nao sao tolerados em <region.name>!
 ENDDO
 GUARD_ATTACK <UID>

ELIF (<tag.subject>==GUARD_WARN_FORBIDDEN)
 IF (<act.region.defname>==<GET_TOWN>)
  DOSWITCH <tag0.warn>
   say Voce nao eh bem <act.sex vindo/vinda> aqui! Va embora!
   say Deixe <region.name> agora ou sofrera as consequencias!
   say Meu ultimo aviso, <act.name>... Saia de <region.name>
   GUARD_ATTACK <UID>
  ENDDO
  tag0.warn += 1
 ELSE
  say E nao volte!
  act=04fffffff
  GUARD_RETRIEVE_TASK 
 ENDIF
ENDIF




//*****************************************************************************
[FUNCTION GUARD_ADD_WAYPOINT]
//*****************************************************************************
//Adiciona um waypoint na lista
ref1=<argv[0]>
say Este aqui.
for wp 0 <eval <DEF.GUARD_WP_MAX>-1>
 IF (<IsEmpty <ref1.tag.wp_<LOCAL.wp>>>) && (!<LOCAL.ret>)
  try ref1.tag.wp_<LOCAL.wp>=<targp>
  LOCAL.ret=1
  sysmessageorange Agora <ref1.name> tem <eval <LOCAL.wp>+1>/<eval <DEF.GUARD_WP_MAX>> waypoints.
 ENDIF
endfor
IF (<LOCAL.ret>)
 ref1.timerf 1,say,Tomarei conta deste ponto enquanto patrulhar.
 return 1
ENDIF
for wp 0 <eval <DEF.GUARD_WP_MAX>-1>
 LOCAL.l=<ref1.tag.wp_<hval <LOCAL.wp>+1>>
 try ref1.tag.wp_<LOCAL.wp>=<LOCAL.l>
 sysmessageorange <ref1.name> ja tinha <eval <DEF.GUARD_WP_MAX>> waypoints. O primeiro foi substituido.
endfor
ref1.tag.wp_<hval <DEF.GUARD_WP_MAX>-1>=<targp>
obj=<ref1>
obj.GAURD_FOLLOW <uid>
obj.timerf 1,say,Tomarei conta deste ponto enquanto patrulhar.

//*****************************************************************************
[FUNCTION GUARD_CLEAR_WAYPOINTS]
//*****************************************************************************
//Remove todos waypoints
LOCAL.i=0
WHILE (!<IsEmpty <tag.wp_<LOCAL.i>>>)
 try tag.wp_<LOCAL.i>=
 LOCAL.i += 1
ENDDO
tag.waypoint=
src.sysmessageorange Todos os waypoints foram apagandos.

//*****************************************************************************
[FUNCTION GUARD_FOOD]
//*****************************************************************************
//Faz o guarda comer
LOCAL.food=<findtype t_food>
if (<LOCAL.food>)
 anim 022
 SFX 03b
 emotegreen comendo <UID.<LOCAL.FOOD>.name>
 food += <R50,75>
 uid.<local.food>.decrement
 return 1
endif
return 0

//*****************************************************************************
[FUNCTION GUARD_BUY]
//*****************************************************************************
//Faz o guarda comprar provisões.
if (<restest 2 i_coin_copper>) && (<food> < 50)
 LOCAL.item={ i_ribs_cooked 1 i_chicken_leg 1 i_lamb_leg 1 i_fish_cut_cooked 1 }
 consume 2 i_coin_copper
 serv.newitem <LOCAL.item>
 new.more1=<R2,4>
 new.amount=<R12,20>
 new.cont=<uid>
endif
if (!<restest <tag0.level> i_bottle_yellow>) && (<restest 7 i_coin_copper>)
 consume 7 i_coin_copper
 serv.newitem=i_bottle_yellow
 new.more2=150.0
 new.cont=<uid>
elif (!<restest 1 i_bottle_orange>) && (<restest 5 i_coin_copper>)
 consume 5 i_coin_copper
 serv.newitem=i_bottle_orange
 new.more2=200.0
 new.cont=<uid>
elif (!<restest <tag0.level> i_bottle_red>) && (<restest 6 i_coin_copper>)
 consume 6 i_coin_copper
 serv.newitem=i_bottle_red
 new.more2=200.0
 new.cont=<uid>
elif (!<restest 2 i_bottle_white>) && (<restest 20 i_coin_copper>)
 consume 20 i_coin_copper
 serv.newitem=i_bottle_white
 new.more2=890
 new.cont=<uid>
endif
if (<restest 1 i_bow>) || (<restest 1 i_bow_long>) || (<restest 1 i_bow_composite>) || (<restest 1 i_bow_elven>)
 if (!<restest 40 i_arrow>) && (<restest 4 i_coin_copper>)
  consume 4 i_coin_copper
  serv.newitem i_arrow
  new.amount=<R40,70>
  new.cont=<uid>
 endif
endif

//*****************************************************************************
[FUNCTION GuardMustWarn]
//*****************************************************************************
//Retorna se um guarda precisa me alertar sobre meu comportamento errado.
//ARGV0 é a UID do guarda.
obj=<argv[0]>
IF (<IsGM>) || (<IsEvent.e_guard_town>) || ( (<NPC>) && (<karma> > -2000) ) || (!<ischar>)
 return 0
ELIF (STRMATCH(*<UID>*,<obj.TOWN_GET_FORB_UID>)) && (<region.defname>==<obj.GET_TOWN>)
 return GUARD_WARN_FORBIDDEN
ELIF (<restest 1 i_mry_lockpick_fechadura>)
 TOWN_BAN_PLAYER <UID>
 return GUARD_WARN_LOCKPICK
ELIF (<npc>==BRAIN_MONSTER) || (<karma> < -2000)
 return GUARD_ATTACK_MONSTER
ELIF (<flags>&statf_war) && (<TOWN_CHECK_PRIVS> < 2)
 return GUARD_WARN_WARMODE
ELIF (<weapon>) && (<TOWN_CHECK_PRIVS> < 2)
 if (!<f_isWeaponTool <weapon>>)
  return GUARD_WARN_WEAPON
 endif
ELIF (STRMATCH(*<UID>*,<obj.TOWN_GET_FRIEND_UID>))
 return 0
ELIF (STRMATCH(*<tag.raca>*,<obj.TOWN_GET_FORB_RACE>)) && (<region.defname>==<obj.GET_TOWN>)
 return GUARD_WARN_FORBIDDEN
ENDIF


//*****************************************************************************
[FUNCTION GUARD_AI]
//*****************************************************************************
//Core do AI. Toma decisões.
//say <tag.task>
IF (<tag0.task>==GUARD_TASK_WARN)
 GUARD_WARN_STEP
ELIF (<tag0.task>==GUARD_TASK_ATTACK)
 if (<act>==04fffffff)
  GUARD_RETRIEVE_TASK
 endif
ELIF (<tag0.task>==GUARD_TASK_REMOVE)
 IF (<act.distance <p>> > 20)
  remove
  return 1
 ENDIF
ELIF (<tag0.task>==GUARD_TASK_PATROL)
 GUARD_PATROL
ELIF (<tag0.task>==GUARD_TASK_GO)
 IF (<distance <actp>> < 2)
  HOME=<actp>
  TAG.home=<actp>
  ACTION=06D
  TAG.TASK=GUARD_TASK_WANDER
 ENDIF
ELIF (<tag0.task>==GUARD_TASK_WANDER)
 GUARD_CORRECT_HOME
 ACTION=06D
 tag0.task=GUARD_TASK_POST
ENDIF
timerf 3,GUARD_AI


[DIALOG d_town]
100,75
page 0

//Decoração
resizepic 50 31 2620 500 400
checkertrans 55 38 490 385
gumppic 0 0 10400
gumppic 0 160 10401
gumppic 0 356 10402
gumppic 518 -2 10410
gumppic 518 160 10411
gumppic 518 356 10412

//Parte 1
gumppic 225 45 2501
dtext 275 45 26 <region.name>
resizepic 92 87 2620 416 147
dtext <TOWN_GET_DIALOG_TITLE>
dtext 104 120 66 Raca:
dtext 139 120 4 <STRSUB 0 30 <explode ;,<REGION.TAG.TOWN_RACE>>>
dtext 103 137 66 Responsavel:
dtext 179 136 4 <uid.<region.tag.town_master>.tag.name>
button 102 156 30008 30009 1 0 1
dtext 119 153 66 Conselheiros:
dtext 202 153 4 <eval <TOWN_COUNSELORS>>
call TOWN_RESOLV_DEMENCES
dtext 103 169 66 Capital:
dtext 150 169 4 <Local.kingdom>
dtext 328 169 66 Cidades:
dtext 384 169 4 <QVAL <IsNum <LOCAL.city>>?<dLOCAL.city>:<LOCAL.city>>
dtext 424 169 66 Vilas:
dtext 464 169 4 <dLOCAL.village>
dtext 103 185 66 Tesouro:
dtext 150 185 4 <TOWN_GET_STRTRESURE>
dtext 279 185 66 Funcionarios:
dtext 357 185 4 <eval <TOWN_SERVANTS <region.defname>>>
dtext 103 201 66 Periodo de pagamento:
dtext 237 201 4 <f_timestring <DEF.TOWN_TRIG_TIME>>
dtext 320 201 66 Gasto periodico:
dtext 414 201 4 <STRTRESURE <region.tag0.town_LASTPAY>>

//Parte 2
resizepic 92 246 2620 416 89
IF (<TOWN_CHECK_PRIVS> > 2)
 button 101 259 30008 30009 1 0 2
 button 101 275 30008 30009 1 0 3
ENDIF

dtext 120 256 66 Racas amigas:
dtext 256 256 4 <STRSUB 0 30 <explode ;,<REGION.TAG.TOWN_RACE_FRIEND>>>
//Botão dar raças inimigas
dtext 120 272 66 Racas inimigas:
dtext 256 272 4 <STRSUB 0 30 <explode ;,<REGION.TAG.TOWN_RACE_FORBIDDEN>>>
button 101 291 30008 30009 1 0 4
dtext 120 288 66 Extrangeiros amigos:
dtext 256 288 4 <eval <TOWN_FRIEND_UIDs>>
button 101 307 30008 30009 1 0 5
dtext 120 304 66 Inimigos publicos:
dtext 256 304 4 <eval <TOWN_FORBIDDEN_UIDs>>

//Parte 3
//Um dia quem sabe,neh?
//resizepic 92 348 2620 416 65
//button 102 362 30008 30009 1 0 6
//dtext 120 360 66 Plebicitos:
//dtext 256 360 4 (Plebicitos)
//button 102 378 30008 30009 1 0 7
//dtext 120 376 66 Peticoes:
//dtext 256 376 4 (Peticoes)

[DIALOG d_town text]
<region.name>
(tipo de town)
Raca:
(raca)
Responsavel:
<uid.<region.tag.town_master>.tag.name>
Conselheiros:
(cons)
Reino:
(reino)
Cidades:
(cidades)
Vilas:
(vilas)
Tesouro:
(tesouro)
Funcionarios:
(funcionarios)
Periodo de pagamento:
(payperiod)
Gasto periodico:
(lastpay)
Racas amigas:
(friendraces)
Racas inimigas:
(forbraces)
Extrangeiros amigos:
(UID_FRIENDS)
Inimigos publicos:
(UID_forbbinden)
Plebicitos:
(Plebicitos)
Peticoes:
(Peticoes)

[DIALOG d_town button]
ON=0
return 1

ON=1
// Conselheiros
COUNSELORS_LIST

ON=2
// Raças amigas
DIALOG d_town_admin 3

ON=3
// Raças inimigas
DIALOG d_town_admin 3

ON=4
// UID Friends
FRIEND_UID_LIST

ON=5
// UID Forbidden
FORBIDDEN_UID_LIST

ON=6
// Plebicitos

ON=7
// Petições


[DIALOG d_town_admin]
100,75
page 0
resizepic 50 31 2620 500 400
checkertrans 55 38 490 385
gumppic 0 0 10400
gumppic 0 160 10401
gumppic 0 356 10402
gumppic 518 -2 10410
gumppic 518 160 10411
gumppic 518 356 10412
gumppic 225 45 2501
dtext 275 45 26 <region.name>
resizepic 92 87 2620 416 288
dtext 120 96 48 <GET_TOWN_LIST_NAME>

page 1
call TOWN_POP_LIST
IF (<LOCAL.prev>)
 button 94 381 5603 5607 1 0 1
ENDIF
IF (<LOCAL.next>)
 button 489 381 5601 5605 1 0 2
ENDIF
IF ( (<TOWN_CHECK_PRIVS> > 2) && (<ctag0.town_list>==TOWN_LIST_COUNSELORS) ) || ( (<TOWN_CHECK_PRIVS> > 1) && (<ctag0.town_list>==TOWN_LIST_FORBIDDEN) ) || ( (<TOWN_CHECK_PRIVS> > 1) && (<ctag0.town_list>==TOWN_LIST_FRIENDS) )
 button 277 380 2443 2444 1 0 3
 dtext 295 382 32 ADD
ENDIF

page 2
dhtmlgump 108 120 390 181 1 1 <GET_TOWN_PROFILE <ctag.char>>
IF ( ( (<TOWN_CHECK_PRIVS> > 2) || (<UID>==<ctag0.char>) ) && (<ctag0.town_list>==TOWN_LIST_COUNSELORS) ) || ( (<TOWN_CHECK_PRIVS> > 1) && (<ctag0.town_list>==TOWN_LIST_FORBIDDEN) )  || ( (<TOWN_CHECK_PRIVS> > 1) && (<ctag0.town_list>==TOWN_LIST_FRIENDS) )
 button 109 309 30008 30009 1 0 21
 dtext 128 307 47 Remover da lista de <GET_TOWN_LIST_NAME>
ENDIF
IF (<TOWN_CHECK_PRIVS> > 2) && (<ctag0.town_list>==TOWN_LIST_COUNSELORS)
 button 109 339 30008 30009 1 0 22
 dtext 128 337 47 Passar a coroa de <region.name> para <rec_fGetName <ctag.char>>
ENDIF
button 94 381 5603 5607 1 0 20

page 3
resizepic 110 128 9200 178 224
resizepic 304 128 9200 178 224
dtext 156 136 62 Racas amigas
dtext 348 136 32 Racas inimigas
dtext 152 168 92 Anoes
dtext 344 168 92 Anoes
dtext 152 200 92 Elfos
dtext 344 200 92 Elfos
dtext 152 232 92 Drows
dtext 344 232 92 Drows
dtext 152 264 92 Humanos
dtext 344 264 92 Humanos
dtext 152 296 92 Orcs
dtext 344 296 92 Orcs

IF (STRMATCH(*Anao*,<region.tag.TOWN_RACE_FRIEND>))
 LOCAL.anao1=1
 LOCAL.anao0=0
ELSE
 LOCAL.anao1=0
 LOCAL.anao0=1
ENDIF

IF (STRMATCH(*Elfo*,<region.tag.TOWN_RACE_FRIEND>))
 LOCAL.elfo1=1
 LOCAL.elfo0=0
ELSE
 LOCAL.elfo1=0
 LOCAL.elfo0=1
ENDIF

IF (STRMATCH(*Drow*,<region.tag.TOWN_RACE_FRIEND>))
 LOCAL.drow1=1
 LOCAL.drow0=0
ELSE
 LOCAL.drow1=0
 LOCAL.drow0=1
ENDIF

IF (STRMATCH(*Humano*,<region.tag.TOWN_RACE_FRIEND>))
 LOCAL.humano1=1
 LOCAL.humano0=0
ELSE
 LOCAL.humano1=0
 LOCAL.humano0=1
ENDIF

IF (STRMATCH(*Orc*,<region.tag.TOWN_RACE_FRIEND>))
 LOCAL.orc1=1
 LOCAL.orc0=0
ELSE
 LOCAL.orc1=0
 LOCAL.orc0=1
ENDIF

//Anões
Group 1
radio 128 168 208 209 <LOCAL.anao1> 1
radio 320 168 208 209 <LOCAL.anao0> 0

//Elfos
Group 2
radio 128 200 208 209 <LOCAL.elfo1> 2
radio 320 200 208 209 <LOCAL.elfo0> 0

//Drows
Group 3
radio 128 232 208 209 <LOCAL.drow1> 3
radio 320 232 208 209 <LOCAL.drow0> 0

//Humanos
Group 4
radio 128 264 208 209 <LOCAL.humano1> 4
radio 320 264 208 209 <LOCAL.humano0> 0

//Orcs
Group 5
radio 128 296 208 209 <LOCAL.orc1> 5
radio 320 296 208 209 <LOCAL.orc0> 0

button 96 384 247 248 1 0 41
button 440 384 241 242 1 0 42

[DIALOG d_town_admin text]
<region.name>
(tipo de lista)
(nome do cabra)
ADD
HtmlGump id.4
Remover da lista de 
Passar a coroa de() para ()
Racas amigas
Racas inimigas
Anoes
Anoes
Elfos
Elfos
Drows
Drows
Humanos
Humanos
Orcs
Orcs

[DIALOG d_town_admin button]
ON=0
DIALOG d_town

ON=1
// Page -1
ctag0.page -= 1
DIALOG d_town_admin 1

ON=2
// Page +1
ctag0.page += 1
DIALOG d_town_admin 1

ON=3
// Adicionar a lista
IF (<ctag0.town_list>==TOWN_LIST_COUNSELORS)
 TOWN_ADD_COUNSELOR
ELIF (<ctag0.town_list>==TOWN_LIST_FORBIDDEN)
 TOWN_ADD_FORBIDDEN
ELIF (<ctag0.town_list>==TOWN_LIST_FRIENDS)
 TOWN_ADD_FRIEND
ENDIF

ON=20
// Previous
//IF (<ctag.town_list>==TOWN_LIST_COUNSELORS)
 DIALOG d_town_admin 1
//ENDIF

ON=21
// Remover da lista
IF (<ctag.town_list>==TOWN_LIST_COUNSELORS)
 TOWN_REM_COUNSELOR <ctag.char>
ELIF (<ctag.town_list>==TOWN_LIST_FORBIDDEN)
 TOWN_REM_FORBIDDEN <ctag.char>
ELIF (<ctag.town_list>==TOWN_LIST_FRIENDS)
 TOWN_REM_FRIEND <ctag.char>
ENDIF

ON=22
// Passar coroa


ON=41
// Muda raças
LOCAL.friend=
LOCAL.forbidden=
IF <ARGCHK[1]>
 LOCAL.friend .= ;Anao
ELSE
 LOCAL.forbidden .= ;Anao
ENDIF
IF <ARGCHK[2]>
 LOCAL.friend .= ;Elfo
ELSE
 LOCAL.forbidden .= ;Elfo
ENDIF
IF <ARGCHK[3]>
 LOCAL.friend .= ;Drow
ELSE
 LOCAL.forbidden .= ;Drow
ENDIF
IF <ARGCHK[4]>
 LOCAL.friend .= ;Humano
ELSE
 LOCAL.forbidden .= ;Humano
ENDIF
IF <ARGCHK[5]>
 LOCAL.friend .= ;Orc
ELSE
 LOCAL.forbidden .= ;Orc
ENDIF
LOCAL.friend=<STRSUB 2 30 <LOCAL.friend>>
LOCAL.forbidden=<STRSUB 2 30 <LOCAL.FORBIDDEN>>

IF (strlen(<LOCAL.friend>) > 2)
 REGION.TAG.TOWN_RACE_FRIEND=<LOCAL.friend>
ELSE
 REGION.TAG.TOWN_RACE_FRIEND=Nenhuma
ENDIF

IF (strlen(<LOCAL.forbidden>) > 2)
 REGION.TAG.TOWN_RACE_FORBIDDEN=<LOCAL.forbidden>
ELSE
 REGION.TAG.TOWN_RACE_FORBIDDEN=Nenhuma
ENDIF

TOWN

ON=42
// Raças cancel
TOWN

on=43
say Botao <eval <argn>>

ON=100 200
// Abrir profile
LOCAL.idx=<eval <argn>-100>
IF (<ctag.town_list>==TOWN_LIST_COUNSELORS)
 ctag.char=<TOWN_COUNSELORS <LOCAL.idx>>
ELIF (<ctag.town_list>==TOWN_LIST_FORBIDDEN)
 ctag.char=<TOWN_FORBIDDEN_UIDs <LOCAL.idx>>
ELIF (<ctag.town_list>==TOWN_LIST_FRIENDS)
 ctag.char=<TOWN_FRIEND_UIDs <LOCAL.idx>>
ENDIF
DIALOG d_town_admin 2

//#######################################################################################
//
//                  FUNCTIONS do DIALOG
//
//#######################################################################################
[FUNCTION TOWN_GET_DIALOG_TITLE]
if (<REGION.tag.TOWN_TYPE>==TOWN_TYPE_CAPITAL)
 return 234 96 48 A capital de <region.tag.town_kingdom>
elif (<REGION.tag.TOWN_TYPE>==TOWN_TYPE_CITY)
 return 224 96 48 Uma cidade de <region.tag.town_kingdom>
elif (<REGION.tag.TOWN_TYPE>==TOWN_TYPE_VILLAGE)
 return 150 96 48 Uma vila da cidade de <serv.resources.<region.tag.town_capital>.name>, do reino de <region.tag.town_kingdom>
endif



//ttttttttttttttttttttttttttttttttttttttttttttttttt
//           conselheiros
//ttttttttttttttttttttttttttttttttttttttttttttttttt
[FUNCTION TOWN_ADD_COUNSELOR]
//Adiciona um conselheiro na town
//Se <argv[0]> não é uma region, a region de quem chama é usada.
IF (!<serv.resources.<argv[0]>.tag.TOWN_TYPE>)
 IF (<region.tag.town_type>)
  LOCAL.town=<region.defname>
 else
  sysmessagered <region.name> nao eh town.
  return 1
 endif
else
 LOCAL.town=<argv[0]>
endif
sysmessagegreen Que conselheiro voce gostaria de adicionar ao conselho de <serv.resources.<LOCAL.town>.name>?
targetf _TOWN_ADD_COUNSELOR <LOCAL.town>

[FUNCTION _TOWN_ADD_COUNSELOR]
LOCAL.town=<argv[0]>
if (!<argo.ischar>)
 sysmessagered Sim... E que tal uma pedra ou uma espada?
 return 1
elif (!<argo.isplayer>)
 sysmessagered Sim... E que tal um sapo ou um paroxysmus?
 return 1
elif (<serv.resources.<LOCAL.town>.tag.TOWN_MASTER>==<argo.uid>)
 sysmessageyellow <rec_fGetName <argo.uid>> eh o responsavel por <serv.resources.<LOCAL.town>.name>.
 return 1
elif (STRMATCH(*<argo.uid>*,<serv.resources.<LOCAL.town>.tag.TOWN_COUNSELORS>))
 sysmessageyellow <rec_fGetName <argo.uid>> ja faz parte do conselho de <serv.resources.<LOCAL.town>.name>.
 return 1
elif (STRMATCH(*<argo.tag.raca>*,<serv.resources.<LOCAL.town>.TAG.TOWN_RACE_FORBIDDEN>))
 sysmessageyellow <rec_fGetName <argo.uid>> pertence a uma raca proibida em <serv.resources.<LOCAL.town>.name>.
 return 1
elif (STRMATCH(*<argo.uid>*,<serv.resources.<LOCAL.town>.TAG.TOWN_UID_FORBIDDEN>))
 sysmessageyellow <rec_fGetName <argo.uid>> eh um inimigo publico de <serv.resources.<LOCAL.town>.name>.
 return 1
endif
if (<IsEmpty <serv.resources.<LOCAL.town>.TAG.TOWN_COUNSELORS>>)
 serv.resources.<LOCAL.town>.TAG.TOWN_COUNSELORS=<argo.uid>
else
 serv.resources.<LOCAL.town>.TAG.TOWN_COUNSELORS .= ;<argo.uid>
endif
sysmessageorange <rec_fGetName <argo.uid>> foi adicionado ao conselho de <serv.resources.<LOCAL.town>.name>.
COUNSELORS_LIST

[FUNCTION TOWN_REM_COUNSELOR]
region.tag.TOWN_COUNSELORS=<f_removeList <argn>,<region.tag.TOWN_COUNSELORS>>
sysmessageorange <rec_fGetName <argn>> foi removido do conselho de <region.name>!!!
DIALOG d_town_admin 1


[FUNCTION COUNSELORS_LIST]
ctag.town_list=TOWN_LIST_COUNSELORS
ctag.page=0
DIALOG d_town_admin 1

[FUNCTION TOWN_ADD_FORBIDDEN]
//Adiciona um player na lista de inimigos públicos
//Se <argv[0]> não é uma region, a region de quem chama é usada.
IF (!<serv.resources.<argv[0]>.tag.TOWN_TYPE>)
 IF (<region.tag.town_type>)
  LOCAL.town=<region.defname>
 else
  sysmessagered <region.name> nao eh town.
  return 1
 endif
else
 LOCAL.town=<argv[0]>
endif
sysmessagegreen Quem eh o novo inimigo publico de <serv.resources.<LOCAL.town>.name>?
targetf _TOWN_ADD_FORBIDDEN <LOCAL.town>

[FUNCTION _TOWN_ADD_FORBIDDEN]
LOCAL.town=<argv[0]>
if (!<argo.ischar>)
 sysmessagered Sim... E que tal uma pedra ou uma espada?
 return 1
//elif (!<argo.isplayer>)
// sysmessagered Sim... E que tal um sapo ou um paroxysmus?
// return 1
elif (<serv.resources.<LOCAL.town>.tag.TOWN_MASTER>==<argo.uid>)
 sysmessageyellow <rec_fGetName <argo.uid>> eh o responsavel por <serv.resources.<LOCAL.town>.name>.
 return 1
elif (STRMATCH(*<argo.uid>*,<serv.resources.<LOCAL.town>.tag.TOWN_COUNSELORS>))
 sysmessageyellow <rec_fGetName <argo.uid>> faz parte do conselho de <serv.resources.<LOCAL.town>.name>.
 return 1
elif (STRMATCH(*<argo.uid>*,<serv.resources.<LOCAL.town>.TAG.TOWN_UID_FORBIDDEN>))
 sysmessageyellow <rec_fGetName <argo.uid>> ja eh inimigo publico de <serv.resources.<LOCAL.town>.name>.
 return 1
endif
if (<IsEmpty <serv.resources.<LOCAL.town>.TAG.TOWN_UID_FORBIDDEN>>)
 serv.resources.<LOCAL.town>.TAG.TOWN_UID_FORBIDDEN=<argo.uid>
else
 serv.resources.<LOCAL.town>.TAG.TOWN_UID_FORBIDDEN .= ;<argo.uid>
endif
argo.sysmessageorange Voce foi banido de <serv.resources.<LOCAL.town>.name>!!!
sysmessageorange <rec_fGetName <argo.uid>> eh agora inimigo publico de <serv.resources.<LOCAL.town>.name>.
FORBIDDEN_UID_LIST

[FUNCTION TOWN_BAN_PLAYER]
LOCAL.town=<GET_TOWN>
if (STRMATCH(*<argn>*,*<serv.resources.<LOCAL.town>.TAG.TOWN_UID_FORBIDDEN>*)) || (<argn>==<serv.resources.<LOCAL.town>.TAG0.TOWN_UID_FORBIDDEN>)
 return 0
endif
if (<IsEmpty <serv.resources.<LOCAL.town>.TAG.TOWN_UID_FORBIDDEN>>)
 serv.resources.<LOCAL.town>.TAG.TOWN_UID_FORBIDDEN=<argn>
else
 serv.resources.<LOCAL.town>.TAG.TOWN_UID_FORBIDDEN .= ;<argn>
endif
serv.log [TOWNS] <uid.<argn>.tag.name> [<uid.<argn>.account.name>] foi banido de <serv.resources.<LOCAL.town>.name>
uid.<argn>.sysmessageorange Voce foi banido de <serv.resources.<LOCAL.town>.name>!!!

[FUNCTION FORBIDDEN_UID_LIST]
ctag.town_list=TOWN_LIST_FORBIDDEN
ctag.page=0
DIALOG d_town_admin 1

[FUNCTION TOWN_REM_FORBIDDEN]
region.tag.TOWN_UID_FORBIDDEN=<f_removeList <argn>,<region.TAG.TOWN_UID_FORBIDDEN>>
sysmessageorange <rec_fGetName <argn>> nao eh mais inimigo publico de <region.name>.
DIALOG d_town_admin 1

[FUNCTION TOWN_ADD_FRIEND]
//Adiciona um player na lista de extrangeiros amigos
//Se <argv[0]> não é uma region, a region de quem chama é usada.
IF (!<serv.resources.<argv[0]>.tag.TOWN_TYPE>)
 IF (<region.tag.town_type>)
  LOCAL.town=<region.defname>
 else
  sysmessagered <region.name> nao eh town.
  return 1
 endif
else
 LOCAL.town=<argv[0]>
endif
sysmessagegreen Quem eh o novo extrangeiro bem vindo em <serv.resources.<LOCAL.town>.name>?
targetf _TOWN_ADD_FRIEND <LOCAL.town>

[FUNCTION _TOWN_ADD_FRIEND]
LOCAL.town=<argv[0]>
if (!<argo.ischar>)
 sysmessagered Sim... E que tal uma pedra ou uma espada?
 return 1
elif (!<argo.isplayer>)
 sysmessagered Sim... E que tal um sapo ou um paroxysmus?
 return 1
//elif (<serv.resources.<LOCAL.town>.tag.TOWN_MASTER>==<argo.uid>)
// sysmessageyellow <rec_fGetName <argo.uid>> eh o responsavel por <serv.resources.<LOCAL.town>.name>.
// return 1
elif (STRMATCH(*<argo.uid>*,<serv.resources.<LOCAL.town>.TAG.TOWN_UID_FORBIDDEN>))
 sysmessageyellow <rec_fGetName <argo.uid>> eh inimigo publico de <serv.resources.<LOCAL.town>.name>.
 return 1
endif
if (<IsEmpty <serv.resources.<LOCAL.town>.TAG.TOWN_UID_FRIEND>>)
 serv.resources.<LOCAL.town>.TAG.TOWN_UID_FRIEND=<argo.uid>
else
 serv.resources.<LOCAL.town>.TAG.TOWN_UID_FRIEND .= ;<argo.uid>
endif
argo.sysmessageorange Voce eh agora um extrangeiro bem vindo em <serv.resources.<LOCAL.town>.name>!!!
sysmessageorange <rec_fGetName <argo.uid>> eh bem vindo em <serv.resources.<LOCAL.town>.name>.
FRIEND_UID_LIST

[FUNCTION FRIEND_UID_LIST]
ctag.town_list=TOWN_LIST_FRIENDS
ctag.page=0
DIALOG d_town_admin 1

[FUNCTION TOWN_REM_FRIEND]
region.tag.TOWN_UID_FRIEND=<f_removeList <argn>,<region.TAG.TOWN_UID_FRIEND>>
sysmessageorange <rec_fGetName <argn>> nao eh mais bem vindo em <region.name>.
DIALOG d_town_admin 1

[FUNCTION TOWN_POP_LIST]
IF (<ctag0.town_list>==TOWN_LIST_COUNSELORS)
 call TOWN_POP_COUNSELORS_LIST
ELIF (<ctag0.town_list>==TOWN_LIST_FORBIDDEN)
 call TOWN_POP_FORBIDDEN_LIST
ELIF (<ctag0.town_list>==TOWN_LIST_FRIENDS)
 call TOWN_POP_FRIEND_LIST
ENDIF

[FUNCTION TOWN_POP_COUNSELORS_LIST]
DORIGIN 117 98
FOR i 1 8
 LOCAL.j=<eval (<ctag0.page>*8)+<LOCAL.i>>
 LOCAL.uid=<TOWN_COUNSELORS <dLOCAL.j>>
 IF (<LOCAL.uid>)
  button - *30 30008 30009 1 0 <eval 100+<LOCAL.j>>
  dtext +20 -2 1 <rec_fGetName <local.uid>>
 ENDIF
ENDFOR
IF (<TOWN_COUNSELORS <eval 1+<LOCAL.j>>>)
 LOCAL.next=1
ENDIF
IF (<ctag0.page>)
 LOCAL.prev=1
ENDIF

[FUNCTION TOWN_POP_FORBIDDEN_LIST]
DORIGIN 117 98
FOR i 1 8
 LOCAL.j=<eval (<ctag0.page>*8)+<LOCAL.i>>
 LOCAL.uid=<TOWN_FORBIDDEN_UIDs <dLOCAL.j>>
 IF (<LOCAL.uid>)
  button - *30 30008 30009 1 0 <eval 100+<LOCAL.j>>
  dtext +20 -2 1 <rec_fGetName <local.uid>>
 ENDIF
ENDFOR
IF (<TOWN_FORBIDDEN_UIDs <eval 1+<LOCAL.j>>>)
 LOCAL.next=1
ENDIF
IF (<ctag0.page>)
 LOCAL.prev=1
ENDIF

[FUNCTION TOWN_POP_FRIEND_LIST]
DORIGIN 117 98
FOR i 1 8
 LOCAL.j=<eval (<ctag0.page>*8)+<LOCAL.i>>
 LOCAL.uid=<TOWN_FRIEND_UIDs <dLOCAL.j>>
 IF (<LOCAL.uid>)
  button - *30 30008 30009 1 0 <eval 100+<LOCAL.j>>
  dtext +20 -2 1 <rec_fGetName <local.uid>>
 ENDIF
ENDFOR
IF (<TOWN_FRIEND_UIDs <eval 1+<LOCAL.j>>>)
 LOCAL.next=1
ENDIF
IF (<ctag0.page>)
 LOCAL.prev=1
ENDIF

[FUNCTION GET_TOWN_LIST_NAME]
IF (<ctag0.town_list>==TOWN_LIST_COUNSELORS)
 return conselheiros de <region.name>
ELIF (<ctag0.town_list>==TOWN_LIST_FORBIDDEN)
 return animigos publicos de <region.name>
ELIF (<ctag0.town_list>==TOWN_LIST_FRIENDS)
 return extrangeiros amigos de <region.name>
ENDIF

[FUNCTION GET_TOWN_PROFILE]
obj=<argn>
LOCAL.p=<DEF.center>Perfil de <rec_fGetName <argn>><DEF.centere><DEF.br>
LOCAL.p .= <obj.profile>
RETURN <LOCAL.p>

//town_addcash( amount )
[FUNCTION TOWN_ADDCASH]
if <region.tag0.town_type>
    region.tag0.town_tresure += <argn1>
endif

[EOF]