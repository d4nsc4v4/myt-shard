//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE COMBATE por
//  Galthar
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:
//-indicar tempo restante de desmaiio ao player
//-permitir que ele escolha morrer ao inves de esperar.
//-logar no DB mortes/assassinos

//A cada <max_desmaios> o player morre imediatamente, senao ele fica em estado
//de coma. O tempo de coma depende do numero de desmaios (x 1 min). Se o nume-
//ro de desmaios for menor que <max_desmaios/2>, o player volta a vida sozinho
//apos o tempo, caso contrario ele morre se não for atendido.

//Apos morto pode-se ressucitar em qualquer ank, desde que tenha pontos de alma
//positivos. Esses pontos tambem influenciam no tempo minimo em que o player
//permanece como fantasma. Ao dar ress o player é transportado ao corpo.

//A cada morte o player perde 1 ponto de alma. Ao serem esgotados todos os pontos
//o char nao pode mais ressucitar.

//Enquanto o player esta desmaiado, seu corpo pode ser:
// - Arrastado por qualquer um (60% str e 40% stam)
// - Curado por qualquer um (sistema de healing)
// - Cortado ( perde 2 pontos de alma )
// - Looteado ( perde 1 ponto de alma )

//Enquanto o char estiver desmaiado, o espirito pode vagar nas proximidades do
//corpo. Se ele tentar ir mais longe sera teleportado de volta.

//TAGS usadas:
//tag.morte_desmaios
//tag.morte_local
//tag.morte_horario
//tag.fpk
//tag.lastkillUID
//tag.lastkillname
//tag.pontosdealma
//ctag.morte_corpse
//*****************************************************************************


//*****************************************************************************
//*****************************************************************************
// DEFNAMEs
//*****************************************************************************
//*****************************************************************************
[DEFNAME morte]
max_desmaios=5

//*****************************************************************************
//*****************************************************************************
// EVENTs
//*****************************************************************************
//*****************************************************************************
[EVENTS e_sistema_de_morte]
ON=@DEATH

serv.log [MORTE] <account> (<uid>) morreu: <flags>

//Limpa buffs e debuffs
f_CleanDeBuffs

//marca a hora/local
tag.morte_horario=<time>/(10)
tag.morte_local=<p>

hits=1
flags |= statf_dead

//aos fade to black
sendpacket 02C 0

//desmaia e solta espirito
f_faint 1

//se puder ficar desmaiado
if (<tag0.morte_desmaios> < <DEF.max_desmaios>)
    tag.morte_desmaios=<eval <tag0.morte_desmaios>+1>

    SERV.NEWITEM=i_mry_desmaio
    NEW.link=<uid>
    NEW.equip
    NEW.timer=<eval (60*<tag0.morte_desmaios>)>
    
    //indica morte apos 'timer'
    if (<tag0.morte_desmaios> > (<DEF.max_desmaios>/2) )
        NEW.more1=1 
    endif

    timerf 3,DIALOG,d_desmaio

    sysmessageyellow Voce perdeu a consciencia (<tag0.morte_desmaios> min)!!!
else
    f_morre
endif
return 1
 
//*****************************************************************************
//*****************************************************************************
// TYPEDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// t_shrine
//*****************************************************************************
[TYPEDEF t_shrine]
ON=@DCLICK
 //ressurect?
 if (<src.flags>&statf_dead)
    f_shrine
 else
  src.sysmessageyellow Voce possui <eval <src.tag0.pontosdealma>> pontos de alma...
  DOSWITCH <tag0.rp_grade>
   sysmessagered Sua representacao esta pessima ou não foi avaliada.
   sysmessagered Sua representacao esta pessima. Procure melhorar.
   sysmessagered Sua representacao esta pessima. Procure melhorar.
   sysmessageorange Sua representacao esta ruim. Continue se esforcando.
   sysmessageorange Sua representacao esta ruim. Continue se esforcando.
   sysmessageyellow Sua representacao mediana. Pode melhorar muito
   sysmessageyellow Sua representacao esta satisfatoria, mas pode ser bem melhor.
   sysmessagegreen Sua representacao esta boa.
   sysmessagegreen Sua representacao esta muito boa.
   sysmessageblue Sua representacao esta excelente.
   sysmessageblue Sua representacao esta impecavel!
  ENDDO
 endif
 return 1

ON=@STEP
 //ressurect?
 if (<src.flags>&statf_dead)
    f_shrine
 endif
 return 1

//*****************************************************************************
// t_corpse
//*****************************************************************************
[TYPEDEF t_corpse]
ON=@DCLICK
 //so permite vivos lootear ou mexer nos corpos de outros players
 if ( !(<src.flags>&statf_dead) && (<link>!=04fffffff) && (<link>!=<src.uid>)
  src.ctag.morte_corpse=<uid>
  //abre menu para player morto ou desmaiado
  src.DIALOG d_corpse
  return 1
 endif
 
ON=@CLICK
  //indica se corpo está morto ou inconsciente
  if (<link>!=04fffffff)
   if (<link.flags>&statf_dead)
    message <link.name> morto
   else
    message <link.name> inconsciente
   endif
   return 1
  endif
 
ON=@CLIENTTOOLTIP
if (<link>!=04fffffff)
    f_sendTooltip <link.tag.raca>,Conhecido como: <link.rec_getName>
else
    f_sendTooltipData <name>
endif

ON=@TIMER
//faz com que o espirito dos desmaiados estejam perto do corpo
if (<link>==04fffffff)
    return 0
else
    timer=5
    //se desmaiado
    if (<link.findid.i_mry_desmaio>)
        if (<link.distance <uid>> > 5)
            link.movenear <uid> 3
            link.sysmessageyellow Voce deve permanecer proximo de seu corpo.
        endif
    endif
endif
return 1

//*****************************************************************************
//*****************************************************************************
// FUNCTIONS
//*****************************************************************************
//*****************************************************************************
 
[FUNCTION f_shrine]
//somente se tiver pontos de alma
if (<src.tag0.pontosdealma><=0)
 src.sysmessageyellow Sua alma esta fadada ao descanso eterno.
 src.sysmessageyellow Nunca mais voltara a vida
 return 1
endif  

//verifica se ja passou tmepo suficiente para recuperacao
if (<src.tag0.pontosdealma> == 1)
 LOCAL.ressconcluido=<eval <src.tag.morte_horario>+(60 * 15)>
elif (<src.tag0.pontosdealma> == 2)
 LOCAL.ressconcluido=<eval <src.tag.morte_horario>+(60 * 10)>
elif (<src.tag0.pontosdealma> == 3)
 LOCAL.ressconcluido=<eval <src.tag.morte_horario>+(60 * 5)>
else
 LOCAL.ressconcluido=<eval <src.tag.morte_horario>+(60 * 2)>
endif

if (<LOCAL.ressconcluido> > <eval <time>/(10)>)
 LOCAL.x=<eval <LOCAL.ressconcluido>-<eval <time>/(10)>>
 src.sysmessageyellow Voce precisa recuperar um pouco suas forcas (<dlocal.x>s)...
 return 1
else
 src.f_returnToLife
 return 1
endif

//*****************************************************************************
// f_faint <debuff>
//*****************************************************************************
//faz com que o char desmaie
[FUNCTION f_faint]

if (<tag0.corpse>)
    return 1 //ja esta em faint
endif

if <argn>
    //Testa e retira polymorph
    if (<flags>&statf_polymorph)
        findid.i_spl_polymorph.trigger=@destroy
        findid.i_spl_polymorph.remove
    endif

    //Testa e retira stun
    if (<flags>&statf_freeze)
        findid.i_stun.remove
    endif

    if (<flags>&statf_poisoned)
        spelleffect s_cure,1000
    endif

    flags &= ~(statf_war|statf_poisoned)
endif

flags |= (statf_invisible|statf_invul|statf_insubstantial)

if (<tag0.comcapuz>)
    capuz
endif

dismount

serv.newitem i_corpse
new.amount=<body>
new.morep=<body>,0,<dir>
new.link=<uid>
new.more2=<uid>
new.color=<color>
new.p=<p>
for layer 1 24
    obj=<findlayer.<local.layer>>
    if (<obj>)
        obj.cont=<new>
        obj.layer=<local.layer>
    endif
endfor
//timer para manter espirito proximo
new.timer=5
new.update
tag.corpse=<new>
new.sangrar

//body de morto
IF (<isplayer>)
 body=<body>+2
 serv.newitem i_deathshroud
 new.cont=<uid>
endif
update

//atalho
[FUNCTION faint]
f_faint
 
//*****************************************************************************
// f_wakeup
//***************************************************************************** 
//acorda um char desmaiado/morto
[FUNCTION f_wakeup]

body=<obody>
findid.i_deathshroud.remove
ref2=<uid>
ref1=<tag.corpse>
if (!<ref1>)
    serv.log [MORTE] ATENCAO: BUG DA MORTE
    serv.log UID: <uid> (<name>) CORPSE: <tag.corpse>
    tag.corpse=
    return 1
endif
forcont <ref1> 0
    ref2.equip <uid>
endfor
p=<ref1.p>
ref1.remove
tag.corpse=

flags &= ~(statf_invisible|statf_invul|statf_insubstantial)

if <flags>&statf_dead
    hits=1
    findid.i_mry_invul.remove

    serv.log [MORTE] INVUL CRIADO <uid>
    serv.newitem i_mry_invul
    new.cont=<uid>
    flags |= statf_invul
endif

flags &= ~(statf_dead)

f_correctLight <region.flags>
if strmatch(<tag.raca>,Elfo)) || strmatch(<tag.raca>,Drow))
    f_elfsight
endif
findid.i_mry_desmaio.remove
update

//*****************************************************************************
// f_returnToLife
//***************************************************************************** 
//chama f_wakeup e mostra mensagens, assim podemos ter f_wakeup para outros
//usos alem do sistema de morte.
[FUNCTION f_returnToLife]
//somente retorna a vida se tiver corpse
if (<tag0.corpse>)
    f_wakeup
    sysmessagegreen Voce recobrou a consciencia.
    emotegreen hug!
endif

//atalho
[FUNCTION ress]
f_returnToLife

//*****************************************************************************
// f_morre
//***************************************************************************** 
//faz com que o char fique morto
//default: morto
[FUNCTION f_morre]
//somente morre se tiver corpse
if (<tag0.corpse>)
    tag.morte_desmaios=0
    morte_decrementaPontos 1

    sysmessageyellow Voce perdeu a ligacao com seu corpo.
    uid.<tag.corpse>.emotered morreu

    findid.i_mry_desmaio.remove
endif

//*****************************************************************************
// morte_incrementaPontos
//*****************************************************************************
[FUNCTION morte_incrementaPontos]
 tag.pontosdealma=<eval <tag.pontosdealma>+<argn>>
 sysmessagegreen Voce ganhou: <argn> ponto(s) de alma
 
//*****************************************************************************
// morte_decrementaPontos
//*****************************************************************************
[FUNCTION morte_decrementaPontos]
 tag.pontosdealma=<eval <tag.pontosdealma>-<argn>>
 sysmessagered Voce perdeu: <argn> ponto(s) alma
 
//*****************************************************************************
// morte_arrastar(uid)
//*****************************************************************************
[FUNCTION morte_arrastar]
 if <SRC.distance <argn1>> > 2
    src.sysmessageyellow Esta muito longe para arrastar...
 else
    SERV.NEWITEM=i_mry_arrastar
    NEW.link=<argn1>
    NEW.equip
    NEW.dclick
endif    
    
//*****************************************************************************
// morte_corta(uid)
//*****************************************************************************
[FUNCTION morte_corta]
 if (<findlayer(layer_hand1).type>==t_weapon_fence) || (<findlayer(layer_hand1).type>==t_weapon_axe) || (<findlayer(layer_hand1).type>==t_weapon_sword) || (<findlayer(layer_hand2).type>==t_weapon_sword) || (<findlayer(layer_hand2).type>==t_weapon_axe) || (<findlayer(layer_hand2).type>==t_weapon_fence)
  OBJ=<argn>
  OBJ.emotered assassinado
  
  OBJ.link.f_morre
  OBJ.link.tag.lastkillname=<tag.name>
  OBJ.link.tag.lastkillUID=<act.uid>
  tag.fpk=<eval (<tag0.fpk>+1)>
 else 
    sysmessageyellow Precisa de algo cortante na mao.
 endif
 
//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_deathshroud
//*****************************************************************************
//redefine para remover scripts padrao sphere
[ITEMDEF 0204E]
DEFNAME=i_deathshroud
NAME=Death Shroud
TYPE=t_clothing

CATEGORY=MyT - Provisions - GM Items
SUBSECTION=Special Items
DESCRIPTION=Death Shroud

//nao deixa equipar com dclick pra evitar bugs
ON=@DCLICK
if !<src.isgm>
    src.sysmessageyellow Esse item nao pode ser usado e sera destruido.
    remove
    return 1   
endif    

//*****************************************************************************
// i_mry_arrastar
//*****************************************************************************
//more1: constante de forca/stamina
[ITEMDEF i_mry_arrastar]
 NAME=arrastar
 id=i_deed
 type=t_eq_script
 layer=layer_special

ON=@CREATE
 attr=attr_can_decay
 return 1

ON=@DCLICK
 //calcula tempo de arrasto (0 a 20 segundos)
 more1=((<cont.str>*5)+(<cont.stamina>*3))/40
 cont.emoteyellow arrastando corpo
 timer=1
 return 1
 
ON=@TIMER
 if (<more1> > 0) && !(<cont.flags>&statf_war) && (<link.link.flags>&statf_dead)
  cont.stam=<cont.stam>-4
  more1=<more1>-1
  //arrasta corpo (e vira)
  link.p=<cont.p>
  link.morep=<link.amount>,0,<cont.dir>
  dorand 4
    link.sangrar 1
  enddo
//  link.update
  
  //sava nova posicao do corpo
  link.link.tag.morte_local=<cont.p>
  timer=1
 else
  cont.emoteyellow largou
  cont.sysmessageyellow O corpo escapou de suas maos
  remove
 endif
 return 1
 
//*****************************************************************************
// i_mry_desmaio
//*****************************************************************************
[ITEMDEF i_mry_desmaio]
 id=i_deed
 name=desmaio
 type=t_eq_script
 layer=layer_special

ON=@CREATE
 attr=attr_decay

ON=@TIMER
//recobra consciencia se more1 != 1
 if (<more1>!=1)
    cont.f_returnToLife
 else
    cont.f_morre
 endif
 remove
 return 1

//*****************************************************************************
// i_mry_invul
//*****************************************************************************
[ITEMDEF i_mry_invul]
 id=i_deed
 name=Mem. Invul
 type=t_eq_script
 layer=layer_special

ON=@CREATE
 attr=attr_decay|attr_can_decay
 timer = 10
 
ON=@TIMER
 cont.flags &= ~statf_invul
 remove
 return 1
 
//*****************************************************************************
//*****************************************************************************
// GUMPs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// CORPO INCONSCIENTE
//*****************************************************************************
[MENU m_corpse]
                -=Corpo Inconsciente=-
on=0 cortar garganta (perde 2 pontos de alma)
 morte_decrementaPontos 2
 morte_corta <ctag.morte_corpse>
 return 1

on=0 roubar o corpo (perde 1 ponto de alma)
 src.morte_decrementaPontos 1
 uid.<ctag.morte_corpse>.open
 return 1

on=0 arrastar o corpo
 morte_arrastar <ctag.morte_corpse>
 return 1

//*****************************************************************************
// CORPO MORTO
//*****************************************************************************
[MENU m_corpse_morto]
                   -=Corpo Morto=-
on=0 roubar o corpo (perde 1 ponto de alma)
 morte_decrementaPontos 1
 uid.<ctag.morte_corpse>.open
 return 1

on=0 arrastar o corpo
 morte_arrastar <ctag.morte_corpse>
 return 1

//*****************************************************************************
// DESMAIO
//*****************************************************************************
[DIALOG d_desmaio]
175, 125
noclose
PAGE 0

if (<tag0.morte_desmaios>==1)
 LOCAL.l1=Esta e a primeira vez que isso
 LOCAL.l2=lhe aconteceu. Logo estara acordado
 LOCAL.l3=novamente.
elif (<tag0.morte_desmaios>==2)
 LOCAL.l1=Esta e a segunda vez que isso
 LOCAL.l2=lhe aconteceu. Em algum tempo voce
 LOCAL.l3=podera voltar a andar.
elif (<tag0.morte_desmaios>==3)
 LOCAL.l1=Esta e a terceira vez que isso
 LOCAL.l2=lhe aconteceu. Voce precisa de 
 LOCAL.l3=cuidados medicos ou morrera.
else
 LOCAL.l1=Voce precisa de cuidados 
 LOCAL.l2=medicos ou morrera
 LOCAL.l3= logo.
endif

gumppic 0 0 2080
gumppic 17 37 2081
gumppic 17 107 2082
gumppic 18 177 2083
text 73 33 32 0
text 76 52 32 1
dtext 42 80 1152 <LOCAL.l1>
dtext 28 99 1152 <LOCAL.l2>
dtext 29 118 1152 <LOCAL.l3>
button 118 145 2311 2312 1 0 0
text 123 5 152 2

[DIALOG d_desmaio TEXT]
Voce caiu no chao e
perdeu a consciencia.
(MyT)

[DIALOG d_desmaio BUTTON]

//*****************************************************************************
// CORPO
//*****************************************************************************
[DIALOG d_corpse]
100,75
page 0
obj=<ctag.morte_corpse>
if (<IsEmpty <tag.uid_<obj.link>_name>>)
 local.text= Est<obj.link.sex e/a> <obj.link.tag.raca> esta
else
 local.text= <tag.uid_<obj.link>_name> esta
endif
if (<obj.link.restest i_mry_desmaio>)
 local.text .= " desmaiado."
else
 local.text .= " morto."
endif
resizepic 50 31 2620 500 298
checkertrans 55 38 490 283
gumppic 0 0 10400
gumppic 0 200 10402
gumppic 518 0 10410
gumppic 518 200 10412
gumppic 225 45 2501
dtext 231 45 26 Mystical Tales Shard
resizepic 92 87 2620 416 200
dhtmlgump 106 102 387 77 1 1 <local.text>
dorigin 100 166

button +2 *20 2224 2223 1 0 1
dtext +26 -4 166 Arrastar o corpo

if (<obj.link.restest i_mry_desmaio>)
 button +2 *20 2224 2223 1 0 2
 if (<ctag0.rp_grade> > 6)
  dtext +26 -4 166 Cortar garganta
 else
  dtext +26 -4 166 Cortar garganta (custa 1 ponto de alma)
 endif
endif

button +2 *20 2224 2223 1 0 3
if (<ctag0.rp_grade> > 7)
 dtext +26 -4 166 Roubar o corpo
else
 dtext +26 -4 166 Roubar o corpo (custa 2 ponto de alma)
endif


if (<Forensics> > 0)
 button +2 *20 2224 2223 1 0 4
 dtext +26 -4 166 Fazer analize forence
endif

[DIALOG d_corpse text]

[DIALOG d_corpse button]
ON=1
// Arrastar corpo
 morte_arrastar <ctag.morte_corpse>
 return 1

ON=2
// Cortar garganta
 if (<ctag.rp_grade> <= 6)
  morte_decrementaPontos 1
 endif
 trysrc <uid> morte_corta <ctag.morte_corpse>
 return 1

ON=3
// Roubar corpo
 if (<ctag.rp_grade> <= 7)
  morte_decrementaPontos 2
 endif
 obj=<ctag.morte_corpse>
 trysrc <uid> obj.open
 //uid.<ctag.morte_corpse>.open
 return 1
 
ON=4
// Forensics
sysmessagered Nao implementado.
return 1

[EOF]
