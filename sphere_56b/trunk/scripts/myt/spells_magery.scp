//###################################################################################
//                         Mystical Tales Shard
//                      MAGIAS DE CAMINHO "Destruição"
//                          Galthar, o Errante
//                                04/2009
//###################################################################################
//
//                              RANGE: 100 a 150
//
//###################################################################################

[comment spells_magery]
nº      Ataque
100     Dardos de fogo
104     Fireball
106     Parede de fogo
108     Combustão
110     Cela de fogo
112     Flamestrike
116     Explosão
118     Chuva de meteoros
        
        Buff
107     Chama Mística
105     Cauterizar
109     Tocha mágica
        
        Debuff
113     Esquentar arma
103     Sensibilidade ao fogo
        
        Summon
114     Horror do abismo
117     Homem Lagarto
119     Elemental do fogo
        
        Poly
111     Fogo Fátuo
        
        RP
115     Transformar em cobra
102     Quimadura
101     Fogos de artifício


Arc     Magia
Ben     Bem
Cae     Arma
Dart    Ódio
Eke     Para (motivo)
Fex     Criar
Grov    Madeira, floresta, natureza
Hag     Negar
Ixen    Fogo
Jax     Muro
Kot     Demonio
Lit     Armadura
Maj     Dar, conseder
Noch    Ganho, dinehiro, abôno
Oka     Casa
Pok     Parar
Qarac   Ultimo, anterior
Rhy     Bife, carne
Sve     Raio, trovão
Tur     Grande
Ukris   Dizer, falar, comandar
Val     Queimar
Wux     Você
Xin     Pequeno
Yesh    Conjurar, chamar
Zyak    Consequencia

//***********************************************************
//      SPELL fire darts
//***********************************************************

[ITEMDEF i_scroll_firedarts]
ID=01f2d
NAME=Pergaminho Dardos de Fogo
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 20.0, MAGERY 10.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Dardos de fogo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=100

[Spell 100]
DEFNAME=s_firedarts
NAME=Dardos de fogo
SOUND=054
RUNES=IJ
CAST_TIME=8
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa
RUNE_ITEM=2244
SCROLL_ITEM=i_scroll_firedarts
FLAGS=SPELLFLAG_HARM | spellflag_targ_char | spellflag_scripted | spellflag_eval_boosts | spellflag_dir_anim | spellflag_fx_bolt | spellflag_fx_targ
EFFECT_ID=i_fx_fireball_small
EFFECT=3,12
MANAUSE=4
SKILLREQ=MAGERY 10.0
INTERRUPT=25.0,100.0

on=@Effect
EFFECT 0 i_fx_fireball_small 6 16 1
DAMAGESPELL s_firedarts,dam_fire,<argn2>,<src.uid>
return 0

//***********************************************************
//      SPELL FIRE Fireworks
//***********************************************************
[ITEMDEF i_scroll_fireworks]
ID=01f2d
NAME=Pergaminho fogos de artificio
RESOURCES=i_carta,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 15.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Fogos de artificio

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=101

[Spell 101]
DEFNAME=s_fireworks
NAME=Fogos de artificio
SOUND=
RUNES=ABS
CAST_TIME=30
RESOURCES=i_r_cinza_sulfurosa
RUNE_ITEM=2282
SCROLL_ITEM=i_scroll_fireworks
FLAGS=spellflag_scripted
EFFECT_ID=
EFFECT=3,20
MANAUSE=10
SKILLREQ=MAGERY 10.0
INTERRUPT=100.0,100.0

on=@start
sfx 0357

on=@Success
f_Fireworks <EFFECTSPELL s_fireworks,<magery>,<uid>>

[FUNCTION f_Fireworks]
IF (!<argn>)
 return 1
ENDIF
local.item={i_fx_bless_effect 1 i_fx_sparkle 1 i_fx_heal_effect 1 i_fx_sparkle_2 1 i_fx_explode 1 i_fx_explosion_3 1 i_fx_glow_spike 1}
local.sound=<hval {011C 1 011E 1 0308 1 04CF 1}>
newitem <local.item>
new.color=04EC
new.movenear <uid> 3
new.timer <R1,4>
new.attr=02
new.sfx <local.sound>
timerf <R1,<R1,2>>,f_Fireworks,<eval <argn>-1>
new.update

//***********************************************************
//      SPELL FIRE Queimadura
//***********************************************************
[ITEMDEF i_scroll_queimadura]
ID=01f2d
NAME=Pergaminho queimadura
RESOURCES=i_carta,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 15.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Queimadura

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=102

[Spell 102]
DEFNAME=s_queimadura
NAME=Queimadura
SOUND=0359
RUNES=VED
CAST_TIME=5
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
RUNE_ITEM=2267
SCROLL_ITEM=i_scroll_queimadura
FLAGS=spellflag_scripted | spellflag_targ_char | spellflag_harm
EFFECT_ID=
EFFECT=35,50
DURATION=12,30
MANAUSE=3
SKILLREQ=MAGERY 20.0
INTERRUPT=100.0,100.0

on=@Effect
DAMAGE <R1,3>,DAM_FIRE,<src.uid>
EFFECT 3 i_fx_smoke_small 0 16 0 032

//***********************************************************
//      SPELL Fire Weakness
//***********************************************************
[ITEMDEF i_scroll_fire_weakness]
ID=01f2d
NAME=Pergaminho Sensibilidade ao fogo
RESOURCES=i_carta,i_r_teia_de_aranha,i_r_raiz_de_mandrake,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 30.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Sensibilidade ao fogo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=103

[ITEMDEF i_rune_fire_weakness]
NAME=Fire Weakness
ID=020A4
TYPE=t_eq_script
LAYER=layer_special

on=@equip
cont.sysmessagered Voce esta extremamente sensivel ao fogo!
cont.tag0.aggravate |= <DEF.DAM_FIRE>


on=@unequip
cont.sysmessageblue Sua sensibilidade ao fogo passou.
cont.sfx 0208
cont.tag0.aggravate &= ~<DEF.DAM_FIRE>

on=@timer
remove
return 1

[Spell 103]
DEFNAME=s_fire_weakness
NAME=Sensibilidade ao fogo
RUNES=YDV
CAST_TIME=15
SOUND=0349
RESOURCES=i_r_teia_de_aranha,i_r_raiz_de_mandrake,i_r_cinza_sulfurosa
RUNE_ITEM=2283
FLAGS=spellflag_targ_char | spellflag_scripted | spellflag_eval_boosts | spellflag_harm | spellflag_scripted
SCROLL_ITEM=i_rune_fire_weakness
RESOURCES=i_r_teia_de_aranha,i_r_raiz_de_mandrake,i_r_asa_morcego,i_r_cinza_sulfurosa
EFFECT_ID=0
EFFECT=0,66
DURATION=1,45
MANAUSE=48
SKILLREQ=MAGERY 20.0
INTERRUPT=20.0,100.0

on=@EFFECT
IF (<tag0.aggravate>&DAM_FIRE) //Já tem dano agravado por fogo?
 if (<restest i_rune_fire_weakness>) //O que agrava o dano é essa magia?
  findid.i_rune_fire_weakness.timerd 1 //Então cancele a magia e fim.
 endif //Se ele já tiver agravamento por fogo, pode ser natural ou ter outra fonte. Abortar.
ELIF (<tag0.immune>&DAM_FIRE) || (<tag0.regendam>&DAM_FIRE) //Ele pode sofrer agravante por fogo?
 src.sysmessagered <name> eh imune a fogo!
ELSE //Ele ainda não tem agravamento de dano para fogo, não é imune e nem se regenera com fogo.
 newitem i_rune_fire_weakness
 trysrc <uid> src.new.equip
 new.timer=<DURATIONSPELL 103,<argn2>,<src.uid>>
 effect 3 i_fx_curse 0 33 0
ENDIF
return 1


//***********************************************************
//      SPELL Fireball
//***********************************************************
[ITEMDEF i_scroll_fire_ball]
ID=01f2d
NAME=Pergaminho Bola de Fogo
RESOURCES=i_carta,i_r_raiz_de_mandrake,i_r_perola_negra,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 45.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Bola de fogo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=104

[Spell 104]
DEFNAME=s_fire_ball
NAME=Bola de fogo
SOUND=snd_SPELL_FIREBALL
RUNES=VI
CAST_TIME=17
RESOURCES=i_r_raiz_de_mandrake,i_r_perola_negra,i_r_cinza_sulfurosa
RUNE_ITEM=2257
SCROLL_ITEM=i_scroll_FIRE_BALL
FLAGS=SPELLFLAG_TARG_OBJ | SPELLFLAG_DIR_ANIM | SPELLFLAG_HARM | SPELLFLAG_EVAL_BOOSTS
EFFECT=12,22
MANAUSE=9
SKILLREQ=MAGERY 30.0
INTERRUPT=100.0,100.0

on=@Effect
EFFECT 0 i_fx_fireball 6 16 1
DAMAGESPELL s_fireball,dam_fire,<src.magery>,<src.uid>
return 0

//***********************************************************
//      SPELL Cauterizar
//***********************************************************
[ITEMDEF i_scroll_cauterizar]
ID=01f2d
NAME=Pergaminho Cauterizar
RESOURCES=i_carta,i_r_ginseng,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Cauterizar

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=105

[Spell 105]
DEFNAME=s_cauterizar
NAME=Cauterizar
SOUND=0356
RUNES=MXG
CAST_TIME=30
RESOURCES=i_r_ginseng,i_r_cinza_sulfurosa
RUNE_ITEM=2273
SCROLL_ITEM=i_scroll_cauterizar
FLAGS=SPELLFLAG_TARG_CHAR | SPELLFLAG_GOOD | spellflag_eval_boosts
EFFECT_ID=0
EFFECT=8,18
DURATION=0.0
MANAUSE=4
SKILLREQ=MAGERY 30.0
INTERRUPT=100.0,100.0

on=@start
if (<src.act.distance <UID>> > 1)
 sysmessageyellow Muito longe...
 return 1
ELIF (<src.act.hits> > <eval <src.act.maxhits>/2>)
 sysmessageyellow Seu alvo nao precisa de cauterizacao...
 return 1
endif
sfx 0227

on=@Effect
EFFECT 3 i_fx_smoke_small 0 64 0
hits += <EFFECTSPELL s_cauterizar,<src.magery>>
cry
return 1

//***********************************************************
//      SPELL Fire Wall
//***********************************************************

[ITEMDEF i_scroll_fire_wall]
ID=01f2d
NAME=Pergaminho Parede de fogo
RESOURCES=i_carta,i_r_teia_de_aranha,i_r_asa_morcego,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Parede de fogo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=106

[Spell 106]
DEFNAME=s_parede_fogo
NAME=Parede de fogo
RUNES=FJI
CAST_TIME=30
RUNE_ITEM=2267
SCROLL_ITEM=i_scroll_fire_wall
RESOURCES=i_r_teia_de_aranha,i_r_asa_morcego,i_r_cinza_sulfurosa
FLAGS=SPELLFLAG_TARG_XYZ | SPELLFLAG_HARM | SPELLFLAG_FIELD| SPELLFLAG_RESIST | spellflag_scripted
EFFECT_ID=0
DURATION=6,25
EFFECT=1,7
MANAUSE=22
SKILLREQ=MAGERY 40.0
INTERRUPT=25.0,100.0

on=@start
sfx snd_spell_fire_field

on=@Success
if (<belltest <magery>,100.0>) && !(<R0,3>)
 LOCAL.CREATEOBJECT1 = <eval i_fire_column>
 LOCAL.CREATEOBJECT2 = <eval i_fire_column>
else
 LOCAL.CREATEOBJECT1 = <eval i_fx_field_fire>
 LOCAL.CREATEOBJECT2 = <eval i_fx_field_fire_ns>
endif
call f_line_field <EFFECTSPELL 106,<argn2>,<src.uid>>,<DURATIONSPELL 106,<argn2>,<src.uid>>
return 0


//***********************************************************
//      SPELL Mystic Flame
//***********************************************************
[ITEMDEF i_scroll_mysticFlame]
ID=01f2d
NAME=Pergaminho Chama Mistica
RESOURCES=i_carta,i_r_asa_morcego,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Chama Mistica

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=107

[Spell 107]
DEFNAME=s_mysticFlame
NAME=Chama mistica
SOUND=0
RUNES=VC
CAST_TIME=35
RESOURCES=i_r_asa_morcego,i_r_cinza_sulfurosa
RUNE_ITEM=21015
SCROLL_ITEM=i_scroll_mysticFlame
FLAGS=SPELLFLAG_TARG_ITEM | spellflag_good | spellflag_eval_boosts
EFFECT_ID=0
EFFECT=2,10
DURATION=2,60
MANAUSE=40
SKILLREQ=MAGERY 60.0
INTERRUPT=100.0,100.0

on=@start
sfx 05c1

on=@Effect
if (!<IsWeapon <UID>>) || !(STRMATCH(*ingot*,<serv.itemdef.<baseid>.resources>))
 src.sysmessageyellow Apenas armas de metal.
 return 1
ELIF (<more2>)
 src.sysmessageyellow Esta arma ja possui um tratamento especial
 return 1
ELIF (<src.distance <topobj>> > 1)
 src.sysmessageyellow Muito longe...
 return 1
endif
src.sfx 0356
f_mysticFlame <EFFECTSPELL s_mysticFlame,<src.magery>>

[FUNCTION f_mysticFlame]
tag.hcolor=<color>
more2=1
morex=51
dmgfire += <SRC.EFFECTSPELL s_mysticflame, <src.magery>, <src.uid>>
color=04ec
attr=<attr>|020
timerf <SRC.DURATIONSPELL s_mysticflame, <src.magery>, <src.uid>>,_f_mysticFlame,<dmgfire>
updatex

[FUNCTION _f_mysticFlame]
color=<tag.hcolor>
tag.hcolor
more2=0
morex=0
dmgfire -= <argn>
attr &= ~020
sfx 0208
updatex

//***********************************************************
//      SPELL Combustion
//***********************************************************
[ITEMDEF i_scroll_combustion]
ID=01f2d
NAME=Pergaminho Ccombustao
RESOURCES=i_carta,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 60.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Combustao

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=108

[Spell 108]
DEFNAME=s_combustion
NAME=Combustao
SOUND=snd_SPELL_FIRE_BOLT
RUNES=WV
RESOURCES=i_r_cinza_sulfurosa
CAST_TIME=9
RUNE_ITEM=20736
SCROLL_ITEM=i_scroll_combustion
FLAGS=SPELLFLAG_TARG_CHAR | SPELLFLAG_DIR_ANIM | SPELLFLAG_HARM 
EFFECT_ID=0
EFFECT=5,12
DURATION=5,12
MANAUSE=5
SKILLREQ=MAGERY 50.0
INTERRUPT=25.0,100.0

on=@start
if (<src.act.distance <UID>> > 3)
 sysmessageyellow Muito longe...
 return 1
endif

on=@Effect
f_pega_fogo <EFFECTSPELL s_combustion,<src.magery>,<src.uid>>

//***********************************************************
//      SPELL MAGIC TORCH
//***********************************************************
[ITEMDEF i_scroll_magictorch]
ID=01f2d
NAME=Pergaminho Tocha Magica
RESOURCES=i_carta,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 45.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Tocha Magica

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=109

[Spell 109]
DEFNAME=s_magictorch
NAME=Tocha magica
SOUND=054
RUNES=FI
CAST_TIME=45
RESOURCES=i_r_cinza_sulfurosa
RUNE_ITEM=2245
SCROLL_ITEM=i_scroll_magictorch
FLAGS=SPELLFLAG_GOOD | spellflag_scripted
EFFECT_ID=0
DURATION=5,300
MANAUSE=21
SKILLREQ=MAGERY 50.0
INTERRUPT=100.0,100.0

on=@start
sfx 05c1

on=@Effect
EFFECT 3 i_fx_sparkle_2 0 16 0 032
serv.newitem i_torch_lit
new.color 044eb
src.equip <new>
new.attr=08030
local.t=<DURATIONSPELL s_magicTorch,<argn2>>
new.timerf <LOCAL.t>,remove
src.timerf <local.t>,sfx,04b8
return 0


//***********************************************************
//      SPELL FIRE CELL
//***********************************************************
[ITEMDEF i_scroll_fire_cell]
ID=01f2d
NAME=Pergaminho Cela do Fogo
RESOURCES=i_carta,i_r_teia_de_aranha,i_r_raiz_de_mandrake,i_r_asa_morcego,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Cela de fogo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=110

[Spell 110]
DEFNAME=s_CelaFogo
NAME=Cela de Fogo
SOUND=snd_SPELL_FIRE_FIELD
RESOURCES=i_r_teia_de_aranha,i_r_raiz_de_mandrake,i_r_asa_morcego,i_r_cinza_sulfurosa
RUNES=FXOI
CAST_TIME=50
RUNE_ITEM=2267
SCROLL_ITEM=i_scroll_fire_cell
FLAGS=SPELLFLAG_HARM | spellflag_scripted | spellflag_targ_char
EFFECT_ID=0
DURATION=5,12
MANAUSE=30
SKILLREQ=MAGERY 60.0
INTERRUPT=100.0,100.0

on=@Effect
f_cela_fogo <DURATIONSPELL s_CelaFogo,<src.Magery>>
return 0


//***********************************************************
//      SPELL FIRE Polymorph Fogo Fatuo
//***********************************************************
[ITEMDEF i_scroll_poly_fogo_fatuo]
ID=01f2d
NAME=Pergaminho Forma de Fogo Fatuo
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
SKILLMAKE=INSCRIPTION 25.0, MAGERY 65.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Poly Fogo Fatuo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=111

[Spell 111]
DEFNAME=s_polymorph_fogo_fatuo
NAME=Forma de Fogo Fatuo
SOUND=snd_spell_fire_field
RUNES=WIHR
CAST_TIME=40
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
RUNE_ITEM=2280
SCROLL_ITEM=i_scroll_poly_fogo_fatuo
FLAGS=spellflag_scripted | spellflag_playeronly
EFFECT_ID=
EFFECT=35,50
DURATION=12,90
MANAUSE=53
SKILLREQ=MAGERY 60.0
INTERRUPT=100.0,100.0

on=@start
sfx snd_spell_fire_field

on=@Success
LOCAL.timer=<DURATIONSPELL s_polymorph_fogo_fatuo,<magery>,<UID>>
src.f_polymorph c_wisp,04EC,<LOCAL.timer>
src.Buff_STR <MAXIMUM <eval 30-<str>>,-30>,<LOCAL.timer>
src.Buff_DEX 50,<LOCAL.timer>

on=@Effect
//Inibe criação de RUNE_ITEM
return 0


//***********************************************************
//      SPELL FIRE Flamestrike
//***********************************************************
[ITEMDEF i_scroll_flame_strike]
ID=01f2d
NAME=Pergaminho Jato de Chamas
RESOURCES=i_carta,i_r_musgo_sangue,i_r_nightshade,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Jato de chamas

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=112

[Spell 112]
DEFNAME=s_flame_strike
NAME=Jato de chamas
SOUND=snd_SPELL_FLAMESTRIKE
RUNES=TV
CAST_TIME=23
RESOURCES=i_r_musgo_sangue,i_r_nightshade,i_r_cinza_sulfurosa
RUNE_ITEM=2290
SCROLL_ITEM=i_scroll_flame_strike
FLAGS=SPELLFLAG_TARG_OBJ|SPELLFLAG_HARM| SPELLFLAG_FX_TARG| SPELLFLAG_RESIST | spellflag_eval_boosts
EFFECT_ID=03709
EFFECT=10,35
DURATION=0.0
MANAUSE=12
SKILLREQ=MAGERY 70.0
INTERRUPT=100.0,50.0

on=@Effect
EFFECT 3 i_fire_column 0 16 0
DAMAGE <EFFECTSPELL 112,<argn2>,<src.uid>>,dam_fire,<src.uid>
return 0



//***********************************************************
//      SPELL Heat Weapon
//***********************************************************
[ITEMDEF i_scroll_heatWeapon]
ID=01f2d
NAME=Pergaminho Esquentar arma
RESOURCES=i_carta,i_r_olho_morto_vivo,i_r_perola_negra,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 62.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Esquentar arma

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=113

[Spell 113]
DEFNAME=s_heatWeapon
NAME=Esquentar arma
SOUND=snd_SPELL_FIREBALL
RUNES=HWC
CAST_TIME=25
RESOURCES=i_r_raiz_de_mandrake,i_r_teia_de_aranha,i_r_cinza_sulfurosa
RUNE_ITEM=23002
SCROLL_ITEM=i_scroll_heatWeapon
FLAGS=SPELLFLAG_TARG_OBJ | SPELLFLAG_HARM | SPELLFLAG_RESIST
EFFECT_ID=i_fire
EFFECT=3,12
DURATION=0.0
MANAUSE=30
SKILLREQ=MAGERY 70.0
INTERRUPT=50.0,100.0

on=@Effect
EFFECT 3 i_fire 0 16 0
DAMAGESPELL s_heatWeapon,dam_fire,<argn2>,<src.uid>
if (<weapon>)
 if (STRMATCH(*ingot*,<weapon.resources>)) && !(<weapon.attr>&08d10)
  f_heatWeapon <eval <EFFECTSPELL s_heatWeapon,<argn2>>*3>
  sysmessagered Sua arma esta muito quente para segurar!
 else
  src.sysmessagered A arma do seu oponente nao esquenta!
 endif
else
 src.sysmessagered Seu oponente nao esta armado!
endif
return 1

[FUNCTION f_heatWeapon]
obj=<weapon>
obj.tag.hcolor=<obj.color>
obj.tag.hname=<obj.name>
obj.name=<obj.name> (quente)
obj.color=04ec
obj.attr=<obj.attr>|010
obj.p=<topobj.p>
obj.timerf <argn>,_f_heatWeapon
obj.timer -1

[FUNCTION _f_heatWeapon]
color=<tag.hcolor>
tag.hcolor
name=<tag.hname>
tag.hname
attr &= ~010
attr |= 2
timer=600
emotered esfriou



//***********************************************************
//      SPELL FIRE Summon Horror do Abismo
//***********************************************************
[ITEMDEF i_scroll_horror_abismo]
ID=01f2d
NAME=Pergaminho conjurar horror d'abismo
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue,i_r_raiz_de_mandrake,i_r_teia_de_aranha
SKILLMAKE=INSCRIPTION 25.0, MAGERY 90.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Horror do Abismo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=114

[Spell 114]
DEFNAME=s_horror_abismo
NAME=Conjurar Horror d'Abismo
SOUND=
RUNES=YXK
CAST_TIME=20
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue,i_r_raiz_de_mandrake,i_r_teia_de_aranha
RUNE_ITEM=23008
SCROLL_ITEM=i_scroll_horror_abismo
FLAGS=spellflag_targ_xyz | spellflag_scripted | spellflag_eval_boosts
EFFECT_ID=0
EFFECT=35,50
DURATION=12,90
MANAUSE=30
SKILLREQ=MAGERY 80.0
INTERRUPT=100.0,100.0

on=@start
sfx snd_spell_fire_field

on=@Success
LOCAL.timer=<EFFECTSPELL s_horror_abismo,<magery>,<UID>>
obj=<f_summon_creature c_horror_abismo,<LOCAL.timer>>
obj.p=<targp>
obj.update


//***********************************************************
//      SPELL FIRE Polymorph Cobra
//***********************************************************
[ITEMDEF i_scroll_poly_cobra]
ID=01f2d
NAME=Pergaminho forma de cobra
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
SKILLMAKE=INSCRIPTION 25.0, MAGERY 85.0
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Poly Cobra

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=115

[Spell 115]
DEFNAME=s_polymorph_cobra
NAME=Forma de Cobra
SOUND=snd_spell_fire_field
RUNES=WXR
CAST_TIME=60
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
RUNE_ITEM=21019
SCROLL_ITEM=i_scroll_poly_cobra
FLAGS=spellflag_scripted | spellflag_targ_char | spellflag_harm | spellflag_eval_boosts
EFFECT_ID=
EFFECT=35,50
DURATION=12,30
MANAUSE=60
SKILLREQ=MAGERY 80.0
INTERRUPT=100.0,100.0

on=@start
sfx snd_spell_fire_field

on=@Effect
LOCAL.timer=<DURATIONSPELL s_polymorph_cobra,<src.magery>,<src.UID>>
f_polymorph c_snake,colors_green,<LOCAL.timer>


//***********************************************************
//      SPELL FIRE Explosion
//***********************************************************
[ITEMDEF i_scroll_explosao]
ID=01f2d
NAME=Pergaminho explosao
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Jato de chamas

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=116

[Spell 116]
DEFNAME=s_explosao
NAME=Explosao
SOUND=snd_SPELL_EXPLOSION
RUNES=VOF
CAST_TIME=33
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue
RUNE_ITEM=2282
SCROLL_ITEM=i_scroll_EXPLOSION
FLAGS=SPELLFLAG_TARG_CHAR |SPELLFLAG_DIR_ANIM| SPELLFLAG_HARM |SPELLFLAG_FX_TARG| SPELLFLAG_RESIST | SPELLFLAG_SCRIPTED
EFFECT_ID=i_fx_explode
EFFECT=25,40
DURATION=0.0
MANAUSE=18
SKILLREQ=MAGERY 90.0
INTERRUPT=100.0,100.0

ON=@EFFECT
local.dam=<EFFECTSPELL 116,<argn2>,<src.uid>>
obj=<src.uid>
ref1=<uid>
damage <local.dam>,dam_fire,<obj>
effect 3 i_fx_explosion_2 0 16 0
forchars <eval <local.dam>/20>
 if (<uid>!=<obj>) && (<uid>!=<ref1>)
  damage <eval <local.dam>/<distance <ref1>>>,dam_fire,<obj>
  effect 3 i_fx_explosion_3 0 16 0
 endif
end
sfx snd_SPELL_EXPLOSION
return 0



//***********************************************************
//      SPELL FIRE Summon Homem Lagarto
//***********************************************************
[ITEMDEF i_scroll_homem_lagarto]
ID=01f2d
NAME=Pergaminho conjurar homem lagarto
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue,i_r_raiz_de_mandrake,i_r_teia_de_aranha
SKILLMAKE=INSCRIPTION 25.0, MAGERY 110.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Homem Lagarto

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=117

[Spell 117]
DEFNAME=s_homem_lagarto
NAME=Conjurar Homem Lagarto
SOUND=
RUNES=YAR
CAST_TIME=40
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue,i_r_raiz_de_mandrake,i_r_teia_de_aranha
RUNE_ITEM=2302
SCROLL_ITEM=i_scroll_homem_lagarto
FLAGS=spellflag_targ_xyz | spellflag_scripted | spellflag_eval_boosts
EFFECT_ID=0
EFFECT=35,50
DURATION=12,90
MANAUSE=60
SKILLREQ=MAGERY 90.0
INTERRUPT=100.0,100.0

on=@start
sfx snd_spell_fire_field

on=@Success
say Olaaaa!
LOCAL.timer=<DURATIONSPELL s_homem_lagarto,<magery>,<UID>>
obj=<src.f_summon_creature <hval {c_lizardman 3 c_lizardman_spear 2 c_lizardman_mace 1}>,<LOCAL.timer>>
obj.p=<targp>
obj.update
return 0


//***********************************************************
//      SPELL FIRE Meteor Shower
//***********************************************************
[ITEMDEF i_scroll_chuva_meteoros]
ID=01f2d
NAME=Pergaminho chuva de meteoros
RESOURCES=i_carta,i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue,i_r_asa_morcego,i_r_teia_de_aranha
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Chuva de Meteoros

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=118

[ITEMDEF i_rune_chuva_meteoros]
NAME=chuva de meteoros
ID=i_gold
TYPE=T_EXPLOSION

//act.morex=10 // damage
//act.morey=090 // wFlags DAMAGE_GENERAL
//act.morez=4 // iDist

on=@create
attr=04092

on=@timer
newitem <hval {01364 0136b}>
new.color=04ec
new.name=meteorito
new.attr=012
new.timer <R10,65>
new.p=<p>
new.effect 2 i_fire_column 0 16 0
new.effect 2 i_fire 0 3000 0
sfx {011b 1 011D 1 0306 1 0207 1}
remove
return 0


[Spell 118]
// NOTE: Does Meteor Swarm have a duration or does it just send out one blast of 'rocks' ??? IF a duration then certainly no more the 30secs IMO.
DEFNAME=s_chuva_meteoros
NAME=Chuva de Meteoros
SOUND=011b
RUNES=FKDY
CAST_TIME=70
RESOURCES=i_r_perola_negra,i_r_cinza_sulfurosa,i_r_musgo_sangue,i_r_asa_morcego,i_r_teia_de_aranha
RUNE_ITEM=2294
SCROLL_ITEM=i_scroll_chuva_meteoros
FLAGS=SPELLFLAG_TARG_XYZ | SPELLFLAG_HARM | spellflag_scripted
EFFECT_ID=0
EFFECT=15,38
DURATION=0.0
MANAUSE=37
SKILLREQ=MAGERY 100.0
INTERRUPT=100.0,100.0

on=@Start
if (<src.flags>&statf_indoors) || (<region.flags>&region_flag_underground)
 src.sysmessageyellow Impossivel em lugares cobertos
 return 1
endif

on=@Success
src.newitem i_rune_chuva_meteoros
new.p=<src.actp>
new.f_spl_chuva_meteoros <argn2>

[FUNCTION f_spl_chuva_meteoros]
local.rad=<eval <EFFECTSPELL 118,<argn1>,<src.uid>>/6>
local.dam=<EFFECTSPELL 118,<argn1>,<src.uid>>
morex=<local.dam>
morey=dam_fire
morez=3
timerd <R2,6>
src.effect 4 2
local.ii=<eval <EFFECTSPELL 118,<argn1>,<src.uid>>/5>
for i 1 <local.ii>
 newitem <baseid>
 new.morex <R<eval <local.dam>/2>,<local.dam>>
 new.morey=dam_fire
 new.morez=<R2,3>
 new.link=<src>
 new.movenear <uid>,<R1,<local.rad>>
 new.timerd <R2,<R10,32>>
endif


//***********************************************************
//      SPELL Summon Fire Elemental
//***********************************************************
[ITEMDEF i_scroll_summon_elem_fire]
ID=01f2d
NAME=Pergaminho Conjurar Elemental do Fogo
RESOURCES=i_carta,i_r_teia_de_aranha,i_r_asa_morcego,i_r_cinza_sulfurosa
SKILLMAKE=INSCRIPTION 25.0, MAGERY 72.5
TYPE=T_SCROLL
WEIGHT=1
//CIRCLE=1
CATEGORY=MyT - Provisions - Alchemy & Magic
SUBSECTION=Scrolls - Magery
DESCRIPTION=Elemntal do fogo

ON=@Create
        ATTR=ATTR_MAGIC
        MOREX=119

[Spell 119]
DEFNAME=s_summon_elem_fire
NAME=Conjurar Elem. do Fogo
RUNES=YDV
CAST_TIME=65
RESOURCES=i_r_teia_de_aranha,i_r_asa_morcego,i_r_cinza_sulfurosa
RUNE_ITEM=2302
FLAGS=spellflag_targ_xyz | spellflag_scripted | spellflag_eval_boosts
SCROLL_ITEM=i_scroll_summon_elem_fire
RESOURCES=i_r_teia_de_aranha,i_r_raiz_de_mandrake,i_r_asa_morcego,i_r_cinza_sulfurosa
EFFECT_ID=0
EFFECT=12,90
MANAUSE=64
SKILLREQ=MAGERY 100.0
INTERRUPT=25.0,100.0

on=@start
sfx snd_spell_fire_field

on=@Effect
LOCAL.timer=<EFFECTSPELL s_summon_elem_fire,<magery>,<UID>>
obj=<f_summon_creature c_elem_fire,<LOCAL.timer>>
obj.p=<targp>
obj.update
return 1







[EOF]
