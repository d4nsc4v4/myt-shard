//*****************************************************************************
//**                        Sistema de plantação v1.6                        **
//**                          Mystical Tales Shard                           **
//*****************************************************************************

//Tabela de valor MORE2 contido em cada objeto de semente para cada fruto a ser colhido.
//
//  MORE2       FRUTO       ITEMDEF
//
//  0001        batata      i_sem_batata
//  0002        milho       i_sem_milho
//  0003        ervilha     i_sem_ervilha
//  0004        trigo       i_sem_trigo
//  0005        cenoura     i_sem_cenoura
//  0006        alface      i_sem_alface
//  0007        ginsen      i_sem_ginseng
//  0008        mandrake    i_sem_mandrake
//  0009        alho        i_sem_alho
//  0010        nightshade  i_sem_nightshade
//  0011        macieira    i_sem_maca
//  0012        parreira    i_sem_uva
//  0013        bananeira   i_sem_banana
//  0014        algodoeiro  i_sem_algodao

//*****************************************************************************
//**                        Configurações do sistema                         **
//*****************************************************************************

//####################### Mude conforme a necessidade #########################

[DEFNAME Config_de_plantação]
// configurações gerais. Altere conforme necessário.

tempo_de_plantacao=60*60
//Tempo total de crescimento de uma planta para que ela se torne madura.
//No caso das árvores perenes, o valor será triplicado automaticamente.

chance_colher_sementes=50
//Porcentagem de chance que um player tem de retirar novas sementes quando 
//terminam os frutos da planta.

tempo_de_decay_planta=15
//Tempo em segundos entre a colheita do ultimo fruto e a 'morte' da planta.
//O player só poderá tentar colher sementes durante este intervalo de tempo.

ferramenta_para_colher=3921
//DispID Decimal da ferramente que deve estar na mão do player para ele conseguir colher
//os frutos. Coloque 0 (zero) para desabilitar a necessidade de uma ferramenta.

//*****************************************************************************
//**                                TYPEDEF                                  **
//*****************************************************************************

[TYPEDEF t_seed]
ON=@CLICK
MESSAGE <name> (<more1>)
return 1

ON=@CLIENTTOOLTIP
f_sendTooltip <name>,Quantidade: <eval <more1>>
return 1

ON=@dclick
target Selecione a terra arada:
return 1

ON=@TARGON_GROUND
TARGET Voce nao pode plantar nisso. Selecione onde plantar:
RETURN 1

ON=@TARGON_CHAR
TARGET Voce nao pode plantar em criaturas. Selecione onde plantar:
RETURN 1

ON=@TARGON_ITEM
if (<src.targ.baseid>==i_terra_arada)
    if (<src.targ.distance> > 1)
        SRC.SYSMESSAGEred Voce esta muito lonje.
        return 1
    endif
    if (<src.targ.link> != 04fffffff)
        SRC.SYSMESSAGEred Esta terra ja esta sendo usada. Use outra.
        return 1
    endif
//-?-QUE TIPO DE SEMENTE É? QUE PLANTA VAI NASCER?
    if (<more2>==0001)
        serv.newitem i_plan_batata1
    elseif (<more2>==0002)
        serv.newitem i_plan_milho1
    elseif (<more2>==0003)
        serv.newitem i_plan_ervilha1
    elseif (<more2>==0004)
        serv.newitem i_plan_trigo1
    elseif (<more2>==0005)
        serv.newitem i_plan_cenoura1
    elseif (<more2>==0006)
        serv.newitem i_plan_alface1
    elseif (<more2>==0007)
        serv.newitem i_plan_ginsen1
    elseif (<more2>==0008)
        serv.newitem i_plan_mandrake1
    elseif (<more2>==0009)
        serv.newitem i_plan_alho1
    elseif (<more2>==0010)
        serv.newitem i_plan_nightshade1
    elseif (<more2>==0011)
        serv.newitem i_plan_maca1
    elseif (<more2>==0012)
        serv.newitem i_plan_uva1
    elseif (<more2>==0013)
        serv.newitem i_plan_banana1
    elseif (<more2>==0014)
        serv.newitem i_plan_algodao1    //-?-PONHA AQUI O NOVOS TIPOS DE PLANTAS.
    else
        //tipo indefinido
        src.sysmessageyellow Erro: Semente inexistente.
        remove
        return 1
    endif
    new.p=<src.targp.x>,<src.targp.y>,<eval <src.targp.z>+1>,<src.targp.m>
    new.link=<src.targ.uid>
    src.targ.link=<new.uid>
    new.timer=<EVAL <DEF.tempo_de_plantacao>/2>
    more1=(<more1>-1)
    update
    src.act=0
    src.f_planta
    if (<more1>==0)
        remove
    endif
    return 1
else
    src.sysmessagered Voce nao pode plantar nisso.
    return 1
endif

[TYPEDEF t_broto]
ON=@TIMER
plant_timerBroto
return 1

ON=@DCLICK
src.sysmessageyellow Voce nao pode colher o broto.
return 1

[TYPEDEF t_planta]
ON=@CLICK
if (!<morex>)
    MESSAGE <name> (morreu)
    return 1
else
    MESSAGE <name> (<eval <morex>>)
    return 1
endif

ON=@CLIENTTOOLTIP
f_sendTooltip <name>,<QVAL <morex> ? <eval <morex>> frutos:morreu>
return 1

ON=@DCLICK
if (<src.distance <P>> > 1)
    src.sysmessageyellow Voce esta muito longe.
    return 1
elif (<src.ctag0.busy> > <serv.time>)
    src.sysmessageyellow Deve esperar antes de tentar novamente.
    return 1
elif (<morex>)
    src.ctag.busy=<eval <serv.time>+20>
    src.f_colhe <tag.fruto>
    if (<morex>==1)
        timer=<def.tempo_de_decay_planta>
    endif
    src.timerf 2 uid.<uid>.use
    morex -= 1
elif (!<isEmpty <tag.semente>>)
    src.f_sementes <tag.semente>
    timer=1
    tag.semente=
endif
update
return 1

ON=@TIMER
if (!<morey>) && (!<morex>)
 EMOTERED morreu
 morey=1
 timer=2
elif (!<morex>)
 if (<LINK>)
  link.more1 -= 1
  if (<link.more1> < 1)
   link.timer 2
  endif
  link.link=-1
 endif
 remove
endif
return 1

[TYPEDEF t_arvore]

ON=@CREATE
attr=010
timer=<def.tempo_de_plantacao>
more1=0
more2=0

ON=@TIMER
more1=1
more2=<R3,7>
return 1

ON=@DCLICK
if ( <src.targ.distance> > 1)
    src.sysmessageyellow Voce esta muito longe.
    return 1
elif (<src.ctag0.busy> > <serv.time>)
    src.sysmessageyellow Deve esperar antes de tentar novamente.
    return 1
elif (<more2>)
    src.ctag.busy=<eval <serv.time>+10>
    src.f_colhe <tdata1>
    more2 -= 1
    update
else
    src.sysmessageyellow Nao ha frutos nessa arvore.
    if (<more1>==1)
        timer=<def.tempo_de_plantacao>
        more1=0
    endif
endif
return 1



//*****************************************************************************
//**                           Functions e Memorys                           **
//*****************************************************************************


[ITEMDEF i_mry_planta]
ID=I_MEMORY
TYPE=T_EQ_SCRIPT
NAME=Planta

ON=@CREATE
SRC.ACT.EQUIP
TIMER=1

ON=@EQUIP
if (!<cont.isgm>)
    cont.flags |= 000000004
endif
return 1

ON=@DCLICK
SRC.ACT.EQUIP
return 1

ON=@TIMER
    sfx 0145
    cont.anim 12
if (<more1>==5)
    if (!<cont.isgm>)
        cont.flags ^= 000000004
    endif
    remove
    return 1
else
    more1=(<more1>+1)
    TIMER=1
    return 1
endif

[FUNCTION F_PLANTA]
src.newitem=i_mry_planta
src.act.equip
RETURN 1

[FUNCTION f_colhe]
if (<eval <src.findlayer.layer_hand1.id>> == <DEF.ferramenta_para_colher> ) || (<DEF.ferramenta_para_colher>==0)
    src.newitem=<args>
    src.anim 12
    src.act.bounce
    src.sfx 004f
    src.emoteyellow colhendo <src.act.name>
    src.skill_gain SKILL_CAMPING
    return 1
else
    src.sysmessagered Voce nao esta equipado com  ferramenta correta.
    src.emotered estragou um fruto
    return 1
endif


[FUNCTION f_sementes]
if ( chance_colher_sementes >= <r100>)
    if (<eval <src.findlayer.layer_hand1.id>> == <DEF.ferramenta_para_colher> ) || (<DEF.ferramenta_para_colher> == 0)
        serv.newitem=<args>
        src.bounce <new>
        src.anim 12
        src.sfx 004f
        src.emoteyellow colhendo <new.name>
        src.sysmessageyellow Voce tirou algumas novas sementes da planta.
        return 1
    else
        src.sysmessagered Voce nao esta equipado com  ferramenta correta.
        src.skill_gain SKILL_CAMPING
        return 1
    endif
else
    src.anim 12
    src.sfx 004f
    src.sysmessageyellow Voce nao tirou novas sementes da planta.
    return 1
endif

[FUNCTION plant_checkTile]
sysmessageyellow Selecione aonde arar:
TARGETFG _plant_checkTile

[FUNCTION _plant_checkTile]
LOCAL.t <serv.map(<targp.x>,<targp.y>).type>
LOCAL.s <serv.map(<targp.x>,<targp.y>).statics(0).name>
//serv.log <serv.map(<targp.x>,<targp.y>).statics(0).name>/<serv.map(<targp.x>,<targp.y>).statics(0).ID>
IF (<distance <targp>> > 1)
 sysmessageyellow Muito longe.
 return 0
ELIF (<region.flags>&region_flag_nobuilding)
 sysmessageyellow Nao se pode plantar ou construir nessa area por ordens superiores.
 return 0
ELIF (<LOCAL.t>!=t_grass)
 sysmessageyellow Voce so pode fazer isso na grama.
 return 0
ELIF (<region.flags>&region_flag_underground) || (<flags>&statf_indoors)
 sysmessageyellow Aqui nao bate sol. Um vegetal nao pode crescer aqui.
 return 0
ELIF (!<ISNUM <LOCAL.s>>)
 sysmessageyellow Nao eh um bom lugar para plantar.
 return 0
ELIF (<flags>&0c0c4240e)
 sysmessageyellow Voce nao esta em condicoes de trabalhar.
 return 0
ELIF (<ARGO.UID>)
 IF (<argo.baseid>==i_terra_arada)
  sysmessageyellow Esta terra ja foi arada.
  return 0
 ELSE
  sysmessageyellow <argo.name> vai atrapalhar o plantio.
  return 0
 ENDIF
ELSE
 OBJ=<UID>
 FORITEMS 2
  IF (<p.x>==<obj.targp.x>) && (<p.y>==<obj.targp.y>) && (<p.z>-<obj.z> <= 20) && (<p.z>-<obj.z> >= -5)
   obj.sysmessageyellow <name> vai atrapalhar o plantio.
   RETURN 0
  ENDIF
 ENDFOR
ENDIF
plant_arar <targp>
RETURN 1

[FUNCTION plant_arar]
cTAG.cp=<argn1>,<argn2>,<argn3>
cTAG.ctimer=<BETWEEN2 1,10,<camping>,1000>
cTAG.p.x=<p.x>
cTAG.p.y=<p.y>
_plant_arar

[FUNCTION _plant_arar]
if (<p.x>!=<cTAG0.p.x>) || (<p.y>!=<cTAG0.p.y>)
 sysmessagered Voce parou de arar
 return 1
endif
sfx 0145
anim 12
cTAG.ctimer -= 1
IF (<cTAG0.cTIMER>)
 TIMERF 1,_plant_arar
ELSE
 serv.newitem i_terra_arada
 new.p=<cTAG.cp>
// new.attr=010
 new.more1=<BETWEEN 4,10,<camping>,1000>
 if (<ctag0.craft_tool>)
  uid.<ctag0.craft_tool>.hits -= 1
  if (!<uid.<ctag0.craft_tool>.hits>)
   uid.<ctag0.craft_tool>.remove
   sysmessagered Seu arado foi arruinado.
  else
   uid.<ctag0.craft_tool>.update
  endif
  ctag.craft_tool= 
 endif
 timer=-1
ENDIF

[FUNCTION plant_timerBroto]
if (!<IsEmpty <tag.planta>>)
 serv.newitem <tag.planta>
 new.p=<p>
 new.timer=<eval <DEF.tempo_de_plantacao>/2>
 new.link=<link>
 link.link=<new.uid>
 remove
endif

//*****************************************************************************
//**                             Terra e Arado                               **
//*****************************************************************************


[ITEMDEF i_terra_arada]
//Terra para plantar
NAME=terra arada
ID=0914
DEFNAME=i_terra_arada
TYPE=t_script
FLIP=0
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Terra arada

ON=@CREATE
attr=010
more1=<r2,3>
timer=-1

ON=@TIMER
if (<more1> < 1)
 remove
endif
return 1

on=@click
message <name>
if (<src.camping> > 199)
 message [Camping] Mais <eval <more1>> <QVAL <more1> < 2?plantacao:plantacoes>
endif
return 1

ON=@CLIENTTOOLTIP
if (<src.camping> > 199)
 f_sendTooltip <name>,[Camping] Mais <eval <more1>> <QVAL <more1> < 2?plantação:plantações>
endif
return 1

[ITEMDEF i_arado]
//Arado
NAME=arado
ID=01507
DEFNAME=i_arado
RESOURCES=2 i_ingot_iron, 1 i_log
SKILLMAKE=32.0 Tinkering
TYPE=t_crafting_tool
WEIGHT=8
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Arado
TDATA4=60

ON=Create
MAXHITS={10 40}
HITS={5 <MAXHITS>}

ON=@DCLICK
src.plant_checkTile
return 1

//*****************************************************************************
//**                                Sementes                                 **
//*****************************************************************************

//########################### Vegetais Comestíveis#############################


[ITEMDEF i_sem_batata]
NAME=sementes de batata
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de batata

ON=@CREATE
MORE1=<BETWEEN 1,6,<src.camping>,1000>
MORE2=0001 //caracteriza que esta semente é de batata.

[ITEMDEF i_sem_milho]
NAME=sementes de milho
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de milho

ON=@CREATE
MORE1=<BETWEEN 2,6,<src.camping>,1000>
MORE2=0002 //caracteriza que esta semente é de milho.


[ITEMDEF i_sem_ervilha]
NAME=sementes de ervilha
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de ervilha

ON=@CREATE
MORE1=<BETWEEN 2,6,<src.camping>,1000>
MORE2=0003 //caracteriza que esta semente é de ervilha.

[ITEMDEF i_sem_trigo]
NAME=sementes de trigo
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de trigo

ON=@CREATE
MORE1=7
MORE2=0004 //caracteriza que esta semente é de trigo.

[ITEMDEF i_sem_cenoura]
NAME=sementes de cenoura
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de cenoura

ON=@CREATE
MORE1=<BETWEEN 2,6,<src.camping>,1000>
MORE2=0005 //caracteriza que esta semente é de cenoura.

[ITEMDEF i_sem_alface]
NAME=sementes de alface
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de alface

ON=@CREATE
MORE1=<BETWEEN 2,6,<src.camping>,1000>
MORE2=0006 //caracteriza que esta semente é de alface.


//################################ Reagentes ##################################

[ITEMDEF i_sem_ginseng]
NAME=sementes de ginseng
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de Ginseng

ON=@CREATE
MORE1=<BETWEEN 4,8,<src.camping>,1000>
MORE2=0007 //caracteriza que esta semente é de ginsen.

[ITEMDEF i_sem_mandrake]
NAME=sementes de mandragora
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de mandrake

ON=@CREATE
MORE1=<BETWEEN 4,8,<src.camping>,1000>
MORE2=0008 //caracteriza que esta semente é de mandrake root.

[ITEMDEF i_sem_alho]
NAME=sementes de alho
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de alho

ON=@CREATE
MORE1=<BETWEEN 4,8,<src.camping>,1000>
MORE2=0009 //caracteriza que esta semente é de alho.

[ITEMDEF i_sem_nightshade]
NAME=sementes de sombradanoite
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de nightshade

ON=@CREATE
MORE1=<BETWEEN 4,8,<src.camping>,1000>
MORE2=0010 //caracteriza que esta semente é de nightshade.

//########################### arvores persistentes ############################

[ITEMDEF i_sem_maca]
NAME=semente de macieira
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de maçã

ON=@CREATE
MORE1=1
MORE2=0011 //caracteriza que esta semente é de macieira.

[ITEMDEF i_sem_uva]
NAME=semente de parreira
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de uva

ON=@CREATE
MORE1=<BETWEEN 1,6,<src.camping>,1000>
MORE2=0012 //caracteriza que esta semente é de parreira.

[ITEMDEF i_sem_banana]
NAME=semente de bananeira
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de banana

ON=@CREATE
MORE1=<BETWEEN 1,6,<src.camping>,1000>
MORE2=0013 //caracteriza que esta semente é de bananeira.

//################################### Outros #############################


[ITEMDEF i_sem_algodao]
NAME=sementes de pe de algodao
ID=i_bag
TYPE=t_seed
WEIGHT=1
CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=Semente de algodão

ON=@CREATE
MORE1=<BETWEEN 1,6,<src.camping>,1000>
MORE2=0014 //caracteriza que esta semente é de algodao.


//*****************************************************************************
//**                         Plantas e Suas Evoluções                        **
//*****************************************************************************

//############################ Vegetais Comestíveis ###########################

//****batata****

[ITEMDEF i_plan_batata1]
ID=0c5e
TYPE=t_broto
NAME=batata (broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_batata2

[ITEMDEF i_plan_batata2]
ID=0c5f
TYPE=t_broto
NAME=batata (jovem)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_batata3

[ITEMDEF i_plan_batata3]
ID=0c60
TYPE=t_planta
NAME=batata (madura)
// por sections de menu

ON=@CREATE
attr=010
MOREx=<R2,3>
tag.fruto=I_BATATA
tag.semente=i_sem_batata

//****milho****

[ITEMDEF i_plan_milho1]
ID=0cb5
TYPE=t_broto
NAME=milho (broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_milho2

[ITEMDEF i_plan_milho2]
ID=0c7e
TYPE=t_broto
NAME=milho (jovem)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_milho3

[ITEMDEF i_plan_milho3]
ID=0c7d
TYPE=t_planta
NAME=milho (maduro)
// por sections de menu

ON=@CREATE
attr=010
MOREX=1
tag.fruto=I_FRUIT_CORN
tag.semente=i_sem_milho


//****ervilha****

[ITEMDEF i_plan_ervilha1]
ID=0cb5
TYPE=t_broto
NAME=ervilha (broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_ervilha2

[ITEMDEF i_plan_ervilha2]
ID=0cb0
TYPE=t_broto
NAME=ervilha (jovem)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_ervilha3

[ITEMDEF i_plan_ervilha3]
ID=0c8d
TYPE=t_planta
NAME=ervilha (madura)
// por sections de menu

ON=@CREATE
attr=010
MOREX=<R2,3>
tag.fruto=I_ERVILHA
tag.semente=i_sem_ervilha


//****trigo****

[ITEMDEF i_plan_trigo1]
ID=01EBF
TYPE=t_broto
NAME=trigo(broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_trigo2

[ITEMDEF i_plan_trigo2]
ID=0c59
TYPE=t_broto
NAME=trigo (jovem)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_trigo3

ON=@DCLICK
src.sysmessage Voce nao pode colher isso jovem.
return 1

[ITEMDEF i_plan_trigo3]
ID=0c5a
TYPE=t_planta
NAME=trigo (maduro)
// por sections de menu

ON=@CREATE
attr=010
MOREX=1
tag.fruto=i_wheat_sheaf
tag.semente=i_sem_trigo


//****cenoura****

[ITEMDEF i_plan_cenoura1]
ID=0c68
TYPE=t_broto
NAME=cenoura(broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_cenoura2

[ITEMDEF i_plan_cenoura2]
ID=0c69
TYPE=t_broto
NAME=cenoura (jovem)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_cenoura3

[ITEMDEF i_plan_cenoura3]
ID=0c76
TYPE=t_planta
NAME=cenoura (madura)
// por sections de menu

ON=@CREATE
attr=010
MOREX=<R5,6>
tag.fruto=i_fruit_carrot
tag.semente=i_sem_cenoura


//****alface****

[ITEMDEF i_plan_alface1]
ID=0c71
TYPE=t_broto
NAME=alface(broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_alface2

[ITEMDEF i_plan_alface2]
ID=0c71
TYPE=t_broto
NAME=alface (jovem)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_alface3

[ITEMDEF i_plan_alface3]
ID=0c71
TYPE=t_planta
NAME=alface (madura)
// por sections de menu

ON=@CREATE
attr=010
MOREX=1
tag.fruto=i_fruit_lettuce
tag.semente=i_sem_alface


//################################ Reagentes ##################################

//****ginsen****

[ITEMDEF i_plan_ginsen1]
ID=0cb5
TYPE=t_broto
NAME=ginseng (broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_ginsen2

[ITEMDEF i_plan_ginsen2]
ID=0cb0
TYPE=t_broto
NAME=ginseng (jovem)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_ginsen3

[ITEMDEF i_plan_ginsen3]
ID=018e9
TYPE=t_planta
NAME=ginseng (maduro)
// por sections de menu

ON=@CREATE
attr=010
MOREX=<R1,2>
tag.fruto=i_r_ginseng
tag.semente=i_sem_ginseng

//****mandrake root****

[ITEMDEF i_plan_mandrake1]
ID=018df
TYPE=t_broto
NAME=mandragora (broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_mandrake2

[ITEMDEF i_plan_mandrake2]
ID=018df
TYPE=t_broto
NAME=mandragora (jovem)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_mandrake3

[ITEMDEF i_plan_mandrake3]
ID=018df
TYPE=t_planta
NAME=mandragora (maduro)
// por sections de menu

ON=@CREATE
attr=010
MOREX=<R1,2>
tag.fruto=i_r_raiz_de_mandrake
tag.semente=i_sem_mandrake


//****alho****

[ITEMDEF i_plan_alho1]
ID=018e1
TYPE=t_broto
NAME=alho (broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_alho2

[ITEMDEF i_plan_alho2]
ID=018e1
TYPE=t_broto
NAME=alho (jovem)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_alho3

[ITEMDEF i_plan_alho3]
ID=018e1
TYPE=t_planta
NAME=alho (maduro)
// por sections de menu

ON=@CREATE
attr=010
MOREX=<R1,2>
tag.fruto=i_r_alho
tag.semente=i_sem_alho

//****nightshade****

[ITEMDEF i_plan_nightshade1]
ID=0cb5
TYPE=t_broto
NAME=sombra da noite (broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_nightshade2

[ITEMDEF i_plan_nightshade2]
ID=018e5
TYPE=t_broto
NAME=sombra da noite (jovem)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_nightshade3

[ITEMDEF i_plan_nightshade3]
ID=018e5
TYPE=t_planta
NAME=sombra da noite (maduro)
// por sections de menu

ON=@CREATE
attr=010
MOREX=<R1,2>
tag.fruto=i_r_nightshade
tag.semente=i_sem_nightshade

//################################### Arvores persistentes #############################

// **** macieira ****

[ITEMDEF i_plan_maca1]
ID=0cb5
TYPE=t_broto
NAME=macieira (broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_maca2

[ITEMDEF i_plan_maca2]
ID=0CC7
TYPE=t_broto
NAME=macieira (pequena arvore)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_maca3

[ITEMDEF i_plan_maca3]
NAME=macieira
ID=0cd3
type=t_script

on=@create
attr=010
timerf 1,timerd,1

on=@timer
serv.newitem=i_arvore_macieira2
new.p=<p>
return 1

[ITEMDEF i_arvore_macieira2]
NAME=macieira
ID=0cd4
TYPE=t_arvore
TDATA1=i_fruit_apple

// **** parreira ****

[ITEMDEF i_plan_uva1]
ID=0B1
TYPE=t_broto
NAME=poste da parreira
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_uva2

ON=@DCLICK
return 1

[ITEMDEF i_plan_uva2]
ID=0CEA
TYPE=t_broto
NAME=parreira (pequena arvore)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_uva3
return 1

[ITEMDEF i_plan_uva3]
NAME=parreira
ID=0ced
type=t_arvore
TDATA1=i_fruit_grapes

[ITEMDEF i_plan_banana1]
ID=0CAB
TYPE=t_broto
NAME=bananeira (broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_banana2

[ITEMDEF i_plan_banana2]
ID=0CA8
TYPE=t_broto
NAME=bananeira (pequena arvore)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_banana3

ON=@DCLICK
src.sysmessage Esta arvore e muito jovem para dar frutos.
return 1

[ITEMDEF i_plan_banana3]
NAME=bananeira
ID=0CAA
type=t_arvore
TDATA1=i_fruit_banana

//################################### Outros #############################


//*** algodao ***

[ITEMDEF i_plan_algodao1]
ID=0cb5
TYPE=t_broto
NAME=pe de algodao (broto)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_algodao2

[ITEMDEF i_plan_algodao2]
ID=0C51
TYPE=t_broto
NAME=pe de algodao (jovem)
// por sections de menu

ON=@create
attr=010
tag.planta=i_plan_algodao3

[ITEMDEF i_plan_algodao3]
ID=0C53
TYPE=t_planta
NAME=pe de algodao (maduro)
// por sections de menu

ON=@CREATE
attr=010
MOREX=<R5,6>
tag.fruto=i_cotton
tag.semente=i_sem_algodao


//*****************************************************************************
//**                               Novos frutos                              **
//*****************************************************************************



[ITEMDEF I_BATATA]
ID=01728
NAME=batata
WEIGHT=.1
VALUE=3
TYPE=T_FOOD_RAW
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Batata
DEFNAME2=BATATA

ON=@Create
COLOR=000f7

[ITEMDEF I_ERVILHA]
ID=0c7f
NAME=ervilha
TYPE=T_FOOD_RAW
VALUE={2 4}
WEIGHT=0.2
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Ervilha

ON=@CREATE
COLOR=00111

[ITEMDEF 09D0]
DEFNAME=i_fruit_apple
NAME=maca
VALUE=1
TYPE=T_FRUIT
WEIGHT=0.1
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Maçã
[ITEMDEF 09d1]
DEFNAME=i_fruit_grapes
NAME=uvas
VALUE=1
TYPE=T_FRUIT
WEIGHT=0.1
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Cacho de uvas
DUPELIST=0d1a

[ITEMDEF 09d2]
DEFNAME=i_fruit_peach
NAME=pessego
VALUE=1
TYPE=T_FRUIT
WEIGHT=0.1
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Pêssego
DUPELIST=0172c

[ITEMDEF 0c70]
// DEFANAME2=HEAD_OF_LETTUCE
//Head of Lettuce
DEFNAME=i_fruit_lettuce
NAME=alface
TYPE=T_FRUIT
VALUE=2
WEIGHT=1
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Alface
DUPELIST=0c71

[ITEMDEF 0c7f]
//Ear of Corn
// DEFNAME2=CORN
DEFNAME=i_fruit_corn
NAME=milho
RESOURCES=I_CROP_CORN
TYPE=T_FOOD
VALUE={2 4}
WEIGHT=0.2
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Milho
DUPELIST=0c80,0c81,0c82

[ITEMDEF 0c77]
//Carrot
DEFNAME=i_fruit_carrot
NAME=cenoura%s
RESOURCES=1 i_crop_carrot
TYPE=T_FRUIT
TDATA1=i_crop_carrot
VALUE={2 4}
WEIGHT=0.1
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Cenoura
DUPELIST=0c78

[ITEMDEF 0df9]
// DEFNAME2=RAW_COTTON
DEFNAME=i_cotton
NAME=algodao
VALUE=3
//RESOURCES=16 i_crop_COTTON
TYPE=t_COTTON
CATEGORY=MyT - Items by Professions
SUBSECTION=Tailor
DESCRIPTION=Algodão
DUPELIST=0def
WEIGHT=2

[ITEMDEF 01ebd]
//Food for animals only? Is there a distinction?
DEFNAME=i_wheat_sheaf
NAME=trigo
TYPE=t_grain
VALUE=5
WEIGHT=10
CATEGORY=MyT - Items by Professions
SUBSECTION=Cook
DESCRIPTION=Trigo

//*****************************************************************************
//**                            Itens de emergência                          **
//*****************************************************************************


// Caixa de sementes
//
// Spawna sementes dentro dela de tanto em tanto tempo
//
//  morex - numero de sementes de reagentes
//  morey - numero de sementes de comida
//  morez - numero de semente de oturos
//  morem - numero de sementes de arvores persistentes
//  more2 - tempo de regeneração

[ITEMDEF i_caixa_sementes]
NAME=cesta de sementes
ID=i_basket_bushel
TYPE=t_container

CATEGORY=MyT - Sistemas
SUBSECTION=Plantacao
DESCRIPTION=cesta de sementes

on=@create
 morex=3
 morey=2
 morez=2
 more2=60*60*24*1 //1 dias

on=@dclick
 local.next_spawn=<eval <more1>+<more2>>
 if (<eval <SERV.TIME>/10> > <local.next_spawn>)
  empty
  serv.newitem i_sem_alface
  new.cont=<uid>
  new.dupe <eval <morey>-1>
  serv.newitem i_sem_cenoura
  new.cont=<uid>
  new.dupe <eval <morey>-1>
  serv.newitem i_sem_ervilha
  new.cont=<uid>
  new.dupe <eval <morey>-1>
  serv.newitem i_sem_trigo
  new.cont=<uid>
  new.dupe <eval <morey>-1>
  serv.newitem i_sem_milho
  new.cont=<uid>
  new.dupe <eval <morey>-1>
  serv.newitem i_sem_batata
  new.cont=<uid>
  new.dupe <eval <morey>-1>
  serv.newitem i_sem_alho
  new.cont=<uid>
  new.dupe <eval <morex>-1>
  serv.newitem i_sem_mandrake
  new.cont=<uid>
  new.dupe <eval <morex>-1>
  serv.newitem i_sem_nightshade
  new.cont=<uid>
  new.dupe <eval <morex>-1>
  serv.newitem i_sem_ginseng
  new.cont=<uid>
  new.dupe <eval <morex>-1>
  serv.newitem i_sem_algodao
  new.cont=<uid>
  new.dupe <eval <morez>-1>
  more1=<eval <SERV.TIME>/10>
  update
 endif

//**********************************************************************************************
//**                    Script de plantação do Mystical Tales Shard v1.6                      ** 
//**                               Por: Galthar, O Errante                                    **
//**                             e-mail: galtar@terra.com.br                                  **
//**                                     UIN# 44477266                                        **
//**                                                                                          **
//**                               24 de junho de 2003, 20:58hs (v1.2)                        **
//**                               28 de outubro de 2007, 23:25hs (v1.5)                      **
//**                               24 de novembro de 2007, 1:16hs (v1.6)                      **
//**                               28 de janeiro de 2010, 15:30hs (v1.6.1)                    **
//**                                                                                          **
//**                                    "Deus é O Cara!!!"                                    **
//**********************************************************************************************
//**                                                                                          **
//**            Por favor, não altere este arquivo sem o consentimento do autor               **
//**                                                                                          **
//**********************************************************************************************

[EOF]
