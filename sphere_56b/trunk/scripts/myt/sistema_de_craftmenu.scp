// Created 19/5/2009 01:06:30, with Gump Studio.
// Exported with with SphereExporter ver 1.1.
// Script for 0.56/Revisions

[DEFNAME craft_menu]
CRAFT_PAGE_ROOT         1
CRAFT_PAGE_LIST         2
CRAFT_PAGE_INFO         3
CRAFT_PAGE_CRAFT        4
CRAFT_PAGE_EMPTY        5
CRAFT_PAGE_ADD          6
CRAFT_PAGE_ADD_MENU     7
CRAFT_PAGE_ADD_FUNCTION 8
CRAFT_PAGE_ADD_ITEM     9

[FUNCTION craftmenu]
if (<IsEmpty <args>>)
 if (<IsGM>)
  ctag.craft_level=0
  ctag.craft_task=<DEF.CRAFT_PAGE_ROOT>
  craft_loadLevel 0
  return 1
 else
  return 0
 endif
elif (<IsNum <argv0>>)
  ctag.craft_level=<argn>
  ctag.craft_task=<DEF.CRAFT_PAGE_LIST>
elif (<serv.itemdef.<args>>)
 ctag.craft_item=<args>
 ctag.craft_task=<DEF.CRAFT_PAGE_CRAFT>
//elif (<serv.skill.<args>>)
endif
DIALOG d_crafting

[FUNCTION craft_clear]
//limpa as tags do sistema, menos tabelas.
ctag.craft_item=
ctag.craft_task=
ctag.craft_level=
ctag.craft_father=
ctag.craft_pages=
ctag.craft_page=
ctag.craft_Restriction=

[FUNCTION craft_flush]
//Limpa a tabela do sistema
ctag.craft_itens=0
local.r=1
WHILE (!<IsEmpty <ctag.craft_menu_button_<dlocal.r>>>)
 try ctag.craft_menu_id_<dlocal.r>=
 try ctag.craft_menu_button_<dlocal.r>=
 try ctag.craft_menu_name_<dlocal.r>=
 try ctag.craft_menu_type_<dlocal.r>=
 try ctag.craft_menu_code_<dlocal.r>=
 local.r += 1
end

[FUNCTION craft_TestIf]
FOR i 0 <eval <argv>-1>
 local.try=<STRSUB 0 50 <explode ;,<argv[<dlocal.i>]>>>
 if (STRMATCH(*>*,<local.try>)
  local.skill=<<STRARG <local.try>>>
  local.skill=<hval <local.skill>>
  local.diff=<streat <streat <local.try>>>
  local.diff=<eval <local.diff>>
  if (<local.skill> >= <local.diff>)
   local.do=1
  else
   local.do=0
  endif
 else
  try local.do=<<local.try>>
 endif
 if (!<local.do>)
  return 0
 endif
END
return 1

[FUNCTION craft_loadLevel]
craft_clear
ctag.craft_level=<argn>
DB.QUERY="SELECT father, def FROM craftmenu WHERE id=<eval <argn>>;"
ctag.craft_father=<DB.ROW.0.father>
ctag.craft_menu_name=<qval <IsEmpty <DB.ROW.0.def>>?Raiz (GM):<DB.ROW.0.def>>
craft_flush
DB.QUERY="SELECT * FROM craftmenu WHERE father=<eval <argn>> ORDER BY type,def;"
if (<DB.ROW.NUMROWS>)
 local.i=0
 for r 0 <eval <DB.ROW.NUMROWS>-1>
  local.do=1
  if ( (strmatch(menu,<DB.ROW.<dlocal.r>.type>)) || (strmatch(item,<DB.ROW.<dlocal.r>.type>)) ) && (!<IsNum <DB.ROW.<dlocal.r>.code>>) && (!<IsGM>)
   local.do=<craft_TestIf <STRSUB 0 50 <explode ;,<DB.ROW.<dlocal.r>.code>>>>//<DB.ROW.<dlocal.r>.code>>
  endif
  if (<local.do>)
   local.i += 1
   try ctag.craft_menu_id_<dlocal.i>=<DB.ROW.<dlocal.r>.id>
   try ctag.craft_menu_button_<dlocal.i>=<eval <DB.ROW.<dlocal.r>.id>+1000>
   try ctag.craft_menu_name_<dlocal.i>=<DB.ROW.<dlocal.r>.def>
   try ctag.craft_menu_type_<dlocal.i>=<DB.ROW.<dlocal.r>.type>
   try ctag.craft_menu_code_<dlocal.i>=<DB.ROW.<dlocal.r>.code>
  endif
 end
 ctag.craft_itens=0<local.i>
 ctag.craft_pages=<eval (0<local.i>/27)+1>
 ctag.craft_page=1
 ctag.craft_task=<DEF.CRAFT_PAGE_LIST>
endif
if (!<DB.ROW.NUMROWS>) || (!<local.i>)
 ctag.craft_task=<DEF.CRAFT_PAGE_EMPTY>
endif
DIALOG d_crafting

[FUNCTION craft_loadItem]
craft_clear
craft_flush
DB.QUERY="SELECT father, def FROM craftmenu WHERE id=<eval <argn>>;"
ctag.craft_father=<DB.ROW.0.father>
ctag.craft_item=<DB.ROW.0.def>
ctag.craft_task=<DEF.CRAFT_PAGE_CRAFT>
DIALOG d_crafting

[FUNCTION craft_deleteItem]
if !(STRCMP(s,<argv1>))
 DB.EXECUTE="DELETE FROM craftmenu WHERE id=<eval <argv0>>;"
endif
craft_loadLevel <ctag0.craft_level>

[FUNCTION craft_addMenu]
sysmessagered Guardando menu.
IF (!<IsEmpty <ctag.craft_Restriction>>)
 DB.EXECUTE="INSERT INTO craftmenu SET def='<DB.ESCAPEDATA <argtxt[0]>>', type='menu', father=<dctag0.craft_level>, code='<ctag.craft_Restriction>';"
ELSE 
 DB.EXECUTE="INSERT INTO craftmenu SET def='<DB.ESCAPEDATA <argtxt[0]>>', type='menu', father=<dctag0.craft_level>;"
ENDIF
craft_loadLevel <ctag0.craft_level>

[FUNCTION craft_addFunction]
ctag.craft_item=<ARGTXT[0]>
ctag.craft_restriction=<ARGTXT[1]>
IF (<eval STRLEN(<argtxt[0]>)> < 2) || (<eval STRLEN(<argtxt[1]>)> < 3)
 sysmessagered Parametros invalidos. Tente novamente.
 DIALOG d_crafting
ELSE
 sysmessagered Guardando funcao.
 DB.EXECUTE="INSERT INTO craftmenu SET def='<DB.ESCAPEDATA <argtxt[0]>>', type='function', father=<dctag0.craft_level>, code='<ARGTXT[1]>';"
 craft_loadLevel <ctag0.craft_level>
ENDIF

[FUNCTION craft_addItem]
sysmessagered Guardando item.
IF (!<IsEmpty <ctag.craft_Restriction>>)
 //Tem restrição. Guarde o TESTIF
 DB.EXECUTE="INSERT INTO craftmenu SET def='<DB.ESCAPEDATA <argtxt[0]>>', type='item', father=<dctag0.craft_level>, code='<ctag.craft_Restriction>';"
ELSE
 //Não tem restrição. Default Behavior da tabela é code='1'.
 DB.EXECUTE="INSERT INTO craftmenu SET def='<DB.ESCAPEDATA <argtxt[0]>>', type='item', father=<dctag0.craft_level>;"
ENDIF
craft_loadLevel <ctag0.craft_level>

[FUNCTION craft_addRestrictionSkill]
ctag.craft_addRestriction=
list_new Escolha a skill:
list_newCol 35,Skill
cTAG.list_numItems=55
for s 0 54
 try cTAG.list_row<local.s>_01=<serv.skill.<local.s>.name>
end
list_setActionBack DIALOG d_crafting
list_setAction craft_setRestrictionSkill
cTAG.list_numPages=4
list_show 1

[FUNCTION craft_setRestrictionSkill]
ctag.craft_addRestriction=<serv.skill.<argn>.name> >
prompt_new Especifique um valor de 0.0 a 124.9 para <serv.skill.<argn>.name>:<DEF.BR>Ex.: 55.0, 65.5 ou 12.3
prompt_setActionBack craft_addRestrictionSkill
prompt_setAction craft_setRestrictionSkillVal
prompt_show

[FUNCTION craft_addRestrictionItem]
prompt_new Entre com um valor de ITEMDEF para o item de restricao ou 't' para escolher um item no world:
prompt_setActionBack DIALOG d_crafting
prompt_setAction craft_setRestrictionItemVal
prompt_show

[FUNCTION craft_setRestrictionItemVal]
IF (<argo>)
 if (<argo.IsItem>)
  ctag.craft_RestictionItem = <argo.baseid>
 else
  sysmessagered Item invalido.
  craft_addRestrictionItem
  return 0
 endif
ELIF (STRMATCH(t,<args>))
 sysmessageyellow Selecione um item no mundo:
 targetf craft_setRestrictionItemVal
 return 0
ELIF (!<IsEmpty <args>>)
 if (<DEF.<serv.itemdef.<args>.baseid>>)
  ctag.craft_RestictionItem = <args>
 else
  sysmessagered Item invalido.
  craft_addRestrictionItem
  return 0
 endif
ENDIF
prompt_new Qual a quantidade de <serv.itemdef.<ctag.craft_RestictionItem>.name> necessaria?
prompt_setActionBack craft_addRestrictionItem
prompt_setAction craft_setRestrictionItem2
prompt_show

[FUNCTION craft_setRestrictionItem2]
if (<IsNum <argn>>)
 if (<IsEmpty <ctag.craft_Restriction>>)
  ctag.craft_Restriction = restest <eval <argn>> <ctag.craft_RestictionItem>
 else
  ctag.craft_Restriction .= ; restest <eval <argn>> <ctag.craft_RestictionItem>
 endif
 ctag.craft_RestictionItem=
 DIALOG d_crafting
else
 sysmessagered valor invalido.
 craft_setRestrictionSkill <ctag.craft_addRestriction>
endif

[FUNCTION craft_setRestrictionSkillVal]
if (<IsNum <argn>>)
 if (<IsEmpty <ctag.craft_Restriction>>)
  ctag.craft_Restriction = <ctag.craft_addRestriction> <fval <argn>>
 else
  ctag.craft_Restriction .= ; <ctag.craft_addRestriction> <fval <argn>>
 endif
 ctag.craft_addRestriction=
 DIALOG d_crafting
else
 sysmessagered valor invalido.
 craft_setRestrictionSkill <ctag.craft_addRestriction>
endif

[FUNCTION craft_addRestrictionCustom]
IF (<IsEmpty <args>>)
 prompt_new Digite uma linha de scritp de teste que nao contenha ',' e nem ';':
 prompt_setActionBack DIALOG d_crafting
 prompt_setAction craft_addRestrictionCustom
 prompt_show
else
 if (<IsEmpty <ctag.craft_Restriction>>)
  ctag.craft_Restriction = <args>
 else
  ctag.craft_Restriction .= ; <args>
 endif
 DIALOG d_crafting
endif

[FUNCTION craft_deleteRestriction]
//Delerta a restrição ARGN
ctag.b=<eval <argn>-1>
craft_deleteRestriction2 <explode ;, <ctag.craft_Restriction>>

[FUNCTION craft_deleteRestriction2]
ctag.craft_Restriction=
//Auxiliar de craft_deleteRestriction
for i 0 <argv>  //Começa no 2 pq a função Explode retorna ';xxx;yyy;zzz...'
 if (<local.i>!=<ctag.b>)
  if (<IsEmpty <ctag.craft_Restriction>>)
   ctag.craft_Restriction = <argv[<local.i>]>
  else
   ctag.craft_Restriction .= ; <argv[<local.i>]>
  endif
 endif
end
ctag.b=

[FUNCTION craft_drawRestriction]
dorigin 30 155
for i 2 <argv>  //Começa no 2 pq a função Explode retorna ';xxx;yyy;zzz...'
 button - *20 4017 4017 1 0 <eval 5000+<local.i>>
 dtext +35 +1 2100 <argv[<eval <local.i>-1>]>
end
local.restrictions = <eval <argv>-1>

[FUNCTION craft_info_craft]
local.t=<DEF.BFONT_DCYAN>DIFICULDADE:<DEF.BFONT_WHITE><DEF.BR>
local.t .= <fval <serv.itemdef.<args>.skillmake.0.val>>% <serv.skill.<serv.itemdef.<args>.skillmake.0.key>.name><DEF.BR>
try local.exp=<DEF.<args>_exp>
if (<local.exp>)
 LOCAL.SKILL=<fval <src.<DEF.<serv.itemdef.<args>.skillmake.0.key>>>>
 LOCAL.DIFFIC=<MAXIMUM 1.0, <eval <LOCAL.SKILL>-<serv.itemdef.<args>.skillmake.0.val>>>
 LOCAL.CHANCE=<eval <MINIMUM 625,<LOCAL.DIFFIC>,<Armslore>>/5>
 local.t .= <fval <local.chance>>% de chance para Excepcional<DEF.BR>
endif
local.t .=<DEF.BR>
local.t .=<DEF.BFONT_DCYAN>MATERIA PRIMA:<DEF.BFONT_WHITE><DEF.BR>
local.i=1
WHILE (<serv.itemdef.<args>.resources.<local.i>.val>)
 local.t .= -<eval <serv.itemdef.<args>.resources.<local.i>.val>> <serv.itemdef.<serv.itemdef.<args>.resources.<local.i>.key>.name><DEF.br>
 local.i += 1
END

return <local.t>

[FUNCTION d_crafting_solve]
if (<ctag.craft_task>==<DEF.CRAFT_PAGE_EMPTY>)
        resizepic 20 40 2620 460 320
        IF (<ctag0.craft_father>) || (<IsGM>)
         button 25 365 5603 5607 1 0 3
        ENDIF
        dorigin 150 130
        dtext +33 *20 66 Essa lista esta vazia.
        if (<IsGM>) && (<ctag0.craft_page> == <ctag0.craft_pages>)
         button +70 *20 2224 2224 1 0 7
         dtext +90 -4 24 Add
        else
         dtext +13 *20 66 Possivelmente voce nao tem
         dtext +35 *20 66 skill suficiente para
         dtext - *20 66 fazer nada do que consta nela.
        endif
        
elif (<ctag.craft_task>==<DEF.CRAFT_PAGE_CRAFT>) || (<ctag.craft_task>==<DEF.CRAFT_PAGE_INFO>)
        //DB.QUERY="SELECT father FROM craftmenu WHERE id=<dctag.craft_level>;"
        //ctag.craft_father=<DB.ROW.0.father>
        resizepic 20 40 2620 150 175
        tilepichue 28 50 <eval <serv.itemdef.<ctag.craft_item>.id>> <itemforge_DBColor <ctag.craft_item>>
        resizepic 180 40 2620 300 320
        dtext 190 50 3 Item:
        dtext 228 50 86 <serv.itemdef.<ctag.craft_item>.name>
        dhtmlgump 190 74 284 278 0 1 <craft_info_craft <ctag.craft_item>>
        resizepic 20 225 2620 150 136
        button 29 239 2224 248 1 0 4
        dtext 51 235 66 Geral
        button 29 256 2224 248 1 0 5
        dtext 51 252 66 Manufatura
        dtext 57 286 86 Qtd.:
        resizepic 93 284 9200 31 26
        dtextentry 100 287 18 20 0 0 1
        button 61 319 2443 2444 1 0 6
        dtext 75 320 33 Fazer
        IF (!<IsEmpty <ctag0.craft_father>>)
         button 25 365 5603 5607 1 0 3
        ENDIF

elif (<ctag.craft_task>==<DEF.CRAFT_PAGE_ROOT>) || (<ctag.craft_task>==<DEF.CRAFT_PAGE_LIST>)
        resizepic 20 40 2620 460 320
        dhtmlgump 30 50 440 18 0 0 <DEF.BFONT_GREEN><DEF.CENTER><ctag.craft_menu_name><DEF.DIV_E>
        dorigin 30 54
        if (<IsGM>)
         dorigin 60 54
        endif
        local.first=<eval ((<dctag.craft_page>-1)*28)+1>
        local.last=<minimum <dctag.craft_page>*28,<dctag.craft_itens>>
        local.j=<local.first>+14
        FOR i <local.first> <local.last>
         if (<local.i>==<local.j>)
          dorigin *215 54
          local.j += 14
         endif
         if (STRMATCH(menu,<ctag.craft_menu_type_<dlocal.i>>))
          button - *20 2224 2224 1 0 <eval 1000+<local.i>>
          dtext +20 -4 54 <ctag.craft_menu_name_<dlocal.i>>
         elif (STRMATCH(function,<ctag.craft_menu_type_<dlocal.i>>))
          button - *20 2224 2224 1 0 <eval 1000+<local.i>>
          dtext +20 -4 44 <ctag.craft_menu_name_<dlocal.i>>
         else
          button +3 *20 2103 2104 1 0 <eval 1000+<local.i>>
          dtext +20 -4 2100 <serv.itemdef.<ctag.craft_menu_name_<dlocal.i>>.name>
         endif
         if (<IsGM>)
          button -30 -6 4017 4017 1 0 <eval 5000+<local.i>>
         endif
        END
        if (<IsGM>) && (<ctag0.craft_page> == <ctag0.craft_pages>)
         button - *20 2224 2224 1 0 7
         dtext +20 -4 24 Add
        endif
        if (<ctag0.craft_page> > 1)
         button 25 365 5603 5607 1 0 1
        else
         IF (<ctag0.craft_father>) || (<IsGM>)
          button 25 365 5603 5607 1 0 3
         ENDIF
        endif
        if (<ctag0.craft_page> != <ctag0.craft_pages>)
         button 465 365 5601 5605 1 0 2
        endif
        if (<ctag0.craft_pages> > 1)
         dtext 232 365 2100 <dctag0.craft_page>/<dctag0.craft_pages>
        endif

elif (<ctag.craft_task>==<DEF.CRAFT_PAGE_ADD>)        
        resizepic 20 40 2620 460 320
        dhtmlgump 25 48 448 172 0 0 <DEF.BFONT_DCYAN>  Adicionar um novo item em <ctag.craft_menu_name>:<DEF.BFONTE>
        button 178 228 2151 2151 1 0 8
        dtext 213 232 152 Novo sub-menu aqui
        button 178 266 2151 2151 1 0 9
        dtext 213 270 152 Nova funcao aqui
        button 178 305 2151 2151 1 0 10
        dtext 213 309 152 Novo item aqui
        button 25 365 5603 5607 1 0 3
        
elif (<ctag.craft_task>==<DEF.CRAFT_PAGE_ADD_MENU>)
        resizepic 20 40 2620 460 320
        dhtmlgump 25 48 448 83 0 0 <DEF.BFONT_DCYAN>  Preencha o nome imediatamente.<DEF.BR>  Caso seja necessario que o item so apareca no menu em casos especiais, crie uma ou mais condicoes de teste pelos botoes na parte de baixo.<DEF.BFONTE>
        resizepic 37 144 9200 431 26
        dtext 44 146 4 Nome:
        dtextentry 100 147 300 20 0 0 <QVAL <IsEmpty <ctag.craft_item>>?<DEF.L1>NOME<DEF.R1>:<ctag.craft_item>>
        if (!<IsEmpty <ctag.craft_Restriction>>)
         call craft_drawRestriction <explode ;, <ctag.craft_Restriction>>
        endif
        if (<local.restrictions> != 4)
         //Máximo de 4 restrições
         button 29 327 4005 4005 1 0 11
         dtext 63 329 2100 Testar skill
         button 140 327 4005 4005 1 0 12
         dtext 174 329 2100 Testar item
         button 253 327 4005 4005 1 0 13
         dtext 285 328 2100 Receita
         button 340 327 4005 4005 1 0 14
         dtext 372 328 2100 Personalizado
        endif
        button 410 365 247 248 1 0 100//OK
        button 25 365 241 242 1 0 3
        
elif (<ctag.craft_task>==<DEF.CRAFT_PAGE_ADD_FUNCTION>)
        resizepic 20 40 2620 460 320
        dhtmlgump 25 48 448 120 0 0 <DEF.BFONT_DCYAN>  Preencha o nome imediatamente. O nome aparecera no meu para os jogadores<DEF.BR>  Abaixo digite a linha de comando que não deve conter variaveis como amount, tag.xxx, ArmsLore entre sinais de menor e maior.<DEF.BFONTE>
        resizepic 37 144 9200 431 26
        dtext 44 146 4 Nome:
        dtextentry 100 147 300 20 0 0 <QVAL <IsEmpty <ctag.craft_item>>?<DEF.L1>NOME<DEF.R1>:<ctag.craft_item>>
        resizepic 37 184 9200 431 66
        dtext 44 186 4 Funcao:
        dtextentry 100 187 300 60 0 1 <QVAL <IsEmpty <ctag.craft_Restriction>>?<DEF.L1>FUNCAO<DEF.R1>:<ctag.craft_Restriction>>
        button 410 365 247 248 1 0 100//OK
        button 25 365 241 242 1 0 3
        
elif (<ctag.craft_task>==<DEF.CRAFT_PAGE_ADD_ITEM>)
        ctag.craft_father=<ctag.craft_level>
        resizepic 20 40 2620 460 320
        dhtmlgump 25 48 448 83 0 0 <DEF.BFONT_DCYAN>  Preencha o DEFNAME do item imediatamente.<DEF.BR>  Caso seja necessario que o item so apareca no menu em casos especiais, crie uma ou mais condicoes de teste pelos botoes na parte de baixo.<DEF.BFONTE>
        resizepic 37 144 9200 431 26
        dtext 44 146 4 Defname:
        dtextentry 100 147 300 20 0 0 <QVAL <IsEmpty <ctag.craft_item>>?i_:<ctag.craft_item>>
        button 436 146 9011 9011 1 0 0//Itemforge
        if (!<IsEmpty <ctag.craft_Restriction>>)
         call craft_drawRestriction <explode ;, <ctag.craft_Restriction>>
        endif
        if (<local.restrictions> != 4)
         //Máximo de 4 restrições
         button 29 327 4005 4005 1 0 11
         dtext 63 329 2100 Testar skill
         button 140 327 4005 4005 1 0 12
         dtext 174 329 2100 Testar item
         button 253 327 4005 4005 1 0 13
         dtext 285 328 2100 Receita
         button 340 327 4005 4005 1 0 14
         dtext 372 328 2100 Personalizado
        endif
        button 410 365 247 248 1 0 100//OK
        button 25 365 241 242 1 0 3
endif
[DIALOG d_crafting]
100,75
page 0
resizepic 0 0 2620 500 400
checkertrans 5 7 490 385
gumppic 175 14 2501
dtext 185 14 26 Mystical Tales Shard
call d_crafting_solve

[DIALOG d_crafting text]

[DIALOG d_crafting button]
//ON=0 12000
//say <eval <argn>>
ON=1
// -pg
ctag.craft_page -= 1
DIALOG d_crafting

ON=2
// +pg
ctag.craft_page += 1
DIALOG d_crafting

ON=3
// Back
ctag.craft_task=<DEF.CRAFT_PAGE_LIST>
craft_loadLevel <ctag.craft_father>

ON=4
// Info geral
// 

ON=5
// info craft
// 

ON=6
// Craft item
craftItem <ctag.craft_item>,<argtxt[0]>

ON=7
//Add here
ctag.craft_task=<DEF.CRAFT_PAGE_ADD>
DIALOG d_crafting

ON=8
//Add Menu here
ctag.craft_task=<DEF.CRAFT_PAGE_ADD_MENU>
DIALOG d_crafting

ON=9
//Add Function here
ctag.craft_task=<DEF.CRAFT_PAGE_ADD_FUNCTION>
DIALOG d_crafting

ON=10
//Add Item here
ctag.craft_task=<DEF.CRAFT_PAGE_ADD_ITEM>
DIALOG d_crafting

ON=11
//Add restrição por skill
ctag.craft_item=<ARGTXT[0]>
craft_addRestrictionSkill

ON=12
//Add restrição por item
ctag.craft_item=<ARGTXT[0]>
craft_addRestrictionItem

ON=13
//Add restricao por projeto/recipe
ctag.craft_item=<ARGTXT[0]>
sysmessagered Nao implementado. PASSA AMANHA!
DIALOG d_crafting

ON=14
ctag.craft_item=<ARGTXT[0]>
craft_addRestrictionCustom

ON=100
//Botão OKAY
DOSWITCH <ctag.craft_task>
 return 1                       //Sem CRAFT_PAGE_*
 return 1                       //CRAFT_PAGE_ROOT
 return 1                       //CRAFT_PAGE_LIST
 return 1                       //CRAFT_PAGE_INFO
 return 1                       //CRAFT_PAGE_CRAFT
 return 1                       //CRAFT_PAGE_EMPTY
 return 1                       //CRAFT_PAGE_ADD
 call CRAFT_ADDMENU             //CRAFT_PAGE_ADD_MENU
 call CRAFT_ADDFUNCTION         //CRAFT_PAGE_ADD_FUNCTION
 call CRAFT_ADDITEM             //CRAFT_PAGE_ADD_ITEM
END

ON=1000 5000
// Itens
local.b=<eval <argn>-1000>
if (strmatch(item,<ctag.craft_menu_type_<dlocal.b>>))
 craft_loadItem <ctag.craft_menu_id_<dlocal.b>>
elif (strmatch(function,<ctag.craft_menu_type_<dlocal.b>>))
 try <ctag.craft_menu_code_<dlocal.b>>
 craft_flush
else
 craft_loadLevel <ctag.craft_menu_id_<dlocal.b>>
endif

ON=5000 10000
local.b=<eval <argn>-5000>
IF (<ctag.craft_task>==<DEF.CRAFT_PAGE_ADD_FUNCTION>)||(<ctag.craft_task>==<DEF.CRAFT_PAGE_ADD_ITEM>)
 //Remover uma restrição de TESTIF
 ctag.craft_item=<ARGTXT[0]>
 craft_deleteRestriction <local.b>
 DIALOG d_crafting
ELIF (<ctag.craft_task>==<DEF.CRAFT_PAGE_ROOT>)||(<ctag.craft_task>==<DEF.CRAFT_PAGE_LIST>)
 //Remover item da lista de crafting
 prompt_new Apagar <ctag.craft_menu_name_<dlocal.b>> deste menu?<DEF.BR>(s/n)
 prompt_setAction craft_deleteItem <ctag.craft_menu_id_<dlocal.b>>,
 prompt_setActionBack DIALOG d_crafting
 prompt_show
ENDIF
[EOF]
