[comment magias_de_fogo]
fireballzinho
tocha mágica
fireballzão
Esquentar arma (desarmar)
cauterizar
chama mistica (encantar com fogo)
explodir
cela de fogo
forma de elemental
meteor swarm

//***********************************************************
//      SPELL fire darts
//***********************************************************
[Spell 300]
DEFNAME=s_firedarts
NAME=Dardos de fogo
SOUND=054
RUNES=""
CAST_TIME=12
//RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_RUNE_ALCHEM_3
//SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_HARM | spellflag_targ_char | spellflag_scripted
EFFECT_ID=0
EFFECT=1,10
//DURATION=30.0
MANAUSE=9
SKILLREQ=MAGERY 10.0
INTERRUPT=100.0,100.0

on=@select
return <trycast s_firedarts,spl_fogo>

on=@Fail
f_consumeMana <ctag.cMana>

on=@SUCCESS
f_consumeMana <ctag.cMana>

on=@Effect
EFFECT 0 i_fx_fireball_small 6 16 1
DAMAGESPELL s_firedarts,dam_fire,<src.ctag.cSkill>,<src.uid>
return 0

//***********************************************************
//      SPELL MAGIC TORCH
//***********************************************************
[Spell 301]
DEFNAME=s_magictorch
NAME=Tocha magica
SOUND=054
RUNES=""
CAST_TIME=12
//RESOURCES=i_reag_blood_moss,i_reag_ginseng,i_reag_mandrake_root,i_reag_sulfur_ash
RUNE_ITEM=i_RUNE_ALCHEM_3
//SCROLL_ITEM=i_scroll_EARTHQUAKE
FLAGS=SPELLFLAG_GOOD | spellflag_scripted
EFFECT_ID=0
EFFECT=20,120
//DURATION=30.0,120.0
MANAUSE=18
SKILLREQ=MAGERY 45.0
INTERRUPT=100.0,100.0

on=@start
sfx 05c1

on=@select
return <trycast s_magictorch,spl_fogo>

on=@Fail
f_consumeMana <ctag.cMana>

on=@SUCCESS
f_consumeMana <ctag.cMana>

on=@Effect
EFFECT 3 i_fx_sparkle_2 0 16 0 032
serv.newitem i_torch_lit
new.color 044eb
src.equip <new>
new.attr=08030
local.t=<EFFECTSPELL s_magicTorch,<src.ctag.cskill>>
new.timerf <LOCAL.t>,remove
src.timerf <local.t>,sfx,04b8
return 0

//***********************************************************
//      SPELL Fireball
//***********************************************************
[Spell 18]
DEFNAME=s_fireball
NAME=Fireball
SOUND=snd_SPELL_FIREBALL
//RUNES=VF
CAST_TIME=3.0
RESOURCES=i_reag_black_pearl,i_reag_sulfur_ash
RUNE_ITEM=i_rune_FIREBALL
SCROLL_ITEM=i_scroll_FIREBALL
FLAGS=SPELLFLAG_TARG_OBJ | SPELLFLAG_DIR_ANIM | SPELLFLAG_HARM | SPELLFLAG_RESIST
EFFECT_ID=0
EFFECT=8,16
DURATION=0.0
MANAUSE=9
SKILLREQ=MAGERY 20.0
INTERRUPT=100.0,100.0

on=@select
return <trycast s_fireball,spl_fogo>

on=@Fail
f_consumeMana <ctag.cMana>

on=@SUCCESS
f_consumeMana <ctag.cMana>

on=@Effect
EFFECT 0 i_fx_fireball 6 16 1
DAMAGESPELL s_fireball,dam_fire,<src.ctag.cSkill>,<src.uid>
return 0

//***********************************************************
//      SPELL Heat Weapon
//***********************************************************
[Spell 302]
DEFNAME=s_heatWeapon
NAME=Esquentar arma
SOUND=snd_SPELL_FIREBALL
//RUNES=VF
CAST_TIME=4.0
RESOURCES=i_reag_black_pearl,i_reag_sulfur_ash
RUNE_ITEM=i_rune_FIREBALL
SCROLL_ITEM=i_scroll_FIREBALL
FLAGS=SPELLFLAG_TARG_OBJ | SPELLFLAG_HARM | SPELLFLAG_RESIST
EFFECT_ID=i_fire
EFFECT=3,12
DURATION=0.0
MANAUSE=32
SKILLREQ=MAGERY 52.5
INTERRUPT=100.0,100.0

on=@select
return <trycast s_heatWeapon,spl_fogo>

on=@Fail
f_consumeMana <ctag.cMana>

on=@SUCCESS
f_consumeMana <ctag.cMana>

on=@Effect
EFFECT 3 i_fire 0 16 0
DAMAGESPELL s_heatWeapon,dam_fire,<src.ctag.cSkill>,<src.uid>
if (<weapon>)
 if (STRMATCH(*ingot*,<serv.itemdef.<weapon.baseid>.resources>)) && !(<weapon.attr>&08d14)
  f_heatWeapon <eval <EFFECTSPELL s_heatWeapon,<src.ctag.cSkill>>*3>
  sysmessagered Sua arma esta muito quente para segurar!
 else
  src.sysmessagered A arma do seu oponente nao esquenta!
 endif
else
 src.sysmessagered Seu oponente nao esta armado!
endif
return 1

[FUNCTION f_heatWeapon]
obj=<weapon>
obj.tag.hcolor=<obj.color>
obj.tag.hname=<obj.name>
obj.name=<obj.name> (quente)
obj.color=04ec
obj.attr=<obj.attr>|08010
obj.p=<topobj.p>
obj.timerf <argn>,_f_heatWeapon

[FUNCTION _f_heatWeapon]
color=<tag.hcolor>
tag.hcolor
name=<tag.hname>
tag.hname
attr=<attr>&~08010
timer=600
emotered esfriou

//***********************************************************
//      SPELL Cauterizar
//***********************************************************
[Spell 303]
DEFNAME=s_cauterizar
NAME=cauterizar
SOUND=0356
//RUNES=VF
CAST_TIME=4.0
RESOURCES=i_reag_black_pearl,i_reag_sulfur_ash
RUNE_ITEM=i_rune_FIREBALL
SCROLL_ITEM=i_scroll_FIREBALL
FLAGS=SPELLFLAG_TARG_CHAR | SPELLFLAG_GOOD
EFFECT_ID=0
EFFECT=8,18
DURATION=0.0
MANAUSE=12
SKILLREQ=MAGERY 62.5
INTERRUPT=100.0,100.0

on=@start
if (<src.act.distance <UID>> > 1)
 sysmessageyellow Muito longe...
 return 1
ELIF (<src.act.hits> > <eval <src.act.maxhits>/2>)
 sysmessageyellow Seu alvo nao precisa de cauterizacao...
 return 1
endif
sfx 0227

on=@select
return <trycast s_cauterizar,spl_fogo>

on=@Fail
f_consumeMana <ctag.cMana>

on=@SUCCESS
f_consumeMana <ctag.cMana>

on=@Effect
EFFECT 3 i_fx_smoke_small 0 64 0
hits += <EFFECTSPELL s_cauterizar,<src.ctag.cSkill>>
cry
return 1

//***********************************************************
//      SPELL Mystic Flame
//***********************************************************
[Spell 304]
DEFNAME=s_mysticFlame
NAME=Chama mistica
SOUND=0
//RUNES=VF
CAST_TIME=5.0
RESOURCES=i_reag_black_pearl,i_reag_sulfur_ash
RUNE_ITEM=i_rune_FIREBALL
SCROLL_ITEM=i_scroll_FIREBALL
FLAGS=SPELLFLAG_TARG_ITEM | spellflag_good
EFFECT_ID=0
EFFECT=3,5
DURATION=0.0
MANAUSE=25
SKILLREQ=MAGERY 72.5
INTERRUPT=100.0,100.0

on=@select
return <trycast s_mysticFlame,spl_fogo>

on=@start
sfx 05c1

on=@Fail
f_consumeMana <ctag.cMana>

on=@SUCCESS
f_consumeMana <ctag.cMana>

on=@Effect
if (!<IsWeapon <UID>>) || !(STRMATCH(*ingot*,<serv.itemdef.<baseid>.resources>))
 src.sysmessageyellow Apenas armas de metal.
 src.mana += <ctag.cMana>
 return 1
ELIF (<more2>)
 src.sysmessageyellow Esta arma ja possui um tratamento especial
 src.mana += <ctag.cMana>
 return 1
ELIF (<src.distance <topobj>> > 1)
 src.sysmessageyellow Muito longe...
 src.mana += <ctag.cMana>
 return 1
endif
src.sfx 0356
f_mysticFlame <EFFECTSPELL s_mysticFlame,<src.ctag.cSkill>>

[FUNCTION f_mysticFlame]
tag.hcolor=<color>
more2=51
morex=1
dmgfire=<argn>
color=04ec
attr=<attr>|020
timerf <eval <argn>*10>,_f_mysticFlame,<argn>
updatex

[FUNCTION _f_mysticFlame]
color=<tag.hcolor>
tag.hcolor
more2=0
morex=0
dmgfire -= <argn>
attr=<attr>&~020
sfx 0208
updatex

//***********************************************************
//      SPELL Conjure Fire Elemental
//***********************************************************

[Spell 305]
DEFNAME=s_conjure_elem_fire
NAME=Conjurar elemental do fogo
//RUNES=""
CAST_TIME=28
RUNE_ITEM=i_rune_summon_elem_fire
FLAGS=spellflag_targ_xyz | spellflag_scripted
EFFECT_ID=0
EFFECT=3,8
MANAUSE=48
SKILLREQ=MAGERY 65.0
INTERRUPT=100.0,100.0

on=@start
sfx snd_spell_fire_field

on=@select
return <trycast s_conjure_elem_fire,spl_fogo>

on=@Success
LOCAL.timer=<f_rangeValue 5,120,<src.cTag.cSkill>>
obj=<f_conjure_creature c_elem_fire,<LOCAL.timer>>
obj.p=<src.targp>
obj.update
f_consumeMana <ctag.cMana>

on=@Fail
f_consumeMana <ctag.cMana>

//***********************************************************
//      SPELL Summon Fire Elemental
//***********************************************************

[Spell 306]
DEFNAME=s_summon_elem_fire
NAME=Invocar elemental do fogo
//RUNES=""
CAST_TIME=32
RUNE_ITEM=i_rune_summon_elem_fire
FLAGS=spellflag_targ_xyz | spellflag_scripted
EFFECT_ID=0
EFFECT=3,8
MANAUSE=60
SKILLREQ=MAGERY 75.0
INTERRUPT=100.0,100.0

on=@start
sfx snd_spell_fire_field

on=@select
return <trycast s_summon_elem_fire,spl_fogo>

on=@Success
LOCAL.timer=<f_rangeValue 5,260,<src.cTag.cSkill>>
obj=<f_summon_creature c_elem_fire,<LOCAL.timer>>
obj.p=<src.targp>
obj.update
f_consumeMana <ctag.cMana>

on=@Fail
f_consumeMana <ctag.cMana>

//***********************************************************
//      SPELL FIRE CELL
//***********************************************************
[Spell 307]
DEFNAME=s_CelaFogo
NAME=Cela de Fogo
SOUND=snd_SPELL_FIRE_FIELD
RUNES=""
CAST_TIME=62
RUNE_ITEM=i_RUNE_ALCHEM_3
FLAGS=SPELLFLAG_HARM | spellflag_scripted | spellflag_resist | spellflag_targ_char
EFFECT_ID=0
EFFECT=7,60
MANAUSE=12
SKILLREQ=MAGERY 70.0
INTERRUPT=100.0,100.0

on=@select
return <trycast s_CelaFogo,spl_fogo>

on=@Fail
f_consumeMana <ctag.cMana>

on=@Effect
f_cela_fogo <effectspell s_CelaFogo,<src.cTAG.cSkill>>
return 0

//***********************************************************
//      SPELL Fire Bolt
//***********************************************************

[Spell 308]
DEFNAME=s_firebolt
NAME=Fire Bolt
SOUND=snd_SPELL_FIRE_BOLT
RUNES=""
CAST_TIME=70
RUNE_ITEM=i_rune_FIREBALL
FLAGS=SPELLFLAG_TARG_CHAR | SPELLFLAG_DIR_ANIM | SPELLFLAG_HARM | SPELLFLAG_FX_BOLT
EFFECT_ID=0
EFFECT=25,50
DURATION=0.0
MANAUSE=15
SKILLREQ=MAGERY 70.0
INTERRUPT=100.0,100.0

on=@select
return <trycast s_fire_bolt,spl_fogo>

on=@Fail
f_consumeMana <ctag.cMana>

on=@SUCCESS
f_consumeMana <ctag.cMana>

on=@Effect
EFFECT 0 i_fx_firesnake 6 16 1
DAMAGESPELL s_firebolt,dam_fire,<src.ctag.cSkill>,<src.uid>

//***********************************************************
//      SPELL FIRE FIELD
//***********************************************************

[Spell 28]
DEFNAME=s_fire_field
NAME=Fire Field
SOUND=snd_SPELL_FIRE_FIELD
RUNES=""
CAST_TIME=42
RUNE_ITEM=i_rune_FIRE_FIELD
FLAGS=SPELLFLAG_TARG_XYZ | SPELLFLAG_HARM | SPELLFLAG_FX_TARG | SPELLFLAG_FIELD| SPELLFLAG_RESIST
EFFECT_ID=i_fx_curse
EFFECT=2,20
DURATION=5.0,60.0
MANAUSE=20
SKILLREQ=MAGERY 50.0
INTERRUPT=100.0,100.0

on=@select
return <trycast s_fire_field,spl_fogo>

on=@Fail
f_consumeMana <ctag.cMana>

on=@SUCCESS
f_consumeMana <ctag.cMana>
LOCAL.CREATEOBJECT1 = <eval i_fogo_armadilha_lo>
LOCAL.CREATEOBJECT2 = <eval i_fogo_armadilha_ns>

[EOF]