///////////////////////////////////////////////
/////////  Sistema de recipientes  ///////////
////////    Jean Gabin (MyT)      ///////////
///////       Beta v. 0.162      ///////////
//////              ///////////
/////      Galthar, o Errante  ///////////
////        v.1.1         ///////////
////////////////////////////////////////

[DEFNAME pool_can]
POOL            i_blood_pool_large
POOL_STICKY     01  //2 a 5 segundos preso na poça
POOL_FLAMABLE   02  //dclick pega fogo
POOL_INCINERATE 04  //Faz as coisas em volta pegarem fogo tb (log, kidling e poças com POOL_FLAMABE ou POOL_INCINERATE)
POOL_SLIPPERY   08  //Escorrega e cai
POOL_EXTINGUISH 010 //Apaga fogo



[DEFNAME recipients_can]
RECIPIENT_TARG_EMPTY    01  //@DClick quando vazio abre target
RECIPIENT_TARG_FULL     02  //@DClick quando cheio abre target
RECIPIENT_WASH_HANDS    04  //@DClick quando cheio emote *lava maos e rosto com <DEF.LIQUID_0*_NAME>*
RECIPIENT_DRINK         08  //@DClick tenta beber e @TARGON_SELF tenta beber
RECIPIENT_WET_CHAR      010 //@TARGON_CHAR molha o char e cria poça
RECIPIENT_TARG_DRINK    020 //@TARGON_SELF tenta beber
RECIPIENT_WASH_BANDAGE  040 //Se tiver agua dentro, pode lavar bandagens
RECIPIENT_DYE           080 //@Fill tinge o recipiente da cor do item


[DEFNAME liquid_type]
LIQUID_CAN_DRINK        01  //Tomavel. Dá 1 ou MOREY de food. Se tentar tomar sem esse flag, cospe.
LIQUID_CAN_POISON       02  //Poisona com força MOREY
LIQUID_CAN_BOOZE        04  //Deixa BOOZED com MOREY de força
LIQUID_CAN_BANDAGE      08  //Limpa bandagens e vira sangue.
LIQUID_CAN_EXTINGUISH   010 //Apaga fogo
LIQUID_CAN_MORVOR       020 //Recupera stats? A ver.

LIQUID_EMPTY        00
LIQUID_00_NAME      vazio
LIQUID_00_CAN       0

LIQUID_WATER        01
LIQUID_t_water      01
LIQUID_01_NAME      agua
LIQUID_01_CAN       LIQUID_CAN_DRINK|LIQUID_CAN_EXTINGUISH|LIQUID_CAN_BANDAGE
LIQUID_01_FORCE     1
LIQUID_01_POOL      i_pool
LIQUID_01_COLOR     0
LIQUID_01_ITEM      i_dose_water

LIQUID_TAR          02
LIQUID_t_tar        02
LIQUID_02_NAME      piche
LIQUID_02_CAN       LIQUID_CAN_POISON
LIQUID_02_FORCE     100
LIQUID_02_POOL      i_pool_tar
LIQUID_02_COLOR     035E
LIQUID_02_BOIL      LIQUID_OIL
LIQUID_02_ITEM      i_dose_tar

LIQUID_OIL          03
LIQUID_t_oil        03
LIQUID_03_NAME      oleo
LIQUID_03_CAN       LIQUID_CAN_POISON
LIQUID_03_FORCE     100
LIQUID_03_POOL      i_pool_oil
LIQUID_03_COLOR     02B4
LIQUID_03_ITEM      i_dose_oil

LIQUID_WAX          04
LIQUID_t_wax_hard   04
LIQUID_04_NAME      cera dura
LIQUID_04_CAN       0
LIQUID_04_FORCE     0
LIQUID_04_POOL      i_pool_wax
LIQUID_04_COLOR     0B92
LIQUID_04_BOIL      LIQUID_WAX_MELT
LIQUID_04_ITEM      i_dose_wax

LIQUID_SWAMP        05
LIQUID_t_swamp      05
LIQUID_05_NAME      agua de pantano
LIQUID_05_CAN       LIQUID_CAN_POISON|LIQUID_CAN_EXTINGUISH
LIQUID_05_FORCE     150
LIQUID_05_POOL      i_pool_swamp
LIQUID_05_COLOR     08A2
LIQUID_05_BOIL      LIQUID_WATER

LIQUID_BLOOD        06
LIQUID_t_BLOOD      06
LIQUID_06_NAME      sangue
LIQUID_06_CAN       LIQUID_CAN_EXTINGUISH
LIQUID_06_FORCE     0
LIQUID_06_POOL      i_pool_blood
LIQUID_06_COLOR     026
LIQUID_06_ITEM      i_dose_blood

LIQUID_GLUE         07
LIQUID_t_GLUE       07
LIQUID_07_NAME      cola
LIQUID_07_CAN       LIQUID_CAN_BOOZE|LIQUID_CAN_EXTINGUISH
LIQUID_07_FORCE     100
LIQUID_07_POOL      i_pool_glue
LIQUID_07_COLOR     0B92

LIQUID_WAX_MELT     08
LIQUID_t_wax_melt   08
LIQUID_08_NAME      cera derretida
LIQUID_08_CAN       0
LIQUID_08_FORCE     0
LIQUID_08_POOL      i_pool_wax
LIQUID_08_COLOR     0B92
LIQUID_08_ITEM      i_dose_wax

LIQUID_WINE         010
LIQUID_t_wine       010
LIQUID_010_NAME     vinho
LIQUID_010_CAN      LIQUID_CAN_DRINK|LIQUID_CAN_BOOZE|LIQUID_CAN_EXTINGUISH
LIQUID_010_FORCE    15
LIQUID_010_POOL     i_pool_wine
LIQUID_010_COLOR    026

LIQUID_CIDER        011
LIQUID_t_cider      011
LIQUID_011_NAME     cidra
LIQUID_011_CAN      LIQUID_CAN_DRINK|LIQUID_CAN_BOOZE|LIQUID_CAN_EXTINGUISH
LIQUID_011_FORCE    5
LIQUID_011_POOL     i_pool_cider
LIQUID_011_COLOR    02C
LIQUID_011_ITEM     i_dose_cider

LIQUID_RUM          012
LIQUID_t_rum        012
LIQUID_012_NAME     rum
LIQUID_012_CAN      LIQUID_CAN_DRINK|LIQUID_CAN_BOOZE
LIQUID_012_FORCE    27
LIQUID_012_POOL     i_pool_rum
LIQUID_012_COLOR    0164
LIQUID_012_ITEM      i_dose_rum

LIQUID_BEER         013
LIQUID_t_beer       013
LIQUID_013_NAME     cerveja
LIQUID_013_CAN      LIQUID_CAN_DRINK|LIQUID_CAN_BOOZE|LIQUID_CAN_EXTINGUISH
LIQUID_013_FORCE    12
LIQUID_013_POOL     i_pool_beer
LIQUID_013_COLOR    0fe
LIQUID_013_ITEM     i_dose_beer

LIQUID_MORVOR       014
LIQUID_t_morvor     014
LIQUID_014_NAME     morvor
LIQUID_014_CAN      LIQUID_CAN_DRINK|LIQUID_CAN_MORVOR|LIQUID_CAN_EXTINGUISH
LIQUID_014_FORCE    3
LIQUID_014_POOL     i_pool_morvor
LIQUID_014_COLOR    0122
LIQUID_014_ITEM     i_dose_morvor

LIQUID_HONEY        015
LIQUID_015_NAME     mel
LIQUID_015_CAN      LIQUID_CAN_DRINK|LIQUID_CAN_EXTINGUISH
LIQUID_015_FORCE    3
LIQUID_015_POOL     i_pool_honey
LIQUID_015_COLOR    032
LIQUID_015_ITEM     i_dose_honey

LIQUID_MILK         016
LIQUID_016_NAME     leite
LIQUID_016_CAN      LIQUID_CAN_DRINK|LIQUID_CAN_EXTINGUISH
LIQUID_016_FORCE    3
LIQUID_016_POOL     i_pool_milk
LIQUID_016_COLOR    0A20
LIQUID_016_ITEM     i_dose_milk

LIQUID_POISON       020
LIQUID_t_poison     020
LIQUID_020_NAME     veneno
LIQUID_020_CAN      LIQUID_CAN_DRINK|LIQUID_CAN_POISON|LIQUID_CAN_EXTINGUISH
LIQUID_020_FORCE    1000
LIQUID_020_POOL     i_pool_poison
LIQUID_020_COLOR    016A
LIQUID_020_BOIL     LIQUID_SWAMP
LIQUID_020_ITEM     i_dose_poison

LIQUID_PRE_WINE     030
LIQUID_030_NAME     fermetado de uva
LIQUID_030_CAN      LIQUID_CAN_EXTINGUISH
LIQUID_030_POOL     i_pool_wine
LIQUID_030_COLOR    026
LIQUID_030_BOIL     LIQUID_WINE

LIQUID_PRE_CIDER    031
LIQUID_031_NAME     fermentado de maca
LIQUID_031_CAN      LIQUID_CAN_EXTINGUISH
LIQUID_031_FORCE    5
LIQUID_031_POOL     i_pool_cider
LIQUID_031_COLOR    02C
LIQUID_031_BOIL     LIQUID_CIDER

LIQUID_PRE_RUM      032
LIQUID_032_NAME     fermentado de frutas
LIQUID_032_CAN      LIQUID_CAN_EXTINGUISH
LIQUID_032_POOL     i_pool_rum
LIQUID_032_COLOR    0164
LIQUID_032_BOIL     LIQUID_RUM

LIQUID_PRE_BEER     033
LIQUID_033_NAME     fermentado de trigo
LIQUID_033_CAN      LIQUID_CAN_EXTINGUISH
LIQUID_033_POOL     i_pool_beer
LIQUID_033_COLOR    0fe
LIQUID_033_BOIL     LIQUID_BEER

LIQUID_PRE_MORVOR   034
LIQUID_034_NAME     fermentado elfico de frutas
LIQUID_034_CAN      LIQUID_CAN_EXTINGUISH
LIQUID_034_POOL     i_pool_morvor
LIQUID_034_COLOR    0122
LIQUID_034_BOIL     LIQUID_MORVOR

//**************************************************************************************************
//**************************************************************************************************
//
//                      TYPEDEFS
//
//**************************************************************************************************
//**************************************************************************************************

[TYPEDEF t_water]
TERRAIN = 01797 017b2
TERRAIN = 0a8   0ab
TERRAIN = 0136  0137

[TYPEDEF t_swamp]
TERRAIN = 07dc 0808
TERRAIN = 083b 0867
TERRAIN = 03d65
TERRAIN = 03dc0 03df1
TERRAIN = 03ef0

[TYPEDEF t_oil]
//Para compatibilidade com sistema de caldeirao.
on=@step
src.DAMAGE <R3,5> DAM_FIRE <src.uid>

[TYPEDEF t_glue]
on=@step
IF (!<argn1>) && (!<src.IsGM>)
 src.sysmessageorange Seus pes estao presos em <name>!
 src.nomove <R7,15>
ENDIF

//Bebidas t_* para compatibilidade com sistema de caldeirao.
[TYPEDEF t_wine]

[TYPEDEF t_cider]

[TYPEDEF t_rum]

[TYPEDEF t_beer]

[TYPEDEF t_morvor]

//Transferir para o skill_poisoning.scp
[TYPEDEF t_poison]


//**************************************************************************************************
// t_pool
//**************************************************************************************************
//tdata1 timer de decay
//more1 flags de ações POOL_*
[TYPEDEF t_pool]
on=@step
IF (!<argn1>) && (!<src.IsGM>)
 IF (<more1>&POOL_STICKY)
  src.sysmessageorange Seus pes estao presos em <name>!
  src.nomove <R7,15>
 ELIF (<more1>&POOL_SLIPPERY)
  emotered escorregou
  src.f_faint
  LOCAL.timer=<R2,5>
  src.nomove <EVAL <LOCAL.timer>+2>
  src.timerf <LOCAL.timer>,f_wakeup
  src.timerf <LOCAL.timer>,emotered,levanta
 ENDIF
ENDIF

on=@dclick
if (<src.distance <UID>> > 1) && (!<SRC.IsGM>) && (<more1>&POOL_FLAMABLE)
 src.sysmessageyellow Chegue mais perto...
 return 1
endif
IF (<more1>&POOL_FLAMABLE)
 pool_take_fire
 return 1
endif

[FUNCTION pool_init]
//O que deveria estar no @create do TYPEDEF, mas TYPEDEF não tem @create ainda
timerf 1,attr,012
timerf 1,timer,<TDATA1>

[FUNCTION pool_take_fire]
 serv.newitem i_fire
 new.p=<p>
 new.timer=<R60,360>
 new.attr=012
 new.effect 2, i_fire_column, 0, 27, 0
 new.sfx 047
 IF (<more1>&POOL_INCINERATE)
  new.timerf <R2,3>,pool_incinerate
 ENDIF
 remove

[FUNCTION pool_incinerate]
foritems 1
 if (<baseid>==i_log) || (<baseid>==i_tabua) || (<baseid>==i_gravetos) || (<baseid>==i_kindling) || ( (<more1>&POOL_INCINERATE) && (<type>==t_pool) )
  IF (!<LOCAL.done>)
   more1 |= POOL_INCINERATE
   pool_take_fire
   LOCAL.done += 1
  endif
 endif
endfor
IF (<local.done>)
 timerf <R4,5>,pooL_incinerate
 emotered alastra
endif


//**************************************************************************************************
// t_recipient
//**************************************************************************************************
//
//  ***CONSTANTS***
//tdata1    máximo de doses
//tdata2    mask [DEFNAME recipients_can] (morez sobrescreve)
//tdata3    id_cheio (opcional)
//
//  ***VARIABLES***
//more1     quantidade atual de doses
//more2     tipo de líquido LIQUID_*
//MOREX     mask *_CAN (herdado [DEFNAME liquid_type])
//MOREY     Força do veneno, morvor ou embreaguês
//MOREZ     mask [DEFNAME recipient_can]. Se não for 0, sobrescreve TDATA2
//      (para preservar o TDATA2 original dos containeres)
//
//  ***TRIGGERS**
//@DClick   Se vazio (<more2>==<more1>==0) e <TDATA2>&RECIPIENT_TARG_EMPTY abre target
//      Se cheio (<more2>==LIQUID_0*) e <TDATA2>&RECIPIENT_TARG_FULL abre target
//      Se <TDATA2>&RECIPIENT_WASH_HANDS faz emote
//      Se <TDATA2>&RECIPIENT_DRINK tenta tomar
//
//@TARGON_CHAR  Se <TDATA2>&RECIPIENT_WET_CHAR molha o target
//
//@TARGON_SELF  Se <TDATA2>&RECIPIENT_WET_CHAR molha o char
//
//@TARGON_ITEM  Se vazio (<more2>==<more1>==0) triga @FillTry
//      Se cheio (<more2>==LIQUID_*) triga @EmptyTry
//
//@TryFill
//
//  IN/OUT
//  <argv[0]>   TYPE do target
//  <argv[1]>   UID do target (0 = static/map)
//
//  RETURN
//  1       Não enche e não faz nada
//  0       Triga @Fill
//
//@Fill
//
//  IN/OUT
//  <argv[0]>   TYPE do target
//  <argv[1]>   UID do target (0 = static/map)
//
//  RETURN
//  1       Não enche e não faz nada
//  0       Triga @FillEnd
//
//@Empty
//          Trigado depois que não há mais doses no recipiente
//FUNCTIONS
//FillRecipient <tipo>,<doses>
//EmptyRecipient
//TransferRecipient <targ.UID>
//ConsumeRecipient <doses>
//PoolRecipient <p.x>,<p.y>,<p.z>,<p.m>
//PourRecipient <p.x>,<p.y>,<p.z>,<p.m>
//IsConteinerRecipient
//DrkinkRecipient

[TYPEDEF t_recipient]
on=@click
IF (<more2>)
 message=@036 <serv.itemdef.<baseid>.name> de <DEF.LIQUID_<more2>_NAME>
ELSE
 message=@036 <serv.itemdef.<baseid>.name> vazio(a)
ENDIF
if (<link>!=04fffffff)
 more1=<link.amount>
endif
message Doses: <eval <more1>>/<eval <tdata1>>
RETURN 1

on=@clientToolTip
if (<link>!=04fffffff)
 more1=<link.amount>
endif
if (<more2>) && (!<more1>)
 more2=
 link=04fffffff
endif
f_sendToolTip <serv.itemdef.<baseid>.name>,(<DEF.LIQUID_<more2>_NAME>)<DEF.BR>Doses: <eval <more1>>/<eval <tdata1>>
return 1

on=@Dropon_ground
//Manter dispid de cheio, se cheio
if (<obj.TDATA3>) && (<more1>)
 obj.dispid=<obj.TDATA3>
 update
endif
//src.sysmessage DropOn_ground SRC:<src.name> ARGN1:<argn1> ARGS:<args>
if (<link>!=04fffffff)
 recip_ReleaseContent
endif

on=@DClick
if (<more2>) && (!<more1>)
 more2=
 link=04fffffff
 update
endif
IF (<topobj>!=<SRC.UID>) && (<DISTANCE <src>> > 1) && (!<SRC.IsGM>)
 src.sysmessageyellow Muito longe...
 return 1
endif
IF (<GetRecipientCan>&RECIPIENT_WASH_HANDS) && (<more2>)
 //Lavar maos e rosto
 src.sfx 023E
 if (<more2>==LIQUID_WATER)
  src.emoteyellow lavando as maos e o rosto
 else
  src.emoteyellow se lava com  <DEF.LIQUID_<MORE2>_NAME>
 endif
 src.anim 17
ELIF (<GetRecipientCan>&RECIPIENT_DRINK) && (<more2>)
 //Bebivel se tiver conteudo
 DrinkRecipient
ELIF (<GetRecipientCan>&RECIPIENT_TARG_EMPTY) && (!<more2>)
 //está vazio e dclick vazio abre target
 src.sysmessageyellow Com o que voce deseja encher <serv.itemdef.<baseid>.name>?
 src.targetfg recip_targ <UID>
ELIF (<GetRecipientCan>&RECIPIENT_TARG_FULL) && (<more2>)
 //está cheio e dclick cheio abre target
 src.sysmessageyellow Aonde voce deseja por <DEF.LIQUID_0<EVAL <more2>>_NAME>?
 src.targetfg recip_targ <UID>
ELSE
 src.sysmessagered Voce nem imagina como usar isso.
endif
return 1

on=@TARGON_GROUND
IF (<src.distance <src.targp>> > 2) && (!<SRC.IsGM>)
 src.sysmessageyellow Muito longe...
 return 1
endif
IF (<more1> < <TDATA1>)
//não-cheio ou vazio.
 IF (<serv.map(<argv[0]>,<argv[1]>).statics(0)>)
  FOR z 0 3
   IF (<serv.map(<argv[0]>,<argv[1]>).statics(<LOCAL.z>)>)
    IF (!<ISEMPTY <DEF.LIQUID_<serv.map(<argv[0]>,<argv[1]>).statics(<LOCAL.z>).type>>>)
     LOCAL.t=<serv.map(<argv[0]>,<argv[1]>).statics(<LOCAL.z>).type>
    ENDIF
   ENDIF
  ENDFOR
  IF (!<LOCAL.t>)
   IF (<more1>)
    src.emoteyellow jogando <DEF.LIQUID_<more2>_NAME> fora
    PourRecipient <src.targp>
    return 1
   else
    sysmessagered Nao ha nada ai.
    return 1
   endif
  ENDIF
 ELSE
  LOCAL.t <serv.map(<argv[0]>,<argv[1]>).type>
 ENDIF
 trigger @TryFill,<DEF.TAT_AS_ARGS>,<LOCAL.t>,0
ELIF (<more1>==<TDATA1>)
 src.emoteyellow jogando <DEF.LIQUID_<more2>_NAME> fora
 PourRecipient <src.targp>
ENDIF

on=@TryFill
IF (<ARGV[1]>)
//Encher a partir de item
 IF (<argv[0]>==t_recipient)
  LOCAL.liquid=<UID.<argv[1]>.more2>
 ELSE
  LOCAL.liquid <DEF.LIQUID_<argv[0]>>
 ENDIF
ELSE
//Encher a partir de static/mapa
 LOCAL.liquid=<DEF.LIQUID_<argv[0]>>
ENDIF
IF (!<ISEMPTY <DEF.LIQUID_<LOCAL.liquid>>>) || (!<LOCAL.liquid>)
 IF (<more1>)
  src.emoteyellow jogando <DEF.LIQUID_<more2>_NAME> fora
  PourRecipient <src.targp>
  return 1
 else
  sysmessagered Nao ha nada ai.
  return 1
 endif
ENDIF
IF (<LOCAL.liquid>!=<more2>) && (<more2>)
//Tem coisa dentro mas é diferente do que quer se por.
 src.sysmessagered <serv.itemdef.<baseid>.name> ja contem <DEF.LIQUID_<MORE2>_NAME>
 return 1
ENDIF
trigger @Fill,<DEF.TAT_AS_ARGS>,<LOCAL.liquid>,<argv[1]>
if  !(<region.tag0.permissions>&TOWN_CAN_RESOURCES)
 if !(<src.ctag.town_<region.defname>_privs>&TOWN_PRIV_RESOURCES)
  src.TRIGGER @CallGuards,<DEF.TAT_AS_ARGN>,<DEF.TOWN_CAN_RESOURCES>
  src.sysmessageyellow Eh bom que voce saiba que nao se pode coletar recursos naturais aqui.
 endif
endif



on=@Fill
//Determina capacidade
LOCAL.cap=<TDATA1>-<more1>

//Determina quanto encher
IF (<ARGV[1]>)
 //tem um item
 obj=<argv[1]>
 if (!<obj.more2>) && (<obj.type>==t_recipient)
  src.sysmessagered <obj.more2> esta vazio.
  return 0
 ELIF (<obj.type>!=t_recipient)
  FillRecipient <argv[0]>,<TDATA1>
  src.sfx 05A4
  src.BOW
  //recip_UpdateContent
 else
  obj.TransferRecipient <uid>
  src.sfx 0240
  //recip_UpdateContent
 endif
ELSE
//Enchendo de tile static/map
 FillRecipient <argv[0]>,<TDATA1>
 recip_UpdateContent
 src.sfx 05A4
 src.BOW
ENDIF
f_renameRecipient
src.EMOTEYELLOW enchendo <serv.itemdef.<baseid>.name> com <DEF.LIQUID_<argv[0]>_NAME>


on=@EmptyRecipient
f_renameRecipient
//recip_ReleaseContent

on=@TARGON_ITEM
IF (<src.targ.uid>==<uid>)
    src.sysmessagered Voce nem imagina como usar isso.
    return 1
endif
IF (!<more2>)
    //vazio
    trigger @TryFill,<DEF.TAT_AS_ARGS>,<SRC.TARG.TYPE>,<SRC.TARG.UID>
ELSE
//Ja tem coisa
    IF (!<IsEmpty <DEF.LIQUID_<src.targ.type>>>) || ( (<src.targ.type>==t_recipient) && (<src.targ.more1>) )
    //por mais conteudo?
        IF (<src.targ.IsContainerRecipient>) && (<src.targ.type>==t_container)
            src.targ.type=<TYPE>
            src.targ.trigger @TryFill,<DEF.TAT_AS_ARGS>,<TYPE>,<UID>  
        ELIF (<src.targ.more2>!=<more2>)
            src.sysmessagered <serv.itemdef.<src.targ.baseid>.name> ja contem <DEF.LIQUID_<src.targ.more2>_NAME>.
            return 1
        ELIF (<src.targ.more1> >= <src.targ.TDATA1>)
            src.sysmessagered <serv.itemdef.<src.targ.baseid>.name> ja esta ate a boca.
            return 1
        ENDIF
        src.targ.trigger @TryFill,<DEF.TAT_AS_ARGS>,<TYPE>,<UID>
    ELIF (<src.targ.type>==t_recipient) && ( (!<src.targ.more1>) || (<src.targ.more2>==<more2>) )
        //transferir conteudo para recipiente vazio
        src.targ.trigger @TryFill,<DEF.TAT_AS_ARGS>,<TYPE>,<UID>  
    ELIF (<src.targ.IsContainerRecipient>) && (<src.targ.type>==t_container)
        //Transferir para container-recipiente
        src.targ.type=<TYPE>
        src.targ.trigger @TryFill,<DEF.TAT_AS_ARGS>,<TYPE>,<UID>
    ELIF (<src.targ.type>==t_fire) || (<src.targ.type>==t_forge)
        IF (<MOREx>&LIQUID_CAN_EXTINGUISH)
            src.targ.more1=
            src.targ.timerd 1
            src.emoteyellow apaga <src.targ.name> com <DEF.LIQUID_<MORE2>_NAME>
            PourRecipient <src.targ.p>
        ELSE
            src.sysmessagered Voce nao pode apagar fogo com este liquido.
        ENDIF
    ELIF (<src.targ.baseid>==i_frasco_oleo_vazio)
        IF (<more2>==LIQUID_OIL)
            src.targ.decrement
            serv.newitem i_frasco_oleo
            src.bounce <new>
            src.emoteyellow guardando oleo em frasco
            ConsumeRecipient 1
            src.sfx 0240
        ELSE
            src.sysmessagered Voce so pode colocar oleo ai.
        ENDIF
    ELSE
        src.sysmessagered Coloque este liquido em um recipiente mais adequado.
    ENDIF
ENDIF

on=@TARGON_CHAR

//tem liquido?
if (<more2>)
    if (<GetRecipientCan>&RECIPIENT_WET_CHAR)
        if (<src.targ.npc>) && (<src.targ.karma> < -2000)
            src.targ.attack <src>
        endif
        if (<src.targ.tag0.pegandofogo>) && (<moreX>&LIQUID_CAN_EXTINGUISH)
            src.targ.tag.pegandofogo=
            src.emoteyellow joga <DEF.LIQUID_<more2>_NAME> no fogo
        else
            src.emoteyellow joga <DEF.LIQUID_<more2>_NAME>
        endif
        src.targ.emotered molhado
        PourRecipient <src.targ.p>
    endif
else
    if (<src.targ.body> == c_vaca) || (<src.targ.body> == c_vaca2)
        src.emoteyellow ordenhando vaca
        src.targ.emoteyellow mooo
        src.targ.cry
        FillRecipient 016,<TDATA1>
        src.sfx 05A4
        src.bow
    else
        src.sysmessagered Nao eh possivel encher um recipiente com seres vivos, no maximo, pode-se tirar leite de uma vaca.
    endif
    return 1
endif

on=@TARGON_SELF
IF (!<more2>)
 src.sysmessagered E o que voce acha que esta fazendo? Vai urinar ou cospir para encher?
 return 1
endif
IF (<GetRecipientCan>&RECIPIENT_WET_CHAR)
 if (<src.tag0.pegandofogo>) && (<moreX>&LIQUID_CAN_EXTINGUISH)
  src.tag.pegandofogo=
  src.emoteyellow joga <DEF.LIQUID_<more2>_NAME> no fogo
 else
  src.emotered se molha com <DEF.LIQUID_<more2>_NAME>
 endif
 PourRecipient <src.p>
ELIF (<GetRecipientCan>&RECIPIENT_TARG_DRINK)
 DrinkRecipient
ENDIF

on=@DropOn_Char
//src.sysmessage DropOn_Char SRC:<src.name> ACT:<act.name> ARGO:<argo.name>
recip_UpdateContent

on=@DropOn_Item
//src.sysmessage DropOn_Item SRC:<src.name> ARGO:<argo.name>
if (<more2>) && (<argo.topobj.IsChar>)
 if  (<argo.inSpecialLayer>)
  recip_ReleaseContent
 else
  if (<cont.topobj> != <argo.topobj>) || (<link>==04fffffff)
   recip_UpdateContent
  endif
 endif
else
 recip_ReleaseContent
endif

ON=@Sell
serv.log Sell 
recip_ReleaseContent

ON=@Buy
serv.log Buy
recip_UpdateContent

on=@DropOn_Self
recip_UpdateContent

[TYPEDEF t_liquid]
on=@Consume
//serv.log AT.Consume <argn> <name>/<link.name>
topobj.timerf 1,recip_UpdatePack

[FUNCTION recip_UpdatePack]
FORCONTTYPE t_recipient
        update
ENDFOR

[FUNCTION recip_targ]
//Bypass de target pq @TARGON_GROUND não triga se for num tile sem static (direto no mapa)

IF (<DISTANCE <TARGP>> > 2) && (!<IsGM>)
 sysmessagered Muito longe...
 return 1
endif

obj=<argv[0]>
if (<argo>)
 src.TARG=<argo.uid>
 IF (<ARGO.IsItem>)
  obj.trigger @TARGON_ITEM,<DEF.TAT_AS_ARGN>,<argo.UID>
 ELIF (<argo.IsChar>)
  IF (<argo.UID>==<src.UID>)
   obj.trigger @TARGON_SELF,<DEF.TAT_AS_ARGS>,<argo.UID>
  ELSE
   obj.trigger @TARGON_CHAR,<DEF.TAT_AS_ARGS>,<argo.UID>
  ENDIF
 ENDIF
else
 src.TARGP=<targp>
 obj.trigger @TARGON_GROUND,<DEF.TAT_AS_ARGS>,<targp.x>,<targp.y>
endif

[FUNCTION ConsumeRecipient]
if (<TYPE>!=t_recipient) || (!<argn>)
 return 0
endif
IF (<argn> >= <more1>)
 LOCAL.ret=<more1>
 EmptyRecipient
ELSE
 LOCAL.ret=<argn>
 more1 -= <argn>
ENDIF
if (<link.uid>)
 if (<more1>)
  link.amount=<more1>
 else
  link.remove
  link=04fffffff
 endif
endif
update
RETURN <LOCAL.ret>

[FUNCTION EmptyRecipient]
if (<TYPE>!=t_recipient)
 return 0
endif
trigger @TryEmptyRecipient
if (<tag0.return>==1)
 tag.return=
 return 1
endif
if (<link>!=04fffffff)
 recip_ReleaseContent
endif
more1=
more2=
dispid=<baseid>
if (<GetRecipientCan>&RECIPIENT_DYE)
 color=
endif
update
trigger @EmptyRecipient
RETURN 1

[FUNCTION FillRecipient]
if (<TYPE> != t_recipient) || (!<argv[0]>) || (!<argv[1]>)
 RETURN 0
ENDIF

IF (<more2>!=<argv[0]>) && (<more2>)
 more2=
 more1=
 morex=
endif
more2=<argv[0]>

LOCAL.space=<eval <TDATA1>-<more1>>

IF (<argv[1]> > <LOCAL.space>)
 LOCAL.ret=<eval <argv[1]>-<LOCAL.space>>
 LOCAL.doses=<TDATA1>
ELSE
 LOCAL.ret=0
 LOCAL.doses=<eval <argv[1]>+<more1>>
ENDIF
more1=<LOCAL.doses>

recip_UpdateContent

morex=<DEF.LIQUID_<more2>_CAN>
morey=<DEF.LIQUID_<more2>_FORCE>

if (<TDATA3>)
 dispid=<TDATA3>
endif
if (<GetRecipientCan>&RECIPIENT_DYE)
 color=<DEF.LIQUID_<argv[0]>_COLOR>
endif
update
trigger @FillEnd
RETURN <LOCAL.ret>

[FUNCTION TransferRecipient]
if (<IsEmpty <argv[0]>>)
 serv.log [RECIPIENTES] <name> (<UID>) transferencia sem destino.
 return 0
endif
if (!<IsNum <argv[0]>>)
 serv.log [RECIPIENTES] <name> (<UID>) referencia do destino nao eh uma UID.
 return 0
endif
obj=<argv[0]>
if (!<obj.IsItem>)
 serv.log [RECIPIENTES] <name> (<UID>) -> <obj.name> (<obj.UID>) destino da transferencia nao eh item.
 return 0
endif
if (<obj.type>!=t_recipient) && (!<obj.IsContainerRecipient>)
 serv.log [RECIPIENTES] <name> (<UID>) -> <obj.name> (<obj.UID>) destino da transferencia nao eh recipiente.
 return 0
endif
if (!<more2>) || (!<more1>)
 serv.log [RECIPIENTES] <name> (<UID>) -> <obj.name> (<obj.UID>) origem da transferencia eh vazia ou invalida.
 return 0
endif
if (<obj.more2> != <more2>) && (<obj.more2>)
 serv.log [RECIPIENTES] <name> (<UID>) -> <obj.name> (<obj.UID>) destino da transferencia tem conteudo destinto.
 return 0
endif

trigger @TryEmptyRecipient
if (<tag0.return>==1)
 tag.return=
 return 1
endif

LOCAL.remain=<obj.FillRecipient <more2>,<more1>>

if (!<Local.Remain>)
 EmptyRecipient
 if (<link>!=04fffffff)
  recip_ReleaseContent 
 endif
ELSE
 more1=<LOCAL.remain>
 recip_UpdateContent
 update
endif

[FUNCTION PoolRecipient]
//PoolRecipient <p.x>,<p.y>,<p.z>,<p.m>
IF (<IsEmpty <ARGV[0]>>) || (<IsEmpty <ARGV[1]>>)
 serv.log [RECIPIENTES] <name> (<UID>) tentando criar poca sem coordenadas.
 return 0
ENDIF
IF (<IsEmpty <ARGV[2]>>) || (<IsEmpty <ARGV[3]>>)
 LOCAL.p=<ARGV[0]>,<ARGV[1]>,<SRC.P.Z>,<SRC.P.M>
ELSE
 LOCAL.p=<ARGV[0]>,<ARGV[1]>,<ARGV[2]>,<ARGV[3]>
ENDIF
serv.newitem <DEF.Liquid_<more2>_POOL>
new.p=<LOCAL.p>

[FUNCTION PourRecipient]
PoolRecipient <args>
EmptyRecipient
src.sfx 0364

[FUNCTION DrinkRecipient]
 SRC.SFX {030 1 031 1 02D6 1 04CC 1 04Cd 1}
 src.emoteyellow tomando <DEF.LIQUID_<MORE2>_name> em <name>
 src.anim 34
 IF (<morex>&LIQUID_CAN_DRINK)
  IF (<morex>&LIQUID_CAN_POISON)
   src.timerf 4,veneno,<morey>
  ELIF (<src.food> < 50)
   IF (!<morey>)
    src.food += 1
   else
    src.food += <morey>
   endif
  ENDIF
 ELSE
  src.timerf 2,cospir
  src.timerf 2,sysmessageorange,Isso tem um gosto bem ruim...
 endif
 ConsumeRecipient 1

[FUNCTION IsContainerRecipient]
IF (<dispiddec>==0E77) || (<dispiddec>==0E7B) || (<dispiddec>==0E83) || (<dispiddec>==0154D)
 RETURN 1
ELSE
 RETURN 0
ENDIF

[FUNCTION GetRecipientCan]
IF (<morez>)
 return <morez>
else
 return <TDATA2>
endif

[FUNCTION f_renameRecipient]
if (<type>==t_container)
 name=<serv.itemdef.<baseid>.name>
elif (<more1>)
 name=<serv.itemdef.<baseid>.name> com <eval <more1>> doses de <DEF.LIQUID_<more2>_NAME>
else
 name=<serv.itemdef.<baseid>.name> vazio(a)
endif

[FUNCTION recip_UpdateContent]
//Faz correções na memory que contem o conteúdo
if (!<topobj.IsChar>)
 return 0
endif
//Tem conteúdo?
if (<more2>)
 if (<link>!=04fffffff)  //já existe memory
 //em char
  if (<topobj.IsChar>)
  //banco?
   if (<inSpecialLayer>)
    recip_ReleaseContent
   elif (<topobj>!=<link.topobj>)
    link.cont=<topobj>
   endif
   link.amount=<more1>
  else
   recip_ReleaseContent
  endif
 else //Não existe memory
  if (<topobj.IsChar>)
  //banco?
   if (<argo>)
    if (<inSpecialLayer>)
     src.newitem <DEF.LIQUID_<more2>_ITEM>
     src.equip <new>
     new.amount <more1>
     link=<new>
    endif
   else
    src.newitem <DEF.LIQUID_<more2>_ITEM>
    src.equip <new>
    new.amount <more1>
    link=<new>
    new.link=<uid>
   endif
  endif
 endif
else //vazio
 if (<link>!=04fffffff)
  recip_ReleaseContent
 endif
endif

[FUNCTION recip_ReleaseContent]
//Tira do player a memory que contém o conteúdo
link.remove
link=04fffffff

//**************************************************************************************************
//**************************************************************************************************
//
//                      ITEMDEFS
//
//**************************************************************************************************
//**************************************************************************************************

//**************************************************************************************************
// i_pool
//**************************************************************************************************
[ITEMDEF i_pool]
ID=POOL
type=t_pool
name=poca d'agua
TDATA1=60

on=@create
color=05f
pool_init

//**************************************************************************************************
// i_dose_water
//**************************************************************************************************
[ITEMDEF i_dose_water]
ID=0e24
NAME=Dose%s% de agua
TYPE=t_eq_script
LAYER=layer_special
WEIGHT=1.0
TEVENTS=t_liquid

on=@create
color=04f2


//**************************************************************************************************
// i_pool_chuva
//**************************************************************************************************
[ITEMDEF i_pool_chuva]
ID=POOL
type=t_pool
name=poca d'agua
TDATA1=7

on=@create
tag.nosave=1
color=05f
pool_init

//**************************************************************************************************
// i_pool_blood
//**************************************************************************************************
[ITEMDEF i_pool_blood]
ID=POOL
type=t_pool
name=poca de sangue
TDATA1=60

on=@create
color=0
pool_init

//**************************************************************************************************
// i_dose_blood
//**************************************************************************************************
[ITEMDEF i_dose_blood]
ID=0e24
NAME=Dose%s% de sangue
TYPE=t_eq_script
LAYER=layer_special
WEIGHT=1.6
TEVENTS=t_liquid

ON=@Create
color=026

//**************************************************************************************************
// i_pool_glue
//**************************************************************************************************
[ITEMDEF i_pool_glue]
ID=POOL
name=poca de cola
TYPE=t_pool
TDATA1=120

on=@create
MORE1=POOL_STICKY
color=0b92
pool_init

//**************************************************************************************************
// i_dose_glue
//**************************************************************************************************
[ITEMDEF i_dose_glue]
ID=0e24
NAME=Dose%s% de cola
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_liquid
WEIGHT=2.1

ON=@Create
color=0B92

//**************************************************************************************************
// i_pool_tar
//**************************************************************************************************
[ITEMDEF i_pool_tar]
ID=POOL
name=poca de piche
TYPE=t_pool
TDATA1=120

on=@create
MORE1=POOL_SLIPPERY|POOL_INCINERATE
color=035e
pool_init

//**************************************************************************************************
// i_dose_tar
//**************************************************************************************************
[ITEMDEF i_dose_tar]
ID=0e24
NAME=Dose%s% de pixe
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_liquid
WEIGHT=5.0

ON=@Create
color=035E

//**************************************************************************************************
// i_pool_oil
//**************************************************************************************************
[ITEMDEF i_pool_oil]
ID=POOL
name=poca de oleo
TYPE=t_pool
TDATA1=120

on=@create
MORE1=POOL_SLIPPERY|POOL_FLAMABLE|POOL_INCINERATE
color=02b4
pool_init

//**************************************************************************************************
// i_dose_tar
//**************************************************************************************************
[ITEMDEF i_dose_oil]
ID=0e24
NAME=Dose%s% de oleo
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_liquid
WEIGHT=0.8

ON=@Create
color=02B4

//**************************************************************************************************
// i_pool_swamp
//**************************************************************************************************
[ITEMDEF i_pool_swamp]
ID=POOL
name=poca de agua de pantano
TYPE=t_pool
TDATA1=120

on=@create
MORE1=0
color=08A2
pool_init

//**************************************************************************************************
// i_dose_swamp
//**************************************************************************************************
[ITEMDEF i_dose_swamp]
ID=0e24
NAME=Dose%s% de agua de pantano
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_liquid
WEIGHT=1.0

ON=@Create
color=08A2

//**************************************************************************************************
// i_pool_wax
//**************************************************************************************************
[ITEMDEF i_pool_wax]
ID=POOL
name=poca de cera dura
TYPE=t_pool
TDATA1=60

on=@create
MORE1=0
color=0B92
pool_init

//**************************************************************************************************
// i_dose_wax
//**************************************************************************************************
[ITEMDEF i_dose_wax]
ID=0e24
NAME=Dose%s% de cera
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_liquid
WEIGHT=4.2

ON=@Create
color=0B92

//**************************************************************************************************
// i_pool_wine
//**************************************************************************************************
[ITEMDEF i_pool_wine]
ID=POOL
name=poca de vinho
TYPE=t_pool
TDATA1=60

on=@create
MORE1=0
color=013A
pool_init

//**************************************************************************************************
// i_dose_wine
//**************************************************************************************************
[ITEMDEF i_dose_wine]
ID=0e24
NAME=Dose%s% de vinho
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_liquid
WEIGHT=0.9

ON=@Create
color=013A

//**************************************************************************************************
// i_pool_cider
//**************************************************************************************************
[ITEMDEF i_pool_cider]
ID=POOL
name=poca de cidra
TYPE=t_pool
TDATA1=60

on=@create
MORE1=0
color=092
pool_init

//**************************************************************************************************
// i_dose_cider
//**************************************************************************************************
[ITEMDEF i_dose_cider]
ID=0e24
NAME=Dose%s% de cidra
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_liquid
WEIGHT=0.9

ON=@Create
color=016A

//**************************************************************************************************
// i_pool_rum
//**************************************************************************************************
[ITEMDEF i_pool_rum]
ID=POOL
name=poca de rum
TYPE=t_pool
TDATA1=60

on=@create
MORE1=0
color=0164
pool_init

//**************************************************************************************************
// i_dose_rum
//**************************************************************************************************
[ITEMDEF i_dose_rum]
ID=0e24
NAME=Dose%s% de rum
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_liquid
WEIGHT=0.9

ON=@Create
color=0164

//**************************************************************************************************
// i_pool_beer
//**************************************************************************************************
[ITEMDEF i_pool_beer]
ID=POOL
name=poca de cerveja
TYPE=t_pool
TDATA1=60

on=@create
MORE1=0
color=0fe
pool_init

//**************************************************************************************************
// i_dose_beer
//**************************************************************************************************
[ITEMDEF i_dose_beer]
ID=0e24
NAME=Dose%s% de cerveja
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_liquid
WEIGHT=0.9

ON=@Create
color=0FE

//**************************************************************************************************
// i_pool_morvor
//**************************************************************************************************
[ITEMDEF i_pool_morvor]
ID=POOL
name=poca de morvor
TYPE=t_pool
TDATA1=60

on=@create
MORE1=0
color=0122
pool_init

//**************************************************************************************************
// i_dose_morvor
//**************************************************************************************************
[ITEMDEF i_dose_morvor]
ID=0e24
NAME=Dose%s% de morvor
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_liquid
WEIGHT=0.6

ON=@Create
color=0122

//**************************************************************************************************
// i_pool_poison
//**************************************************************************************************
[ITEMDEF i_pool_poison]
ID=POOL
name=poca de veneno
TYPE=t_pool
TDATA1=60

on=@create
MORE1=0
color=016A
pool_init

//**************************************************************************************************
// i_dose_poison
//**************************************************************************************************
[ITEMDEF i_dose_poison]
ID=0e24
NAME=Dose%s% de veneno
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_liquid
WEIGHT=3.0

ON=@Create
color=016A

//**************************************************************************************************
// i_pool_honey
//**************************************************************************************************
[ITEMDEF i_pool_honey]
ID=POOL
name=poca de mel
TYPE=t_pool
TDATA1=12

on=@create
MORE1=0
color=032
pool_init

//**************************************************************************************************
// i_dose_honey
//**************************************************************************************************
[ITEMDEF i_dose_honey]
ID=0e24
NAME=Dose%s% de mel
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_liquid
WEIGHT=2.2

ON=@Create
color=032

//**************************************************************************************************
// i_pool_milk
//**************************************************************************************************
[ITEMDEF i_pool_milk]
ID=POOL
name=poca de leite
TYPE=t_pool
TDATA1=60

on=@create
MORE1=0
color=0A20
pool_init

//**************************************************************************************************
// i_dose_milk
//**************************************************************************************************
[ITEMDEF i_dose_milk]
ID=0e24
NAME=Dose%s% de leite
TYPE=t_eq_script
LAYER=layer_special
TEVENTS=t_liquid
WEIGHT=1.8

ON=@Create
color=0A20

//**************************************************************************************************
// i_balde
//**************************************************************************************************
[ITEMDEF i_balde]
ID=014E0
NAME=balde
TYPE=t_recipient
SKILLMAKE=35.0 Carpentry
RESOURCES=1 i_madeiraenvergada, 12 i_pregos
VALUE=12
WEIGHT=5.0
TDATA1=10
TDATA2=RECIPIENT_TARG_FULL|RECIPIENT_TARG_EMPTY|RECIPIENT_WET_CHAR|RECIPIENT_WASH_BANDAGE
TDATA3=0FFA

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Balde

on=@create
f_renameRecipient

//**************************************************************************************************
// i_garrafa
//**************************************************************************************************
[ITEMDEF i_garrafa]
ID=099b
NAME=garrafa
SKILLMAKE=35.0 Tinkering
RESOURCES=2 i_areia
VALUE=8
FLIP=0
PILE=1
TYPE=t_recipient
WEIGHT=0.8
TDATA1=8
TDATA2=RECIPIENT_TARG_FULL|RECIPIENT_DYE|RECIPIENT_TARG_DRINK|RECIPIENT_TARG_EMPTY
TDATA3=099b

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Garrafa

on=@create
f_renameRecipient

on=@Dropon_ground
//Manter dispid de garrafa
dispid=<TDATA3>
update

//**************************************************************************************************
// i_cantil
//**************************************************************************************************
[ITEMDEF i_cantil]
ID=09d6
NAME=cantil
SKILLMAKE=12.3 Tailoring
RESOURCES=1 i_couro_curtido, 1 i_tread
VALUE=7
TYPE=t_recipient
WEIGHT=0.6
TDATA1=5
TDATA2=RECIPIENT_DRINK|RECIPIENT_TARG_EMPTY|RECIPIENT_TARG_DRINK|RECIPIENT_TARG_FULL

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Cantil

on=@create
f_renameRecipient

//**************************************************************************************************
// i_bacia
//**************************************************************************************************
[ITEMDEF i_bacia]
ID=i_basin
NAME=bacia
SKILLMAKE=22.8 Tinkering
RESOURCES=3 i_areia
VALUE=12
TYPE=t_recipient
WEIGHT=2.0
TDATA1=5
TDATA2=RECIPIENT_TARG_FULL|RECIPIENT_TARG_EMPTY|RECIPIENT_WET_CHAR|RECIPIENT_WASH_BANDAGE
TDATA3=01008

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Bacia

on=@FillEnd
if (<more2>==LIQUID_BLOOD)
 dispid=i_water_bloody
ELIF (<more2>)
 dispid=<TDATA3>
ENDIF
update


on=@Dropon_ground
//Manter dispid de cheio de sangue, se cheio de sangue
if (<more2>==LIQUID_BLOOD)
 dispid=i_water_bloody
ELIF (<more2>)
 dispid=<TDATA3>
ENDIF
update

on=@create
f_renameRecipient

//**************************************************************************************************
// i_caneca_vidro
//**************************************************************************************************
[ITEMDEF i_caneca_vidro]
ID=01F82
NAME=caneca
SKILLMAKE=58.6 Tinkering
RESOURCES=1 i_areia
VALUE=7
TYPE=t_recipient
WEIGHT=0.1
TDATA1=3
TDATA2=RECIPIENT_TARG_EMPTY|RECIPIENT_DYE|RECIPIENT_TARG_DRINK|RECIPIENT_TARG_FULL

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Caneca de vidro

on=@create
f_renameRecipient

//**************************************************************************************************
// i_caneca_ceramica
//**************************************************************************************************
[ITEMDEF i_caneca_ceramica]
ID=0995
NAME=caneca
SKILLMAKE=7.5 Tinkering
VALUE=1
RESOURCES=1 i_areia
TYPE=t_recipient
WEIGHT=0.2
TDATA1=3
TDATA2=RECIPIENT_TARG_EMPTY|RECIPIENT_TARG_DRINK|RECIPIENT_TARG_FULL

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Caneca de ceramica

on=@create
f_renameRecipient

//**************************************************************************************************
// i_taca_prata
//**************************************************************************************************
[ITEMDEF i_taca_prata]
ID=i_goblet
NAME=taca de prata
SKILLMAKE=64.1 Blacksmithing
RESOURCES=1 i_ingot_silver
VALUE=50
TYPE=t_recipient
WEIGHT=1.0
TDATA1=3
TDATA2=RECIPIENT_TARG_EMPTY|RECIPIENT_TARG_DRINK|RECIPIENT_TARG_FULL

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Taça de prata

on=@create
f_renameRecipient

//**************************************************************************************************
// i_taca_ouro
//**************************************************************************************************
[ITEMDEF i_taca_ouro]
ID=i_goblet
NAME=taca de ouro
SKILLMAKE=81.3 Blacksmithing
RESOURCES=1 i_ingot_gold
VALUE=85
TYPE=t_recipient
WEIGHT=3.0
TDATA1=3
TDATA2=RECIPIENT_TARG_EMPTY|RECIPIENT_TARG_DRINK|RECIPIENT_TARG_FULL

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Taça de ouro

on=@create
f_renameRecipient

//**************************************************************************************************
// i_jarra_ceramica
//**************************************************************************************************
[ITEMDEF i_jarra_ceramica]
ID=09C8
NAME=moringa
SKILLMAKE=22.2 Tinkering
RESOURCES=3 i_areia
VALUE=10
TYPE=t_recipient
WEIGHT=2.0
TDATA1=15
TDATA2=RECIPIENT_TARG_FULL|RECIPIENT_TARG_EMPTY|RECIPIENT_TARG_DRINK

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Moringa

on=@create
f_renameRecipient

//**************************************************************************************************
// i_jarra_vidro
//**************************************************************************************************
[ITEMDEF i_jarra_vidro]
ID=0ff9
NAME=jarra de vidro
SKILLMAKE=64.7 Tinkering
RESOURCES=4 i_areia
VALUE=10
TYPE=t_recipient
WEIGHT=2.0
TDATA1=15
TDATA2=RECIPIENT_TARG_FULL|RECIPIENT_TARG_EMPTY|RECIPIENT_DYE|RECIPIENT_WET_CHAR

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Jarra de vidro

on=@create
f_renameRecipient

//**************************************************************************************************
// i_barrilete
//**************************************************************************************************
[ITEMDEF i_barrilete]
ID=i_keg_wood
NAME=barrilete
SKILLMAKE=62.7 Tinkering
RESOURCES=1 i_torneira, 1 i_madeiraenvergada, 10 i_pregos
VALUE=3.2
TYPE=t_recipient
WEIGHT=50
TDATA1=30
TDATA2=RECIPIENT_TARG_FULL|RECIPIENT_TARG_DRINK

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Barrilete

on=@create
f_renameRecipient

//**************************************************************************************************
// i_barrel_open
//**************************************************************************************************
[ITEMDEF 0e77]
DEFNAME=i_barrel_open
NAME=barril
SKILLMAKE=82.5 CARPENTRY
RESOURCES=3 i_madeiraenvergada, 40 i_pregos
VALUE=100
TYPE=T_CONTAINER
TDATA1=100
TDATA2=03E
TDATA3=0154D
FLIP=1
WEIGHT=97.5

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Barril

on=@Fill
morez=RECIPIENT_WASH_HANDS|RECIPIENT_WASH_BANDAGE

on=@EmptyRecipient
IF (!<more1>) && (!<more2>)
 TYPE=t_container
 f_renameRecipient
 update
ENDIF

//**************************************************************************************************
// i_tub_empty
//**************************************************************************************************
[ITEMDEF 0e83]
//empty tub
DEFNAME=i_tub_empty
NAME=bacia
SKILLMAKE=55.2 Carpentry
RESOURCES=2 i_madeiraenvergada, 25 i_pregos
VALUE=23
TYPE=T_CONTAINER
TDATA2=03E
FLIP=1
WEIGHT=1.3
TDATA1=30
TDATA3=0FAB

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Bacia

CATEGORY=Provisions - Miscellaneous
SUBSECTION=Containers
DESCRIPTION=Empty Tub

on=@Fill
morez=RECIPIENT_WASH_HANDS|RECIPIENT_WASH_BANDAGE|RECIPIENT_DYE

on=@FillEnd
IF (<more2>==LIQUID_WATER)
 dispid=0e7b
ELSE
 color=<DEF.LIQUID_<more2>_COLOR>
endif
update

on=@Dropon_ground
//Manter dispid de cheio, se cheio
morez=RECIPIENT_WASH_HANDS|RECIPIENT_WASH_BANDAGE|RECIPIENT_DYE
IF (<more2>==LIQUID_WATER)
 dispid=0e7b
 update
endif

on=@EmptyRecipient
IF (!<more1>) && (!<more2>)
 TYPE=t_container
 f_renameRecipient
 update
ENDIF


//**************************************************************************************************
// i_vaso_oleo
//**************************************************************************************************
[ITEMDEF i_vaso_oleo]
ID=0b46
NAME=Vaso de metal
SKILLMAKE=13.6 Blacksmithing
RESOURCES=1 i_plate_iron
VALUE=2.7
TYPE=T_RECIPIENT
WEIGHT=38
TDATA1=18
TDATA2=RECIPIENT_TARG_FULL|RECIPIENT_TARG_EMPTY|RECIPIENT_WET_CHAR

CATEGORY=MyT - Sistemas
SUBSECTION=Recipientes
DESCRIPTION=Jarra para óleo

on=@create
color=03be
f_renameRecipient

//**************************************************************************************************

//**************************************************************************************************
// Itens para loot
//**************************************************************************************************

//**************************************************************************************************


//**************************************************************************************************
// i_garrafa_vinho
//**************************************************************************************************

[ITEMDEF i_garrafa_vinho]
ID=099b
NAME=garrafa
RESOURCES=2 i_areia
TYPE=t_recipient
WEIGHT=1
TDATA1=8
TDATA2=RECIPIENT_TARG_FULL|RECIPIENT_DYE|RECIPIENT_TARG_DRINK|RECIPIENT_TARG_EMPTY

CATEGORY=MyT - Provisions - miscellaneous
SUBSECTION=Bebidas
DESCRIPTION=Garrafa de vinho

on=@create
FillRecipient <DEF.LIQUID_WINE>,<R2,8>
f_renameRecipient

[ITEMDEF i_garrafa_cidra]
ID=099b
NAME=garrafa
RESOURCES=2 i_areia
TYPE=t_recipient
WEIGHT=1
TDATA1=8
TDATA2=RECIPIENT_TARG_FULL|RECIPIENT_DYE|RECIPIENT_TARG_DRINK|RECIPIENT_TARG_EMPTY

CATEGORY=MyT - Provisions - miscellaneous
SUBSECTION=Bebidas
DESCRIPTION=Garrafa de cidra

on=@create
FillRecipient <DEF.LIQUID_CIDER>,<R2,8>
f_renameRecipient

[ITEMDEF i_garrafa_rum]
ID=099b
NAME=garrafa
RESOURCES=2 i_areia
TYPE=t_recipient
WEIGHT=1
TDATA1=8
TDATA2=RECIPIENT_TARG_FULL|RECIPIENT_DYE|RECIPIENT_TARG_DRINK|RECIPIENT_TARG_EMPTY

CATEGORY=MyT - Provisions - miscellaneous
SUBSECTION=Bebidas
DESCRIPTION=Garrafa de rum

on=@create
FillRecipient <DEF.LIQUID_RUM>,<R2,8>
f_renameRecipient

[ITEMDEF i_garrafa_cerveja]
ID=099b
NAME=garrafa
RESOURCES=2 i_areia
TYPE=t_recipient
WEIGHT=1
TDATA1=8
TDATA2=RECIPIENT_TARG_FULL|RECIPIENT_DYE|RECIPIENT_TARG_DRINK|RECIPIENT_TARG_EMPTY

CATEGORY=MyT - Provisions - miscellaneous
SUBSECTION=Bebidas
DESCRIPTION=Garrafa de cerveja

on=@create
FillRecipient <DEF.LIQUID_BEER>,<R2,8>
f_renameRecipient

[eof]
