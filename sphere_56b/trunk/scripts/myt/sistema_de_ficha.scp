//*****************************************************************************
//*****************************************************************************
//
// SISTEMA DE FICHA por 
//      Galthar 
//      Pomp
//      UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************
//TODO:

//TAGS usadas:


//CREATE TABLE  `mytserver`.`rpGrade` (
//  `idGrade` int(10) unsigned NOT NULL auto_increment,
//  `timestamp` timestamp NOT NULL default CURRENT_TIMESTAMP on update CURRENT_TIMESTAMP,
//  `charUid` int(10) unsigned NOT NULL,
//  `charAcct` int(10) unsigned NOT NULL,
//  `gmUid` int(10) unsigned NOT NULL,
//  `gmAcct` int(10) unsigned NOT NULL,
//  `grade` smallint(6) NOT NULL,
//  PRIMARY KEY  (`idGrade`),
//  UNIQUE KEY `Index_2` USING BTREE (`charUid`,`gmAcct`)
//) ENGINE=MyISAM AUTO_INCREMENT=1 DEFAULT CHARSET=latin1;

[DEFNAME ficha_emote]
f_emote1    vomitar
f_emote2    arrotar
f_emote3    tossir
f_emote4    espirrar
f_emote5    peidar
f_emote6    rir
f_emote7    observar
f_emote8    gritar
f_emote9    chamar
f_emote10   afastar
f_emote11   ola
f_emote12   saudar
f_emote13   comemorar
f_emote14   susto
f_emote15   confuso
f_emote16   surpreso
f_emote17   ops
f_emote18   cospir
f_emote19   erguer

[PLEVEL 2]
ficha
prender

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// ficha
//*****************************************************************************
//usado pelos GMs
[FUNCTION ficha]
IF (!<argo>)
    sysmessageyellow Selecione o Jogador:
    targetf ficha
ELIF (!<argo.IsPlayer>)
    sysmessagered Jogador invalido!
ELSE
    ctag.admin_player=<argo>
    ctag0.admin_action=ADMIN_PAGE_CHAR
    dialogclose d_sphereadmin
    dialog d_sphereadmin
ENDIF

[FUNCTION ficha_]
if (<argo.isplayer>)
    src.targ=<argo>
    
    DB.QUERY "SELECT char2Uid FROM eventsLog WHERE charUid=<eval <argo>> AND eventType='Kill PC' ORDER BY timestamp DESC LIMIT 1"
    if (<DB.ROW.NUMROWS> > 0)
        obj=<DB.ROW.0.char2Uid>
        src.ctag.ficha_lastkillname=<obj.name>
        src.ctag.ficha_lastkilluid=<obj>
    else
        src.ctag.ficha_lastkillname=N/A
        src.ctag.ficha_lastkilluid=0
    endif
    
    src.ctag.ficha_lastagr1name=N/A
    src.ctag.ficha_lastagr2name=N/A
    src.ctag.ficha_lastagr1uid=0
    src.ctag.ficha_lastagr2uid=0
    DB.QUERY "SELECT charUid FROM eventsLog WHERE char2Uid=<eval <argo>> AND charAcct != 0 AND eventType='Kill PC' ORDER BY timestamp DESC LIMIT 2"
    if (<DB.ROW.NUMROWS> > 0)
        for R 0 <eval <DB.ROW.NUMROWS>-1>
            obj=<DB.ROW.<eval <LOCAL.R>>.charUid>
            TRY src.ctag.ficha_lastagr<eval <LOCAL.R>+1>name=<obj.name>
            TRY src.ctag.ficha_lastagr<eval <LOCAL.R>+1>uid=<obj>
        end
    endif
    
    DB.QUERY "SELECT count(char2Uid) AS cnt FROM eventsLog WHERE charUid=<eval <argo>> AND eventType='Kill PC'"
    src.ctag.ficha_pkCount=<DB.ROW.0.cnt>

    for R 1 8
        TRY src.ctag.comment<eval <LOCAL.R>>GM=N/A
        TRY src.ctag.comment<eval <LOCAL.R>>data=N/A
        TRY src.ctag.comment<eval <LOCAL.R>>=N/A
    end
    DB.QUERY "SELECT message, DATE_FORMAT(timestamp,'%d.%m.%y') AS t, username FROM eventsLog LEFT JOIN accounts ON (charAcct=idAcct) WHERE char2Uid=<eval <argo>> AND eventType='GM Note' ORDER BY timestamp DESC LIMIT 8"
    if (<DB.ROW.NUMROWS> > 0)
        for R 1 <DB.ROW.NUMROWS>
            TRY src.ctag.comment<eval <LOCAL.R>>GM= <DB.ROW.<eval <LOCAL.R>-1>.username>
            TRY src.ctag.comment<eval <LOCAL.R>>data= <DB.ROW.<eval <LOCAL.R>-1>.t>
            TRY src.ctag.comment<eval <LOCAL.R>>= <DB.ROW.<eval <LOCAL.R>-1>.message>
        end
    endif
    
    src.targ.rp_updateGrade
    src.targ.rp_getGmGrade <src.tag0.idAcct>
    
    dialog d_ficha_1
else
    sysmessageyellow Nao eh um jogador valido.
endif

//*****************************************************************************
[FUNCTION admin_banAccount]
//*****************************************************************************
//Function para banir player
obj=<ctag0.admin_player>
if (<argn>==<ctag0.chalenge>)
    if (<obj.account.plevel> < <account.plevel>)
        if (!<obj.account.block>)
            trysrv obj.kick
            sb Conta <obj.account> banida!
            serv.log [BANIMENTO] Conta <obj.account.name> banida por '<src.account.name>'!!!
        else
            obj.account.block=0
            sb Conta '<obj.account>' reincluida!
            serv.log [BANIMENTO] Conta '<obj.account.name>' DESbanida por '<src.account.name>'!!!
    else
        sysmessagered Voce nao tem privilegio para isso.
    endif
else
    sysmessagered Codigo errado.
    dialog d_sphereadmin
endif

//*****************************************************************************
[FUNCTION admin_RPnote]
//*****************************************************************************
//Adiciona uma nota <args> ao RP do char <ctag.admin_player>
if (<eval strlen(<args>)> < 6)
    sysmessagered Nota muito curta!
else
    event_RPnote <ctag.admin_player>,<args>
endif
DIALOG d_sphereadmin

//*****************************************************************************
[FUNCTION admin_GMnote]
//*****************************************************************************
//Adiciona uma nota <args> à conta do char <ctag.admin_player>
if (<eval strlen(<args>)> < 6)
    sysmessagered Nota muito curta!
else
    event_note <ctag.admin_player>,<args>
endif
DIALOG d_sphereadmin

//*****************************************************************************
// prender ([motivo])
//*****************************************************************************
//usado pelos GMs
[FUNCTION prender]
sysmessageyellow Selecione o Jogador:
targetfg prender_

[FUNCTION prender_]
if (<argo.isplayer>)
    ctag.admin_player=<argo>
    DB.QUERY "SELECT count(idEvent) AS cnt FROM eventsLog WHERE char2Acct=<argo.dtag0.idAcct> AND eventType='Jail'"
    ctag.admin_jail_h=<MAXIMUM <DB.ROW.0.cnt>,1>
    dialog d_prender
else
    sysmessageyellow Nao eh um jogador valido.
endif

//*****************************************************************************
// jail_Prompt (UID)
//*****************************************************************************
[FUNCTION jail_Prompt]
//Dialog para prender o jogador <argn>
obj=<argn>
if (!<obj.IsPlayer>)
    return
elif (!<obj.IsOnline>)
    return
endif
ctag.admin_player=<obj>
DB.QUERY "SELECT count(idEvent) AS cnt FROM eventsLog WHERE char2Acct=<obj.dtag0.idAcct> AND eventType='Jail'"
ctag.admin_jail_h=<MAXIMUM <DB.ROW.0.cnt>,1>
dialog d_prender

//***********************************************************
[FUNCTION macroer]
//***********************************************************
//Prende e penaliza um jogador por macro
if (!<argo>) && (!<argn>)
    sysmessageyellow Selecione o delinquente:
    targetf macroer
elif (!<argo.IsPlayer>) && (!<uid.<argn>.IsPlayer>)
    sysmessagered Jogador invalido.
else
    if (<argo>)
        obj=<argo>
    else
        obj=<argn>
    endif
    ctag.macroer_hours=<eval 4+<obj.account.tag0.macro_count>>
    ctag.macroer_minutes=0
    ctag.macroer_penalty=-100
    ctag.macroer=<obj>
    dialog d_macroer
endif

//***********************************************************
[FUNCTION macroer_Resume]
//***********************************************************
// Cria resumo do dialog de macroer
ref1=<ctag.macroer>
local.t = <ref1.tag.name> [<ref1.account>] ja foi preso e penalizado por macro <ref1.account.dtag0.macro_count> vezes.<DEF.BR>
local.t .= Isso fara ele ficar preso pelo tempo descrito abaixo, ganhar uma anotacao na ficha e perder skill, na quantidade descrita abaixo e das skills escolhidas abaixo.
return <local.t>

//***********************************************************
[FUNCTION macroer_skill]
//***********************************************************
//Lista skills para descer no macroer
ref1=<ctag.macroer>
list_new Escolha uma skill para penalizar o macroer
list_newCol 20,Skill
list_newCol 200,valor atual
cTAG.list_action=macroer_addSkill
cTAG.list_numItems=54
cTAG.list_numPages=4
for s 0 54
 try cTAG.list_row<hval <local.s>+1>_01=<serv.skill.<dlocal.s>.name>
 try cTAG.list_row<hval <local.s>+1>_02=<ref1.<dlocal.s>>
end
list_show 1

//***********************************************************
[FUNCTION macroer_addSkill]
//***********************************************************
// Adiciona a skill <argn> no dialog do macroer
local.i=0
WHILE (!<IsEmpty <cTAG.macroer_skill_<dlocal.i>>>)
    local.i += 1
ENDWHILE
TRY cTAG.macroer_skill_<dlocal.i>=<eval <ARGN>-1>
dialogclose d_macroer
dialog d_macroer

//***********************************************************
[FUNCTION macroer_DeleteSkill]
//***********************************************************
//Remove a skill de indice <argn> da lista de skills de um macroer
local.n=<argn>

//Grava cash excluindo o item
local.i=0
local.j=0
WHILE (!<IsEmpty <cTAG.macroer_skill_<dlocal.i>>>)
    IF (<local.i>!=<local.n>)
        TRY local.skill_<dlocal.j>=<cTAG.macroer_skill_<dlocal.i>>
        local.j += 1
    ENDIF
    TRY cTAG.macroer_skill_<dlocal.i>=
    local.i += 1
ENDWHILE

//Joga o cash nas cTAGS
if (<local.j>)
    FOR i 0 <eval <local.j>-1>
        TRY cTAG.macroer_skill_<dlocal.i>=<local.skill_<dlocal.i>>
    ENDFOR
ENDIF
dialogclose d_macroer
dialog d_macroer

//*****************************************************************************
// detem(tempo,jailer,motivo)
//*****************************************************************************
//usado pelos sistemas
[FUNCTION detem]
if (<restest 1 i_mry_GM_jail>)
    sysmessagered Infracao detectada. Prisao aumentada em 10 minutos.
    findid.i_mry_gm_jail.more1=<findid.i_mry_gm_jail.more1>+600
    return 1
endif
LOCAL.t=<eval (<tag0.prisao_total>/2)+<argv0>>
sysmessagered Preso por <f_timestring <LOCAL.t>>.
sysmessagered Motivo: <argv2>

serv.newitem i_mry_gm_jail
new.cont=<uid>
new.morep=<p>
new.more1=<LOCAL.t>
new.timer=1
tag.prisao_total=<eval <tag0.prisao_total>+<argv0>>
//dorand (3)
// go=5291,1166
// go=5284,1166
// go=5277,1166
//enddo
go 5275,1164

if (<DB.connected>)
    REF1=<argv1>
    if (<REF1>)
        LOCAL.u=<REF1>
        LOCAL.n=<REF1.account>
    else
        LOCAL.u=0
        LOCAL.n=Servidor
    endif
    //DB.EXECUTE "INSERT INTO jail SET timestamp=NOW(),uidJailed=<eval <uid>>,jailed='<account>',duration=<eval <argv0>>,reason='<argv2>',uidJailer=<eval <LOCAL.u>>,jailer='<LOCAL.n>'"
    DB.EXECUTE "INSERT INTO eventsLog SET timestamp=NOW(), eventType='Jail',charUid=<eval <REF1>>,charAcct=<eval <REF1.tag0.idAcct>>,x=<eval <p.x>>,y=<eval <p.y>>,z=<eval <p.z>>,char2Uid=<eval <uid>>,char2Acct=<eval <tag0.idAcct>>,message='<DB.ESCAPEDATA <f_timestring <argv0>>: <argv2>>'"
endif

//*****************************************************************************
// rp_updateGrade
//*****************************************************************************
//gera a nota media do <DEFAULT> a partir do DB
[FUNCTION rp_updateGrade]
if (<DB.connected>)
    DB.QUERY "SELECT round(avg(grade)) AS grade FROM rpGrade WHERE charUid=<eval <uid>>"
    
    if (<DB.ROW.NUMROWS> > 0)
        CTAG.rp_grade=<DB.ROW.0.grade>
    else
        CTAG.rp_grade=0
    endif
endif
skill_init

//*****************************************************************************
// rp_getGmGrade(gmDbId)
//*****************************************************************************
//pega a nota dada pelo GM para o <DEFAULT>
[FUNCTION rp_getGmGrade]
if (<DB.connected>)
    DB.QUERY "SELECT grade FROM rpGrade WHERE charUid=<eval <uid>> AND gmAcct=<eval <argv0>>"
    
    if (<DB.ROW.NUMROWS> > 0)
        TRY CTAG.rp_<argv0>=<DB.ROW.0.grade>
    else
        TRY CTAG.rp_<argv0>=0
    endif
endif

//*****************************************************************************
// rp_setGrade(nota)
//*****************************************************************************
//muda a nota de RP do <DEFAULT> dada pelo GM <SRC>
[FUNCTION rp_setGrade]

if <argv0> > 10
    LOCAL.n = 10
elif <argv0> < 0
    LOCAL.n = 0
else
    LOCAL.n = <argv0>
endif

    LOCAL.n *= 2
    LOCAL.n -= 10
    
serv.log [RP] <account> (<uid>) recebeu nota: <eval <LOCAL.n>> do <src.account> 

if (<DB.connected>)
    LOCAL.q = "timestamp=NOW(), charUid=<eval <uid>>, charAcct=<eval <tag0.idAcct>>, gmUid=<eval <src>>, gmAcct=<eval <src.tag0.idAcct>>, grade=<eval <LOCAL.n>>"
    DB.EXECUTE "INSERT INTO rpGrade SET <LOCAL.q> ON DUPLICATE KEY UPDATE <LOCAL.q>"
endif

rp_updateGrade
rp_getGmGrade <src.tag0.idAcct>

//*****************************************************************************
// virtue_bt
//*****************************************************************************
//Acionado quando usa o Virtue Button
[FUNCTION virtue_bt]
ficha_pc <uid>

//*****************************************************************************
// fichastat
//*****************************************************************************
//Ficha de um char
[FUNCTION fichastat]
if (!<argo>)
    sysmessageyellow Selecione o jogador:
    targetf fichastat
else
    ficha_pc <argo.uid>
endif

//*****************************************************************************
// ficha_pc
//*****************************************************************************
//Ficha do seu proprio char
[FUNCTION ficha_pc]
ctag.ficha_uid=<argn>
ctag.ficha_page=1
SDIALOG d_ficha_pc

//*****************************************************************************
// ficha_pc_stats
//*****************************************************************************
//Elabora texto de estatísticas do char do DIALOG d_ficha_pc
[FUNCTION ficha_pc_stats]
obj=<argn>
local.s = <DEF.BFONT_DCYAN>Idade real: <f_timestring <obj.age>><DEF.BR>
local.s .= Tempo de jogo: <f_timestring <eval <obj.age>*minutos_por_tick>><DEF.BR>
//count pack cash
LOCAL.copper=<obj.rescount i_coin_copper>
LOCAL.silver=<obj.rescount i_coin_silver>
LOCAL.gold=<obj.rescount i_gold>

//Check bank
LOCAL.copper += <obj.findlayer(layer_bankbox).rescount i_coin_copper>
LOCAL.silver += <obj.findlayer(layer_bankbox).rescount i_coin_silver>
LOCAL.gold += <obj.findlayer(layer_bankbox).rescount i_gold>

LOCAL.m=<EVAL <LOCAL.copper>+(<LOCAL.silver>*10)+(<LOCAL.gold>*100)>

local.s .= <DEF.BFONT_DYELLOW>Dinheiro: <STRTRESURE <local.m>><DEF.BR>
local.s .= Peso no banco: <eval <obj.findlayer.layer_bankbox.weight>/10>/<eval <obj.findlayer.layer_bankbox.tag.OVERRIDE.MAXWEIGHT>/10><DEF.BR>
local.s .= <DEF.BFONT_DPURPLE>Maximo de skill permitido: <fval <obj.TAG.skill_maxSkillValue>><DEF.BR>
local.s .= Soma das skills: <fval <obj.skilltotal>> (max: <fval <obj.TAG.skill_maxSkillSumValue>>)<DEF.BR>
local.s .= <DEF.BFONT_RED>Pontos de alma: <obj.dtag.pontosdealma><DEF.BR>
local.s .= Desmaios: <obj.dtag.morte_desmaios>/<dDEF.max_desmaios><DEF.BR>
local.s .= <DEF.BFONT_DCYAN>Alimentacao: <obj.comida_msg>
return <local.s>

//*****************************************************************************
//*****************************************************************************
// TYPEDEFs
//*****************************************************************************
//*****************************************************************************
[TYPEDEF t_jail_stone]
on=@dclick
src.clearctags list_
local.t=<f_timestring <src.findid.i_mry_GM_jail.more1>>
src.sysmessagegreen Faltam <local.t> para que voce seja solto.
src.timerf 3 sysmessageyellow Voce deve permanecer online para que o tempo conte.
src.list_new Tempo de prisao restante: <local.t>. Prisoes que sua conta ja sofreu:
src.list_setAction clearctags list_
src.list_setActionBack clearctags list_
src.list_newDbCol 25,timestamp,Data
src.list_newDbCol 150,message,Tempo: Motivo
src.list_loadFromDb SELECT DATE_FORMAT(timestamp,'%d/%m/%Y %H:%i') AS timestamp, message FROM eventsLog WHERE char2Acct=<src.dtag0.idAcct> AND eventType='Jail' ORDER BY timestamp DESC;
src.list_show 1
return 1

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// i_mry_GM_jail
//*****************************************************************************
[ITEMDEF i_mry_GM_jail]
id=i_memory
name=Preso por GM
type=t_eq_script
layer=30

on=@create
attr=0c010

on=@timer
timer=1
if (<cont.isonline>)
    more1 -= 1
    if (<more1> <= 0)
        cont.forgive
        cont.go=<morep>
        cont.tag.ficha_jail_time=5
        cont.update
        cont.sysmessagegreen Certo, voce cumpriu sua pena. Cuidado com a sua RePresentacao.
        remove
    endif 
endif
return 1

//*****************************************************************************
//*****************************************************************************
// DIALOGs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// d_ficha_1
//*****************************************************************************
[DIALOG d_ficha_1]
0, 0
PAGE 0
resizepic 50 74 3600 640 460
resizepic 85 265 83 574 79
resizepic 85 181 83 574 79
gumppic 237 99 1143
gumppic 0 0 10440
gumppic 658 0 10441
text 290 101 32 0
text 301 129 102 1
text 98 190 1153 2
text 98 231 1153 3
text 302 190 1153 4
text 493 191 1153 5
text 302 230 1153 6
text 490 225 1153 7
text 107 296 1153 8
gumppic 200 297 2444
textentry 212 297 20 15 0 1 9
button 270 293 2151 2152 1 0 2
if <src.account.plevel> > 4
    dtext 305 297 1153 (Media <eval (<src.targ.ctag0.rp_grade>+10)/2>)
endif
//button 107 295 250 251 1 0 1
//button 123 295 252 253 1 0 2
//text 144 296 162 8
//text 173 296 1153 9
button 404 300 252 253 1 0 3
button 420 300 250 251 1 0 4
text 440 301 162 10
text 471 302 1153 11
resizepic 85 356 83 574 124
text 126 375 1153 12
text 126 410 1153 13
text 126 445 1153 14
text 260 375 1153 15
text 260 445 1153 16
text 420 375 1153 17
gumppic 449 434 2444
text 138 190 152 18
text 141 231 152 19
text 342 190 152 20
text 342 230 152 21
text 543 191 152 22
text 560 225 152 23//text 590 225 152 23
text 166 375 152 24
text 166 410 152 25
text 166 445 152 26
text 300 375 152 27
text 259 410 1153 28
text 299 410 152 29
text 301 445 152 30
text 419 404 152 31
text 464 435 162 32
button 588 488 4005 4006 1 0 5
text 496 488 1153 33
PAGE 1

[DIALOG d_ficha_1 TEXT]
Mystical Tales Shard
Ficha de personagem
Nome
Conta
Raca
Classe
UID
Tempo Online
Sua Nota RP
<eval (<src.targ.ctag0.rp_<src.tag0.idAcct>>+10)/2>
<eval 0<src.targ.tag.pontosdealma>>
Pontos de Alma
STR
DEX
INT
Hits
Mana
Melhor habilidade
<src.targ.tag.name>
<src.targ.account>
<src.targ.tag.raca>
<src.targ.uid>
<src.targ.tag.classe>
<f_timestring <src.targ.account.TOTALCONNECTTIME>>//<eval 0<src.targ.account.TOTALCONNECTTIME>/60>:<eval (0<src.targ.account.TOTALCONNECTTIME>-(0<src.targ.account.TOTALCONNECTTIME>/60)*60)> //<eval 0<src.targ.account.TOTALCONNECTTIME>> 
<src.targ.str>
<src.targ.dex>
<src.targ.int>
<src.targ.hits>
Stam
<src.targ.stam>
<src.targ.mana>
<src.targ.tag.habesp>
<eval <src.targ.<src.targ.tag.habesp>>>
Estatisticas


[DIALOG d_ficha_1 BUTTON]
on=1
//RP+
//src.targ.tag.luck=<eval <src.targ.tag.luck>+1>
src.dialog d_ficha_1
return 1

on=2
//nova nota RP
src.targ.rp_setGrade <argtxt[1]>
src.dialog d_ficha_1
return 1

on=3
//alma-
src.targ.tag.pontosdealma=<eval <src.targ.tag.pontosdealma>-1>
src.dialog d_ficha_1
return 1

on=4
//alma+
src.targ.tag.pontosdealma=<eval <src.targ.tag.pontosdealma>+1>
src.dialog d_ficha_1
return 1

on=5
src.dialog d_ficha_2
return 1



//*****************************************************************************
// d_ficha_2
//*****************************************************************************
[DIALOG d_ficha_2]
0, 0
PAGE 0
resizepic 50 74 3600 640 460
resizepic 85 181 83 575 185
gumppic 237 99 1143
gumppic 0 0 10440
gumppic 658 0 10441
text 290 101 32 0
text 301 129 102 1
text 284 191 1153 2
gumppic 120 222 1143
gumppic 421 222 2445
gumppic 120 282 1143
gumppic 421 282 2445
gumppic 120 312 1143
gumppic 421 312 2445
text 270 254 1153 3
resizepic 85 371 83 575 105
button 106 392 250 251 1 0 1
button 120 392 252 253 1 0 2
button 106 427 250 251 1 0 3
button 120 427 252 253 1 0 4
button 371 391 250 251 1 0 5
button 385 391 252 253 1 0 6
button 371 427 250 251 1 0 7
button 386 427 252 253 1 0 8
text 133 222 152 4
text 133 282 152 5
text 133 312 152 6
text 443 222 152 7
text 443 282 152 8
text 443 312 152 9
text 139 392 162 10
text 139 427 162 11
text 404 392 162 12
text 404 427 162 13
text 154 392 1153 14
text 154 427 1153 15
text 419 427 1153 16
text 419 392 1153 17
button 550 219 2151 2152 1 0 9
button 550 277 2151 2152 1 0 10
button 550 310 2151 2152 1 0 11
button 91 488 4014 4015 1 0 12
button 609 488 4005 4006 1 0 13
text 124 489 1153 18
text 553 489 1153 19
PAGE 1

[DIALOG d_ficha_2 TEXT]
Mystical Tales Shard
Ficha de personagem
Morto pela ultima vez por
Matou pela ultima vez 
<src.ctag.ficha_lastkillname>
<src.ctag.ficha_lastagr1name>
<src.ctag.ficha_lastagr2name>
<src.ctag.ficha_lastkilluid>
<src.ctag.ficha_lastagr1uid>
<src.ctag.ficha_lastagr2uid>
<eval 0<src.ctag.ficha_pkCount>>
<eval 0<src.targ.tag.fconf>>
<eval 0<src.targ.tag.warn>>
<eval 0<src.targ.tag.ficha_jail_time>>
Player killing
Meuteu-se em confusoes
Minutos na ultima prisao
Avisos de Anti-RP
Dados
Verbos


[DIALOG d_ficha_2 BUTTON]
on=3
//Confusao+
src.targ.tag.fconf=<src.targ.tag0.fconf>+1
src.dialog d_ficha_2
return 1

on=4
//Confusao-
src.targ.tag.fconf=<src.targ.tag0.fconf>+(-1)
src.dialog d_ficha_2
return 1

on=5
//Warn +
src.targ.tag.warn=<src.targ.tag0.warn>+1
src.dialog d_ficha_2
return 1

on=6
//Warn -
src.targ.tag.warn=<src.targ.tag.warn0>+(-1)
src.dialog d_ficha_2
return 1

on=7
//preso +
src.targ.tag.ficha_jail_time=<src.targ.tag0.ficha_jail_time>+1
src.dialog d_ficha_2
return 1

on=8
//preso -
src.targ.tag.ficha_jail_time=<src.targ.tag0.ficha_jail_time>+(-1)
src.dialog d_ficha_2
return 1

on=9
//GoLastPK
gouid <src.ctag.ficha_lastkilluid>

on=10
//GoLastAgr1
gouid <src.ctag.ficha_lastagr1uid>

on=11
//GoLastAgr2
gouid <src.ctag.ficha_lastagr2uid>

on=12
//dados
src.dialog d_ficha_1
return 1

on=13
//Verbos
src.dialog d_ficha_3
return 1

//*****************************************************************************
// d_ficha_3
//*****************************************************************************
[DIALOG d_ficha_3]
0, 0
PAGE 0
resizepic 50 74 3600 640 460
resizepic 85 181 83 575 185
gumppic 237 99 1143
gumppic 0 0 1581
gumppic 658 0 1582
text 290 101 32 0
text 301 129 102 1
resizepic 85 371 83 575 105
button 106 392 250 251 1 0 1
button 120 392 252 253 1 0 2
button 91 488 4014 4015 1 0 3
text 124 489 1153 2
text 139 390 152 3
text 160 391 32 4
text 171 423 32 5
text 325 391 32 6
text 297 423 32 7
text 530 391 32 8
text 483 423 32 9
button 219 394 2117 2118 1 0 4
button 219 426 2117 2118 1 0 5
button 382 394 2117 2118 1 0 6
button 382 428 2117 2118 1 0 7
button 616 394 2117 2118 1 0 8
button 616 428 2117 2118 1 0 9
button 110 206 250 251 1 0 10
button 123 206 252 253 1 0 11
text 140 206 152 10
text 160 206 32 11
button 115 247 2117 2118 1 0 12
button 115 267 2117 2118 1 0 13
text 143 245 32 12
text 143 265 32 13
button 620 488 4005 4006 1 0 14
text 534 489 1152 14
PAGE 1

[DIALOG d_ficha_3 TEXT]
Mystical Tales Shard
Ficha de personagem
Estatisticas
<eval 0<src.targ.tag.ficha_jail_time>/60>
Prender
Banir
Matar
Desconectar
mensagem
Apagar personagem
<eval 0<src.targ.tag.morte_desmaios>>
Desmaios
Tirar do desmaio
Desmaiar
Comentarios


[DIALOG d_ficha_3 BUTTON]
on=1
//prender +
src.targ.tag.ficha_jail_time=<src.targ.tag0.ficha_jail_time>+60
src.dialog d_ficha_3
return 1

on=2
//prender -
src.targ.tag.ficha_jail_time=<src.targ.tag0.ficha_jail_time>+(-60)
src.dialog d_ficha_3
return 1

on=3
//estatisticas
src.dialog d_ficha_2
return 1

on=4
//prender
src.targ.detem <src.targ.tag0.ficha_jail_time>,<src>,"Preso por GM"
return 1

on=5
//banir
var.AccBan=<src.targ.Account>
src.menu m_banir
return 1

on=6
//matar
src.targ.morte_incrementaDesmaios <DEF.max_desmaios>
src.targ.suicide
return 1

on=7
//desconectar
SRC.TARG.DISCONNECT 1
return 1

on=8
//mensagem
src.dialog d_ficha_msg
return 1

on=9
//apagar
tryp 0 src.targ.remove 1
return 1

on=10
//+desm
src.targ.morte_incrementaDesmaios 1
src.dialog d_ficha_3
return 1

on=11
//-desm
src.targ.morte_decrementaDesmaios 1
src.dialog d_ficha_3
return 1

on=12
//nodesm
src.targ.ress
src.dialog d_ficha_3
return 1

on=13
//desm
src.targ.suicide
return 1


on=14
//coments
src.dialog d_ficha_5
return 1

//*****************************************************************************
// d_ficha_5
//*****************************************************************************
[DIALOG d_ficha_5]
0, 0
PAGE 0
text 82 76 1152 0
resizepic 50 74 3600 640 460
gumppic 237 99 1143
gumppic 121 95 9000
gumppic 550 93 9000
gumppic 0 0 10440
gumppic 658 0 10441
text 290 101 32 1
text 259 125 152 2
text 129 209 162 3
resizepic 212 226 83 364 33
resizepic 212 256 83 364 33
resizepic 212 286 83 364 33
resizepic 212 316 83 364 33
resizepic 212 346 83 364 33
resizepic 212 376 83 364 33
resizepic 212 406 83 364 33
resizepic 212 436 83 364 33
resizepic 212 466 83 364 33
gumppic 96 231 2445
gumppic 96 261 2445
gumppic 96 291 2445
gumppic 96 321 2445
gumppic 96 351 2445
gumppic 96 381 2445
gumppic 96 411 2445
gumppic 96 441 2445
gumppic 96 471 2445
gumppic 583 231 2444
gumppic 583 261 2444
gumppic 583 291 2444
gumppic 583 321 2444
gumppic 583 351 2444
gumppic 583 381 2444
gumppic 583 411 2444
gumppic 583 411 2444
gumppic 583 441 2444
gumppic 583 471 2444
text 595 209 162 5
text 221 209 162 6
//textentry 102 231 95 250 2301 0 4 <src.targ.tag.comment1GM>
dtext 102 231 162 Novo Comentario:
dtextentry 223 231 340 250 2301 1 
//textentry 589 231 55 250 2301 2 8 <src.targ.tag.comment1data>
dtext 102 261 2301 <src.ctag.comment1GM>
dtext 102 291 2301 <src.ctag.comment2GM>
dtext 102 321 2301 <src.ctag.comment3GM>
dtext 102 351 2301 <src.ctag.comment4GM>
dtext 102 381 2301 <src.ctag.comment5GM>
dtext 102 411 2301 <src.ctag.comment6GM>
dtext 102 441 2301 <src.ctag.comment7GM>
dtext 102 471 2301 <src.ctag.comment8GM>
dtext 223 261 2301 <src.ctag.comment1>
dtext 223 291 2301 <src.ctag.comment2>
dtext 223 321 2301 <src.ctag.comment3>
dtext 223 351 2301 <src.ctag.comment4>
dtext 223 381 2301 <src.ctag.comment5>
dtext 223 411 2301 <src.ctag.comment6>
dtext 223 441 2301 <src.ctag.comment7>
dtext 223 471 2301 <src.ctag.comment8>
dtext 589 261 55 <src.ctag.comment1data>
dtext 589 291 55 <src.ctag.comment2data>
dtext 589 321 55 <src.ctag.comment3data>
dtext 589 351 55 <src.ctag.comment4data>
dtext 589 381 55 <src.ctag.comment5data>
dtext 589 411 55 <src.ctag.comment6data>
dtext 589 441 55 <src.ctag.comment7data>
dtext 589 471 55 <src.ctag.comment8data>
text 288 26 95 9
button 346 494 239 240 1 0 1


[DIALOG d_ficha_5 TEXT]
Ð
Mystical Tales Shard
Comentarios sobre <src.targ.tag.name>
GM
<src.targ.tag.comment1GM>
Data
Comentario
xx
yy
Reclama agora, PomP!!!

[DIALOG d_ficha_5 BUTTON]
//Okay
on=1
if (!<isempty <argtxt[1]>>)
    event_note <src.targ>,<argtxt[1]>
endif
return 1

//*****************************************************************************
// d_ficha_msg
//*****************************************************************************
[DIALOG d_ficha_msg]
0, 0
PAGE 0
gumppic 0 0 1140
gumppic 62 70 1143
gumppic 62 95 1143
gumppic 63 120 1143
gumppic 62 145 1143
button 52 205 4014 4015 1 0 1
button 311 206 4005 4006 1 0 2
textentry 74 71 250 250 0 0
textentry 74 96 250 250 0 1
textentry 74 121 250 250 0 2
textentry 74 146 250 250 0 3
text 75 45 1153 4

[DIALOG d_ficha_msg TEXT]
¦
¦
¦
¦
mensagem para <src.targ.tag.name>.


[DIALOG d_ficha_msg BUTTON]
on=1
//voltar
src.dialog d_ficha_3
return 1

on=2
//enviar
src.targ.sysmessageyellow *mensagem*
src.targ.sysmessageorange <argtxt[0]>
src.targ.sysmessageorange <argtxt[1]>
src.targ.sysmessageorange <argtxt[2]>
src.targ.sysmessageorange <argtxt[3]>
src.targ.sysmessageorange Por: GM <src.tag.name>
return 1

//*****************************************************************************
// d_prender
//*****************************************************************************

[DIALOG d_prender]
100,75
page 0
resizepic 0 0 9270 403 173
dhtmlgump 16 15 371 34 0 0 <DEF.CENTER><DEF.BFONT_LRED>Prender <uid.<ctag.admin_player>.tag.name> [<uid.<ctag.admin_player>.account>]<DEF.DIV_E>
dtext 20 57 53 Prender durante
gumppic 127 53 2444
dtext 196 57 53 horas e
gumppic 245 53 2444
dtext 313 57 53 minutos
dtext 21 81 53 Motivo:
resizepic 72 84 9300 310 48
dtextentry 134 54 45 20 0 0 <dctag0.admin_jail_h>
dtextentry 255 54 45 20 0 1 0
dtextentry 78 89 294 43 0 2
button 272 136 247 248 1 0 1
button 69 136 241 242 1 0 0

[DIALOG d_prender text]

[DIALOG d_prender button]
ON=0
// Cancel
if (<ctag0.admin_player>)
    DIALOG d_sphereadmin
endif


ON=1
// Okay
obj=<ctag.admin_player>
local.t=<eval ((<argtxt[0]>*60)+<argtxt[1]>)*60>
if (<restest 1 i_mry_GM_jail>)
    obj.sysmessagered Infracao detectada. Prisao aumentada em <f_timestring <local.t>>.
    obj.findid.i_mry_gm_jail.more1=<obj.findid.i_mry_gm_jail.more1>+<local.t>
    return 1
endif

if (<DB.connected>)
    DB.EXECUTE "INSERT INTO eventsLog SET timestamp=NOW(), eventType='Jail', char2Uid=<eval <obj.uid>>,char2Acct=<eval <obj.tag.idAcct>>,x=<eval <obj.p.x>>,y=<eval <obj.p.y>>,z=<eval <obj.p.z>>,charUid=<eval <src.uid>>,charAcct=<eval <src.tag0.idAcct>>,message='<DB.ESCAPEDATA <f_timestring <local.t>>: <argtxt[2]>>'"
endif

obj.sysmessagered Preso por <f_timestring <local.t>>.
obj.sysmessagered Motivo: <argtxt[2]>

sysmessagered Preso por <f_timestring <local.t>>.
sysmessagered Motivo: <argtxt[2]>

serv.newitem i_mry_gm_jail
new.cont=<obj>
new.morep=<obj.p>
new.more1=<LOCAL.t>
new.timer=1
obj.tag.prisao_total=<eval <obj.tag0.prisao_total>+<local.t>>
obj.go 5275,1164

DIALOG d_sphereadmin

//*****************************************************************************
// d_ficha_pc
//*****************************************************************************
[DIALOG d_ficha_pc]
//Ficha auxiliar de jogador
100,75
page 0
obj=<ctag.ficha_uid>
resizepic 50 31 2620 500 400
checkertrans 55 38 490 385
gumppic 0 0 10400
gumppic 0 160 10401
gumppic 0 356 10402
gumppic 518 0 10410
gumppic 518 160 10411
gumppic 518 356 10412
gumppic 225 45 2501
dtext 231 45 26 Mystical Tales Shard
resizepic 92 87 2620 416 310
dhtmlgump 105 100 391 52 0 0 <DEF.CENTER><DEF.BFONT_SIZE7><DEF.BFONT_DYELLOW><obj.tag.name> - <obj.tag.raca> - <obj.tag.classe><DEF.CENTERE><DEF.BFONTE>
if (<ctag0.ficha_page>==1)
    dhtmlgump 113 155 377 134 1 1 <ficha_pc_stats <obj>>
    dorigin 115 280
    button - *20 2224 2224 1 0 1
    dtext +25 -4 62 Emotes
    button - *20 2224 2224 1 0 2
    dtext +25 -4 62 Estrategia de batalha
    button - *20 2224 2224 1 0 3
    dtext +25 -4 62 Comandar animais
    button - *20 2224 2224 1 0 4
    dtext +25 -4 62 Reconhecer uma pessoa
    dorigin 315 280
    button - *20 2224 2224 1 0 5
    dtext +25 -4 62 Gerenciar moveis
    button - *20 2224 2224 1 0 6
    dtext +25 -4 62 Arremessar objeto
    button - *20 2224 2224 1 0 7
    dtext +25 -4 62 Tirar objeto de uma pilha
elif (<ctag0.ficha_page>==10)
    dorigin 115 100
    local.i=1
    local.j=14
    WHILE (!<IsEmpty <DEF.f_emote<dlocal.i>>>)
        if (<local.i>==<local.j>)
            dorigin 215 100
        endif
        button - *20 2224 2224 1 0 <eval 100+<dlocal.i>>
        dtext +25 -4 62 <DEF.f_emote<dlocal.i>>
        local.i += 1
    END
endif    

[DIALOG d_ficha_pc text]

[DIALOG d_ficha_pc button]
ON=1
// Emotes
ctag.ficha_page=10
SDIALOG d_ficha_pc

ON=2
// .combate
combate

ON=3
// .pastor
pastor

ON=4
// .r
r

ON=5
// .Moveis
moveis

ON=6
// .arrecessar
arremessar

ON=7
// .unstack
unstack

ON=100 199
obj=<ctag.ficha_uid>
local.n=<eval <argn>-100>
try obj.<DEF.f_emote<dlocal.n>>


// Created 23/2/2010 20:10:00, with Gump Studio.
// Exported with with SphereExporter ver 1.1.
// Script for 0.56/Revisions
//*****************************************************************************
[DIALOG d_macroer]
//*****************************************************************************
// Comando .macroer para prender macroers.
100,75
page 0
ref1=<ctag.macroer>
resizepic 50 31 2620 500 400
checkertrans 55 38 490 385
gumppic 0 0 10400
gumppic -1 157 10401
gumppic 0 356 10402
gumppic 518 0 10410
gumppic 518 160 10411
gumppic 518 356 10412
gumppic 225 45 2501
dtext 231 45 26 Mystical Tales Shard
resizepic 92 87 2620 416 310
dtext 207 94 48 Prender e penalizar por macro
dhtmlgump 115 118 370 64 2 1 <macroer_Resume>
dtext 117 190 166 Prender por
resizepic 196 187 9350 40 25
dtextentry 206 190 25 20 0 0 <dctag.macroer_hours>
dtext 239 190 166 horas
resizepic 279 187 9350 40 25
dtextentry 288 189 25 20 0 1 <dctag.macroer_minutes>
dtext 322 189 166 minutos
dtext 117 220 166 Penalidade de skills
resizepic 239 218 9350 49 25
dtextentry 243 221 36 20 0 2 <fval <ctag.macroer_penalty>>
dtext 118 247 166 (-10.0 eh igual a -100, mas eh diferente de -10)
dorigin 119 253
IF (!<IsEmpty <cTAG.macroer_skill_0>>)
    local.i=0
    WHILE (!<IsEmpty <cTAG.macroer_skill_<dlocal.i>>>)
        button - *20 1209 1210 1 0 <eval 10+<local.i>>
        dtext +20 -3 2100 <serv.skill.<cTAG.macroer_skill_<dlocal.i>>.name> (<REF1.<cTAG.macroer_skill_<dlocal.i>>>)
        local.i += 1
    ENDWHILE
ENDIF
IF (<IsEmpty <cTAG.macroer_skill_4>>)
    button +20 *20 2224 2223 1 0 2
    dtext +40 -3 32 Adicionar skill
ENDIF
button 439 397 247 248 1 0 1//prender
button 96 397 241 242 1 0 0//cancelar

[DIALOG d_macroer text]


[DIALOG d_macroer button]
ON=0
// Fechar/cancelar
ctag.macroer_hours=
ctag.macroer_minutes=
ctag.macroer_penalty=
ctag.macroer=
IF (!<IsEmpty <cTAG.macroer_skill_0>>)
    local.i=0
    WHILE (!<IsEmpty <cTAG.macroer_skill_<dlocal.i>>>)
        TRY cTAG.macroer_skill_<dlocal.i>=
        local.i += 1
    ENDWHILE
ENDIF
sysmessagered Miou. Deixa o meliante ai.

ON=1
// Prender agora
obj=<ctag.macroer>
local.time=<eval ((<ARGTXT[0]>*60)+<ARGTXT[1]>)*60>
local.s=":"
IF (!<IsEmpty <cTAG.macroer_skill_0>>)
    local.i=0
    WHILE (!<IsEmpty <cTAG.macroer_skill_<dlocal.i>>>)
        local.s .= " <serv.skill.<cTAG.macroer_skill_<dlocal.i>>.name>;"
        TRY obj.<dcTAG0.macroer_skill_<dlocal.i>> = <eval <obj.<dcTAG0.macroer_skill_<dlocal.i>>>+<ARGTXT[2]>>
        TRY cTAG.macroer_skill_<dlocal.i>=
        local.i += 1
    ENDWHILE
ENDIF
IF <DB.CONNECTED>
    DB.EXECUTE "INSERT INTO eventsLog SET timestamp=NOW(), eventType='Jail',char2Uid=<eval <obj>>,char2Acct=<eval <obj.tag.idAcct>>,x=<eval <obj.p.x>>,y=<eval <obj.p.y>>,z=<eval <obj.p.z>>,charUid=<eval <uid>>,charAcct=<eval <tag0.idAcct>>,message='<DB.ESCAPEDATA <f_timestring <local.time>>: Macroando<local.s> longe do PC ou fora de contexto RP.>'"
ENDIF

obj.sysmessagered Preso por <f_timestring <local.time>>
obj.sysmessagered Motivo: Macroando <local.s> fora do PC ou fora de contexto RP.

serv.newitem i_mry_gm_jail
new.cont=<obj>
new.morep=<obj.p>
new.more1=<LOCAL.time>
new.timer=1
obj.tag.prisao_total=<eval <obj.tag0.prisao_total>+<local.time>>
obj.go 5275,1164
obj.account.tag.macro_count=<eval <obj.account.tag0.macro_count>+1>

sysmessageyellow DONE! MUAHUAUHAUHAUHAUHAUH!!!
clearctags macroer_

ON=2
// Adicionar skill
ctag.macroer_hours=<ARGTXT[0]>
ctag.macroer_minutes=<ARGTXT[1]>
ctag.macroer_penalty=<ARGTXT[2]>
macroer_skill

ON=10 14
// Remove uma skill
ctag.macroer_hours=<ARGTXT[0]>
ctag.macroer_minutes=<ARGTXT[1]>
ctag.macroer_penalty=<ARGTXT[2]>
macroer_DeleteSkill <eval <argn>-10>

[FUNCTION admin_suspendPages]
obj=<cTAG0.admin_player>
if (<argn> < 1)
    obj.tag.noPagesTil=
else
    obj.tag.noPagesTil=<eval <serv.time>+(<argn>*60*10)>
endif
DIALOG d_sphereadmin

[FUNCTION admin_removeChar]
if (<argn>==<ctag0.chalenge>)
    trysrv uid.<ctag.admin_player>.remove 1
else
    sysmessagered Numero incorreto.
    DIALOG d_sphereadmin
endif

[DEFNAME IM]
IM_RECIVE       1
IM_SEND         2

[FUNCTION InstantMessage]
//Abre o diálogo de mensagem instantanea para player <argn>
if (!<argv>)
    return
endif
clearctags IM_
ctag.IM_to=<argn>
ctag.IM_task=IM_SEND
DIALOGCLOSE d_messenger
DIALOG d_messenger

[DIALOG d_messenger]
75,75
page 0
resizepic 0 0 9270 491 238
IF (<ctag0.IM_TASK>==IM_SEND)
    dhtmlgump 17 16 451 22 0 0 <DEF.BFONT_LRED><DEF.CENTER>Sistema de mensagens online: <uid.<ctag.IM_to>.tag.name><DEF.DIV_E>
    dtext 21 42 52 Mensagem:
    resizepic 93 44 9350 378 64
    dtextentry 98 49 367 55 0 0
    dtext 58 112 52 Link:
    dtext 27 129 52 (opcional)
    resizepic 92 114 9350 378 49
    dtextentry 97 119 367 40 0 1 
    IF (<IsGM>)
        checkbox 97 167 210 211 0 0
        dtext 124 167 4 Permitir responder mensagem
    ENDIF
    button 412 200 247 248 1 0 1
    button 14 200 241 242 1 0 0
    page 2
ELSE
    dhtmlgump 17 16 451 22 0 0 <DEF.BFONT_LRED><DEF.CENTER>Sistema de mensagens online: <uid.<ctag.IM_Sender>.tag.name><DEF.DIV_E>
    dhtmlgump 19 43 448 72 7 1 <ctag.IM_Text>
    if (!<IsEmpty <ctag.IM_link>>)
        dhtmlgump 99 125 372 47 1 0 <ctag.IM_link>
        button 25 126 2224 2224 1 0 2
        dtext 47 122 52 Visitar
    endif
    button 451 206 4017 4018 1 0 0
    if (<ctag0.IM_CanReply>) || (<IsGM>)
        button 183 191 2445 2445 1 0 3
        dtext 209 192 52 Responder
    endif
ENDIF

[DIALOG d_messenger text]

[DIALOG d_messenger button]
ON=0
clearctags IM_

ON=1
//Send
obj=<ctag0.IM_to>
obj.ctag.IM_Sender=<uid>
obj.ctag.IM_Text=<argtxt[0]>
if (<eval strlen(<argtxt[1]>)> > 7 )
    obj.ctag.IM_Link=<argtxt[1]>
endif
obj.ctag.IM_CanReply=<eval <argchk[0]>>
obj.ctag.IM_task=IM_RECIVE
trysrc <obj> src.DIALOG d_messenger

ON=2
//Open Link
if (!<IsEmpty <ctag.IM_link>>)
    if (STRMATCH(http*,<ctag.IM_link>))
        local.link=<STRSUB 7 <eval strlen(<ctag.IM_link>)> <ctag.IM_link>>
    else
        local.link=<ctag.IM_link>
    endif
    WEBLINK=<local.link>
else
    sysmessagered Link invalido. Expirou.
endif
DIALOG d_messenger

ON=3
//Repty
InstantMessage <ctag.IM_sender>

//*****************************************************************************
//*****************************************************************************
// MENUs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// m_banir
//*****************************************************************************
[menu m_banir]
ATENCAO! Banir permanentemente <var.AccBan>?
on=0 NAO
src.dialog d_ficha_3
return 1

on=0 SIM
tryp 0 SERV.ACCOUNT <var.AccBan> BLOCK 1 
src.sysmessage Conta <var.AccBan> banida. Contacte o Administrador.
return 1

on=0 NAO
src.dialog d_ficha_3
return 1


[EOF]
