//*****************************************************************************
//*****************************************************************************
//
// SKILL CAMPING 
//
//  Galthar
//  UsMarine (vinicius.arroyo at gmail.com)
//
//*****************************************************************************
//*****************************************************************************

//TODO:
//diminuir area de cura

//TAGS usadas:

//*****************************************************************************
//*****************************************************************************
// FUNCTIONs
//*****************************************************************************
//*****************************************************************************

//*****************************************************************************
// campheal
//*****************************************************************************
[FUNCTION campheal]

//cancela efeito do camping se alguem em warmode
IF (<FLAGS> & statf_war) 
    OBJ=<argv[1]>
    OBJ.REGION.ALLCLIENTS SYSMESSAGERED <NAME> perturbou o acampamento!
    OBJ.LINK.REMOVE
    OBJ.REMOVE
ENDIF

//faz efeito do healing
IF (<HITS> < <STR>)
    HITS=<HITS>+<argv[0]>
ENDIF
IF (<MANA> < <INT>)
    MANA=<MANA>+<argv[0]>
ENDIF
IF (<STAM> < <DEX>)
    STAM=<STAM>+<argv[0]>
ENDIF

//*****************************************************************************
//*****************************************************************************
// ITEMDEFs
//*****************************************************************************
//*****************************************************************************


//*****************************************************************************
// i_multi_campfire
//*****************************************************************************
[ITEMDEF i_multi_campfire]
ID=i_campfire
TYPE=t_multi
MULTIREGION=-5,-5,5,5

ON=@Create
MOREZ=29
ATTR=attr_move_never
//*****************************************************************************
// i_campfire_tracker - cura hits, mana e stam de quem estiver proximo
//*****************************************************************************
[ITEMDEF i_campfire_tracker]
ID=01ea7
TYPE=t_eq_script

ON=@CREATE
ATTR=attr_move_never|attr_invis

ON=@TIMER
//remove tracker e campfire se acabou o tempo
IF (<MORE2> < 1)
    LINK.REMOVE
    REMOVE
    RETURN 1
ENDIF

//cura quem estiver na regiao
FORCLIENTS 1
 CAMPHEAL <R1,2>,<uid>
ENDFOR

//subtrai tempo percorrido
MORE2=<MORE2>-<MORE1>
TIMER=<MORE1>
RETURN 1

//*****************************************************************************
// i_gravetos
//*****************************************************************************
[ITEMDEF 0de1]
DEFNAME=i_kindling
DEFNAME2=i_gravetos
TYPE=t_kindling
NAME=gravetos%s%
RESOURCES=1 i_log
SKILLMAKE=BOWCRAFT 0.0
REPLICATE=1
WEIGHT=1
CATEGORY=MyT - Items by Professions
SUBSECTION=Camping
DESCRIPTION=gravetos
DUPELIST=0de2

on=@create
ATTR=attr_decay

ON=@DCLICK

if (<cont>)
    src.sysmessageyellow Coloque o graveto no chao para usar.
    return 1
endif

src.skill_gain skill_camping

if (!<belltest <SRC.CAMPING>,400>)
    SRC.emotered falhou ao acender a fogueira
    return 1
endif

SERV.newitem i_campfire
NEW.morez=29
NEW.p=<p>
//tempo total (30 a 330 segundos)
NEW.timer=<EVAL (<SRC.CAMPING>*3)/10>+30

//teste se sera um caping de cura
if (!<belltest <SRC.CAMPING>,700>)
    SRC.emotered acende a fogueira
    remove
    return 1
endif

SERV.NEWITEM i_campfire_tracker
NEW.P=<P>
NEW.LINK=<UID>
//tempo total (max 300 segundos)
NEW.MORE2=<EVAL (<SRC.CAMPING>*3)/10>
//healing interval (de 2 a 12 segundos)
NEW.MORE1=<EVAL 12-(<SRC.CAMPING>/100)>
NEW.TIMER=<NEW.MORE1>
UPDATE
SRC.emotered faz uma bela fogueira
remove
return 1

[ITEMDEF 0de2]
DUPEITEM=0de1

//*****************************************************************************
// i_campfire
//*****************************************************************************
[ITEMDEF 0de3]
DEFNAME=i_campfire
TYPE=t_fire
NAME=fogueira
RESOURCES=i_kindling
CATEGORY=MyT - Items by Professions
SUBSECTION=Camping
DESCRIPTION=fogueira
DUPELIST=0de4,0de5,0de6,0de7,0de8

ON=@PICKUP_GROUND
MOVABLE=0
SRC.RESEND
return 1

on=@timer
if (<more1>)
 more1 -= 1
 timer 60
else
 serv.newitem i_ember_lit
 new.attr=012
 new.timer=<serv.decaytimer>*60
 new.p=<p>
 new.emoteyellow apagou
 remove
endif
return 1

[ITEMDEF 0de4]
DUPEITEM=0de3

[ITEMDEF 0de5]
DUPEITEM=0de3

[ITEMDEF 0de6]
DUPEITEM=0de3

[ITEMDEF 0de7]
DUPEITEM=0de3

[ITEMDEF 0de8]
DUPEITEM=0de3

//*****************************************************************************
// i_ember_lit
//*****************************************************************************
[ITEMDEF 0de9]
NAME=brasa
DEFNAME=i_ember_lit
TYPE=t_fire
VALUE=2
CATEGORY=MyT - Items by Professions
SUBSECTION=Camping
DESCRIPTION=brasas

ON=@PICKUP_GROUND
MOVABLE=0
return 1

on=@timer
if (<more1>)
 more1 -= 1
 timer 60
else
 serv.newitem i_ember
 new.attr=012
 new.timer=<serv.decaytimer>*60
 new.p=<p>
 new.emoteyellow apagou
 remove
endif
return 1

//*****************************************************************************
// i_ember
//*****************************************************************************
[ITEMDEF 0dea]
DEFNAME=i_ember
TYPE=t_garbage
name=madeira queimada
RESOURCES=1 i_kindling
CATEGORY=MyT - Items by Professions
SUBSECTION=Camping
DESCRIPTION=madeira queimada

ON=@PICKUP_GROUND
MOVABLE=0
return 1

[EOF]